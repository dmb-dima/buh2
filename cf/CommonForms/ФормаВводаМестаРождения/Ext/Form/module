Перем мРезультатДиалога;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события ПриОткрытии формы
Процедура ПриОткрытии()

	//Устанавливаем заголовок формы
	Заголовок = ЗаголовокФормы;

	//Выполняем разбор места рождения из строки, используем нижний регистр для представления и верхний регистр для документов.
	Если Тип (ВладелецФормы) = Тип ("Форма") Тогда
		СтруктураМестоРождения = РегламентированнаяОтчетность.РазложитьМестоРождения(МестоРожденияСтрокой, Ложь);

	Иначе
		СтруктураМестоРождения = РегламентированнаяОтчетность.РазложитьМестоРождения(МестоРожденияСтрокой);

	КонецЕсли;

	Особое  = СтруктураМестоРождения.Особое;
	Город   = СтруктураМестоРождения.НаселенныйПункт;
	Район   = СтруктураМестоРождения.Район;
	Область = СтруктураМестоРождения.Область;
	Страна  = СтруктураМестоРождения.Страна;

КонецПроцедуры

// Обработчик события ПередЗакрытием формы
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	Если мРезультатДиалога <> "Отмена" тогда

		МестоРожденияСтрокойПослеРедактирования = ""+СокрЛП(Особое)+","+СокрЛП(Город)+","+СокрЛП(Район)+","+СокрЛП(Область)+","+СокрЛП(Страна);

		Если СокрЛП(МестоРожденияСтрокой) <> СокрЛП(МестоРожденияСтрокойПослеРедактирования) тогда

			Если мРезультатДиалога <> "ОК" тогда

				РезультатВопроса = Вопрос("Данные о месте рождения изменены. Сохранить?",РежимДиалогаВопрос.ДаНетОтмена);

				Если РезультатВопроса=КодВозвратаДиалога.Нет тогда

					Возврат;

				ИначеЕсли РезультатВопроса=КодВозвратаДиалога.Отмена тогда

					Отказ = Истина;

					Возврат;

				Иначе

					мРезультатДиалога = "ОК";

				КонецЕсли;

			КонецЕсли;

			МестоРожденияСтрокой = МестоРожденияСтрокойПослеРедактирования;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Обработчик события ПриЗакрытии формы
Процедура ПриЗакрытии()

	Если мРезультатДиалога = "ОК" тогда
		ОповеститьОВыборе(МестоРожденияСтрокой);
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ ДЕЙСТВИЙ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

//Обработчик события нажатия кнопки "ОК"
Процедура НижняяКоманднаяПанельОК(Кнопка)

	мРезультатДиалога = "ОК";

	Закрыть();

КонецПроцедуры

//Обработчик события нажатия кнопки "Отмена"
Процедура НижняяКоманднаяПанельОтмена(Кнопка)

	мРезультатДиалога = "Отмена";

	Закрыть();

КонецПроцедуры

//Инициализация переменных формы
мРезультатДиалога = "";