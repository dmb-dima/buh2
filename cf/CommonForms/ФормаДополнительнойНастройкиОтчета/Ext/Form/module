
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ТиповыеОтчеты.ЭтоПроизвольныйОтчет(ОтчетОбъект) Тогда
		ЭлементыФормы.ПанельНастройки.Страницы.ПараметрыВывода.Видимость = Ложь;
		ЭлементыФормы.ПанельНастройки.Страницы.УсловноеОформление.Видимость = Ложь;
	КонецЕсли;
	
	Если ВладелецФормы.Расшифровки.Колонки.Количество() > 0 Тогда
		Расшифровки = ВладелецФормы.Расшифровки;
	Иначе
		ВладелецФормы.Расшифровки = Расшифровки;
	КонецЕсли;
	
	ОграниченнаяНастройка = ВладелецФормы.ПараметрыФормы.Свойство("ОграниченнаяНастройка") И ВладелецФормы.ПараметрыФормы["ОграниченнаяНастройка"];
	ЭлементыФормы.Расшифровки.ТолькоПросмотр = ОграниченнаяНастройка;
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ТиповыеОтчеты.ПолучитьСхемуКомпоновкиОбъекта(ОтчетОбъект)));
	
	КомпоновщикНастроек.ЗагрузитьНастройки(ВладелецФормы.КомпоновщикНастроекПользовательский.ПолучитьНастройки());
	
	ПредставлениеЭлементаОтчета = ТиповыеОтчеты.ПолучитьПредставлениеПоЭлементуСтруктуры(ФормаОтчета.КомпоновщикНастроек.Настройки.Структура[0]);
	
	ЭлементыФормы.Расшифровки.Колонки.СохраненнаяНастройка.ТолькоПросмотр = ТиповыеОтчеты.ЭтоПроизвольныйОтчет(ОтчетОбъект);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОсновныеДействияФормыОК(Кнопка)
	
	ТиповыеОтчеты.ЗаполнитьЭлементы(ВладелецФормы.КомпоновщикНастроекПользовательский.Настройки.ПараметрыВывода, КомпоновщикНастроек.Настройки.ПараметрыВывода);
	ТиповыеОтчеты.СкопироватьЭлементы(ВладелецФормы.КомпоновщикНастроекПользовательский.Настройки.УсловноеОформление, КомпоновщикНастроек.Настройки.УсловноеОформление, Ложь);
	
	ВладелецФормы.Расшифровки = Расшифровки;
	ВладелецФормы.Модифицированность = Модифицированность;
	
	ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ChartType"));
	
	Закрыть();
	
КонецПроцедуры

Процедура УсловноеОформлениеПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.Поля.Элементы.Количество() = 0 Тогда
		
		ОформлениеСтроки.Ячейки.ОбластиДляКраткогоОтображенияЭлемента.Текст = НСтр("ru='<Все поля>'");
		ОформлениеСтроки.Ячейки.ОбластиДляПодробногоОтображенияЭлемента.Текст = НСтр("ru='<Все поля>'");
		
	КонецЕсли;
		
	Если ДанныеСтроки.Отбор.Элементы.Количество() = 0 Тогда
		
		ОформлениеСтроки.Ячейки.ОтборДляКраткогоОтображенияЭлемента.Текст = НСтр("ru='<Без условия>'");
		ОформлениеСтроки.Ячейки.ОтборДляПодробногоОтображенияЭлемента.Текст = НСтр("ru='<Без условия>'");
		
	КонецЕсли;
	
	ЦветФона = ДанныеСтроки.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ЦветФона"));
	Если ЦветФона.Использование Тогда
		
		ОформлениеСтроки.ЦветФона = ЦветФона.Значение;
		
	КонецЕсли;
	
	ЦветТекста = ДанныеСтроки.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ЦветТекста"));
	Если ЦветТекста.Использование Тогда
		
		ОформлениеСтроки.ЦветТекста = ЦветТекста.Значение;
		
	КонецЕсли;
	
	Шрифт = ДанныеСтроки.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Шрифт"));
	Если Шрифт.Использование Тогда
		
		ОформлениеСтроки.Шрифт = Шрифт.Значение;
		
	КонецЕсли;
	
	Текст = ДанныеСтроки.Оформление.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Текст"));
	Если Текст.Использование Тогда
		
		ОформлениеСтроки.Ячейки.ОформлениеДляКраткогоОтображенияЭлемента.Текст = Текст.Значение;
		ОформлениеСтроки.Ячейки.ОформлениеДляПодробногоОтображенияЭлемента.Текст = Текст.Значение;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РасшифровкиПоляНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФормаВыбораПоля = ПолучитьОбщуюФорму("ФормаРедактированияСпискаПолейКомпоновки", ЭтаФорма);
	ФормаВыбораПоля.КомпоновщикНастроек = ВладелецФормы.КомпоновщикНастроекПользовательский;
	ФормаВыбораПоля.СписокПолей = Элемент.Значение;
	Результат = ФормаВыбораПоля.ОткрытьМодально();
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура РасшифровкиПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.Поля.Количество() = 0 Тогда
		ОформлениеСтроки.Ячейки.Поля.Текст = "<все поля>";
	КонецЕсли;
	
КонецПроцедуры

Процедура РасшифровкиОтчетПриИзменении(Элемент)
	
	ТекущаяСтрока = ЭлементыФормы.Расшифровки.ТекущиеДанные;
	Если Не ТекущаяСтрока.СохраненнаяНастройка.Пустая() Тогда
		Если ТиповыеОтчеты.ЭтоПроизвольныйОтчет(ТекущаяСтрока.Отчет) И ТекущаяСтрока.СохраненнаяНастройка.НастраиваемыйОбъект <> ТекущаяСтрока.Отчет 
		  ИЛИ Не ТиповыеОтчеты.ЭтоПроизвольныйОтчет(ТекущаяСтрока.Отчет) И ТекущаяСтрока.СохраненнаяНастройка.НастраиваемыйОбъект <> "ОтчетОбъект." + ТекущаяСтрока.Отчет Тогда
			ТекущаяСтрока.СохраненнаяНастройка = Справочники.СохраненныеНастройки.ПустаяСсылка();
  		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекущаяСтрока.Отчет) Тогда
		Если ТиповыеОтчеты.ЭтоПроизвольныйОтчет(ТекущаяСтрока.Отчет) Тогда
			ТекущаяСтрока.Представление = ТекущаяСтрока.Отчет.Наименование;
		Иначе
			ТекущаяСтрока.Представление = Метаданные.Отчеты[ТекущаяСтрока.Отчет].Синоним;
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекущаяСтрока.СохраненнаяНастройка) Тогда
			ТекущаяСтрока.Представление = ТекущаяСтрока.Представление + " [" + ТекущаяСтрока.СохраненнаяНастройка.Наименование + "]";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура РасшифровкиОтчетНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТиповыеОтчеты.ОбработкаНачалаВыбораОтчета(ОтчетОбъект, Элемент, СтандартнаяОбработка);
	РасшифровкиОтчетПриИзменении(Элемент);
	
КонецПроцедуры

Процедура РасшифровкиОтчетОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущаяСтрока = ЭлементыФормы.Расшифровки.ТекущиеДанные;
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.СохраненныеНастройки") Тогда
		ТекущаяСтрока.Отчет = ВыбранноеЗначение.НастраиваемыйОбъект;
		ТекущаяСтрока.СохраненнаяНастройка = ВыбранноеЗначение;
	Иначе
		ТекущаяСтрока.Отчет = ВыбранноеЗначение;
		ТекущаяСтрока.СохраненнаяНастройка = Справочники.СохраненныеНастройки.ПустаяСсылка();
	КонецЕсли;
	РасшифровкиОтчетПриИзменении(Неопределено);

КонецПроцедуры

Процедура РасшифровкиСохраненнаяНастройкаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФормаВыбора = Справочники.СохраненныеНастройки.ПолучитьФормуВыбора(, Элемент);
	ФормаВыбора.Отбор.НастраиваемыйОбъект.Установить("ОтчетОбъект." + ЭлементыФормы.Расшифровки.ТекущаяСтрока.Отчет);
	ФормаВыбора.Отбор.ТипНастройки.Установить(Перечисления.ТипыНастроек.НастройкиОтчета);
	ФормаВыбора.ПараметрТекущаяСтрока    = Элемент.Значение;
	ФормаВыбора.РежимСохраненияНастройки = Ложь;
	ФормаВыбора.ЭлементыФормы.СправочникСписокСохраненныеНастройки.Колонки.Владелец.Видимость = Ложь;
	ФормаВыбора.ОткрытьМодально();
	РасшифровкиОтчетПриИзменении(Элемент);
	
КонецПроцедуры

Процедура РасшифровкиСохраненнаяНастройкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Элемент.Значение = ВыбранноеЗначение;
	СтандартнаяОбработка = Ложь;
	РасшифровкиОтчетПриИзменении(Элемент);
	
КонецПроцедуры




