////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем СтруктураПоиска;
Перем НП;

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ПолучитьДанные()

	Запрос = Новый Запрос();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	Если РежимОтбора = Перечисления.РежимыОтбора.ПоОстаткам Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		 |	ХозрасчетныйОстатки.Субконто2 КАК НомерГТД,
		 |	ХозрасчетныйОстатки.Субконто3 КАК СтранаПроисхождения,
		 |	ХозрасчетныйОстатки.КоличествоОстатокДт КАК Количество
		 |ИЗ
		 |	РегистрБухгалтерии.Хозрасчетный.Остатки(
		 |		&КонПериода,
		 |		Счет = &СчетУчетаНомеровГТД,
		 |		,
		 |		Организация = &Организация
		 |			И Субконто1 = &Номенклатура) КАК ХозрасчетныйОстатки";
		 
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОборотыДтКт.СубконтоДт2 КАК НомерГТД,
		|	ХозрасчетныйОборотыДтКт.СубконтоДт3 КАК СтранаПроисхождения,
		|	ХозрасчетныйОборотыДтКт.КоличествоОборотДт КАК Количество
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
		|		&НачПериода,
		|		&КонПериода,
		|		Период,
		|		СчетДт = &СчетУчетаНомеровГТД,
		|		,
		|		,
		|		,
		|		Организация = &Организация
		|			И СубконтоДт1 = &Номенклатура) КАК ХозрасчетныйОборотыДтКт";
	КонецЕсли;

	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НачПериода", НачПериода);
	Запрос.УстановитьПараметр("КонПериода", КонПериода);
    Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("СчетУчетаНомеровГТД", ПланыСчетов.Хозрасчетный.ГТД);
	СписокНомеровГТД = Запрос.Выполнить().Выгрузить();
	
	// устанавливает текущую позицию
	СтруктураПоиска = Новый Структура("НомерГТД, СтранаПроисхождения");
	СтруктураПоиска.НомерГТД = ПараметрНомерГТД;
	СтруктураПоиска.СтранаПроисхождения = ПараметрСтранаПроисхождения;
	
    СписокНомеровГТД.Индексы.Добавить("НомерГТД, СтранаПроисхождения");
	Строки = СписокНомеровГТД.НайтиСтроки(СтруктураПоиска);

	Если Строки.Количество() > 0 Тогда
		ЭлементыФормы.СписокНомеровГТД.ТекущаяСтрока = Строки[0];
	КонецЕсли;

КонецПроцедуры

Процедура ОбновлениеСписка()
	
	Если ЭлементыФормы.СписокНомеровГТД.ТекущаяСтрока = НеОпределено Тогда
		ПараметрНомерГТД = Справочники.НомераГТД.ПустаяСсылка();
		ПараметрСтранаПроисхождения = Справочники.КлассификаторСтранМира.ПустаяСсылка();
		
	Иначе
		ПараметрНомерГТД = ЭлементыФормы.СписокНомеровГТД.ТекущиеДанные.НомерГТД;
		ПараметрСтранаПроисхождения = ЭлементыФормы.СписокНомеровГТД.ТекущиеДанные.СтранаПроисхождения;
	КонецЕсли;
	
	ПолучитьДанные();
	
КонецПроцедуры

Процедура ПриИзмененииРежимаОтбора(ЭтоОткрытиеФормы = Ложь)
	
	Если РежимОтбора = Перечисления.РежимыОтбора.ОтборВыключен Тогда
		ЭлементыФормы.ПанельСписок.ТекущаяСтраница = ЭлементыФормы.ПанельСписок.Страницы.Справочник;	
		
	Иначе
		ЭлементыФормы.ПанельСписок.ТекущаяСтраница = ЭлементыФормы.ПанельСписок.Страницы.Список;
	КонецЕсли;
	
	Если ЭтоОткрытиеФормы Тогда
		Если ФормироватьСписокПриОткрытии Тогда
			ОбновлениеСписка();
		КонецЕсли;
		
	Иначе
		ОбновлениеСписка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияПриВыборе(Элемент, СтандартнаяОбработка, ЭтоВыборИзСправочника)
	
	Если РежимВыбора И 
		Элемент.ТекущиеДанные <> Неопределено И 
		ЭтоВыборИзСправочника И 
		Элемент.ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимВыбора Тогда
		Если Элемент.ТекущиеДанные <> Неопределено Тогда
			Если ЭтоВыборИзСправочника Тогда
				ВладелецФормы.ТекущиеДанные.НомерГТД = Элемент.ТекущиеДанные.Ссылка;
				
			Иначе
				ВладелецФормы.ТекущиеДанные.НомерГТД = Элемент.ТекущиеДанные.НомерГТД;
				ВладелецФормы.ТекущиеДанные.СтранаПроисхождения = Элемент.ТекущиеДанные.СтранаПроисхождения;
			КонецЕсли;
		КонецЕсли;
		
		Закрыть();
    	СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаНастройкаПериодаНажатие(Элемент)

	НП.УстановитьПериод(НачПериода, КонПериода);

	Если НП.Редактировать() Тогда

		НачПериода = НП.ПолучитьДатуНачала();
		КонПериода = НП.ПолучитьДатуОкончания();

	КонецЕсли;

КонецПроцедуры // КнопкаНастройкаПериодаНажатие()

Процедура СвернутьПанельОтбора()
	
	Если НЕ ЭлементыФормы.КоманднаяПанельСписок.Кнопки.Отбор.Пометка Тогда
		ЭлементыФормы.ПанельОтбор.Свертка = РежимСверткиЭлементаУправления.Верх;
		
	Иначе
		ЭлементыФормы.ПанельОтбор.Свертка = РежимСверткиЭлементаУправления.Нет;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	// Если даты не заполнены, то период устанавливается по умолчанию
	Если НЕ ЗначениеЗаполнено(КонПериода) Тогда
		КонПериода = КонецМесяца(ТекущаяДата());
	КонецЕсли;
	
	Если КонПериода = НачалоДня(КонПериода) Тогда
		КонПериода = КонецМесяца(ТекущаяДата());
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(НачПериода) Тогда
		НачПериода = НачалоМесяца(КонПериода);
	КонецЕсли;
	
	// Установка режима отбора
	СохранятьРежимОтбора = ВосстановитьЗначение("СпрНомерГТДПодбор_СохранятьРежимОтбора");
	ФормироватьСписокПриОткрытии   = ВосстановитьЗначение("СпрНомерГТДПодбор_ФормироватьСписокПриОткрытии");
	
	Если СохранятьРежимОтбора Тогда
		РежимОтбора = ВосстановитьЗначение("СпрНомерГТДПодбор_РежимОтбора");
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(РежимОтбора) Тогда
		РежимОтбора = Перечисления.РежимыОтбора.ПоОстаткам;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("Физлицо, Приказ");
	
	// сформировать колонки табличного поля
	КолонкаНомерГТД = ЭлементыФормы.СписокНомеровГТД.Колонки.Добавить("НомерГТД", "Номер ГТД");
	КолонкаНомерГТД.Данные = "НомерГТД";
	ЭлементыФормы.СписокНомеровГТД.ТекущаяКолонка = КолонкаНомерГТД;
	
	КолонкаСтранаПроисхождения = ЭлементыФормы.СписокНомеровГТД.Колонки.Добавить("СтранаПроисхождения", "Страна происхождения");
	КолонкаСтранаПроисхождения.Данные = "СтранаПроисхождения";
	
	КолонкаКоличество = ЭлементыФормы.СписокНомеровГТД.Колонки.Добавить("Количество", "Количество");
	КолонкаКоличество.Данные = "Количество";
	
	Пометка = ВосстановитьЗначение("СпрНомерГТДПодбор_ВидимостьОтбора");
	Пометка = ?(Пометка = Неопределено, Истина, Пометка);
	ЭлементыФормы.КоманднаяПанельСписок.Кнопки.Отбор.Пометка = Пометка;
	ЭлементыФормы.КоманднаяПанельСправочника.Кнопки.Отбор.Пометка = Пометка;
	
	РежимВыбора = Истина;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	НП = Новый НастройкаПериода;
	
	ПриИзмененииРежимаОтбора(Истина);
	
	СвернутьПанельОтбора();
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	СохранитьЗначение( "СпрНомерГТДПодбор_СохранятьРежимОтбора",	СохранятьРежимОтбора);
	СохранитьЗначение( "СпрНомерГТДПодбор_ФормироватьСписокПриОткрытии", 		ФормироватьСписокПриОткрытии);
	СохранитьЗначение( "СпрНомерГТДПодбор_РежимОтбора", 			?(СохранятьРежимОтбора, РежимОтбора, Перечисления.РежимыОтбора.ПоОстаткам));
	СохранитьЗначение( "СпрНомерГТДПодбор_ВидимостьОтбора", 		ЭлементыФормы.КоманднаяПанельСписок.Кнопки.Отбор.Пометка);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура ДействияФормыОбновитьСписок(Кнопка)

	ОбновлениеСписка();

КонецПроцедуры

// Процедура - обработчик нажатия кнопки "Отбор".
//
Процедура КоманднаяПанельФормыОтбор(Кнопка)

	ЭлементыФормы.КоманднаяПанельСписок.Кнопки.Отбор.Пометка = НЕ ЭлементыФормы.КоманднаяПанельСписок.Кнопки.Отбор.Пометка;
	ЭлементыФормы.КоманднаяПанельСправочника.Кнопки.Отбор.Пометка = НЕ ЭлементыФормы.КоманднаяПанельСправочника.Кнопки.Отбор.Пометка;
	СвернутьПанельОтбора();
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки "Выбор".
//
Процедура КоманднаяПанельФормыВыбор(Кнопка)

	ДействияПриВыборе(ЭлементыФормы.СписокНомеровГТД,, Ложь);
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки "Выбор".
//
Процедура КоманднаяПанельФормыСправочникаВыбор(Кнопка)

	ДействияПриВыборе(ЭлементыФормы.СправочникНомеровГТД,, Истина);
	
КонецПроцедуры

// Процедура - обработчик нажатия кнопки "Справка".
//
Процедура КоманднаяПанельФормыСправка(Кнопка)

	ОткрытьСправкуФормы();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

Процедура ОрганизацияПриИзменении(Элемент)

	ОбновлениеСписка();

КонецПроцедуры

Процедура НоменклатураПриИзменении(Элемент)

	ОбновлениеСписка();
	
КонецПроцедуры

Процедура РежимОтбораПриИзменении(Элемент)
	
	ПриИзмененииРежимаОтбора();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

Процедура СписокНомеровГТДВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	ДействияПриВыборе(Элемент, СтандартнаяОбработка, Ложь);

КонецПроцедуры

Процедура СправочникНомеровГТДВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	ДействияПриВыборе(Элемент, СтандартнаяОбработка, Истина);
	
КонецПроцедуры



