 
 
 ///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура настройки состава колонок табличного поля "СписокВыбора".
// В качестве параметров настройки используется структура,
// в которой ключ - имя колонки, кроме того ключ соответствует имени реквизита справочника,
// значение - структура с ключевыми полями "Заголовок", "Ширина" (последний необязательный).
Процедура НастроитьФорму(СтуктураНастроек) Экспорт
	
	ПараметрыКолонок = СтуктураНастроек;
 	Для Каждого Колонка ИЗ ПараметрыКолонок Цикл
		СписокВыбора.Колонки.Добавить(Колонка.Ключ);
		ПараметрыКолонки = Колонка.Значение;
		Если Не ПустаяСтрока(ПараметрыКолонки.Заголовок) Тогда
			КолонкаТП = ЭлементыФормы.СписокВыбора.Колонки.Добавить();
			КолонкаТП.Имя = Колонка.Ключ;
			КолонкаТП.Данные = Колонка.Ключ;
			КолонкаТП.ТекстШапки = ПараметрыКолонки.Заголовок;
			ЕСли ПараметрыКолонки.Ширина <> Неопределено Тогда
				КолонкаТП.Ширина = ПараметрыКолонки.Ширина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура обработки нажатия кнопки "ОК" - выполняет сохранение выбранных элементов
// классификатора в справочник.
Процедура ОсновныеДействияФормыОК(Кнопка)
	Менеджер = Справочники[ТипСправочника];
	Для Каждого СтрокаВыбора Из СписокВыбора Цикл
		Если СтрокаВыбора.Переносить Тогда
			НовыйЭлемент = Менеджер.СоздатьЭлемент();
			Для индекс = 1 по СписокВыбора.Колонки.Количество()-2 Цикл
				НовыйЭлемент[СписокВыбора.Колонки[индекс].Имя] = СтрокаВыбора[СписокВыбора.Колонки[индекс].Имя];				
			КонецЦикла;
			НовыйЭлемент.Записать();			
		КонецЕсли;
	КонецЦикла;
	Закрыть();
КонецПроцедуры

// Процедура-обработчик события "При выводе строки" табличного поля "СписокВыбора"
Процедура СписокВыбораПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если НЕ ДанныеСтроки.Видимость Тогда
		ОформлениеСтроки.ЦветТекста = WebЦвета.Серый;
	КонецЕсли;
КонецПроцедуры

// Процедура-обработчик события "Перед начало изменения" табличного поля "СписокВыбора"
Процедура СписокВыбораПередНачаломИзменения(Элемент, Отказ)
	Если Не Элемент.ТекущиеДанные.Видимость Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры



