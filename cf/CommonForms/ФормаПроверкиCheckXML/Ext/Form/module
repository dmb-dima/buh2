Перем ИмяФайлаРезультатаПроверки;
Перем ПапкаПрограммы;
Перем СоответствиеРасположенияПрограммы;
Перем СписокПроверенныхФайлов;
Перем ЕстьОшибки;

Процедура ПриОткрытии()
	Если ПапкаПрограммы = Неопределено Или ПапкаПрограммы = "" Тогда
	Иначе
		
		ЕстьОшибки = Ложь;
	    СписокПроверенныхФайлов.Очистить();
		ЗапуститьПроверкуВCheckXML(ДокументОбъект);
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПередачаСЗВ4ВПФР") Тогда
			Для каждого СтрокаТЧ Из ДокументОбъект.ПачкиДокументов Цикл
				Объект = СтрокаТЧ.ДокументПачка.ПолучитьОбъект();
				ЗапуститьПроверкуВCheckXML(Объект);
			КонецЦикла;	
		КонецЕсли;
		Если ЕстьОшибки Тогда
			Закрыть();
			Возврат;
		КонецЕсли;
		ОбновитьЭлементыФормы();

	КонецЕсли;	
КонецПроцедуры


// Проводится поиск в реестре записи о месте установки CheckXML,
// и запускается проверка файлов
Процедура ЗапуститьПроверкуВCheckXML(ДокументОбъект) 
	
	ТекстФайла = РегламентированнаяОтчетность.ПолучитьТекстФайла(ДокументОбъект,ЕстьОшибки);
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	Если МетаданныеДокумента.Реквизиты.Найти("Год") <> Неопределено Тогда
		Год = ДокументОбъект.Год
	ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("ОтчетныйПериод") <> Неопределено Тогда
		Если ТипЗнч(ДокументОбъект.ОтчетныйПериод) = Тип("Число") Тогда
			Год = ДокументОбъект.ОтчетныйПериод
		Иначе
			Год = Год(ДокументОбъект.ОтчетныйПериод)
		КонецЕсли;
	ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("ПериодРегистрации") <> Неопределено Тогда
		Год = Год(ДокументОбъект.ПериодРегистрации)
	Иначе	
		Год = Год(ДокументОбъект.Дата)
	КонецЕсли;
	ИмяФайлаПроверки = ПроцедурыПерсонифицированногоУчета.ПолучитьИмяФайлаПФ(ДокументОбъект, Год);
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстФайла); 
	
	ТекстовыйДокумент.Записать(ПапкаПрограммы + "\" + ИмяФайлаПроверки, КодировкаТекста.ANSI);
	КомандаСистемы("CheckXML.exe "+ИмяФайлаПроверки, ПапкаПрограммы);
	ИмяФайлаРезультатаПроверки = СтрЗаменить(ВРег(ИмяФайлаПроверки), ".XML", "-LOG.HTML");
	Если МетаданныеДокумента.Имя = "ПередачаСЗВ4ВПФР" Тогда
		КлючДокумента = "АДВ-11"
	Иначе
		КлючДокумента = "Пачка " + ДокументОбъект;
	КонецЕсли;
	СписокПроверенныхФайлов.Вставить(КлючДокумента, ПапкаПрограммы + "\Log\" + ИмяФайлаРезультатаПроверки);
	
	УдалитьФайлы(ПапкаПрограммы + "\" + ИмяФайлаПроверки);
	
КонецПроцедуры //ЗапуститьПроверкуВCheckXML()

Процедура ОбновитьЭлементыФормы()
	
	//ЭлементыФормы.ПанельРезультатовПроверки.Страницы.Очистить();
	
	Если СписокПроверенныхФайлов.Количество() <= 1 Тогда
		ЭлементыФормы.ПанельРезультатовПроверки.ОтображениеЗакладок = ОтображениеЗакладок.НеИспользовать;
		Для Каждого ЭлементСоответствия Из СписокПроверенныхФайлов Цикл
			ЭлементыФормы.ПолеHTMLДокумента.Перейти(ЭлементСоответствия.Значение);
		КонецЦикла;
	Иначе
		НомерЭлемента = 0;
		ЭлементыФормы.ПанельРезультатовПроверки.ОтображениеЗакладок = ОтображениеЗакладок.СверхуСПрокруткой;
		Для Каждого ЭлементСоответствия Из СписокПроверенныхФайлов Цикл
			НомерЭлемента = НомерЭлемента + 1;
			НазваниеСтраницы = ЭлементСоответствия.Ключ;
			ПоложениеОТ = Найти(НазваниеСтраницы, " от ");
			Если ПоложениеОТ > 0 Тогда
				НазваниеСтраницы = Лев(НазваниеСтраницы, ПоложениеОТ);
			КонецЕсли;
			СтраницаПанели = ЭлементыФормы.ПанельРезультатовПроверки.Страницы.Добавить("Страница1"+ НомерЭлемента, НазваниеСтраницы, ЭлементСоответствия.Значение);
			Если НомерЭлемента = 1 Тогда
				ЭлементыФормы.ПолеHTMLДокумента.Перейти(ЭлементСоответствия.Значение);
				ЭлементыФормы.ПанельРезультатовПроверки.ТекущаяСтраница = СтраницаПанели;
			КонецЕсли;
			ЭлементыФормы.ПанельРезультатовПроверки.Страницы.Страница1.Видимость = Ложь;
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры // ОбновитьЭлементыФормы()

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	УдалитьФайлы(ПапкаПрограммы + "\Log\" + ИмяФайлаРезультатаПроверки);
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	Вопрос = "Перед проверкой необходимо записать документ. Записать?";
	Если НЕ РегламентированнаяОтчетность.ТребованиеЗаписиДокументаУдовлетворено(ДокументОбъект,Вопрос) тогда
		Отказ = Истина;
	КонецЕсли;
	Если НЕ Отказ И ДокументОбъект.ФорматФайла <> Перечисления.ФорматФайлаПФР.Версия07 Тогда
		Предупреждение("Проверка предусмотрена только для формата файла версии 07.00 (XML)");
		Отказ = Истина;
	КонецЕсли;
	
	  
	ПапкаПрограммы = ВосстановитьЗначение("КаталогУстановкиПрограммыCheckXML");
	
	Если НЕ ЗначениеЗаполнено(ПапкаПрограммы) Тогда
		ПапкаПрограммы = ПроцедурыПерсонифицированногоУчета.ПолучитьПредполагаемыйПутьУстановкиCheckXML();
	КонецЕсли;
		
	ИсполняемыйФайл = Новый Файл(ПапкаПрограммы + "\CheckXML.exe" );
	Если ПапкаПрограммы = Неопределено Или ПапкаПрограммы = "" Или Не ИсполняемыйФайл.Существует() Тогда
		Если Вопрос("Не обнаружена программа проверки CheckXML. " + Символы.ПС + "Могли бы вы указать в какую папку она установлена?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да, "Ошибка запуска проверки программой CheckXML") = КодВозвратаДиалога.Да Тогда
			ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
			ВыборФайла.Заголовок = "Выберите папку, в которую установлена программа CheckXML";
			Если ВыборФайла.Выбрать() Тогда
				ПапкаПрограммы = ВыборФайла.Каталог;
			КонецЕсли
		КонецЕсли;
	
	КонецЕсли;
	
	Если ПапкаПрограммы = Неопределено Или ПапкаПрограммы = "" Тогда
		Предупреждение("Не обнаружена программа проверки CheckXML. ");
		Отказ = Истина;
	Иначе
		ИсполняемыйФайл = Новый Файл(ПапкаПрограммы + "\CheckXML.exe" );
		Если ИсполняемыйФайл.Существует() Тогда
			Если ИсполняемыйФайл.ПолучитьВремяИзменения() < Дата(2008, 12, 31) Тогда
				Предупреждение("программа CheckXML устарела, обновите пожалуйста программу до текущего актуального релиза");
				Отказ = Истина;
			КонецЕсли;
			
		Иначе
			Предупреждение("Не обнаружена программа проверки CheckXML. ");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	Если ЗначениеЗаполнено(ПапкаПрограммы) Тогда
		СохранитьЗначение("КаталогУстановкиПрограммыCheckXML", ПапкаПрограммы);
	КонецЕсли;
КонецПроцедуры

Процедура ПанельРезультатовПроверкиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ЭлементыФормы.ПолеHTMLДокумента.Перейти(Элемент.ТекущаяСтраница.Значение);
КонецПроцедуры


СписокПроверенныхФайлов = Новый Соответствие;