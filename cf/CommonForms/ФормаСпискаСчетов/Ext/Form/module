Перем ТаблицаСчетов;
Перем РежимРедактирования;

Процедура КнопкаОКНажатие(Элемент)
	
	Список.Очистить();
	Для Каждого СтрокаТаблицы Из ТаблицаСчетовРасчета Цикл
		Если Не СтрокаТаблицы.Использование Тогда
			Список.Добавить(СтрокаТаблицы.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

Процедура КнопкаОтменаНажатие(Элемент)
	
	Закрыть(КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

Процедура СписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	СтандартныеОтчеты.РаскраскаНедоступныхПриПолученииДанных(Элемент, ОформленияСтрок);
	
	Для Каждого Оформление Из ОформленияСтрок Цикл		
		Если Оформление.ДанныеСтроки.Ссылка.ЗапретитьИспользоватьВПроводках Тогда			
			Оформление.ЦветФона = WebЦвета.СветлоЖелтый;			
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

Процедура СписокПриИзмененииФлажка(Элемент, Колонка)
	
	ТекущаяСтрока = Элемент.ТекущаяСтрока;
	
	Счет = ТекущаяСтрока.Ссылка;
	Для Каждого СтрокаТаблицы Из ТаблицаСчетовРасчета Цикл
		Если СтрокаТаблицы.Ссылка.ПринадлежитЭлементу(Счет) Тогда
			СтрокаТаблицы.Использование = ТекущаяСтрока.Использование;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОткрытии()
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Хозрасчетный.Ссылка В (&СписокСчетов)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Использование,
	|	Хозрасчетный.Ссылка,
	|	Хозрасчетный.Наименование
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Хозрасчетный1
	|		ПО Хозрасчетный.Ссылка = Хозрасчетный1.Ссылка
	|ГДЕ
	|	Хозрасчетный.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|	И Хозрасчетный.ВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|	И (НЕ Хозрасчетный.Забалансовый)
	|	И (НЕ Хозрасчетный1.Забалансовый)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Хозрасчетный.Порядок";
	Запрос.УстановитьПараметр("СписокСчетов", Список);
	
	ТаблицаСчетовРасчета = Запрос.Выполнить().Выгрузить();
	
	Если Тип = 1 Тогда
		ЭлементыФормы.НадписьПояснение.Заголовок = "В таблице приведены счета учета расчетов, по которым формируется отчет.
													|При расчете анализируется остаток по дебету счетов по договорам с видом 
													|""С покупателем"" и ""С комиссионером"".";
	ИначеЕсли Тип = 2 Тогда	
		ЭлементыФормы.НадписьПояснение.Заголовок = "В таблице приведены счета учета расчетов, по которым формируется отчет.
													|При расчете анализируется остаток по кредиту счетов по договорам с видом 
													|""С поставщиком"" и ""С комитентом"".";
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельСпискаДействие(Кнопка)
	
	ТаблицаСчетовРасчета.ЗаполнитьЗначения(Истина, "Использование");
	
КонецПроцедуры

Процедура КоманднаяПанельСпискаДействие1(Кнопка)
	
	ТаблицаСчетовРасчета.ЗаполнитьЗначения(Ложь, "Использование");
	
КонецПроцедуры





