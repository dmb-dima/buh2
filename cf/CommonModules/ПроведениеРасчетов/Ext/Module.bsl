

// получить данные для расчета НДФЛ по ставке 13 процентов
// Параметры: 
//  ПоследнийМесяцНалоговогоПериода - месяц налогового периода, по который (включительно) считается налог. 
//  						Это текущий или будущий месяц относительно периода регистрации
//							Если Неопределено - нужно определить максимальный месяц налогового периода и 
//							минимальный месяц налоговго периода 
//  ПериодРегистрации - дата, по которую учитываются зарегистрированные доходы и начисленные налоги 
//  Организация - ссылка на организацию
//  Регистратор - ссылка на регистратор записей НДФЛ
//  СписокФизЛицТекст - текст запроса выборки списка физлиц, по которым необходимо выполнить расчет налога
//	ДополнительныеПараметрыЗапроса - структура значений параметров запроса, необходимых для выполнения текста запроса "СписокФизЛицТекст"
//
// Возвращаемое значение:
//  Нет.
//
Функция ПолучитьДанныеНДФЛПоРегистратору(Знач ПервыйМесяцНалоговогоПериода,
											Знач ПоследнийМесяцНалоговогоПериода,
											ПериодРегистрации,
											ОбособленноеПодразделение,
											Организация,
											Регистратор,
											Знач СписокФизЛицТекст,
											ДополнительныеПараметрыЗапроса = Неопределено,
											ЭтоПерерасчет = Ложь,
											КомментироватьРасчет = Ложь,
											ВозвращатьДанныеРасчетовБезПодразделений = Ложь) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", Организация);
	Запрос.УстановитьПараметр("НачалоМесяцаРасчета",НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("КонецМесяцаРасчета",КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("НачалоГодаРасчета",НачалоГода(ПериодРегистрации));
	
	// установим дополнительные параметры
	Если ДополнительныеПараметрыЗапроса <> Неопределено Тогда
		Для каждого Поле Из ДополнительныеПараметрыЗапроса Цикл
			Запрос.УстановитьПараметр(Поле.Ключ, Поле.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ Физлицо
	|ПОМЕСТИТЬ ВТСписокФизЛиц
	|ИЗ (" + СписокФизЛицТекст + ") СписокФизЛиц
	|ИНДЕКСИРОВАТЬ ПО Физлицо";
	Запрос.УстановитьПараметр("парамРегистратор", Регистратор);
	Запрос.Выполнить();

	Если ПоследнийМесяцНалоговогоПериода = Неопределено Тогда

		// получим ПоследнийМесяцНалоговогоПериода
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НДФЛСведенияОДоходах.Период КАК Период
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛиц КАК Работники
		|		ПО (Работники.Физлицо = НДФЛСведенияОДоходах.ФизЛицо)
		|ГДЕ
		|	НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
		|	И НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.ПериодРегистрации, МЕСЯЦ) = &НачалоМесяцаРасчета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
		Результат = Запрос.Выполнить();

		Если Результат.Пустой() Тогда
			ПоследнийМесяцНалоговогоПериода = КонецМесяца(ПериодРегистрации);

		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ПоследнийМесяцНалоговогоПериода = КонецМесяца(Выборка.Период);

		КонецЕсли;

	КонецЕсли;

	Если ПервыйМесяцНалоговогоПериода = Неопределено Тогда
		// получим самый ранний месяц налогового периода
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НДФЛСведенияОДоходах.Период КАК Период
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСписокФизЛиц КАК Работники
		|		ПО (Работники.Физлицо = НДФЛСведенияОДоходах.ФизЛицо)
		|ГДЕ
		|	НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
		|	И НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.ПериодРегистрации, МЕСЯЦ) = &НачалоМесяцаРасчета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период";
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			ПервыйМесяцНалоговогоПериода = ПериодРегистрации;
			
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ПервыйМесяцНалоговогоПериода = НачалоМесяца(Выборка.Период);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДатаЗакона229ФЗ = ПроведениеРасчетов.ДатаЗакона229ФЗ();
	Запрос.УстановитьПараметр("ДатаЗакона229ФЗ", ДатаЗакона229ФЗ);
	Запрос.УстановитьПараметр("КонецПериодаРасчета", КонецМесяца(ПоследнийМесяцНалоговогоПериода));
	Запрос.УстановитьПараметр("НачалоГодаПериодаРасчета", НачалоГода(ПервыйМесяцНалоговогоПериода));
	Запрос.УстановитьПараметр("КонецГодаПериодаРасчета", КонецГода(ПоследнийМесяцНалоговогоПериода));
	
	// Таблица ВТДатыПоМесяцам: список дат налоговых периодов, в которых рассчитывается налог 
	// Поля:
	//		Период
	
	НачМесяца = НачалоГода(ПервыйМесяцНалоговогоПериода);
	ДатыПоМесяцамТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	ДатыПоМесяцамТекст = ДатыПоМесяцамТекст + "
	|ПОМЕСТИТЬ ВТДатыПоМесяцам";
	Пока НачМесяца < НачалоМесяца(ПоследнийМесяцНалоговогоПериода) Цикл
		НачМесяца = НачалоМесяца(КонецМесяца(НачМесяца) + 1);
		ДатыПоМесяцамТекст = ДатыПоМесяцамТекст +"
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	Запрос.Текст = ДатыПоМесяцамТекст;
	Запрос.Выполнить();
	
	// первый год
	НачалоГода = НачалоГода(ПоследнийМесяцНалоговогоПериода);	
	ПериодыТекстПоГодам = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачалоГода,"ДФ=гггг,М,д,Ч,м,с") + ") КАК НачалоНалоговогоПериода";
	ПериодыТекстПоГодам = ПериодыТекстПоГодам + "
	|ПОМЕСТИТЬ ВТПериодыПоГодам";
	Для Сч = Год(ПервыйМесяцНалоговогоПериода)+ 1 По Год(ПоследнийМесяцНалоговогоПериода) Цикл
		НачалоГода = НачалоГода(НачалоГода - 1);
		ПериодыТекстПоГодам = ПериодыТекстПоГодам +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачалоГода,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
 	Запрос.Текст = ПериодыТекстПоГодам;
	Запрос.Выполнить();
	
	// Периоды
	// Таблица ВТПериоды: список периодов-физлиц по которым необходимо выполнить расчет налога
	// Поля:
	//		Период
	//		Физлицо
	
	Если ЭтоПерерасчет Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Работники.Физлицо КАК Физлицо,
		|	Месяцы.Период КАК Период,
		|	НАЧАЛОПЕРИОДА(Месяцы.Период, МЕСЯЦ) КАК МесяцНалоговогоПериода,
		|	НАЧАЛОПЕРИОДА(Месяцы.Период, ГОД) КАК НачалоГодаНалоговогоПериода,
		|	КОНЕЦПЕРИОДА(Месяцы.Период, ГОД) КАК КонецГодаНалоговогоПериода
		|ПОМЕСТИТЬ ВТПериодыНалогаНаДоходы
		|ИЗ
		|	ВТСписокФизЛиц КАК Работники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПоМесяцам КАК Месяцы
		|		ПО (ИСТИНА)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Физлицо,
		|	Период";
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПериодыДохода.ФизЛицо КАК ФизЛицо,
		|	Месяцы.Период КАК Период,
		|	НАЧАЛОПЕРИОДА(Месяцы.Период, МЕСЯЦ) КАК МесяцНалоговогоПериода,
		|	НАЧАЛОПЕРИОДА(Месяцы.Период, ГОД) КАК НачалоГодаНалоговогоПериода,
		|	КОНЕЦПЕРИОДА(Месяцы.Период, ГОД) КАК КонецГодаНалоговогоПериода
		|ПОМЕСТИТЬ ВТПериодыНалогаНаДоходы
		|ИЗ
		|	(ВЫБРАТЬ
		|		НДФЛСведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|		МАКСИМУМ(НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ)) КАК ПериодМакс,
		|		МИНИМУМ(ВЫБОР
		|				КОГДА НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.ПериодРегистрации, МЕСЯЦ) = &НачалоМесяцаРасчета
		|					ТОГДА НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, ГОД)
		|				ИНАЧЕ &НачалоГодаРасчета
		|			КОНЕЦ) КАК ПериодМин
		|	ИЗ
		|		РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|	ГДЕ
		|		НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.ПериодРегистрации, МЕСЯЦ) МЕЖДУ &НачалоГодаРасчета И &НачалоМесяцаРасчета
		|		И НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
		|		И НДФЛСведенияОДоходах.ФизЛицо В
		|				(ВЫБРАТЬ
		|					Работники.Физлицо
		|				ИЗ
		|					ВТСписокФизЛиц КАК Работники)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НДФЛСведенияОДоходах.ФизЛицо) КАК ПериодыДохода
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПоМесяцам КАК Месяцы
		|		ПО (Месяцы.Период МЕЖДУ ПериодыДохода.ПериодМин И ПериодыДохода.ПериодМакс)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	Период";
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13) КАК СтавкаНалогообложения,
	|	0.13 КАК Размер
	|ПОМЕСТИТЬ ВТСтавкиНалогаВПроцентах
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09),
	|	0.09
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35),
	|	0.35
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30),
	|	0.3
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15),
	|	0.15
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыРегистра.НачалоНалоговогоПериода КАК НачалоНалоговогоПериода,
	|	УчетнаяПолитикаПоНДФЛ.ОсобенностиИсчисленияНДФЛ
	|ПОМЕСТИТЬ ВТПолитикаПримененияВычетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.НачалоНалоговогоПериода КАК НачалоНалоговогоПериода,
	|		МАКСИМУМ(УчетнаяПолитикаПоНДФЛ.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериодыПоГодам КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаПоНДФЛ
	|			ПО Периоды.НачалоНалоговогоПериода >= УчетнаяПолитикаПоНДФЛ.Период
	|	ГДЕ
	|		УчетнаяПолитикаПоНДФЛ.Организация = &ГоловнаяОрганизация
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.НачалоНалоговогоПериода) КАК ДатыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаПоНДФЛ
	|		ПО ДатыРегистра.ПериодРегистра = УчетнаяПолитикаПоНДФЛ.Период
	|ГДЕ
	|	УчетнаяПолитикаПоНДФЛ.Организация = &ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыРегистра.ФизЛицо КАК ФизЛицо,
	|	ПериодыРегистра.МесяцНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусНалогоплательщика.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) <> ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РезидентРФ
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаКонецГода
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Физлица.ФизЛицо КАК ФизЛицо,
	|		Физлица.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|		МАКСИМУМ(СтатусНалогоплательщика.Период) КАК Период
	|	ИЗ
	|		ВТПериодыНалогаНаДоходы КАК Физлица
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|			ПО Физлица.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|				И (СтатусНалогоплательщика.Период <= ВЫБОР
	|					КОГДА Физлица.КонецГодаНалоговогоПериода > &КонецМесяцаРасчета
	|						ТОГДА &КонецМесяцаРасчета
	|					ИНАЧЕ Физлица.КонецГодаНалоговогоПериода
	|				КОНЕЦ)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Физлица.ФизЛицо,
	|		Физлица.МесяцНалоговогоПериода) КАК ПериодыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|		ПО ПериодыРегистра.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|			И ПериодыРегистра.Период = СтатусНалогоплательщика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыРегистра.ФизЛицо КАК ФизЛицо,
	|	ПериодыРегистра.МесяцНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусНалогоплательщика.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) <> ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Нерезидент)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РезидентРФ
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаКонецМесяца
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Физлица.ФизЛицо КАК ФизЛицо,
	|		Физлица.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|		МАКСИМУМ(СтатусНалогоплательщика.Период) КАК Период
	|	ИЗ
	|		ВТПериодыНалогаНаДоходы КАК Физлица
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|			ПО Физлица.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|				И (СтатусНалогоплательщика.Период <= ВЫБОР
	|					КОГДА Физлица.МесяцНалоговогоПериода > &КонецМесяцаРасчета
	|						ТОГДА &КонецМесяцаРасчета
	|					ИНАЧЕ Физлица.МесяцНалоговогоПериода
	|				КОНЕЦ)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Физлица.ФизЛицо,
	|		Физлица.МесяцНалоговогоПериода) КАК ПериодыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|		ПО ПериодыРегистра.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|			И ПериодыРегистра.Период = СтатусНалогоплательщика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.ФизЛицо,
	|	Периоды.ФизЛицо.Наименование КАК Порядок,
	|	Периоды.МесяцНалоговогоПериода,
	|	Периоды.НачалоГодаНалоговогоПериода,
	|	Периоды.КонецГодаНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА Периоды.НачалоГодаНалоговогоПериода < &ДатаЗакона229ФЗ
	|			ТОГДА РезидентствоФизлицНаКонецГода.РезидентРФ
	|		ИНАЧЕ РезидентствоФизлицНаКонецМесяца.РезидентРФ
	|				И РезидентствоФизлицНаКонецГода.РезидентРФ
	|	КОНЕЦ КАК Резидент
	|ПОМЕСТИТЬ ВТПериодыФизлиц
	|ИЗ
	|	ВТПериодыНалогаНаДоходы КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецМесяца КАК РезидентствоФизлицНаКонецМесяца
	|		ПО Периоды.ФизЛицо = РезидентствоФизлицНаКонецМесяца.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = РезидентствоФизлицНаКонецМесяца.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецГода КАК РезидентствоФизлицНаКонецГода
	|		ПО Периоды.ФизЛицо = РезидентствоФизлицНаКонецГода.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = РезидентствоФизлицНаКонецГода.МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыРегистра.НачалоНалоговогоПериода КАК НачалоНалоговогоПериода,
	|	ДатыРегистра.КодВычета КАК КодВычета,
	|	ЕСТЬNULL(РазмерВычетов.Размер, 0) КАК Размер,
	|	ЕСТЬNULL(РазмерВычетов.ОграничениеПоДоходам, 0) КАК ОграничениеПоДоходам
	|ПОМЕСТИТЬ ВТРазмерыСтандартныхВычетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.НачалоНалоговогоПериода КАК НачалоНалоговогоПериода,
	|		ВидыВычетов.Ссылка КАК КодВычета,
	|		МАКСИМУМ(РазмерВычетов.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериодыПоГодам КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВычетыНДФЛ КАК ВидыВычетов
	|			ПО (ИСТИНА)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛРазмерВычетов КАК РазмерВычетов
	|			ПО (ВидыВычетов.Ссылка = РазмерВычетов.КодВычета)
	|				И Периоды.НачалоНалоговогоПериода >= РазмерВычетов.Период
	|	ГДЕ
	|		ВидыВычетов.ГруппаВычета В (ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Стандартные), ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.СтандартныеНаДетей))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.НачалоНалоговогоПериода,
	|		ВидыВычетов.Ссылка) КАК ДатыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛРазмерВычетов КАК РазмерВычетов
	|		ПО ДатыРегистра.ПериодРегистра = РазмерВычетов.Период
	|			И ДатыРегистра.КодВычета = РазмерВычетов.КодВычета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыРегистра.ФизЛицо КАК ФизЛицо,
	|	ДатыРегистра.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА ПрименениеВычетов.Организация = &ГоловнаяОрганизация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПрименятьВычеты
	|ПОМЕСТИТЬ ВТПрименениеСтандартныхВычетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ПрименениеВычетов.Период) КАК ПериодСреза,
	|		Периоды.ФизЛицо КАК ФизЛицо,
	|		Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода
	|	ИЗ
	|		ВТПериодыФизлиц КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛПрименениеВычетов КАК ПрименениеВычетов
	|			ПО Периоды.МесяцНалоговогоПериода >= ПрименениеВычетов.Период
	|				И Периоды.ФизЛицо = ПрименениеВычетов.Физлицо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.ФизЛицо,
	|		Периоды.МесяцНалоговогоПериода) КАК ДатыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛПрименениеВычетов КАК ПрименениеВычетов
	|		ПО ДатыРегистра.ПериодСреза = ПрименениеВычетов.Период
	|			И ДатыРегистра.ФизЛицо = ПрименениеВычетов.Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыРегистра.ФизЛицо КАК ФизЛицо,
	|	ДатыРегистра.МесяцНалоговогоПериода,
	|	ДатыРегистра.НачалоГодаНалоговогоПериода,
	|	ЕСТЬNULL(ДатыРегистра.КодВычета, ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)) КАК КодВычета,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ВычетыНаДетей.ПериодЗавершения <= ДатыРегистра.МесяцНалоговогоПериода
	|					И ВычетыНаДетей.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА ВычетыНаДетей.КоличествоДетейЗавершения
	|			ИНАЧЕ ВычетыНаДетей.КоличествоДетей
	|		КОНЕЦ, 0) КАК КоличествоДетей
	|ПОМЕСТИТЬ ВТПраваНаВычетыНаДетей
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|		Периоды.ФизЛицо КАК ФизЛицо,
	|		ВычетыНаДетей.КодВычета КАК КодВычета,
	|		МАКСИМУМ(ВычетыНаДетей.Период) КАК ПериодРегистра,
	|		Периоды.НачалоГодаНалоговогоПериода КАК НачалоГодаНалоговогоПериода
	|	ИЗ
	|		ВТПериодыФизлиц КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыНаДетей КАК ВычетыНаДетей
	|			ПО Периоды.МесяцНалоговогоПериода >= ВычетыНаДетей.Период
	|				И Периоды.ФизЛицо = ВычетыНаДетей.Физлицо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.ФизЛицо,
	|		ВычетыНаДетей.КодВычета,
	|		Периоды.МесяцНалоговогоПериода,
	|		Периоды.НачалоГодаНалоговогоПериода) КАК ДатыРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыНаДетей КАК ВычетыНаДетей
	|		ПО ДатыРегистра.ПериодРегистра = ВычетыНаДетей.Период
	|			И ДатыРегистра.ФизЛицо = ВычетыНаДетей.Физлицо
	|			И ДатыРегистра.КодВычета = ВычетыНаДетей.КодВычета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыРегистра.ФизЛицо,
	|	ДатыРегистра.МесяцНалоговогоПериода,
	|	ДатыРегистра.НачалоГодаНалоговогоПериода,
	|	ЕСТЬNULL(ВычетыФизлиц.КодВычетаЛичный, ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)) КАК КодВычета
	|ПОМЕСТИТЬ ВТПраваНаЛичныеВычеты
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ВычетыФизлиц.Период) КАК ПериодСреза,
	|		Периоды.ФизЛицо КАК ФизЛицо,
	|		Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|		Периоды.НачалоГодаНалоговогоПериода КАК НачалоГодаНалоговогоПериода
	|	ИЗ
	|		ВТПериодыФизлиц КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК ВычетыФизлиц
	|			ПО Периоды.МесяцНалоговогоПериода >= ВычетыФизлиц.Период
	|				И Периоды.ФизЛицо = ВычетыФизлиц.Физлицо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.ФизЛицо,
	|		Периоды.МесяцНалоговогоПериода,
	|		Периоды.НачалоГодаНалоговогоПериода) КАК ДатыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК ВычетыФизлиц
	|		ПО ДатыРегистра.ПериодСреза = ВычетыФизлиц.Период
	|			И ДатыРегистра.ФизЛицо = ВычетыФизлиц.Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вычеты.ФизЛицо,
	|	ВЫБОР
	|		КОГДА Вычеты.МесяцНалоговогоПериода < &ДатаЗакона229ФЗ
	|			ТОГДА НАЧАЛОПЕРИОДА(Вычеты.МесяцНалоговогоПериода, ГОД)
	|		ИНАЧЕ Вычеты.МесяцНалоговогоПериода
	|	КОНЕЦ КАК МесяцНалоговогоПериода,
	|	Вычеты.КодВычета,
	|	Вычеты.НалоговыйПериод,
	|	СУММА(Вычеты.Размер) КАК Размер
	|ПОМЕСТИТЬ ВТПравоНаИмущественныеВычеты
	|ИЗ
	|	(ВЫБРАТЬ
	|		Обороты.ФизЛицо КАК ФизЛицо,
	|		НАЧАЛОПЕРИОДА(Обороты.Период, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|		Обороты.КодВычетаИмущественный КАК КодВычета,
	|		Обороты.Год КАК НалоговыйПериод,
	|		Обороты.РазмерПриход КАК Размер
	|	ИЗ
	|		РегистрНакопления.НДФЛИмущественныеВычетыФизлиц.Обороты(
	|				&НачалоГодаПериодаРасчета,
	|				&КонецПериодаРасчета,
	|				Месяц,
	|				ФизЛицо В
	|						(ВЫБРАТЬ
	|							ФизЛица.ФизЛицо
	|						ИЗ
	|							ВТСписокФизЛиц КАК ФизЛица)
	|					И Организация = &ГоловнаяОрганизация) КАК Обороты
	|	ГДЕ
	|		Обороты.Год = ГОД(Обороты.Период)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияРегистратора.ФизЛицо,
	|		НАЧАЛОПЕРИОДА(ДвиженияРегистратора.Период, МЕСЯЦ),
	|		ДвиженияРегистратора.КодВычетаИмущественный,
	|		ДвиженияРегистратора.Год,
	|		ВЫБОР
	|			КОГДА ДвиженияРегистратора.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияРегистратора.Размер
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК ДвиженияРегистратора
	|	ГДЕ
	|		ДвиженияРегистратора.Регистратор = &Регистратор
	|		И ДвиженияРегистратора.ФизЛицо В
	|				(ВЫБРАТЬ
	|					ФизЛица.ФизЛицо
	|				ИЗ
	|					ВТСписокФизЛиц КАК ФизЛица)
	|		И ДвиженияРегистратора.Год = ГОД(ДвиженияРегистратора.Период)) КАК Вычеты
	|
	|СГРУППИРОВАТЬ ПО
	|	Вычеты.ФизЛицо,
	|	ВЫБОР
	|		КОГДА Вычеты.МесяцНалоговогоПериода < &ДатаЗакона229ФЗ
	|			ТОГДА НАЧАЛОПЕРИОДА(Вычеты.МесяцНалоговогоПериода, ГОД)
	|		ИНАЧЕ Вычеты.МесяцНалоговогоПериода
	|	КОНЕЦ,
	|	Вычеты.КодВычета,
	|	Вычеты.НалоговыйПериод
	|
	|ИМЕЮЩИЕ
	|	СУММА(Вычеты.Размер) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обороты.ФизЛицо,
	|	НАЧАЛОПЕРИОДА(Обороты.Период, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	Обороты.КодВычета,
	|	Обороты.СуммаДохода КАК СуммаДоходаОборот,
	|	Обороты.СуммаВычета КАК СуммаВычетаОборот
	|ПОМЕСТИТЬ ВТДоходыФизлиц
	|ИЗ
	|	РегистрНакопления.НДФЛСведенияОДоходах КАК Обороты
	|ГДЕ
	|	Обороты.Организация = &ГоловнаяОрганизация
	|	И Обороты.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|	И (НЕ Обороты.КодДохода.НеОблагаетсяУНалоговогоАгента)
	|	И Обороты.Период МЕЖДУ &НачалоГодаПериодаРасчета И &КонецПериодаРасчета
	|	И Обороты.ПериодРегистрации <= &КонецМесяцаРасчета
	|	И Обороты.ФизЛицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокФизЛиц КАК СписокСотрудников)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизЛицо,
	|	НАЧАЛОПЕРИОДА(ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ПодразделениеОрганизации КАК Подразделение,
	|	СУММА(ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ПримененныйВычет) КАК СуммаОборот
	|ПОМЕСТИТЬ ВТПредоставленныеСтандартныеИСоциальныеВычеты
	|ИЗ
	|	РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ
	|ГДЕ
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Организация = &ГоловнаяОрганизация
	|	И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизЛицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокФизЛиц КАК СписокСотрудников)
	|	И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Регистратор <> &Регистратор
	|	И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНалоговогоПериода МЕЖДУ &НачалоГодаПериодаРасчета И &КонецПериодаРасчета
	|	И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Период <= &КонецМесяцаРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизЛицо,
	|	НАЧАЛОПЕРИОДА(ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНалоговогоПериода, МЕСЯЦ),
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ПодразделениеОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияРегистратора.ФизЛицо КАК ФизЛицо,
	|	НАЧАЛОПЕРИОДА(ДвиженияРегистратора.Период, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	ДвиженияРегистратора.КодВычетаИмущественный КАК КодВычета,
	|	СУММА(ДвиженияРегистратора.Размер) КАК Сумма,
	|	ДвиженияРегистратора.ПодразделениеОрганизации КАК Подразделение
	|ПОМЕСТИТЬ ВТПредоставленоИмущественныхВычетов
	|ИЗ
	|	РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК ДвиженияРегистратора
	|ГДЕ
	|	ДвиженияРегистратора.Организация = &ГоловнаяОрганизация
	|	И ДвиженияРегистратора.Период МЕЖДУ &НачалоГодаПериодаРасчета И &КонецПериодаРасчета
	|	И ДвиженияРегистратора.Регистратор <> &Регистратор
	|	И ДвиженияРегистратора.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДвиженияРегистратора.ФизЛицо В
	|			(ВЫБРАТЬ
	|				ФизЛица.ФизЛицо
	|			ИЗ
	|				ВТСписокФизЛиц КАК ФизЛица)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияРегистратора.ПодразделениеОрганизации,
	|	ДвиженияРегистратора.ФизЛицо,
	|	НАЧАЛОПЕРИОДА(ДвиженияРегистратора.Период, МЕСЯЦ),
	|	ДвиженияРегистратора.КодВычетаИмущественный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Периоды.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА Периоды.Резидент
	|			ТОГДА ЕСТЬNULL(ПредоставленныеВычетыФизЛиц.СуммаОборот, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОборот
	|ПОМЕСТИТЬ ВТСоциальныеВычетыПомесячно
	|ИЗ
	|	ВТПериодыФизлиц КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредоставленныеСтандартныеИСоциальныеВычеты КАК ПредоставленныеВычетыФизЛиц
	|		ПО Периоды.ФизЛицо = ПредоставленныеВычетыФизЛиц.ФизЛицо
	|			И (ПредоставленныеВычетыФизЛиц.КодВычета.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Социальные))
	|			И Периоды.МесяцНалоговогоПериода = ПредоставленныеВычетыФизЛиц.МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.ФизЛицо КАК ФизЛицо,
	|	Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Периоды.Резидент,
	|	ВЫБОР
	|		КОГДА Периоды.Резидент
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|	КОНЕЦ КАК СтавкаНалогообложенияНеРезидента,
	|	ЕСТЬNULL(Обороты.СуммаДоходаОборот, 0) - ВЫБОР
	|		КОГДА Периоды.Резидент
	|			ТОГДА ЕСТЬNULL(Обороты.СуммаВычетаОборот, 0)
	|		КОГДА Обороты.КодВычета.НеПредоставляетсяНерезидентам
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(Обороты.СуммаВычетаОборот, 0)
	|	КОНЕЦ КАК ДоходыБезВычетов
	|ПОМЕСТИТЬ ВТДоходыСУчетомВычетовКДоходам
	|ИЗ
	|	ВТПериодыФизлиц КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыФизлиц КАК Обороты
	|		ПО Периоды.ФизЛицо = Обороты.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = Обороты.МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Периоды.ФизЛицо КАК ФизЛицо,
	|	ЕСТЬNULL(СУММА(Доходы.ДоходыБезВычетов), 0) КАК ОблагаемыйДоход
	|ПОМЕСТИТЬ ВТДоходыРезидентовПоМесяцам
	|ИЗ
	|	ВТПериодыФизлиц КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыСУчетомВычетовКДоходам КАК Доходы
	|		ПО Периоды.МесяцНалоговогоПериода = Доходы.МесяцНалоговогоПериода
	|			И Периоды.ФизЛицо = Доходы.ФизЛицо
	|			И (Доходы.Резидент)
	|
	|СГРУППИРОВАТЬ ПО
	|	Периоды.МесяцНалоговогоПериода,
	|	Периоды.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Периоды.ФизЛицо КАК ФизЛицо,
	|	ЕСТЬNULL(СУММА(Доходы.ДоходыБезВычетов), 0) КАК ОблагаемыйДоходЗаГод
	|ПОМЕСТИТЬ ВТДоходыРезидентовНарастающимЗаГод
	|ИЗ
	|	ВТПериодыФизлиц КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыСУчетомВычетовКДоходам КАК Доходы
	|		ПО (Доходы.МесяцНалоговогоПериода МЕЖДУ Периоды.НачалоГодаНалоговогоПериода И Периоды.МесяцНалоговогоПериода)
	|			И Периоды.ФизЛицо = Доходы.ФизЛицо
	|			И (Доходы.Резидент)
	|
	|СГРУППИРОВАТЬ ПО
	|	Периоды.МесяцНалоговогоПериода,
	|	Периоды.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.ФизЛицо КАК ФизЛицо,
	|	Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ВычетыЛичные.КодВычета КАК КодВычетаЛичный,
	|	ВЫБОР
	|		КОГДА ПрименениеВычетов.ПрименятьВычеты
	|				И Периоды.Резидент
	|				И (ДоходыНарастающимИтогом.ОблагаемыйДоходЗаГод + ЕСТЬNULL(ДоходыСПредыдущегоМестаРаботы.Размер, 0) <= ЕСТЬNULL(РазмерВычетовЛичный.ОграничениеПоДоходам, 0)
	|					ИЛИ ЕСТЬNULL(РазмерВычетовЛичный.ОграничениеПоДоходам, 0) = 0)
	|			ТОГДА ЕСТЬNULL(РазмерВычетовЛичный.Размер, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтандартныеВычетыЛичные,
	|	ВЫБОР
	|		КОГДА ПрименениеВычетов.ПрименятьВычеты
	|				И Периоды.Резидент
	|				И (ДоходыНарастающимИтогом.ОблагаемыйДоходЗаГод + ЕСТЬNULL(ДоходыСПредыдущегоМестаРаботы.Размер, 0) <= РазмерВычетовНаДетей.ОграничениеПоДоходам
	|					ИЛИ РазмерВычетовНаДетей.ОграничениеПоДоходам = 0)
	|			ТОГДА ЕСТЬNULL(РазмерВычетовНаДетей.Размер, 0) * ЕСТЬNULL(ВычетыНаДетей.КоличествоДетей, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтандартныеВычетыДетские,
	|	ВЫБОР
	|		КОГДА ПрименениеВычетов.ПрименятьВычеты
	|				И Периоды.Резидент
	|				И (ДоходыНарастающимИтогом.ОблагаемыйДоходЗаГод + ЕСТЬNULL(ДоходыСПредыдущегоМестаРаботы.Размер, 0) <= РазмерВычетовНаДетей.ОграничениеПоДоходам
	|					ИЛИ РазмерВычетовНаДетей.ОграничениеПоДоходам = 0)
	|			ТОГДА ЕСТЬNULL(РазмерВычетовНаДетейДвойные.Размер, 0) * ЕСТЬNULL(ВычетыНаДетейДвойные.КоличествоДетей, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтандартныеВычетыДетскиеДвойные,
	|	ВЫБОР
	|		КОГДА ПрименениеВычетов.ПрименятьВычеты
	|				И Периоды.Резидент
	|				И (ДоходыНарастающимИтогом.ОблагаемыйДоходЗаГод + ЕСТЬNULL(ДоходыСПредыдущегоМестаРаботы.Размер, 0) <= РазмерВычетовНаДетей.ОграничениеПоДоходам
	|					ИЛИ РазмерВычетовНаДетей.ОграничениеПоДоходам = 0)
	|			ТОГДА ЕСТЬNULL(РазмерВычетовНаДетейДвойныеВторые.Размер, 0) * ЕСТЬNULL(ВычетыНаДетейДвойныеВторые.КоличествоДетей, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтандартныеВычетыДетскиеДвойныеВторые,
	|	ВЫБОР
	|		КОГДА ПрименениеВычетов.ПрименятьВычеты
	|				И Периоды.Резидент
	|				И (ДоходыНарастающимИтогом.ОблагаемыйДоходЗаГод + ЕСТЬNULL(ДоходыСПредыдущегоМестаРаботы.Размер, 0) <= РазмерВычетовНаДетей.ОграничениеПоДоходам
	|					ИЛИ РазмерВычетовНаДетей.ОграничениеПоДоходам = 0)
	|			ТОГДА ЕСТЬNULL(РазмерВычетовНаДетейИнвалидов.Размер, 0) * ЕСТЬNULL(ВычетыНаДетейИнвалидов.КоличествоДетей, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтандартныеВычетыДетскиеИнвалидов,
	|	ВЫБОР
	|		КОГДА ПрименениеВычетов.ПрименятьВычеты
	|				И Периоды.Резидент
	|				И (ДоходыНарастающимИтогом.ОблагаемыйДоходЗаГод + ЕСТЬNULL(ДоходыСПредыдущегоМестаРаботы.Размер, 0) <= РазмерВычетовНаДетей.ОграничениеПоДоходам
	|					ИЛИ РазмерВычетовНаДетей.ОграничениеПоДоходам = 0)
	|			ТОГДА ЕСТЬNULL(РазмерВычетовНаДетейИнвалидовДвойные.Размер, 0) * ЕСТЬNULL(ВычетыНаДетейИнвалидовДвойные.КоличествоДетей, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтандартныеВычетыДетскиеИнвалидовДвойные,
	|	ВЫБОР
	|		КОГДА ПрименениеВычетов.ПрименятьВычеты
	|				И Периоды.Резидент
	|				И (ДоходыНарастающимИтогом.ОблагаемыйДоходЗаГод + ЕСТЬNULL(ДоходыСПредыдущегоМестаРаботы.Размер, 0) <= РазмерВычетовНаДетей.ОграничениеПоДоходам
	|					ИЛИ РазмерВычетовНаДетей.ОграничениеПоДоходам = 0)
	|			ТОГДА ЕСТЬNULL(РазмерВычетовНаДетейИнвалидовДвойныеВторые.Размер, 0) * ЕСТЬNULL(ВычетыНаДетейИнвалидовДвойныеВторые.КоличествоДетей, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтандартныеВычетыДетскиеИнвалидовДвойныеВторые,
	|	РазмерВычетовЛичный.ОграничениеПоДоходам КАК ОграничениеПоДоходамЛичные,
	|	РазмерВычетовНаДетей.ОграничениеПоДоходам КАК ОграничениеПоДоходамНаДетей
	|ПОМЕСТИТЬ ВТПраваНаСтандартныеВычетыПоМесяцам
	|ИЗ
	|	ВТПериодыФизлиц КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПрименениеСтандартныхВычетов КАК ПрименениеВычетов
	|		ПО Периоды.ФизЛицо = ПрименениеВычетов.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ПрименениеВычетов.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыСтандартныхВычетов КАК РазмерВычетовНаДетей
	|		ПО (РазмерВычетовНаДетей.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101))
	|			И Периоды.НачалоГодаНалоговогоПериода = РазмерВычетовНаДетей.НачалоНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаЛичныеВычеты КАК ВычетыЛичные
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыСтандартныхВычетов КАК РазмерВычетовЛичный
	|			ПО ВычетыЛичные.КодВычета = РазмерВычетовЛичный.КодВычета
	|				И ВычетыЛичные.НачалоГодаНалоговогоПериода = РазмерВычетовЛичный.НачалоНалоговогоПериода
	|		ПО Периоды.МесяцНалоговогоПериода = ВычетыЛичные.МесяцНалоговогоПериода
	|			И Периоды.ФизЛицо = ВычетыЛичные.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаВычетыНаДетей КАК ВычетыНаДетей
	|		ПО (ВычетыНаДетей.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101))
	|			И Периоды.ФизЛицо = ВычетыНаДетей.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ВычетыНаДетей.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаВычетыНаДетей КАК ВычетыНаДетейДвойные
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыСтандартныхВычетов КАК РазмерВычетовНаДетейДвойные
	|			ПО ВычетыНаДетейДвойные.КодВычета = РазмерВычетовНаДетейДвойные.КодВычета
	|				И ВычетыНаДетейДвойные.НачалоГодаНалоговогоПериода = РазмерВычетовНаДетейДвойные.НачалоНалоговогоПериода
	|		ПО (ВычетыНаДетейДвойные.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102))
	|			И Периоды.ФизЛицо = ВычетыНаДетейДвойные.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ВычетыНаДетейДвойные.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаВычетыНаДетей КАК ВычетыНаДетейДвойныеВторые
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыСтандартныхВычетов КАК РазмерВычетовНаДетейДвойныеВторые
	|			ПО ВычетыНаДетейДвойныеВторые.КодВычета = РазмерВычетовНаДетейДвойныеВторые.КодВычета
	|				И ВычетыНаДетейДвойныеВторые.НачалоГодаНалоговогоПериода = РазмерВычетовНаДетейДвойныеВторые.НачалоНалоговогоПериода
	|		ПО (ВычетыНаДетейДвойныеВторые.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код111))
	|			И Периоды.ФизЛицо = ВычетыНаДетейДвойныеВторые.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ВычетыНаДетейДвойныеВторые.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаВычетыНаДетей КАК ВычетыНаДетейИнвалидов
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыСтандартныхВычетов КАК РазмерВычетовНаДетейИнвалидов
	|			ПО ВычетыНаДетейИнвалидов.КодВычета = РазмерВычетовНаДетейИнвалидов.КодВычета
	|				И ВычетыНаДетейИнвалидов.НачалоГодаНалоговогоПериода = РазмерВычетовНаДетейИнвалидов.НачалоНалоговогоПериода
	|		ПО (ВычетыНаДетейИнвалидов.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106))
	|			И Периоды.ФизЛицо = ВычетыНаДетейИнвалидов.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ВычетыНаДетейИнвалидов.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаВычетыНаДетей КАК ВычетыНаДетейИнвалидовДвойные
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыСтандартныхВычетов КАК РазмерВычетовНаДетейИнвалидовДвойные
	|			ПО ВычетыНаДетейИнвалидовДвойные.КодВычета = РазмерВычетовНаДетейИнвалидовДвойные.КодВычета
	|				И ВычетыНаДетейИнвалидовДвойные.НачалоГодаНалоговогоПериода = РазмерВычетовНаДетейИнвалидовДвойные.НачалоНалоговогоПериода
	|		ПО (ВычетыНаДетейИнвалидовДвойные.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107))
	|			И Периоды.ФизЛицо = ВычетыНаДетейИнвалидовДвойные.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ВычетыНаДетейИнвалидовДвойные.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаВычетыНаДетей КАК ВычетыНаДетейИнвалидовДвойныеВторые
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыСтандартныхВычетов КАК РазмерВычетовНаДетейИнвалидовДвойныеВторые
	|			ПО ВычетыНаДетейИнвалидовДвойныеВторые.КодВычета = РазмерВычетовНаДетейИнвалидовДвойныеВторые.КодВычета
	|				И ВычетыНаДетейИнвалидовДвойныеВторые.НачалоГодаНалоговогоПериода = РазмерВычетовНаДетейИнвалидовДвойныеВторые.НачалоНалоговогоПериода
	|		ПО (ВычетыНаДетейИнвалидовДвойныеВторые.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код113))
	|			И Периоды.ФизЛицо = ВычетыНаДетейИнвалидовДвойныеВторые.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ВычетыНаДетейИнвалидовДвойныеВторые.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛДоходыПредыдущегоМестаРаботы КАК ДоходыСПредыдущегоМестаРаботы
	|		ПО Периоды.МесяцНалоговогоПериода = ДоходыСПредыдущегоМестаРаботы.МесяцНалоговогоПериода
	|			И Периоды.ФизЛицо = ДоходыСПредыдущегоМестаРаботы.ФизЛицо
	|			И (ДоходыСПредыдущегоМестаРаботы.Организация = &ГоловнаяОрганизация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыРезидентовНарастающимЗаГод КАК ДоходыНарастающимИтогом
	|		ПО Периоды.МесяцНалоговогоПериода = ДоходыНарастающимИтогом.МесяцНалоговогоПериода
	|			И Периоды.ФизЛицо = ДоходыНарастающимИтогом.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Доходы.ФизЛицо КАК ФизЛицо,
	|	СУММА(ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА Доходы.Резидент
	|					ТОГДА 0
	|				ИНАЧЕ Доходы.ДоходыБезВычетов
	|			КОНЕЦ * СтавкиНалогаВПроцентах.Размер КАК ЧИСЛО(13, 0))) КАК НалогНерезидента
	|ПОМЕСТИТЬ ВТНалогНерезидентов
	|ИЗ
	|	ВТДоходыСУчетомВычетовКДоходам КАК Доходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНалогаВПроцентах КАК СтавкиНалогаВПроцентах
	|		ПО Доходы.СтавкаНалогообложенияНеРезидента = СтавкиНалогаВПроцентах.СтавкаНалогообложения
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.ФизЛицо,
	|	Доходы.МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.ФизЛицо КАК ФизЛицо,
	|	Периоды.Порядок КАК Порядок,
	|	Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Периоды.Резидент,
	|	ДоходыРезидентовПоМесяцам.ОблагаемыйДоход КАК ОблагаемыйДоходЗаМесяц,
	|	ДоходыРезидентовНарастающимЗаГод.ОблагаемыйДоходЗаГод КАК ОблагаемыйДоходНарастающимИтогом,
	|	СоциальныеВычеты.СуммаОборот КАК СоциальныйВычет,
	|	ВЫБОР
	|		КОГДА Периоды.Резидент
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(НалогНерезидентов.НалогНерезидента, 0)
	|	КОНЕЦ КАК НалогЗаМесяц,
	|	СтавкиНалогаВПроцентах.Размер КАК СтавкаДляРезидента,
	|	ПравоНаСтандартныеВычеты.КодВычетаЛичный,
	|	ПравоНаСтандартныеВычеты.СтандартныеВычетыЛичные,
	|	ПравоНаСтандартныеВычеты.СтандартныеВычетыДетские,
	|	ПравоНаСтандартныеВычеты.СтандартныеВычетыДетскиеДвойные,
	|	ПравоНаСтандартныеВычеты.СтандартныеВычетыДетскиеДвойныеВторые,
	|	ПравоНаСтандартныеВычеты.СтандартныеВычетыДетскиеИнвалидов,
	|	ПравоНаСтандартныеВычеты.СтандартныеВычетыДетскиеИнвалидовДвойные,
	|	ПравоНаСтандартныеВычеты.СтандартныеВычетыДетскиеИнвалидовДвойныеВторые,
	|	ЕСТЬNULL(ПолитикаПримененияВычетов.ОсобенностиИсчисленияНДФЛ, ЗНАЧЕНИЕ(Перечисление.ОсобенностиИсчисленияНДФЛ.СтандартныеВычетыНарастающимИтогом)) КАК ОсобенностиУчетаВычетов,
	|	ГОД(Периоды.МесяцНалоговогоПериода) КАК НалоговыйПериод,
	|	ПравоНаСтандартныеВычеты.ОграничениеПоДоходамЛичные,
	|	ПравоНаСтандартныеВычеты.ОграничениеПоДоходамНаДетей
	|ИЗ
	|	ВТПериодыФизлиц КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНалогНерезидентов КАК НалогНерезидентов
	|		ПО Периоды.ФизЛицо = НалогНерезидентов.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = НалогНерезидентов.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаСтандартныеВычетыПоМесяцам КАК ПравоНаСтандартныеВычеты
	|		ПО Периоды.ФизЛицо = ПравоНаСтандартныеВычеты.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ПравоНаСтандартныеВычеты.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыРезидентовНарастающимЗаГод КАК ДоходыРезидентовНарастающимЗаГод
	|		ПО Периоды.ФизЛицо = ДоходыРезидентовНарастающимЗаГод.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ДоходыРезидентовНарастающимЗаГод.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыРезидентовПоМесяцам КАК ДоходыРезидентовПоМесяцам
	|		ПО Периоды.ФизЛицо = ДоходыРезидентовПоМесяцам.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ДоходыРезидентовПоМесяцам.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоциальныеВычетыПомесячно КАК СоциальныеВычеты
	|		ПО Периоды.ФизЛицо = СоциальныеВычеты.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = СоциальныеВычеты.МесяцНалоговогоПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНалогаВПроцентах КАК СтавкиНалогаВПроцентах
	|		ПО (СтавкиНалогаВПроцентах.СтавкаНалогообложения = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПолитикаПримененияВычетов КАК ПолитикаПримененияВычетов
	|		ПО Периоды.НачалоГодаНалоговогоПериода = ПолитикаПримененияВычетов.НачалоНалоговогоПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	ФизЛицо,
	|	НалоговыйПериод,
	|	МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.ФизЛицо КАК ФизЛицо,
	|	Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	СУММА(ВЫБОР
	|			КОГДА ПравоНаИмущественныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код311)
	|				ТОГДА ПравоНаИмущественныеВычеты.Размер
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВычетИмущественныйРасходы,
	|	СУММА(ВЫБОР
	|			КОГДА ПравоНаИмущественныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код312)
	|				ТОГДА ПравоНаИмущественныеВычеты.Размер
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВычетИмущественныйПроцентыПоКредитам,
	|	СУММА(ВЫБОР
	|			КОГДА ПравоНаИмущественныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код318)
	|				ТОГДА ПравоНаИмущественныеВычеты.Размер
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ВычетИмущественныйПроцентыПриПерекредитовании
	|ИЗ
	|	ВТПериодыФизлиц КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПравоНаИмущественныеВычеты КАК ПравоНаИмущественныеВычеты
	|		ПО Периоды.ФизЛицо = ПравоНаИмущественныеВычеты.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ПравоНаИмущественныеВычеты.МесяцНалоговогоПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	Периоды.ФизЛицо,
	|	Периоды.МесяцНалоговогоПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизЛицо,
	|	МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.ФизЛицо КАК ФизЛицо,
	|	Доходы.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	Доходы.Подразделение КАК Подразделение,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПериодыФизлиц.ФизЛицо КАК ФизЛицо,
	|		НАЧАЛОПЕРИОДА(ПериодыФизлиц.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|		СведенияОДоходахНДФЛ.ПодразделениеОрганизации КАК Подразделение,
	|		ЕСТЬNULL(СведенияОДоходахНДФЛ.СуммаДохода, 0) - ВЫБОР
	|			КОГДА ПериодыФизлиц.Резидент
	|				ТОГДА ЕСТЬNULL(СведенияОДоходахНДФЛ.СуммаВычета, 0)
	|			КОГДА СведенияОДоходахНДФЛ.КодВычета.НеПредоставляетсяНерезидентам
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(СведенияОДоходахНДФЛ.СуммаВычета, 0)
	|		КОНЕЦ КАК СуммаДохода
	|	ИЗ
	|		ВТПериодыФизлиц КАК ПериодыФизлиц
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛСведенияОДоходах КАК СведенияОДоходахНДФЛ
	|			ПО ПериодыФизлиц.ФизЛицо = СведенияОДоходахНДФЛ.ФизЛицо
	|				И (ПериодыФизлиц.МесяцНалоговогоПериода = НАЧАЛОПЕРИОДА(СведенияОДоходахНДФЛ.Период, МЕСЯЦ))
	|				И (СведенияОДоходахНДФЛ.Организация = &ГоловнаяОрганизация)
	|				И (СведенияОДоходахНДФЛ.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13))
	|				И ((НЕ СведенияОДоходахНДФЛ.КодДохода.НеОблагаетсяУНалоговогоАгента))
	|				И (СведенияОДоходахНДФЛ.ПериодРегистрации <= &КонецМесяцаРасчета)) КАК Доходы
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.ФизЛицо,
	|	Доходы.МесяцНалоговогоПериода,
	|	Доходы.Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизЛицо,
	|	МесяцНалоговогоПериода,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизЛицо КАК ФизЛицо,
	|	НАЧАЛОПЕРИОДА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ПодразделениеОрганизации КАК Подразделение,
	|	-СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Налог) КАК Налог
	|ИЗ
	|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Регистратор <> &Регистратор
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода МЕЖДУ &НачалоГодаПериодаРасчета И &КонецПериодаРасчета
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = &ГоловнаяОрганизация
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизЛицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокФизЛиц КАК СписокСотрудников)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период <= &КонецМесяцаРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ПодразделениеОрганизации,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизЛицо,
	|	МесяцНалоговогоПериода,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛПредоставленныеСтандартныеВычеты.ФизЛицо КАК ФизЛицо,
	|	НДФЛПредоставленныеСтандартныеВычеты.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	НДФЛПредоставленныеСтандартныеВычеты.Подразделение КАК Подразделение,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НДФЛПредоставленныеСтандартныеВычеты.КодВычета В (ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103), ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104), ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105))
	|				ТОГДА НДФЛПредоставленныеСтандартныеВычеты.КодВычета
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		КОНЕЦ) КАК КодВычетаЛичный,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛПредоставленныеСтандартныеВычеты.КодВычета В (ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103), ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104), ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105))
	|				ТОГДА -НДФЛПредоставленныеСтандартныеВычеты.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетЛичный,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛПредоставленныеСтандартныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
	|				ТОГДА -НДФЛПредоставленныеСтандартныеВычеты.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетНаДетей,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛПредоставленныеСтандартныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
	|				ТОГДА -НДФЛПредоставленныеСтандартныеВычеты.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетНаДетейДвойной,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛПредоставленныеСтандартныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код111)
	|				ТОГДА -НДФЛПредоставленныеСтандартныеВычеты.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетНаДетейДвойнойВторой,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛПредоставленныеСтандартныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
	|				ТОГДА -НДФЛПредоставленныеСтандартныеВычеты.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетНаДетейИнвалидов,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛПредоставленныеСтандартныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
	|				ТОГДА -НДФЛПредоставленныеСтандартныеВычеты.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетНаДетейИнвалидовДвойной,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛПредоставленныеСтандартныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код113)
	|				ТОГДА -НДФЛПредоставленныеСтандартныеВычеты.СуммаОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетНаДетейИнвалидовДвойнойВторой
	|ИЗ
	|	ВТПредоставленныеСтандартныеИСоциальныеВычеты КАК НДФЛПредоставленныеСтандартныеВычеты
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛПредоставленныеСтандартныеВычеты.Подразделение,
	|	НДФЛПредоставленныеСтандартныеВычеты.МесяцНалоговогоПериода,
	|	НДФЛПредоставленныеСтандартныеВычеты.ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизЛицо,
	|	МесяцНалоговогоПериода,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Периоды.ФизЛицо КАК ФизЛицо,
	|	Периоды.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ИмущественныеВычеты.Подразделение КАК Подразделение,
	|	СУММА(ВЫБОР
	|			КОГДА ИмущественныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код311)
	|				ТОГДА -ИмущественныеВычеты.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетИмущественныйРасход,
	|	СУММА(ВЫБОР
	|			КОГДА ИмущественныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код312)
	|				ТОГДА -ИмущественныеВычеты.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетИмущественныйПроцентыПоКредитам,
	|	СУММА(ВЫБОР
	|			КОГДА ИмущественныеВычеты.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код318)
	|				ТОГДА -ИмущественныеВычеты.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПримененныйВычетИмущественныйПроцентыПриПерекредитовании
	|ИЗ
	|	ВТПериодыФизлиц КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредоставленоИмущественныхВычетов КАК ИмущественныеВычеты
	|		ПО Периоды.ФизЛицо = ИмущественныеВычеты.ФизЛицо
	|			И Периоды.МесяцНалоговогоПериода = ИмущественныеВычеты.МесяцНалоговогоПериода
	|ГДЕ
	|	ИмущественныеВычеты.Подразделение ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ИмущественныеВычеты.Подразделение,
	|	Периоды.МесяцНалоговогоПериода,
	|	Периоды.ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизЛицо,
	|	МесяцНалоговогоПериода,
	|	Подразделение";
	Результат =	Запрос.ВыполнитьПакет();
	
	ВсегоРезультатов = Результат.Количество();
	ВыборкаРасчетов = Результат[ВсегоРезультатов - 6].Выбрать();
	
	ПравоНаИмущественныеВычеты = Результат[ВсегоРезультатов - 5].Выгрузить();
	ПравоНаИмущественныеВычеты.Индексы.Добавить("ФизЛицо, МесяцНалоговогоПериода");
	
	РаспределениеДоходовПоПодразделениям = Результат[ВсегоРезультатов - 4].Выгрузить();
	РаспределениеДоходовПоПодразделениям.Индексы.Добавить("ФизЛицо, МесяцНалоговогоПериода");
	
	ИсчисленоРанее = Результат[ВсегоРезультатов - 3].Выгрузить();
	СтандартныхВычетовРанее = Результат[ВсегоРезультатов - 2].Выгрузить();
	ИмущественныхВычетовРанее = Результат[ВсегоРезультатов - 1].Выгрузить();
	
	РаспределятьПоПодразделениям = Не ВозвращатьДанныеРасчетовБезПодразделений;
	
	ТипНалог = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(13,0));
	ТипВычет = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2));
	РезультатыРасчетов = СтандартныхВычетовРанее.СкопироватьКолонки();
	РезультатыРасчетов.Колонки.Добавить("Налог",ТипНалог);
	РезультатыРасчетов.Колонки.Добавить("ПримененныйВычетИмущественныйРасход",ТипВычет);
	РезультатыРасчетов.Колонки.Добавить("ПримененныйВычетИмущественныйПроцентыПоКредитам",ТипВычет);
	РезультатыРасчетов.Колонки.Добавить("ПримененныйВычетИмущественныйПроцентыПриПерекредитовании",ТипВычет);
	
	Если ВозвращатьДанныеРасчетовБезПодразделений Тогда
		РезультатыРасчетов.Колонки.Добавить("Резидент", Новый ОписаниеТипов("Булево"));
		РезультатыРасчетов.Колонки.Добавить("ОблагаемыйДоходНарастающимИтогом",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ОграничениеПоДоходамЛичные",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ОграничениеПоДоходамНаДетей",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетЛичныйЗаМесяц",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетНаДетейИнвалидовДвойнойЗаМесяц",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетНаДетейИнвалидовЗаМесяц",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетНаДетейДвойнойВторойЗаМесяц",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетНаДетейДвойнойЗаМесяц",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетНаДетейЗаМесяц",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетИмущественныйРасходыОстаток",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетИмущественныйПроцентыПриПерекредитованииОстаток",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("ВычетИмущественныйПроцентыПоКредитамОстаток",ТипВычет);
		РезультатыРасчетов.Колонки.Добавить("НалогЗаМесяц",ТипВычет);
	КонецЕсли;
	
	Если РаспределятьПоПодразделениям Тогда
		ВспомогательнаяТаблица = РезультатыРасчетов.СкопироватьКолонки();
		СтрокаРесурсов = "Налог,ПримененныйВычетЛичный,ПримененныйВычетНаДетей,ПримененныйВычетНаДетейДвойной,ПримененныйВычетНаДетейДвойнойВторой,ПримененныйВычетНаДетейИнвалидов,ПримененныйВычетНаДетейИнвалидовДвойной,ПримененныйВычетНаДетейИнвалидовДвойнойВторой,ПримененныйВычетИмущественныйРасход,ПримененныйВычетИмущественныйПроцентыПоКредитам,ПримененныйВычетИмущественныйПроцентыПриПерекредитовании";
		МассивРесурсов = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(СтрокаРесурсов);
	КонецЕсли;
	
	Если КомментироватьРасчет Тогда
		КомментарийНДФЛ = ОбщегоНазначенияЗК.КомментарийРасчета("Расчет НДФЛ");
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("ФизЛицо, МесяцНалоговогоПериода");
	
	Пока ВыборкаРасчетов.СледующийПоЗначениюПоля("Порядок") Цикл
		Пока ВыборкаРасчетов.СледующийПоЗначениюПоля("ФизЛицо") Цикл
			
			Если РаспределятьПоПодразделениям Тогда
				СтруктураПоиска.ФизЛицо = ВыборкаРасчетов.ФизЛицо;
				СтруктураПоиска.МесяцНалоговогоПериода = НачалоМесяца(ПериодРегистрации);
				ДоходыМесяцаРасчета = РаспределениеДоходовПоПодразделениям.НайтиСтроки(СтруктураПоиска);
				КоэффициентыПодразделенийМесяцаРасчета = ВыделитьКоэффициентыРаспределенияИзКоллекцииСтрок(ДоходыМесяцаРасчета, "СуммаДохода");
			КонецЕсли;
				
			Пока ВыборкаРасчетов.СледующийПоЗначениюПоля("НалоговыйПериод") Цикл
				
				ВычетЛичныйОстаток = 0;
				ВычетНаДетейОстаток = 0;
				ВычетНаДетейДвойнойОстаток = 0;
				ВычетНаДетейДвойнойВторойОстаток = 0;
				ВычетНаДетейИнвалидовОстаток = 0;
				ВычетНаДетейИнвалидовДвойнойОстаток = 0;
				ВычетНаДетейИнвалидовДвойнойВторойОстаток = 0;
				ВычетИмущественныйРасходыОстаток = 0;
				ВычетИмущественныйПроцентыПоКредитамОстаток = 0;
				ВычетИмущественныйПроцентыПриПерекредитованииОстаток = 0;
				ВычетСоциальныйОстаток = 0;
				ОблагаемыйДоход = 0;
				ДоходЗаМесяцОстаток = 0;
				НалогИсчисленный = 0;
				
				Пока ВыборкаРасчетов.Следующий() Цикл
					
					ЗаполнитьЗначенияСвойств(СтруктураПоиска,ВыборкаРасчетов);
					
					Если РаспределятьПоПодразделениям Тогда
						ВспомогательнаяТаблица.Очистить();
						Доходы = РаспределениеДоходовПоПодразделениям.НайтиСтроки(СтруктураПоиска);
						БазаРаспределения = 0;
						Для каждого СтрокаРаспределения Из Доходы Цикл
							БазаРаспределения = БазаРаспределения + СтрокаРаспределения.СуммаДохода;
						КонецЦикла; 
						Если БазаРаспределения = 0 Тогда
							// если доходов за прошлый месяц нет - будем использовать текущее распределение
							Доходы = ДоходыМесяцаРасчета;
							КоэффициентыПодразделений = КоэффициентыПодразделенийМесяцаРасчета;
							
							Для К = 0 По Доходы.КОличество()-1 Цикл
								Если Не ЗначениеЗаполнено(Доходы[К].Подразделение) Тогда
									КоэффициентыПодразделений[К] = 0;
								КонецЕсли;
							КонецЦикла;
							
						Иначе
							КоэффициентыПодразделений = ВыделитьКоэффициентыРаспределенияИзКоллекцииСтрок(Доходы, "СуммаДохода");
						КонецЕсли;
						ВписатьВРезультатЗарегистрированныеВУчетеДанныеНДФЛ(ВспомогательнаяТаблица, ИсчисленоРанее.НайтиСтроки(СтруктураПоиска), "Налог");
						ВписатьВРезультатЗарегистрированныеВУчетеДанныеНДФЛ(ВспомогательнаяТаблица, СтандартныхВычетовРанее.НайтиСтроки(СтруктураПоиска), "ПримененныйВычетЛичный,ПримененныйВычетНаДетей,ПримененныйВычетНаДетейДвойной,ПримененныйВычетНаДетейДвойнойВторой,ПримененныйВычетНаДетейИнвалидов,ПримененныйВычетНаДетейИнвалидовДвойной,ПримененныйВычетНаДетейИнвалидовДвойнойВторой");
						ВписатьВРезультатЗарегистрированныеВУчетеДанныеНДФЛ(ВспомогательнаяТаблица, ИмущественныхВычетовРанее.НайтиСтроки(СтруктураПоиска), "ПримененныйВычетИмущественныйРасход,ПримененныйВычетИмущественныйПроцентыПоКредитам,ПримененныйВычетИмущественныйПроцентыПриПерекредитовании");
					КонецЕсли;
					
					Если КомментироватьРасчет Тогда
						КомментарийМесяца = ОбщегоНазначенияЗК.КомментарийРасчета("Расчет за " + Формат(ВыборкаРасчетов.МесяцНалоговогоПериода, "ДФ=ММММ"), КомментарийНДФЛ);
						Если ВыборкаРасчетов.Резидент Тогда
							ОбщегоНазначенияЗК.КомментарийРасчета("Статус: резидент РФ (или высококвалифицированный иностранный специалист)", КомментарийМесяца);
						Иначе
							ОбщегоНазначенияЗК.КомментарийРасчета("Статус: не является резидентом РФ", КомментарийМесяца,,, Перечисления.ВидыСообщений.ВажнаяИнформация);
						КонецЕсли;
						ОбщегоНазначенияЗК.КомментарийРасчета("Доход за период, всего: " + ВыборкаРасчетов.ОблагаемыйДоходЗаМесяц, КомментарийМесяца,,,, Ложь);
					КонецЕсли;
					
					Если ВыборкаРасчетов.Резидент Тогда
						
						Если КомментироватьРасчет Тогда
							ОбщегоНазначенияЗК.КомментарийРасчета("Облагаемый доход нарастающим итогом за год: " + ВыборкаРасчетов.ОблагаемыйДоходНарастающимИтогом, КомментарийМесяца);
						КонецЕсли;
						
						// Расчет сумм примененных вычетов и налога
						
						ВычетСоциальныйЗаМесяц = 0;
						ВычетЛичныйЗаМесяц = 0;
						ВычетНаДетейЗаМесяц = 0;
						ВычетНаДетейДвойнойЗаМесяц = 0;
						ВычетНаДетейДвойнойВторойЗаМесяц = 0;
						ВычетНаДетейИнвалидовЗаМесяц = 0;
						ВычетНаДетейИнвалидовДвойнойЗаМесяц = 0;
						ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц = 0;
						ВычетИмущественныйРасходыЗаМесяц = 0;
						ВычетИмущественныйПроцентыПоКредитамЗаМесяц = 0;
						ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц = 0;
						
						ВычетСоциальныйОстаток = ВычетСоциальныйОстаток + ВыборкаРасчетов.СоциальныйВычет;
						СтрокиПравНаИмущественныеВычеты = ПравоНаИмущественныеВычеты.НайтиСтроки(СтруктураПоиска);
						Если СтрокиПравНаИмущественныеВычеты.Количество() > 0 Тогда
							ВычетИмущественныйРасходыОстаток = ВычетИмущественныйРасходыОстаток + СтрокиПравНаИмущественныеВычеты[0].ВычетИмущественныйРасходы;
							ВычетИмущественныйПроцентыПоКредитамОстаток = ВычетИмущественныйПроцентыПоКредитамОстаток + СтрокиПравНаИмущественныеВычеты[0].ВычетИмущественныйПроцентыПоКредитам;
							ВычетИмущественныйПроцентыПриПерекредитованииОстаток = ВычетИмущественныйПроцентыПриПерекредитованииОстаток + СтрокиПравНаИмущественныеВычеты[0].ВычетИмущественныйПроцентыПриПерекредитовании;
						КонецЕсли;
						Если ВыборкаРасчетов.ОсобенностиУчетаВычетов = Перечисления.ОсобенностиИсчисленияНДФЛ.СтандартныеВычетыНарастающимИтогом Тогда
							ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток + ВыборкаРасчетов.ОблагаемыйДоходЗаМесяц;
							ВычетЛичныйОстаток = ВычетЛичныйОстаток + ВыборкаРасчетов.СтандартныеВычетыЛичные;
							ВычетНаДетейОстаток = ВычетНаДетейОстаток + ВыборкаРасчетов.СтандартныеВычетыДетские;
							ВычетНаДетейДвойнойОстаток = ВычетНаДетейДвойнойОстаток + ВыборкаРасчетов.СтандартныеВычетыДетскиеДвойные;
							ВычетНаДетейДвойнойВторойОстаток = ВычетНаДетейДвойнойВторойОстаток + ВыборкаРасчетов.СтандартныеВычетыДетскиеДвойныеВторые;
							ВычетНаДетейИнвалидовОстаток = ВычетНаДетейИнвалидовОстаток + ВыборкаРасчетов.СтандартныеВычетыДетскиеИнвалидов;
							ВычетНаДетейИнвалидовДвойнойОстаток = ВычетНаДетейИнвалидовДвойнойОстаток + ВыборкаРасчетов.СтандартныеВычетыДетскиеИнвалидовДвойные;
							ВычетНаДетейИнвалидовДвойнойВторойОстаток = ВычетНаДетейИнвалидовДвойнойВторойОстаток + ВыборкаРасчетов.СтандартныеВычетыДетскиеИнвалидовДвойныеВторые;
						Иначе
							ДоходЗаМесяцОстаток = ВыборкаРасчетов.ОблагаемыйДоходЗаМесяц;
							Если КомментироватьРасчет Тогда
								ОбщегоНазначенияЗК.КомментарийРасчета("Стандартные вычеты: применяются в пределах месячного дохода", КомментарийМесяца,,,, Ложь);
								Если ДоходЗаМесяцОстаток = 0 Тогда
									ОбщегоНазначенияЗК.КомментарийРасчета("Вычеты не применялись", КомментарийМесяца,,, Перечисления.ВидыСообщений.ВажнаяИнформация);
								КонецЕсли;
							КонецЕсли;
							ВычетЛичныйОстаток = ВыборкаРасчетов.СтандартныеВычетыЛичные;
							ВычетНаДетейОстаток = ВыборкаРасчетов.СтандартныеВычетыДетские;
							ВычетНаДетейДвойнойОстаток = ВыборкаРасчетов.СтандартныеВычетыДетскиеДвойные;
							ВычетНаДетейДвойнойВторойОстаток = ВыборкаРасчетов.СтандартныеВычетыДетскиеДвойныеВторые;
							ВычетНаДетейИнвалидовОстаток = ВыборкаРасчетов.СтандартныеВычетыДетскиеИнвалидов;
							ВычетНаДетейИнвалидовДвойнойОстаток = ВыборкаРасчетов.СтандартныеВычетыДетскиеИнвалидовДвойные;
							ВычетНаДетейИнвалидовДвойнойВторойОстаток = ВыборкаРасчетов.СтандартныеВычетыДетскиеИнвалидовДвойныеВторые;
						КонецЕсли;
						
						Если ДоходЗаМесяцОстаток > 0 Тогда
							
							ВычетСоциальныйЗаМесяц = Мин(ДоходЗаМесяцОстаток,ВычетСоциальныйОстаток);
							ВычетСоциальныйОстаток = ВычетСоциальныйОстаток - ВычетСоциальныйЗаМесяц;
							ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетСоциальныйЗаМесяц;
							
							Если ВыборкаРасчетов.КодВычетаЛичный = Справочники.ВычетыНДФЛ.Код103 Тогда
								ВычетЛичныйЗаМесяц = Мин(ДоходЗаМесяцОстаток,ВычетЛичныйОстаток);
								ВычетЛичныйОстаток = ВычетЛичныйОстаток - ВычетЛичныйЗаМесяц;
								ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетЛичныйЗаМесяц;
							КонецЕсли;
							
							ВычетНаДетейЗаМесяц = Мин(ДоходЗаМесяцОстаток,ВычетНаДетейОстаток);
							ВычетНаДетейОстаток = ВычетНаДетейОстаток - ВычетНаДетейЗаМесяц;
							ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетНаДетейЗаМесяц;
							
							ВычетНаДетейДвойнойЗаМесяц = Мин(ДоходЗаМесяцОстаток,ВычетНаДетейДвойнойОстаток);
							ВычетНаДетейДвойнойОстаток = ВычетНаДетейДвойнойОстаток - ВычетНаДетейДвойнойЗаМесяц;
							ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетНаДетейДвойнойЗаМесяц;
							
							ВычетНаДетейДвойнойВторойЗаМесяц = Мин(ДоходЗаМесяцОстаток,ВычетНаДетейДвойнойВторойОстаток);
							ВычетНаДетейДвойнойВторойОстаток = ВычетНаДетейДвойнойВторойОстаток - ВычетНаДетейДвойнойВторойЗаМесяц;
							ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетНаДетейДвойнойВторойЗаМесяц;
							
							ВычетНаДетейИнвалидовЗаМесяц = Мин(ДоходЗаМесяцОстаток,ВычетНаДетейИнвалидовОстаток);
							ВычетНаДетейИнвалидовОстаток = ВычетНаДетейИнвалидовОстаток - ВычетНаДетейИнвалидовЗаМесяц;
							ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетНаДетейИнвалидовЗаМесяц;
							
							ВычетНаДетейИнвалидовДвойнойЗаМесяц = Мин(ДоходЗаМесяцОстаток,ВычетНаДетейИнвалидовДвойнойОстаток);
							ВычетНаДетейИнвалидовДвойнойОстаток = ВычетНаДетейИнвалидовДвойнойОстаток - ВычетНаДетейИнвалидовДвойнойЗаМесяц;
							ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетНаДетейИнвалидовДвойнойЗаМесяц;
							
							ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц = Мин(ДоходЗаМесяцОстаток,ВычетНаДетейИнвалидовДвойнойВторойОстаток);
							ВычетНаДетейИнвалидовДвойнойВторойОстаток = ВычетНаДетейИнвалидовДвойнойВторойОстаток - ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц;
							ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц;
							
							Если ВыборкаРасчетов.КодВычетаЛичный <> Справочники.ВычетыНДФЛ.Код103 Тогда
								ВычетЛичныйЗаМесяц = Мин(ДоходЗаМесяцОстаток,ВычетЛичныйОстаток);
								ВычетЛичныйОстаток = ВычетЛичныйОстаток - ВычетЛичныйЗаМесяц;
								ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетЛичныйЗаМесяц;
							КонецЕсли;
							
							Если ВыборкаРасчетов.МесяцНалоговогоПериода >= ДатаЗакона229ФЗ Тогда
								ДоходЗаМесяцОстаток = Макс(0, ВыборкаРасчетов.ОблагаемыйДоходЗаМесяц - ВычетСоциальныйЗаМесяц - ВычетЛичныйЗаМесяц - ВычетНаДетейЗаМесяц - ВычетНаДетейДвойнойЗаМесяц - ВычетНаДетейДвойнойВторойЗаМесяц - ВычетНаДетейИнвалидовЗаМесяц - ВычетНаДетейИнвалидовДвойнойЗаМесяц - ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц);
							КонецЕсли;
							
							Если ДоходЗаМесяцОстаток > 0 Тогда
								
								ВычетИмущественныйПроцентыПоКредитамЗаМесяц = Мин(ДоходЗаМесяцОстаток, ВычетИмущественныйПроцентыПоКредитамОстаток);
								ВычетИмущественныйПроцентыПоКредитамОстаток = ВычетИмущественныйПроцентыПоКредитамОстаток - ВычетИмущественныйПроцентыПоКредитамЗаМесяц;
								ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетИмущественныйПроцентыПоКредитамЗаМесяц;
								
								ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц = Мин(ДоходЗаМесяцОстаток, ВычетИмущественныйПроцентыПриПерекредитованииОстаток);
								ВычетИмущественныйПроцентыПриПерекредитованииОстаток = ВычетИмущественныйПроцентыПриПерекредитованииОстаток - ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц;
								ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц;
								
								ВычетИмущественныйРасходыЗаМесяц = Мин(ДоходЗаМесяцОстаток, ВычетИмущественныйРасходыОстаток);
								ВычетИмущественныйРасходыОстаток = ВычетИмущественныйРасходыОстаток - ВычетИмущественныйРасходыЗаМесяц;
								ДоходЗаМесяцОстаток = ДоходЗаМесяцОстаток - ВычетИмущественныйРасходыЗаМесяц;
								
							КонецЕсли;
							
							Если РаспределятьПоПодразделениям Тогда
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетЛичныйЗаМесяц, "ПримененныйВычетЛичный", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетНаДетейЗаМесяц, "ПримененныйВычетНаДетей", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетНаДетейДвойнойЗаМесяц, "ПримененныйВычетНаДетейДвойной", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетНаДетейДвойнойВторойЗаМесяц, "ПримененныйВычетНаДетейДвойнойВторой", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетНаДетейИнвалидовЗаМесяц, "ПримененныйВычетНаДетейИнвалидов", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетНаДетейИнвалидовДвойнойЗаМесяц, "ПримененныйВычетНаДетейИнвалидовДвойной", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц, "ПримененныйВычетНаДетейИнвалидовДвойнойВторой", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетИмущественныйРасходыЗаМесяц, "ПримененныйВычетИмущественныйРасход", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетИмущественныйПроцентыПоКредитамЗаМесяц, "ПримененныйВычетИмущественныйПроцентыПоКредитам", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
								ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц, "ПримененныйВычетИмущественныйПроцентыПриПерекредитовании", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
							КонецЕсли;
							
							Если КомментироватьРасчет Тогда
								
								Если ВычетСоциальныйЗаМесяц + ВычетЛичныйЗаМесяц + ВычетНаДетейЗаМесяц + ВычетНаДетейДвойнойЗаМесяц + ВычетНаДетейДвойнойВторойЗаМесяц + ВычетНаДетейИнвалидовЗаМесяц + ВычетНаДетейИнвалидовДвойнойЗаМесяц + ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц + ВычетИмущественныйПроцентыПоКредитамЗаМесяц + ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц + ВычетИмущественныйРасходыЗаМесяц <> 0 Тогда
									
									КомментарийВычетов = ОбщегоНазначенияЗК.КомментарийРасчета("Примененные вычеты, всего: " + (ВычетСоциальныйЗаМесяц + ВычетЛичныйЗаМесяц + ВычетНаДетейЗаМесяц + ВычетНаДетейДвойнойЗаМесяц + ВычетНаДетейДвойнойВторойЗаМесяц + ВычетНаДетейИнвалидовЗаМесяц + ВычетНаДетейИнвалидовДвойнойЗаМесяц + ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц + ВычетИмущественныйПроцентыПоКредитамЗаМесяц + ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц + ВычетИмущественныйРасходыЗаМесяц), КомментарийМесяца);
									Если ВычетСоциальныйЗаМесяц <> 0 Тогда
										ОбщегоНазначенияЗК.КомментарийРасчета("Примененный социальный вычет: " + ВычетСоциальныйЗаМесяц, КомментарийВычетов);
									КонецЕсли;
									Если ВычетЛичныйЗаМесяц <> 0 Тогда
										ОбщегоНазначенияЗК.КомментарийРасчета("Примененный личный вычет: " + ВычетЛичныйЗаМесяц, КомментарийВычетов);
									КонецЕсли;
									Если ВычетНаДетейЗаМесяц <> 0 Тогда
										ОбщегоНазначенияЗК.КомментарийРасчета("Примененный вычет на детей: " + ВычетНаДетейЗаМесяц, КомментарийВычетов);
									КонецЕсли;
									Если ВычетНаДетейДвойнойЗаМесяц + ВычетНаДетейДвойнойВторойЗаМесяц <> 0 Тогда
										ОбщегоНазначенияЗК.КомментарийРасчета("Примененный вычет на детей (двойной): " + (ВычетНаДетейДвойнойЗаМесяц + ВычетНаДетейДвойнойВторойЗаМесяц), КомментарийВычетов);
									КонецЕсли;
									Если ВычетНаДетейИнвалидовЗаМесяц <> 0 Тогда
										ОбщегоНазначенияЗК.КомментарийРасчета("Примененный вычет на детей-инвалидов: " + ВычетНаДетейИнвалидовЗаМесяц, КомментарийВычетов);
									КонецЕсли;
									Если ВычетНаДетейИнвалидовДвойнойЗаМесяц + ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц <> 0 Тогда
										ОбщегоНазначенияЗК.КомментарийРасчета("Примененный вычет на детей-инвалидов (двойной): " + (ВычетНаДетейИнвалидовДвойнойЗаМесяц + ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц), КомментарийВычетов);
									КонецЕсли;
									Если ВычетИмущественныйПроцентыПоКредитамЗаМесяц + ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц <> 0 Тогда
										ОбщегоНазначенияЗК.КомментарийРасчета("Вычеты на погашение процентов по ипотеке: " + (ВычетИмущественныйПроцентыПоКредитамЗаМесяц + ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц), КомментарийВычетов);
									КонецЕсли;
									Если ВычетИмущественныйРасходыЗаМесяц <> 0 Тогда
										ОбщегоНазначенияЗК.КомментарийРасчета("Вычеты по расходам на строительство: " + ВычетИмущественныйРасходыЗаМесяц, КомментарийВычетов);
									КонецЕсли;
								Иначе
									ОбщегоНазначенияЗК.КомментарийРасчета("Вычеты не применялись", КомментарийМесяца,,, Перечисления.ВидыСообщений.ВажнаяИнформация);
								КонецЕсли;                
							КонецЕсли;
							
							ОблагаемыйДоход = ОблагаемыйДоход + ВыборкаРасчетов.ОблагаемыйДоходЗаМесяц - (ВычетСоциальныйЗаМесяц + ВычетЛичныйЗаМесяц + ВычетНаДетейЗаМесяц + ВычетНаДетейДвойнойЗаМесяц + ВычетНаДетейДвойнойВторойЗаМесяц + ВычетНаДетейИнвалидовЗаМесяц + ВычетНаДетейИнвалидовДвойнойЗаМесяц + ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц + ВычетИмущественныйПроцентыПоКредитамЗаМесяц + ВычетИмущественныйПроцентыПриПерекредитованииЗаМесяц + ВычетИмущественныйРасходыЗаМесяц);
							
						КонецЕсли;
						
						НалогЗаМесяц = Окр(ОблагаемыйДоход * ВыборкаРасчетов.СтавкаДляРезидента) - НалогИсчисленный;
						НалогИсчисленный = НалогИсчисленный + НалогЗаМесяц;
						
						Если ВозвращатьДанныеРасчетовБезПодразделений Тогда
							
							СтрокаРезультата = РезультатыРасчетов.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаРезультата, ВыборкаРасчетов, "ФизЛицо, МесяцНалоговогоПериода, Резидент, ОблагаемыйДоходНарастающимИтогом, ОграничениеПоДоходамЛичные, ОграничениеПоДоходамНаДетей");
							СтрокаРезультата.ВычетЛичныйЗаМесяц = ВычетЛичныйЗаМесяц;
							СтрокаРезультата.ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц = ВычетНаДетейИнвалидовДвойнойВторойЗаМесяц;
							СтрокаРезультата.ВычетНаДетейИнвалидовДвойнойЗаМесяц = ВычетНаДетейИнвалидовДвойнойЗаМесяц;
							СтрокаРезультата.ВычетНаДетейИнвалидовЗаМесяц = ВычетНаДетейИнвалидовЗаМесяц;
							СтрокаРезультата.ВычетНаДетейДвойнойВторойЗаМесяц = ВычетНаДетейДвойнойВторойЗаМесяц;
							СтрокаРезультата.ВычетНаДетейДвойнойЗаМесяц = ВычетНаДетейДвойнойЗаМесяц;
							СтрокаРезультата.ВычетНаДетейЗаМесяц = ВычетНаДетейЗаМесяц;
							СтрокаРезультата.ВычетИмущественныйРасходыОстаток = ВычетИмущественныйРасходыОстаток;
							СтрокаРезультата.ВычетИмущественныйПроцентыПриПерекредитованииОстаток = ВычетИмущественныйПроцентыПриПерекредитованииОстаток;
							СтрокаРезультата.ВычетИмущественныйПроцентыПоКредитамОстаток = ВычетИмущественныйПроцентыПоКредитамОстаток;
							СтрокаРезультата.НалогЗаМесяц = НалогЗаМесяц;
							
						КонецЕсли;
						
						Если КомментироватьРасчет Тогда
							РанееНалогИсчисленный = ИсчисленоРанее.Скопировать(ИсчисленоРанее.НайтиСтроки(СтруктураПоиска),"Налог");
							РанееНалогИсчисленный = РанееНалогИсчисленный.Итог("Налог");
							Если РанееНалогИсчисленный <> 0 Тогда
								ОбщегоНазначенияЗК.КомментарийРасчета("Исчисленный ранее налог нарастающим итогом за год: " + РанееНалогИсчисленный, КомментарийМесяца);
							КонецЕсли;
							ОбщегоНазначенияЗК.КомментарийРасчета("Результат расчета за " + Формат(ВыборкаРасчетов.МесяцНалоговогоПериода, "ДФ=ММММ") + ": " + НалогЗаМесяц, КомментарийНДФЛ);
						КонецЕсли;
						
					Иначе	
						
						НалогЗаМесяц = ВыборкаРасчетов.НалогЗаМесяц;
						
						Если КомментироватьРасчет Тогда
							ОбщегоНазначенияЗК.КомментарийРасчета("Результат расчета за " + Формат(ВыборкаРасчетов.МесяцНалоговогоПериода, "ДФ=ММММ") + ": " + НалогЗаМесяц, КомментарийНДФЛ);
						КонецЕсли;
						
						Если ВозвращатьДанныеРасчетовБезПодразделений Тогда
							ЗаполнитьЗначенияСвойств(РезультатыРасчетов.Добавить(),ВыборкаРасчетов,"ФизЛицо, МесяцНалоговогоПериода, Резидент");
						КонецЕсли;
						
					КонецЕсли;
					
					Если РаспределятьПоПодразделениям Тогда
						
						ВписатьРесурсВРезультатыРасчетовНДФЛ(ВспомогательнаяТаблица, ВыборкаРасчетов.МесяцНалоговогоПериода, НалогЗаМесяц, "Налог", ВыборкаРасчетов, Доходы, КоэффициентыПодразделений);
						
						ВспомогательнаяТаблица.Свернуть("ФизЛицо, МесяцНалоговогоПериода,Подразделение", СтрокаРесурсов);
						Для каждого СтрокаТЗ Из ВспомогательнаяТаблица Цикл
							ЕстьДанныеВСтроке = Ложь;
							Для каждого ИмяРесурса Из МассивРесурсов Цикл
								ЕстьДанныеВСтроке = СтрокаТЗ[ИмяРесурса] <> 0;
								Если ЕстьДанныеВСтроке Тогда
									Прервать;
								КонецЕсли;
							КонецЦикла;
							Если ЕстьДанныеВСтроке Тогда
								ЗаполнитьЗначенияСвойств(РезультатыРасчетов.Добавить(),СтрокаТЗ);
							КонецЕсли;
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если РаспределятьПоПодразделениям Тогда
		РезультатыРасчетов.Колонки.Подразделение.Имя = "ПодразделениеОрганизации";
	КонецЕсли;
	
	Возврат РезультатыРасчетов;

КонецФункции  // ПолучитьДанныеНДФЛПоРегистратору

// По переданным движениям расчетов налогоплательщика по НДФЛ формирует движения по расчетам налогового агента
//
// Параметры
//  РасчетыПоНДФЛ - таблица значений по структуре совпадающая с рег-ром НДФЛРасчетыСБюджетом
//
// Возвращаемое значение:
//  таблица значений по структуре совпадающая с рег-ром РасчетыНалоговыхАгентовСБюджетомПоНДФЛ 
//
Функция РасчетыНалоговогоАгентаПоУдержанномуНДФЛ(РасчетыПоНДФЛ) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.УстановитьПараметр("ПустойКПП","");
	Запрос.УстановитьПараметр("РасчетыПоНДФЛ", РасчетыПоНДФЛ);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетом.НомерСтроки,
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.КодДохода,
	|	НДФЛРасчетыСБюджетом.КПП,
	|	НДФЛРасчетыСБюджетом.КодПоОКАТО,
	|	НДФЛРасчетыСБюджетом.Налог КАК Сумма,
	|	НДФЛРасчетыСБюджетом.Период,
	|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента,
	|	НДФЛРасчетыСБюджетом.ПодразделениеОрганизации,
	|	НДФЛРасчетыСБюджетом.ОбособленноеПодразделение
	|ПОМЕСТИТЬ ВТРасчетыФЛ
	|ИЗ
	|	&РасчетыПоНДФЛ КАК НДФЛРасчетыСБюджетом
	|ГДЕ
	|	НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
	|	И НДФЛРасчетыСБюджетом.Налог <> 0";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДатыРегистра.НомерСтроки,
	|	ЕСТЬNULL(СтатусФизЛицКакНалогоплательщиковНДФЛ.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) КАК Статус
	|ПОМЕСТИТЬ ВТСтатусНалогоплательщика
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыФЛ.ФизЛицо КАК ФизЛицо,
	|		РасчетыФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|		МАКСИМУМ(СтатусФизЛицКакНалогоплательщиковНДФЛ.Период) КАК Период,
	|		РасчетыФЛ.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		ВТРасчетыФЛ КАК РасчетыФЛ
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусФизЛицКакНалогоплательщиковНДФЛ
	|			ПО РасчетыФЛ.ФизЛицо = СтатусФизЛицКакНалогоплательщиковНДФЛ.ФизЛицо
	|				И РасчетыФЛ.МесяцНалоговогоПериода >= СтатусФизЛицКакНалогоплательщиковНДФЛ.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РасчетыФЛ.ФизЛицо,
	|		РасчетыФЛ.МесяцНалоговогоПериода,
	|		РасчетыФЛ.НомерСтроки) КАК ДатыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусФизЛицКакНалогоплательщиковНДФЛ
	|		ПО ДатыРегистра.ФизЛицо = СтатусФизЛицКакНалогоплательщиковНДФЛ.ФизЛицо
	|			И ДатыРегистра.Период = СтатусФизЛицКакНалогоплательщиковНДФЛ.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыФЛ.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	РасчетыФЛ.ФизЛицо,
	|	РасчетыФЛ.ОбособленноеПодразделение КАК Организация,
	|	РасчетыФЛ.МесяцНалоговогоПериода,
	|	РасчетыФЛ.Сумма,
	|	ВЫБОР
	|		КОГДА РасчетыФЛ.КПП <> &ПустойКПП
	|			ТОГДА РасчетыФЛ.КПП
	|		КОГДА ЕСТЬNULL(РасчетыФЛ.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|			ТОГДА РасчетыФЛ.ПодразделениеОрганизации.КПП
	|		ИНАЧЕ ЕСТЬNULL(РасчетыФЛ.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|	КОНЕЦ КАК КПП,
	|	ВЫБОР
	|		КОГДА РасчетыФЛ.КодПоОКАТО <> &ПустойКодПоОКАТО
	|			ТОГДА РасчетыФЛ.КодПоОКАТО
	|		КОГДА ЕСТЬNULL(РасчетыФЛ.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|			ТОГДА РасчетыФЛ.ПодразделениеОрганизации.КодПоОКАТО
	|		ИНАЧЕ ЕСТЬNULL(РасчетыФЛ.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|	КОНЕЦ КАК КодПоОКАТО,
	|	ВЫБОР
	|		КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|			ТОГДА ВЫБОР
	|					КОГДА РасчетыФЛ.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|				КОНЕЦ
	|		КОГДА РасчетыФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|		КОГДА РасчетыФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|		КОГДА РасчетыФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Ставка,
	|	РасчетыФЛ.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТРасчетыФЛ КАК РасчетыФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусНалогоплательщика КАК Статусы
	|		ПО РасчетыФЛ.НомерСтроки = Статусы.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	РасчетыНалоговогоАгента = Запрос.Выполнить().Выгрузить();
	РасчетыНалоговогоАгента.Колонки.Добавить("ОКАТО_КПП");
	Для каждого СтрокаТЗ Из РасчетыНалоговогоАгента Цикл
		СтрокаТЗ.ОКАТО_КПП = СправкиПоНДФЛ.СуммаОКАТОиКПП(СтрокаТЗ.КодПоОКАТО, СтрокаТЗ.КПП)
	КонецЦикла;
	
	Возврат РасчетыНалоговогоАгента
	
КонецФункции // РасчетыНалоговогоАгентаПоУдержанномуНДФЛ()



// Собираем заработок для пособий за переданные календарные годы
//
// Параметры
//  МассивЛет - массив лет, за которые собирается заработок
//
// Возвращаемое значение:
//   Таблица значений
//
Функция ЗаработокДляВыплатыПособийСоцСтрахованияС2011года(МассивЛет, Физлицо, Организация, ТолькоПоОбособленномуПодразделению = Ложь) Экспорт 
	
	РаннийГод = 10000;
	ПозднийГод = 0;
	Для каждого Значение Из МассивЛет Цикл
		РаннийГод = Мин(Значение, РаннийГод);
		ПозднийГод = Макс(Значение, ПозднийГод);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	//Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивЛет", МассивЛет);
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Запрос.УстановитьПараметр("ОбособленноеПодразделение", Организация);
	Запрос.УстановитьПараметр("ПоВсейОрганизации", Не ТолькоПоОбособленномуПодразделению);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("Начало", Дата(РаннийГод, 1, 1));
	Запрос.УстановитьПараметр("Окончание", КонецДня(Дата(ПозднийГод, 12, 31)));
	Запрос.УстановитьПараметр("ДатаДоЗакона212", ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами() - 1);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ОсновныеНачисленияОрганизаций.Ссылка
	|ПОМЕСТИТЬ ВТДополнительныеНачисления
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисленияОрганизаций
	|ГДЕ
	|	(НЕ ОсновныеНачисленияОрганизаций.КодДоходаЕСН.ВходитВБазуФСС)
	|	И ОсновныеНачисленияОрганизаций.КодДоходаСтраховыеВзносы.ВходитВБазуФСС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СрезГражданства.НеИмеетПравоНаПенсию,
	|	НАЧАЛОПЕРИОДА(&Начало, МЕСЯЦ) КАК Период
	|ПОМЕСТИТЬ ВТПравоНаПенсию
	|ИЗ
	|	РегистрСведений.ГражданствоФизЛиц.СрезПоследних(&Начало, ФизЛицо = &ФизЛицо) КАК СрезГражданства
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ГражданствоФизЛиц.НеИмеетПравоНаПенсию,
	|	НАЧАЛОПЕРИОДА(ГражданствоФизЛиц.Период, МЕСЯЦ)
	|ИЗ
	|	РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|ГДЕ
	|	ГражданствоФизЛиц.ФизЛицо = &ФизЛицо
	|	И ГражданствоФизЛиц.Период >= &Начало
	|	И ГражданствоФизЛиц.Период <= &Окончание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(НачисленныеСуммыСПериодомПраваНаПенсию.Заработок) КАК Заработок,
	|	НачисленныеСуммыСПериодомПраваНаПенсию.РасчетныйГод КАК РасчетныйГод,
	|	НАЧАЛОПЕРИОДА(НачисленныеСуммыСПериодомПраваНаПенсию.БазовыйПериодНачало, ГОД) КАК БазовыйПериодНачало
	|ИЗ
	|	(ВЫБРАТЬ
	|		НачисленныеСуммы.Заработок КАК Заработок,
	|		НачисленныеСуммы.РасчетныйГод КАК РасчетныйГод,
	|		НачисленныеСуммы.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|		МАКСИМУМ(ПравоНаПенсию.Период) КАК ПериодПенсии
	|	ИЗ
	|		(ВЫБРАТЬ
	|			СУММА(ДанныеУчета.Заработок) КАК Заработок,
	|			ГОД(ДанныеУчета.Период) КАК РасчетныйГод,
	|			ДанныеУчета.Период КАК БазовыйПериодНачало
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ЕСНСведенияОДоходах.Результат - ЕСНСведенияОДоходах.Скидка КАК Заработок,
	|				ЕСНСведенияОДоходах.Период КАК Период
	|			ИЗ
	|				РегистрНакопления.ЕСНСведенияОДоходах КАК ЕСНСведенияОДоходах
	|			ГДЕ
	|				ЕСНСведенияОДоходах.ФизЛицо = &ФизЛицо
	|				И ЕСНСведенияОДоходах.Организация = &ГоловнаяОрганизация
	|				И ЕСНСведенияОДоходах.КодДоходаЕСН.ВходитВБазуФСС
	|				И ГОД(ЕСНСведенияОДоходах.Период) В (&МассивЛет)
	|				И ЕСНСведенияОДоходах.Период МЕЖДУ &Начало И &Окончание
	|				И (ЕСНСведенияОДоходах.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|						ИЛИ &ПоВсейОрганизации)
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				ОсновныеНачисленияРаботниковОрганизаций.Результат,
	|				ОсновныеНачисленияРаботниковОрганизаций.Ссылка.ПериодРегистрации
	|			ИЗ
	|				Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК ОсновныеНачисленияРаботниковОрганизаций
	|			ГДЕ
	|				ОсновныеНачисленияРаботниковОрганизаций.Физлицо = &ФизЛицо
	|				И ГОД(ОсновныеНачисленияРаботниковОрганизаций.Ссылка.ПериодРегистрации) В (&МассивЛет)
	|				И ОсновныеНачисленияРаботниковОрганизаций.Ссылка.ПериодРегистрации МЕЖДУ &Начало И &ДатаДоЗакона212
	|				И ОсновныеНачисленияРаботниковОрганизаций.Ссылка.Организация = &ОбособленноеПодразделение
	|				И ОсновныеНачисленияРаботниковОрганизаций.ВидРасчета В
	|						(ВЫБРАТЬ
	|							ВидыРасчета.Ссылка
	|						ИЗ
	|							ВТДополнительныеНачисления КАК ВидыРасчета)) КАК ДанныеУчета
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ГОД(ДанныеУчета.Период),
	|			ДанныеУчета.Период
	|		
	|		ИМЕЮЩИЕ
	|			СУММА(ДанныеУчета.Заработок) > 0
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			СтраховыеВзносыСведенияОДоходахОбороты.РезультатОборот - СтраховыеВзносыСведенияОДоходахОбороты.СкидкаОборот,
	|			ГОД(СтраховыеВзносыСведенияОДоходахОбороты.Период),
	|			СтраховыеВзносыСведенияОДоходахОбороты.Период
	|		ИЗ
	|			РегистрНакопления.СтраховыеВзносыСведенияОДоходах.Обороты(
	|					&Начало,
	|					&Окончание,
	|					Месяц,
	|					ВидДохода.ВходитВБазуФСС
	|						И ФизЛицо = &ФизЛицо
	|						И Организация = &ГоловнаяОрганизация
	|						И (ОбособленноеПодразделение = &ОбособленноеПодразделение
	|							ИЛИ &ПоВсейОрганизации)) КАК СтраховыеВзносыСведенияОДоходахОбороты
	|		ГДЕ
	|			ГОД(СтраховыеВзносыСведенияОДоходахОбороты.Период) В (&МассивЛет)
	|			И СтраховыеВзносыСведенияОДоходахОбороты.РезультатОборот > СтраховыеВзносыСведенияОДоходахОбороты.СкидкаОборот) КАК НачисленныеСуммы
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПравоНаПенсию КАК ПравоНаПенсию
	|			ПО (ПравоНаПенсию.Период <= НачисленныеСуммы.БазовыйПериодНачало)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		НачисленныеСуммы.Заработок,
	|		НачисленныеСуммы.РасчетныйГод,
	|		НачисленныеСуммы.БазовыйПериодНачало) КАК НачисленныеСуммыСПериодомПраваНаПенсию
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПравоНаПенсию КАК ПравоНаПенсию
	|		ПО (ПравоНаПенсию.Период = НачисленныеСуммыСПериодомПраваНаПенсию.ПериодПенсии)
	|ГДЕ
	|	ЕСТЬNULL((НЕ ПравоНаПенсию.НеИмеетПравоНаПенсию), ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленныеСуммыСПериодомПраваНаПенсию.РасчетныйГод,
	|	НАЧАЛОПЕРИОДА(НачисленныеСуммыСПериодомПраваНаПенсию.БазовыйПериодНачало, ГОД)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетныйГод";
	ДанныеРасчета = Запрос.Выполнить().Выгрузить();
		
	Возврат ДанныеРасчета	

КонецФункции // ЗаработокДляВыплатыПособийСоцСтрахованияС2011года()


Функция ВыделитьКоэффициентыРаспределенияИзКоллекцииСтрок(КоллекцияСтрокРаспределения, ИмяКолонки) Экспорт
	
	Коэффициенты = Новый Массив;
	
	Для каждого СтрокаРаспределения Из КоллекцияСтрокРаспределения Цикл
		Коэффициенты.Добавить(СтрокаРаспределения[ИмяКолонки]);
	КонецЦикла; 
	
	Возврат Коэффициенты
	
КонецФункции // ВыделитьКоэффициентыОтраженияИзМассиваСтрок()

// определяет ставку налогообложения по коду дохода
// Параметры:
//  КодДохода - ссылка на код дохода НДФЛ
// Возвращаемое значение:
//  число - ставка налогообложения в процентах
Функция СтавкаНалогообложенияДохода(КодДохода) Экспорт

	Если      КодДохода.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13 Тогда
		Возврат 13;

	ИначеЕсли КодДохода.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09 Тогда
		Возврат 9;

	ИначеЕсли КодДохода.СтавкаНалогообложенияРезидента = Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35 Тогда
		Возврат 35;

	КонецЕсли;

	Возврат 0;

КонецФункции

// Проверяет правильность заполнения реквизитов вида расчета 
// для некоторых случаев выдает сообщение об ошибке
// для некоторых - возвращает текст сообщения
// 	Параметры:
//		ВидРасчета - объект Вид расчета
//		Отказ - признак отказа (проверка не прошла)
//	Возвращаемое значение:
//		ТекстСообщения - текст сообщения о результате проверки
Функция ПроверитьНастройкуВидаРасчета(ВидРасчета, Отказ) Экспорт

	МетаданныеВидаРасчета = ВидРасчета.Метаданные();
	
	Если НЕ ЗначениеЗаполнено(ВидРасчета.Наименование) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Необходимо задать имя расчета!", Отказ);
	КонецЕсли; 

	Если НЕ ЗначениеЗаполнено(ВидРасчета.Код) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Необходимо задать код расчета!", Отказ);
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ВидРасчета.СпособОтраженияВБухучете) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан способ отражения в бухучете!", Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидРасчета.КодДоходаЕСН) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан код дохода ЕСН!", Отказ);
	КонецЕсли;
	

	Если Отказ Тогда
	    Возврат "";
	КонецЕсли; 
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОПИСЫВАЮТ СВЯЗИ ПРЕДОПРЕДЕЛЕННЫХ ВИДОВ РАСЧЕТА И СПОСОБОВ ИХ РАСЧЕТА

// Сворачивает таблицу значений в которой расположены движения НДФЛСведенияОДоходах
// опирается на структуру метаданных - сворачивает все ресурсы по измерениям+реквизитам 
// Параметры:
//	ДвиженияНДФЛСведенияОДоходах - таблица значений
// Возвращаемое значение:
//	нет
Процедура СвернутьДвиженияНДФЛСведенияОДоходах(ДвиженияНДФЛСведенияОДоходах) Экспорт

	СтрокаСуммирования = "";

	Для Каждого Изм Из Метаданные.РегистрыНакопления["НДФЛСведенияОДоходах"].Ресурсы Цикл
		СтрокаСуммирования = СтрокаСуммирования + Изм.Имя +",";
	КонецЦикла;

	СтрокаСуммирования = Лев(СтрокаСуммирования, СтрДлина(СтрокаСуммирования)-1);// удалим последнюю запятую

	СтрокаГруппировки = "Регистратор,Период,";

	Для Каждого Изм Из Метаданные.РегистрыНакопления["НДФЛСведенияОДоходах"].Измерения Цикл
		СтрокаГруппировки = СтрокаГруппировки + Изм.Имя +",";
	КонецЦикла;

	Для Каждого Изм Из Метаданные.РегистрыНакопления["НДФЛСведенияОДоходах"].Реквизиты Цикл
		СтрокаГруппировки = СтрокаГруппировки + Изм.Имя +",";
	КонецЦикла;

	СтрокаГруппировки = Лев(СтрокаГруппировки, СтрДлина(СтрокаГруппировки)-1);// удалим последнюю запятую

	ДвиженияНДФЛСведенияОДоходах.Свернуть(СтрокаГруппировки,СтрокаСуммирования);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАБОТЫ С ВИДАМИ РАСЧЕТА

// Эта функция возвращает информацию о виде расчета из переданного соответствия
// Если в соответствии не найдена информация о виде расчета - она подготавливается и 
// помещается в соответствие
// Применяется для работы с формами, в которых активно требуется получать 
// информацию о видах расчета, например, при выводе строки табличного поля
//
// Параметры:      
//	СведенияОВидахРасчета - соответствие, у которого в качестве ключа - ПланВидовРасчетаСсылка, а 
//  				  в качестве значения - структура из элементов
//					  РазмерТребуется - булево - если да, то при вводе такого 
//										вида расчета требуется проставлять значение "размер" 
//										(суммы, проценты и проч. показатели, используемые при расчете)
//
//	ВидРасчета - ПланВидовРасчетаСсылка
//
// Возвращаемое значение:
//  Описанная выше структура
//
Функция ПолучитьСведенияОВидеРасчета(СведенияОВидахРасчета, ВидРасчета) Экспорт
	СведенияОВидеРасчета = СведенияОВидахРасчета[ВидРасчета];
	Если СведенияОВидеРасчета = Неопределено Тогда
		СведенияОВидеРасчета = Новый Структура("КодДоходаНДФЛ");
		ТипВР = ТипЗнч(ВидРасчета);
		Если ТипВР = Тип("ПланВидовРасчетаСсылка.ОсновныеНачисленияОрганизаций") Тогда
			СведенияОВидеРасчета.КодДоходаНДФЛ = ВидРасчета.КодДоходаНДФЛ;
		Иначе
			СведенияОВидеРасчета.КодДоходаНДФЛ = Справочники.ДоходыНДФЛ.КодДоходаПоУмолчанию;
		КонецЕсли;
		СведенияОВидахРасчета[ВидРасчета] = СведенияОВидеРасчета;
	КонецЕсли;
	Возврат СведенияОВидеРасчета;
	
КонецФункции  // ПолучитьСведенияОВидеРасчета

// Возвращает список кодов дохода, не требующих ни ввода вычетов, ни исчисления налога по ставкам 9% или 35%
//
// Параметры
//  нет
//
// Возвращаемое значение:
//  список значений, содержащий ссылки на предопределенные эл-ты спр-ка ДоходыНДФЛ 
//
Функция ПолучитьСписокКодовДоходаОсновныхНачислений() Экспорт 

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДоходыНДФЛ.Ссылка,
	|	ДоходыНДФЛ.КодДляОтчетности + "" ("" + ДоходыНДФЛ.Наименование + "")"" КАК Представление
	|ИЗ
	|	Справочник.ДоходыНДФЛ КАК ДоходыНДФЛ
	|ГДЕ
	|	ДоходыНДФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)");

	СписокКодовДохода = Новый СписокЗначений;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокКодовДохода.Добавить(Выборка.Ссылка,Выборка.Представление);
	КонецЦикла;
	СписокКодовДохода.Добавить(Справочники.ДоходыНДФЛ.ПустаяСсылка(),"");
	Возврат СписокКодовДохода
КонецФункции // ПолучитьСписокКодовДоходаОсновныхНачислений

// Возвращает список кодов дохода, облагаемых по ставке 13%
//
// Параметры
//  нет
//
// Возвращаемое значение:
//  список значений, содержащий ссылки на предопределенные эл-ты спр-ка ДоходыНДФЛ 
//
Функция ПолучитьСписокКодовДоходаДополнительныхНачислений() Экспорт 

	СписокКодовДохода = Новый СписокЗначений;
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДоходыНДФЛ.Ссылка,
	|	ДоходыНДФЛ.Представление
	|ИЗ
	|	Справочник.ДоходыНДФЛ КАК ДоходыНДФЛ
	|ГДЕ
	|	ДоходыНДФЛ.СтавкаНалогообложенияРезидента = &СтавкаНалогообложенияРезидента");
						  
	Запрос.УстановитьПараметр("СтавкаНалогообложенияРезидента",Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокКодовДохода.Добавить(Выборка.Ссылка,Выборка.Представление);
	КонецЦикла;
	СписокКодовДохода.Добавить(Справочники.ДоходыНДФЛ.ПустаяСсылка(),"");
	
	Возврат СписокКодовДохода
	
КонецФункции // ПолучитьСписокКодовДоходаДопНачисленийТребующихДополнительнойОбработки()

Процедура ВписатьВРезультатЗарегистрированныеВУчетеДанныеНДФЛ(РезультатыРасчетов, ЗарегистрированныеВУчетеДанные, ИменаКолонок)
	
	Для каждого СтрокаТЗ Из ЗарегистрированныеВУчетеДанные Цикл
		ЗаполнитьЗначенияСвойств(РезультатыРасчетов.Добавить(), СтрокаТЗ, "ФизЛицо, МесяцНалоговогоПериода, Подразделение," + ИменаКолонок);
	КонецЦикла;
	
КонецПроцедуры



Процедура ВписатьРесурсВРезультатыРасчетовНДФЛ(РезультатыРасчетов, МесяцНалоговогоПериода, Сумма, ИмяРесурса, РаспределяемаяСтрока, СтрокиРаспределения, КоэффициентыРаспределения, СтрокаКлючей = "ФизЛицо, МесяцНалоговогоПериода", ПоказательРаспределения = "Подразделение") Экспорт 
	
	Если Сумма <> 0 Тогда
		РаспределениеРесурса = ОбщегоНазначенияЗК.РаспределитьПропорционально(Сумма,КоэффициентыРаспределения);
		Если РаспределениеРесурса = Неопределено Тогда
			НоваяСтрока = РезультатыРасчетов.Добавить();
			НоваяСтрока[ИмяРесурса] = Сумма; 
			ЗаполнитьЗначенияСвойств(НоваяСтрока, РаспределяемаяСтрока, СтрокаКлючей);
			НоваяСтрока.МесяцНалоговогоПериода = МесяцНалоговогоПериода; 
		Иначе
			Индекс = 0;
			Для каждого СтрокаТЗ Из СтрокиРаспределения Цикл
				НоваяСтрока = РезультатыРасчетов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ, СтрокаКлючей + ", " + ПоказательРаспределения);
				НоваяСтрока[ИмяРесурса] = РаспределениеРесурса[Индекс];
				НоваяСтрока.МесяцНалоговогоПериода = МесяцНалоговогоПериода; 
				Индекс = Индекс + 1;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


// Возвращает список кодов вычетов, относящихся к конкретным доходам
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   список значений, содержащий ссылки на предопределенные эл-ты спр-ка ВычетыНДФЛ
//
Функция ПолучитьСписокВычетовКДоходам() Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВычетыНДФЛ.Ссылка,
	|	ВычетыНДФЛ.Код,
	|	ВычетыНДФЛ.Наименование
	|ИЗ
	|	Справочник.ВычетыНДФЛ КАК ВычетыНДФЛ
	|ГДЕ
	|	(НЕ ВычетыНДФЛ.ПометкаУдаления)
	|	И ВычетыНДФЛ.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВычетыНДФЛ.Код");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СписокВычетов = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		СписокВычетов.Добавить(Выборка.Ссылка,СокрЛП(Выборка.Код) + " (" + СокрЛП(Выборка.Наименование) + ")");
	КонецЦикла;
	
	Возврат СписокВычетов
	
КонецФункции // ПолучитьСписокВычетовКДоходам()

// Возвращает список видов государственных единовременных пособий за счет ФСС
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   Список значений, содержащий подходящий перечень значений перечисления РазмерыГосударственныхПособий
//
Функция ПолучитьСписокРасходовПоСоцСтрахованию() Экспорт
	
	СписокПособий = Новый СписокЗначений;
	
	Для каждого ЗначениеПеречисления Из Перечисления.ВидыПособийСоциальногоСтрахования Цикл
	    Если ЗначениеПеречисления = Перечисления.ВидыПособийСоциальногоСтрахования.ПоУходуЗаРебенкомДоПолутораЛет Тогда
			Продолжить;
		КонецЕсли;
		СписокПособий.Добавить(ЗначениеПеречисления)
	КонецЦикла; 
	
	Возврат СписокПособий
	
КонецФункции

// Выполняет необходимые движения в регистрах по НДФЛ при выплате зарплаты
//
//
Процедура СформироватьРасчетыПоНДФЛПриВыплате(ВыборкаПоШапкеДокумента, ТекстТаблицыВыплаты = Неопределено, НаборЗаписейНДФЛ = Неопределено, НаборЗаписейРасчетыНА = Неопределено) Экспорт

	Если ТекстТаблицыВыплаты = Неопределено Или НаборЗаписейНДФЛ = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос;
    Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНДФЛ", ВыборкаПоШапкеДокумента.ДатаНДФЛ);
	Запрос.УстановитьПараметр("ДокументСсылка" , ВыборкаПоШапкеДокумента.Ссылка);
	Запрос.УстановитьПараметр("ОбособленноеПодразделение", ВыборкаПоШапкеДокумента.ОбособленноеПодразделение);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ВыборкаПоШапкеДокумента.Организация);

	// формирование временной таблицы выплат ВТВыплатыФизлицам
	Запрос.Текст = ТекстТаблицыВыплаты;
	Результат = Запрос.Выполнить().Выгрузить(); 
	
	Если Результат[0].Количество = 0 Тогда // количество произведенных выплат физлицам по документу
		Возврат 
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, ГОД) КАК НалоговыйПериод,
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	СУММА(НДФЛРасчетыСБюджетом.СальдоПоНалогу) КАК СальдоПоНалогу,
	|	СУММА(НДФЛРасчетыСБюджетом.Приходы) КАК Приходы,
	|	СУММА(НДФЛРасчетыСБюджетом.Расходы) КАК Расходы
	|ПОМЕСТИТЬ ВТОстаткиИОбороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|		НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|		НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|		ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НДФЛРасчетыСБюджетом.Налог
	|			ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
	|		КОНЕЦ КАК СальдоПоНалогу,
	|		ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НДФЛРасчетыСБюджетом.Налог
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Приходы,
	|		ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА 0
	|			ИНАЧЕ НДФЛРасчетыСБюджетом.Налог
	|		КОНЕЦ КАК Расходы
	|	ИЗ
	|		РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|	ГДЕ
	|		НДФЛРасчетыСБюджетом.Регистратор <> &ДокументСсылка
	|		И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
	|		И НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|		И НДФЛРасчетыСБюджетом.Период <= &ДатаНДФЛ
	|		И НДФЛРасчетыСБюджетом.ФизЛицо В(ВЫБРАТЬ ФизЛицо ИЗ ВТВыплатыФизлицам)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДФЛКЗачету.ФизЛицо,
	|		НАЧАЛОПЕРИОДА(НДФЛКЗачету.Период, МЕСЯЦ),
	|		ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13),
	|		ВЫБОР
	|			КОГДА НДФЛКЗачету.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НДФЛКЗачету.СуммаНДФЛКЗачету
	|			ИНАЧЕ -НДФЛКЗачету.СуммаНДФЛКЗачету
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА НДФЛКЗачету.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НДФЛКЗачету.СуммаНДФЛКЗачету
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА НДФЛКЗачету.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА 0
	|			ИНАЧЕ НДФЛКЗачету.СуммаНДФЛКЗачету
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.НДФЛКЗачету КАК НДФЛКЗачету
	|	ГДЕ
	|		НДФЛКЗачету.Регистратор <> &ДокументСсылка
	|		И НДФЛКЗачету.Организация = &ГоловнаяОрганизация
	|		И НДФЛКЗачету.Период <= &ДатаНДФЛ
	|		И НДФЛКЗачету.ФизЛицо В(ВЫБРАТЬ ФизЛицо ИЗ ВТВыплатыФизлицам)) КАК НДФЛРасчетыСБюджетом
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента,
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, ГОД)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	СтавкаНалогообложенияРезидента,
	|	НалоговыйПериод,
	|	МесяцНалоговогоПериода";

	Запрос.Выполнить();
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, ГОД) КАК НалоговыйПериод,
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	НДФЛРасчетыСБюджетом.КодПоОКАТО КАК КодПоОКАТО,
	|	НДФЛРасчетыСБюджетом.КПП КАК КПП,
	|	МАКСИМУМ(НДФЛРасчетыСБюджетом.Период) КАК Период,
	|	НДФЛРасчетыСБюджетом.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	НДФЛРасчетыСБюджетом.КодДохода КАК КодДохода,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НДФЛРасчетыСБюджетом.Налог
	|			ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
	|		КОНЕЦ) КАК СальдоПоНалогу,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НДФЛРасчетыСБюджетом.Налог
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Приходы,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА 0
	|			ИНАЧЕ НДФЛРасчетыСБюджетом.Налог
	|		КОНЕЦ) КАК Расходы
	|ПОМЕСТИТЬ ВТОстаткиИОборотыПодробно
	|ИЗ
	|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|ГДЕ
	|	НДФЛРасчетыСБюджетом.Регистратор <> &ДокументСсылка
	|	И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
	|	И НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|	И НДФЛРасчетыСБюджетом.Период <= &ДатаНДФЛ
	|	И НДФЛРасчетыСБюджетом.ФизЛицо В(ВЫБРАТЬ ФизЛицо ИЗ ВТВыплатыФизлицам)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛРасчетыСБюджетом.ФизЛицо,
	|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента,
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ),
	|	НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, ГОД),
	|	НДФЛРасчетыСБюджетом.КодПоОКАТО,
	|	НДФЛРасчетыСБюджетом.КПП,
	|	НДФЛРасчетыСБюджетом.ПодразделениеОрганизации,
	|	НДФЛРасчетыСБюджетом.КодДохода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	СтавкаНалогообложенияРезидента,
	|	НалоговыйПериод,
	|	МесяцНалоговогоПериода";

	Запрос.Выполнить();
	
	// Выборка долгов по НДФЛ в разрезе месяцев налогового периода по обособленному подразделению
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо КАК Физлицо,
	|	ЗарплатаКВыплатеОрганизацииЗарплата.Сумма КАК ВыплаченнаяСумма,
	|	НДФЛРасчетыСБюджетомПоМесяцам.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	НДФЛРасчетыСБюджетомПоГодам.НалоговыйПериод КАК НалоговыйПериод,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоГодам.СальдоПоНалогу, 0) КАК СальдоПоНалогуЗаНалоговыйПериод,
	|	НДФЛРасчетыСБюджетомПоМесяцам.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцам.СальдоПоНалогу, 0) КАК СальдоПоНалогуЗаМесяц,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего.СальдоПоНалогу, 0) КАК ВсегоЗаМесяцПоОКАТО,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.СальдоПоНалогу, 0) КАК СальдоПоОКАТО,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.КодПоОКАТО, """") КАК КодПоОКАТО,
	|	НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.Период КАК ДатаРегистрацииКодаОКАТО,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.КПП, """") КАК КПП,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.ПодразделениеОрганизации, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ПодразделениеОрганизации,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.КодДохода, ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.ПустаяСсылка)) КАК КодДохода
	|ИЗ
	|	ВТВыплатыФизлицам КАК ЗарплатаКВыплатеОрганизацииЗарплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиИОбороты КАК НДФЛРасчетыСБюджетомПоМесяцам
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиИОборотыПодробно КАК НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО
	|			ПО НДФЛРасчетыСБюджетомПоМесяцам.ФизЛицо = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.ФизЛицо
	|				И НДФЛРасчетыСБюджетомПоМесяцам.СтавкаНалогообложенияРезидента = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.СтавкаНалогообложенияРезидента
	|				И НДФЛРасчетыСБюджетомПоМесяцам.МесяцНалоговогоПериода = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.МесяцНалоговогоПериода
	|				И НДФЛРасчетыСБюджетомПоМесяцам.НалоговыйПериод = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.НалоговыйПериод
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|				НДФЛРасчетыСБюджетом.НалоговыйПериод КАК НалоговыйПериод,
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|				СУММА(НДФЛРасчетыСБюджетом.Приходы - НДФЛРасчетыСБюджетом.Расходы) КАК СальдоПоНалогу
	|			ИЗ
	|				ВТОстаткиИОбороты КАК НДФЛРасчетыСБюджетом
	|			
	|			СГРУППИРОВАТЬ ПО
	|				НДФЛРасчетыСБюджетом.ФизЛицо,
	|				НДФЛРасчетыСБюджетом.НалоговыйПериод,
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента) КАК НДФЛРасчетыСБюджетомПоГодам
	|			ПО НДФЛРасчетыСБюджетомПоМесяцам.ФизЛицо = НДФЛРасчетыСБюджетомПоГодам.ФизЛицо
	|				И НДФЛРасчетыСБюджетомПоМесяцам.НалоговыйПериод = НДФЛРасчетыСБюджетомПоГодам.НалоговыйПериод
	|				И НДФЛРасчетыСБюджетомПоМесяцам.СтавкаНалогообложенияРезидента = НДФЛРасчетыСБюджетомПоГодам.СтавкаНалогообложенияРезидента
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|				НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|				НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, ГОД) КАК НалоговыйПериод,
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|				СУММА(НДФЛРасчетыСБюджетом.Приходы - НДФЛРасчетыСБюджетом.Расходы) КАК СальдоПоНалогу
	|			ИЗ
	|				ВТОстаткиИОборотыПодробно КАК НДФЛРасчетыСБюджетом
	|			
	|			СГРУППИРОВАТЬ ПО
	|				НДФЛРасчетыСБюджетом.ФизЛицо,
	|				НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ),
	|				НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, ГОД),
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента) КАК НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего
	|			ПО НДФЛРасчетыСБюджетомПоМесяцам.ФизЛицо = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего.ФизЛицо
	|				И НДФЛРасчетыСБюджетомПоМесяцам.СтавкаНалогообложенияРезидента = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего.СтавкаНалогообложенияРезидента
	|				И НДФЛРасчетыСБюджетомПоМесяцам.МесяцНалоговогоПериода = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего.МесяцНалоговогоПериода
	|				И НДФЛРасчетыСБюджетомПоМесяцам.НалоговыйПериод = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего.НалоговыйПериод
	|		ПО ЗарплатаКВыплатеОрганизацииЗарплата.Физлицо = НДФЛРасчетыСБюджетомПоМесяцам.ФизЛицо
	|ГДЕ
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцам.СальдоПоНалогу, 0) > 0
	|	И ЕСТЬNULL(НДФЛРасчетыСБюджетомПоГодам.СальдоПоНалогу, 0) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Физлицо,
	|	СтавкаНалогообложенияРезидента,
	|	НалоговыйПериод,
	|	МесяцНалоговогоПериода,
	|	ДатаРегистрацииКодаОКАТО";

	ВременнаяТаблица = Новый ТаблицаЗначений;
	ВременнаяТаблица.Колонки.Добавить("КодПоОКАТО");
	ВременнаяТаблица.Колонки.Добавить("КПП");
	ВременнаяТаблица.Колонки.Добавить("ПодразделениеОрганизации");
	ВременнаяТаблица.Колонки.Добавить("КодДохода");
	ВременнаяТаблица.Колонки.Добавить("СальдоПоОКАТО");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Физлицо") Цикл 
		
		СуммаКВыплате = Выборка.ВыплаченнаяСумма;
		ОсталосьРаспределить = СуммаКВыплате;
		
		Пока Выборка.СледующийПоЗначениюПоля("СтавкаНалогообложенияРезидента") Цикл
			
			Пока Выборка.СледующийПоЗначениюПоля("НалоговыйПериод") Цикл
				// детальные записи по физлицу
				ОсталосьРаспределить = Мин(СуммаКВыплате,Выборка.СальдоПоНалогуЗаНалоговыйПериод);
				СуммаКВыплате = Макс(0,СуммаКВыплате - Выборка.СальдоПоНалогуЗаНалоговыйПериод);
				
				Пока Выборка.СледующийПоЗначениюПоля("МесяцНалоговогоПериода") И ОсталосьРаспределить > 0 Цикл
					
					// расчет суммы удерживаемого налога
					ПогашаемаяСумма = Мин(Выборка.СальдоПоНалогуЗаМесяц, ОсталосьРаспределить);
					ОсталосьРаспределить = ОсталосьРаспределить - ПогашаемаяСумма;
					
					Если Выборка.ВсегоЗаМесяцПоОКАТО = Выборка.СальдоПоОКАТО Или Выборка.ВсегоЗаМесяцПоОКАТО = 0 Тогда
						
						// запись удержания
						Движение = НаборЗаписейНДФЛ.Добавить();
						
						ЗаполнитьЗначенияСвойств(Движение,ВыборкаПоШапкеДокумента); // Период, ГоловнаяОрганизация, ОбособленноеПодразделение 
						ЗаполнитьЗначенияСвойств(Движение,Выборка); // ФизЛицо, СтавкаНалогообложенияРезидента, МесяцНалоговогоПериода 
																	// КодПоОКАТО, КПП, ПодразделениеОрганизации
						
						//свойства
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						// ресурсы
						Движение.Налог       = ПогашаемаяСумма;
						// реквизиты
						Движение.ВидСтроки	 = Перечисления.НДФЛРасчетыСБюджетомВидСтроки.Удержание;
						
					Иначе
						
						ФизЛицо = Выборка.ФизЛицо;
						СтавкаНалогообложенияРезидента = Выборка.СтавкаНалогообложенияРезидента;
						МесяцНалоговогоПериода = Выборка.МесяцНалоговогоПериода;
						
						ВременнаяТаблица.Очистить();
						Пока Выборка.Следующий() Цикл
							ЗаполнитьЗначенияСвойств(ВременнаяТаблица.Добавить(),Выборка);
						КонецЦикла;
						
						Результат = ОбщегоНазначенияЗК.РаспределитьПропорционально(ПогашаемаяСумма,ВременнаяТаблица.ВыгрузитьКолонку("СальдоПоОКАТО"));
						
						Для каждого СтрокаОКАТО Из ВременнаяТаблица Цикл
							
							// запись удержания
							Движение = НаборЗаписейНДФЛ.Добавить();
							ЗаполнитьЗначенияСвойств(Движение,ВыборкаПоШапкеДокумента); // Период, ГоловнаяОрганизация, ОбособленноеПодразделение 
							
							//свойства
							Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
							//измерения
							Движение.Физлицо     = ФизЛицо;
							Движение.СтавкаНалогообложенияРезидента = СтавкаНалогообложенияРезидента;
							Движение.МесяцНалоговогоПериода = МесяцНалоговогоПериода;
							// ресурсы
							Движение.Налог       = Результат[ВременнаяТаблица.Индекс(СтрокаОКАТО)];
							// реквизиты
							Движение.ВидСтроки	 = Перечисления.НДФЛРасчетыСБюджетомВидСтроки.Удержание;
							Движение.КодПоОКАТО	 = СтрокаОКАТО.КодПоОКАТО; 
							Движение.КПП		 = СтрокаОКАТО.КПП; 
							Движение.ПодразделениеОрганизации = СтрокаОКАТО.ПодразделениеОрганизации; 
							Движение.КодДохода	 = СтрокаОКАТО.КодДохода; 
							
						КонецЦикла;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
		КонецЦикла;
		
		ВременнаяТаблица.Очистить();
	КонецЦикла;

	Если НаборЗаписейРасчетыНА <> Неопределено И НаборЗаписейНДФЛ.Количество() > 0 Тогда
		НаборЗаписейРасчетыНА.Загрузить(ПроведениеРасчетов.РасчетыНалоговогоАгентаПоУдержанномуНДФЛ(НаборЗаписейНДФЛ.Выгрузить()))
	КонецЕсли;
			
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ДАТЫ ИЗМЕНЕНИЯ ЗАКОНОДАТЕЛЬСТВА

// Возвращает дату вступления в силу Федерального Закона от 24.07.2009 № 212-ФЗ
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаЗаменыЕСНСтраховымиВзносами() Экспорт 

	Возврат '20100101'

КонецФункции // ДатаЗаменыЕСНСтраховымиВзносами()

Функция ДатаРасширенияПеречняЛьготныхТарифовСтраховыхВзносов() Экспорт 

	Возврат '20110101'

КонецФункции // ДатаРасширенияПеречняЛьготныхТарифовСтраховыхВзносов()

// Возвращает дату вступления в силу Федерального Закона от 08.12.2010 № 343-ФЗ
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаНачалаРеформыСоцСтрахования() Экспорт 

	Возврат '20110101'

КонецФункции // ДатаЗаменыЕСНСтраховымиВзносами()

// Возвращает дату вступления в силу Федерального Закона от 27.07.2010 № 229-ФЗ
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаЗакона229ФЗ() Экспорт 

	Возврат '20110101'

КонецФункции // ДатаЗаменыЕСНСтраховымиВзносами()
