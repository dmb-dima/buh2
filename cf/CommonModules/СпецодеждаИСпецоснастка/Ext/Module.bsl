/////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ ПОГАШЕНИЯ СТОИМОСТИ

// Рассчитывает сумму погашения стоимости материалов в эксплуатации по бух.учету
//
// Параметры:
//  ДатаРасчета  - Дата - Дата расчета
//  Организация  - СправочникСсылка.Организации - организация, по которой 
//                 производится погашение стоимости
//
// Возвращаемое значение:
//   ТаблицаЗначений - суммы погашения по объектам
//
Функция РасчетСуммыПогашенияСтоимостиМатериалов(ДокументОбъект, Организация, ТаблицаНоменклатуры = Неопределено, РассчитатьНаКонецМесяца = Ложь, ВыдаватьСообщения = Истина, Отказ = Ложь) Экспорт
	
	// Добавим в таблицу номенклатуры колонки для хранения суммы погашения.
	Если НЕ (ТаблицаНоменклатуры = Неопределено) Тогда
		
		Если ТаблицаНоменклатуры.Колонки.Найти("СуммаПогашения") = Неопределено Тогда
			ТаблицаНоменклатуры.Колонки.Добавить("СуммаПогашения", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
		КонецЕсли;
		
		Если ТаблицаНоменклатуры.Колонки.Найти("СуммаПогашенияПР") = Неопределено Тогда
			ТаблицаНоменклатуры.Колонки.Добавить("СуммаПогашенияПР", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
		КонецЕсли;
		
		Если ТаблицаНоменклатуры.Колонки.Найти("СуммаПогашенияВР") = Неопределено Тогда
			ТаблицаНоменклатуры.Колонки.Добавить("СуммаПогашенияВР", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаСумм = Новый ТаблицаЗначений();
	
	ТаблицаСумм.Колонки.Добавить("Номенклатура");
	ТаблицаСумм.Колонки.Добавить("ДокументПередачи");
	ТаблицаСумм.Колонки.Добавить("ПодразделениеФизЛицо");
	ТаблицаСумм.Колонки.Добавить("НаправлениеАмортизации");
	ТаблицаСумм.Колонки.Добавить("СчетУчета");
	ТаблицаСумм.Колонки.Добавить("СчетАмортизации");
	ТаблицаСумм.Колонки.Добавить("ПодразделениеОрганизации");
	ТаблицаСумм.Колонки.Добавить("Количество");
	ТаблицаСумм.Колонки.Добавить("КоличествоМЦ");
	ТаблицаСумм.Колонки.Добавить("Сумма");
	ТаблицаСумм.Колонки.Добавить("СуммаНУ");
	ТаблицаСумм.Колонки.Добавить("СуммаПР");
	ТаблицаСумм.Колонки.Добавить("СуммаВР");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("НачалоПериода",                       НачалоМесяца(ДокументОбъект.Дата));
	
	Если РассчитатьНаКонецМесяца Тогда
		Запрос.УстановитьПараметр("КонецПериода",                    КонецМесяца(ДокументОбъект.Дата));
	Иначе
		Запрос.УстановитьПараметр("КонецПериода",                    Новый Граница(ДокументОбъект.МоментВремени(), ВидГраницы.Исключая));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация",                         Организация);
	Запрос.УстановитьПараметр("СчетУчетаСпецодеждыЗабалансовыйБУ",   ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный);
	Запрос.УстановитьПараметр("СчетУчетаСпецоснасткиЗабалансовыйБУ", ПланыСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный);
	Запрос.УстановитьПараметр("СчетУчетаМЦ04", 						 ПланыСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежностиВЭксплуатации);
	Запрос.УстановитьПараметр("СчетУчетаСпецодеждыБУ", 				 ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатации);
	Запрос.УстановитьПараметр("СчетУчетаСпецоснасткиБУ", 			 ПланыСчетов.Хозрасчетный.СпецоснасткаВЭксплуатации);
	
	Если ТаблицаНоменклатуры = Неопределено Тогда
		
		ТекстУсловияСпецодежда   = "";
		ТекстУсловияСпецоснастка = "";
		
	Иначе
		
		Запрос.УстановитьПараметр("ВнешнийИсточник", ТаблицаНоменклатуры);
		Запрос.Текст =
		"ВЫБРАТЬ
		|   Номенклатура,
		|   ПартияМатериаловВЭксплуатации,
		|   ПодразделениеФизЛицо,
		|   ПодразделениеОрганизации,
		|	Количество
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ &ВнешнийИсточник КАК ВнешнийИсточник
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,ПартияМатериаловВЭксплуатации,ПодразделениеФизЛицо,ПодразделениеОрганизации
		|;
		|";
		
		Запрос.УстановитьПараметр("СписокПартийМатериаловВЭксплуатации", ОбщегоНазначения.УдалитьПовторяющиесяЭлементыМассива(ТаблицаНоменклатуры.ВыгрузитьКолонку("ПартияМатериаловВЭксплуатации")));
		ТекстУсловияСпецоснастка = " И Субконто1 В (Выбрать Номенклатура из ТаблицаНоменклатуры) И Субконто2 В (&СписокПартийМатериаловВЭксплуатации)";
		
	Конецесли;
	
	ВидыСубконтоСпецодежда = Новый Массив;
	ВидыСубконтоСпецодежда.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ВидыСубконтоСпецодежда.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации);
	ВидыСубконтоСпецодежда.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
	
	ВидыСубконтоСпецоснастка = Новый Массив;
	ВидыСубконтоСпецоснастка.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ВидыСубконтоСпецоснастка.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации);
	
	Запрос.УстановитьПараметр("ВидыСубконтоСпецодежда",   ВидыСубконтоСпецодежда);
	Запрос.УстановитьПараметр("ВидыСубконтоСпецоснастка", ВидыСубконтоСпецоснастка);
	
	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОборотыМЦ.Счет,
	|	ХозрасчетныйОстаткиИОборотыМЦ.Субконто1 КАК Номенклатура,
	|	ХозрасчетныйОстаткиИОборотыМЦ.Субконто2 КАК ДокументПередачи,
	|	ХозрасчетныйОстаткиИОборотыМЦ.Субконто3 КАК ПодразделениеФизЛицо,
	|	ХозрасчетныйОстаткиИОборотыМЦ.Подразделение КАК ПодразделениеОрганизации,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаНачальныйОстатокДт КАК ПервоначальнаяСтоимостьНачальныйОстаток,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаПРНачальныйОстатокДт КАК ПервоначальнаяСтоимостьПРНачальныйОстаток,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаВРНачальныйОстатокДт КАК ПервоначальнаяСтоимостьВРНачальныйОстаток,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаКонечныйОстатокДт КАК ПервоначальнаяСтоимостьКонечныйОстаток,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаПРКонечныйОстатокДт КАК ПервоначальнаяСтоимостьПРКонечныйОстаток,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаВРКонечныйОстатокДт КАК ПервоначальнаяСтоимостьВРКонечныйОстаток,
	|	ХозрасчетныйОстаткиИОборотыМЦ.КоличествоНачальныйОстатокДт КАК КоличествоМЦ
	|ПОМЕСТИТЬ ХозрасчетныйОстаткиИОборотыМЦ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, Период,, Счет В ИЕРАРХИИ (&СчетУчетаСпецодеждыЗабалансовыйБУ), &ВидыСубконтоСпецодежда, Организация = &Организация"+ТекстУсловияСпецоснастка+") КАК ХозрасчетныйОстаткиИОборотыМЦ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОборотыМЦ.Счет,
	|	ХозрасчетныйОстаткиИОборотыМЦ.Субконто1,
	|	ХозрасчетныйОстаткиИОборотыМЦ.Субконто2,
	|	NULL,
	|	ХозрасчетныйОстаткиИОборотыМЦ.Подразделение,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаПРНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаВРНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаПРКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОборотыМЦ.СуммаВРКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОборотыМЦ.КоличествоНачальныйОстатокДт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, Период,, Счет В ИЕРАРХИИ (&СчетУчетаСпецоснасткиЗабалансовыйБУ), &ВидыСубконтоСпецоснастка, Организация = &Организация"+ТекстУсловияСпецоснастка+") КАК ХозрасчетныйОстаткиИОборотыМЦ
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,ДокументПередачи,ПодразделениеФизЛицо,ПодразделениеОрганизации
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Счет,
	|	ХозрасчетныйОстаткиИОбороты.Субконто1,
	|	ХозрасчетныйОстаткиИОбороты.Субконто2,
	|	ХозрасчетныйОстаткиИОбороты.Субконто3,
	|	ХозрасчетныйОстаткиИОбороты.Подразделение,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СтоимостьНачальныйОстаток,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРНачальныйОстатокДт КАК СтоимостьПРНачальныйОстаток,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРНачальныйОстатокДт КАК СтоимостьВРНачальныйОстаток,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СтоимостьКонечныйОстаток,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРКонечныйОстатокДт КАК СтоимостьПРКонечныйОстаток,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокДт КАК СтоимостьВРКонечныйОстаток,
	|	ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстатокДт КАК КоличествоКонечныйОстаток,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК СуммаОборотКт,
	|	ХозрасчетныйОстаткиИОбороты.КоличествоОборотКт КАК КоличествоОборотКт
	|ПОМЕСТИТЬ ХозрасчетныйОстаткиИОбороты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, Период,, Счет В ИЕРАРХИИ (&СчетУчетаСпецодеждыБУ, &СчетУчетаМЦ04),  &ВидыСубконтоСпецодежда, Организация = &Организация"+ТекстУсловияСпецоснастка+") КАК ХозрасчетныйОстаткиИОбороты
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОбороты.Счет,
	|	ХозрасчетныйОстаткиИОбороты.Субконто1,
	|	ХозрасчетныйОстаткиИОбороты.Субконто2,
	|	NULL,
	|	ХозрасчетныйОстаткиИОбороты.Подразделение,
	|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРНачальныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаПРКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстатокДт,
	|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт,
	|	ХозрасчетныйОстаткиИОбороты.КоличествоОборотКт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, Период,, Счет В ИЕРАРХИИ (&СчетУчетаСпецоснасткиБУ), &ВидыСубконтоСпецоснастка, Организация = &Организация"+ТекстУсловияСпецоснастка+") КАК ХозрасчетныйОстаткиИОбороты
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто1,Субконто2,Субконто3,Подразделение
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаМатериаловОбороты.Номенклатура,
	|	ВыработкаМатериаловОбороты.КоличествоОборот
	|ПОМЕСТИТЬ
	|	ВыработкаМатериаловОбороты
	|ИЗ
	|	РегистрНакопления.ВыработкаМатериалов.Обороты(&НачалоПериода, &КонецПериода, Период, Организация = &Организация) КАК ВыработкаМатериаловОбороты
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеФизЛицо,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьНачальныйОстаток, 0) КАК ПервоначальнаяСтоимостьНачальныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьПРНачальныйОстаток, 0) КАК ПервоначальнаяСтоимостьПРНачальныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьВРНачальныйОстаток, 0) КАК ПервоначальнаяСтоимостьВРНачальныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьКонечныйОстаток, 0) КАК ПервоначальнаяСтоимостьКонечныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьПРКонечныйОстаток, 0) КАК ПервоначальнаяСтоимостьПРКонечныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьВРКонечныйОстаток, 0) КАК ПервоначальнаяСтоимостьВРКонечныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.КоличествоМЦ, 0) КАК КоличествоМЦ,
	|	ХозрасчетныйОстаткиИОбороты.Счет,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьНачальныйОстаток, 0) КАК СтоимостьНачальныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьПРНачальныйОстаток, 0) КАК СтоимостьПРНачальныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьВРНачальныйОстаток, 0) КАК СтоимостьВРНачальныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьКонечныйОстаток, 0) КАК СтоимостьКонечныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьВРКонечныйОстаток, 0) КАК СтоимостьВРКонечныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьПРКонечныйОстаток, 0) КАК СтоимостьПРКонечныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстаток, 0) КАК КоличествоКонечныйОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СуммаОборотКт, 0) КАК СуммаОборотКт,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.КоличествоОборотКт, 0) КАК КоличествоОборотКт,
	|	ПередачаМатериаловВЭксплуатациюСпецодежда.НазначениеИспользования КАК НазначениеИспользования,
	|	ПередачаМатериаловВЭксплуатациюСпецодежда.НазначениеИспользования.СпособПогашенияСтоимости КАК СпособПогашенияСтоимости,
	|	ЕСТЬNULL(ПередачаМатериаловВЭксплуатациюСпецодежда.НазначениеИспользования.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользования,
	|	ЕСТЬNULL(ПередачаМатериаловВЭксплуатациюСпецодежда.НазначениеИспользования.ОбщийОбъемПродукцииРабот, 0) КАК ОбъемПродукцииРабот,
	|	ПередачаМатериаловВЭксплуатациюСпецодежда.НазначениеИспользования.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	ЕСТЬNULL(" + ?(ТаблицаНоменклатуры = Неопределено, "ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстаток", "ТаблицаНоменклатуры.Количество") + ", 0) КАК Количество,
	|	0 КАК Выработка
	|ИЗ
	|	ХозрасчетныйОстаткиИОборотыМЦ";
	
	Если ТаблицаНоменклатуры <> Неопределено Тогда
		
		Запрос.Текст = Запрос.Текст +
		" 
		| ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры
		| ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ТаблицаНоменклатуры.Номенклатура
		| И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации
		| И ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеФизЛицо = ТаблицаНоменклатуры.ПодразделениеФизЛицо
		| И (ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации = ТаблицаНоменклатуры.ПодразделениеОрганизации
		|    ИЛИ ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации ЕСТЬ NULL)
		|";
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст +"
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ХозрасчетныйОстаткиИОбороты.Субконто1
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ХозрасчетныйОстаткиИОбороты.Субконто2
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеФизЛицо = ХозрасчетныйОстаткиИОбороты.Субконто3
	|			И (ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации = ХозрасчетныйОстаткиИОбороты.Подразделение
	|			   ИЛИ ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации ЕСТЬ NULL)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаМатериаловВЭксплуатацию.Спецодежда КАК ПередачаМатериаловВЭксплуатациюСпецодежда
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ПередачаМатериаловВЭксплуатациюСпецодежда.Номенклатура
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ПередачаМатериаловВЭксплуатациюСпецодежда.Ссылка
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеФизЛицо = ПередачаМатериаловВЭксплуатациюСпецодежда.ФизЛицо
	|			И (ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации = ПередачаМатериаловВЭксплуатациюСпецодежда.Ссылка.Местонахождение
	|			   ИЛИ ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации ЕСТЬ NULL)
	|ГДЕ
	|	ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию
	|	И ХозрасчетныйОстаткиИОборотыМЦ.Счет В ИЕРАРХИИ(&СчетУчетаСпецодеждыЗабалансовыйБУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеФизЛицо,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьПРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьВРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьПРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьВРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.КоличествоМЦ, 0),
	|	ХозрасчетныйОстаткиИОбороты.Счет,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьПРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьВРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьВРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьПРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СуммаОборотКт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.КоличествоОборотКт, 0),
	|	ПередачаМатериаловВЭксплуатациюСпецоснастка.НазначениеИспользования,
	|	ПередачаМатериаловВЭксплуатациюСпецоснастка.НазначениеИспользования.СпособПогашенияСтоимости,
	|	ЕСТЬNULL(ПередачаМатериаловВЭксплуатациюСпецоснастка.НазначениеИспользования.СрокПолезногоИспользования, 0),
	|	ЕСТЬNULL(ПередачаМатериаловВЭксплуатациюСпецоснастка.НазначениеИспользования.ОбщийОбъемПродукцииРабот, 0),
	|	ПередачаМатериаловВЭксплуатациюСпецоснастка.НазначениеИспользования.СпособОтраженияРасходов,
	|	ЕСТЬNULL(" + ?(ТаблицаНоменклатуры = Неопределено, "ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстаток", "ТаблицаНоменклатуры.Количество") + ", 0),
	|	ЕСТЬNULL(ВыработкаМатериаловОбороты.КоличествоОборот, 0)
	|ИЗ
	|	ХозрасчетныйОстаткиИОборотыМЦ";
	
	Если ТаблицаНоменклатуры <> Неопределено Тогда
		
		Запрос.Текст = Запрос.Текст +
		" 
		| ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры
		| ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ТаблицаНоменклатуры.Номенклатура
		| И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации
		| И ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации = ТаблицаНоменклатуры.ПодразделениеОрганизации
		|";
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст +"
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ХозрасчетныйОстаткиИОбороты.Субконто1
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ХозрасчетныйОстаткиИОбороты.Субконто2
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации = ХозрасчетныйОстаткиИОбороты.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаМатериаловВЭксплуатацию.Спецоснастка КАК ПередачаМатериаловВЭксплуатациюСпецоснастка
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ПередачаМатериаловВЭксплуатациюСпецоснастка.Номенклатура
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ПередачаМатериаловВЭксплуатациюСпецоснастка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыработкаМатериаловОбороты КАК ВыработкаМатериаловОбороты
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ВыработкаМатериаловОбороты.Номенклатура
	|ГДЕ
	|	ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию
	|	И ХозрасчетныйОстаткиИОборотыМЦ.Счет В ИЕРАРХИИ(&СчетУчетаСпецоснасткиЗабалансовыйБУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеФизЛицо,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьПРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьВРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьПРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьВРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.КоличествоМЦ, 0),
	|	ХозрасчетныйОстаткиИОбороты.Счет,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьПРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьВРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьВРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьПРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СуммаОборотКт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.КоличествоОборотКт, 0),
	|	ПартияМатериаловВЭксплуатацииСпецодежда.НазначениеИспользования,
	|	ПартияМатериаловВЭксплуатацииСпецодежда.НазначениеИспользования.СпособПогашенияСтоимости,
	|	ЕСТЬNULL(ПартияМатериаловВЭксплуатацииСпецодежда.НазначениеИспользования.СрокПолезногоИспользования, 0),
	|	ЕСТЬNULL(ПартияМатериаловВЭксплуатацииСпецодежда.НазначениеИспользования.ОбщийОбъемПродукцииРабот, 0),
	|	ПартияМатериаловВЭксплуатацииСпецодежда.НазначениеИспользования.СпособОтраженияРасходов,
	|	ЕСТЬNULL(" + ?(ТаблицаНоменклатуры = Неопределено, "ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстаток", "ТаблицаНоменклатуры.Количество") + ", 0),
	|	0
	|ИЗ
	|	ХозрасчетныйОстаткиИОборотыМЦ";
	
	Если ТаблицаНоменклатуры <> Неопределено Тогда
		
		Запрос.Текст = Запрос.Текст +
		" 
		| ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры
		| ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ТаблицаНоменклатуры.Номенклатура
		| И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации
		| И ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеФизЛицо = ТаблицаНоменклатуры.ПодразделениеФизЛицо
		| И (ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации = ТаблицаНоменклатуры.ПодразделениеОрганизации
		|    ИЛИ ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации ЕСТЬ NULL)
		|";
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст +"
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ХозрасчетныйОстаткиИОбороты.Субконто1
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ХозрасчетныйОстаткиИОбороты.Субконто2
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеФизЛицо = ХозрасчетныйОстаткиИОбороты.Субконто3
	|			И (ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации = ХозрасчетныйОстаткиИОбороты.Подразделение
	|			  ИЛИ ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации ЕСТЬ NULL)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПартияМатериаловВЭксплуатации КАК ПартияМатериаловВЭксплуатацииСпецодежда
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ПартияМатериаловВЭксплуатацииСпецодежда.Номенклатура
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ПартияМатериаловВЭксплуатацииСпецодежда.Ссылка
	|ГДЕ
	|	ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи ССЫЛКА Документ.ПартияМатериаловВЭксплуатации
	|	И ХозрасчетныйОстаткиИОборотыМЦ.Счет В ИЕРАРХИИ(&СчетУчетаСпецодеждыЗабалансовыйБУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеФизЛицо,
	|	ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьПРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьВРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьПРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.ПервоначальнаяСтоимостьВРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОборотыМЦ.КоличествоМЦ, 0),
	|	ХозрасчетныйОстаткиИОбороты.Счет,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьПРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьВРНачальныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьВРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СтоимостьПРКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстаток, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.СуммаОборотКт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты.КоличествоОборотКт, 0),
	|	ПартияМатериаловВЭксплуатацииСпецоснастка.НазначениеИспользования,
	|	ПартияМатериаловВЭксплуатацииСпецоснастка.НазначениеИспользования.СпособПогашенияСтоимости,
	|	ЕСТЬNULL(ПартияМатериаловВЭксплуатацииСпецоснастка.НазначениеИспользования.СрокПолезногоИспользования, 0),
	|	ЕСТЬNULL(ПартияМатериаловВЭксплуатацииСпецоснастка.НазначениеИспользования.ОбщийОбъемПродукцииРабот, 0),
	|	ПартияМатериаловВЭксплуатацииСпецоснастка.НазначениеИспользования.СпособОтраженияРасходов,
	|	ЕСТЬNULL(" + ?(ТаблицаНоменклатуры = Неопределено, "ХозрасчетныйОстаткиИОбороты.КоличествоКонечныйОстаток", "ТаблицаНоменклатуры.Количество") + ", 0),
	|	ЕСТЬNULL(ВыработкаМатериаловОбороты.КоличествоОборот, 0)
	|ИЗ
	|	ХозрасчетныйОстаткиИОборотыМЦ";
	
	Если ТаблицаНоменклатуры <> Неопределено Тогда
		
		Запрос.Текст = Запрос.Текст +
		" 
		| ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры
		| ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ТаблицаНоменклатуры.Номенклатура
		| И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ТаблицаНоменклатуры.ПартияМатериаловВЭксплуатации
		| И ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации = ТаблицаНоменклатуры.ПодразделениеОрганизации
		|";
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ХозрасчетныйОстаткиИОбороты.Субконто1
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ХозрасчетныйОстаткиИОбороты.Субконто2
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ПодразделениеОрганизации = ХозрасчетныйОстаткиИОбороты.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПартияМатериаловВЭксплуатации КАК ПартияМатериаловВЭксплуатацииСпецоснастка
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ПартияМатериаловВЭксплуатацииСпецоснастка.Номенклатура
	|			И ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи = ПартияМатериаловВЭксплуатацииСпецоснастка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыработкаМатериаловОбороты КАК ВыработкаМатериаловОбороты
	|		ПО ХозрасчетныйОстаткиИОборотыМЦ.Номенклатура = ВыработкаМатериаловОбороты.Номенклатура
	|ГДЕ
	|	ХозрасчетныйОстаткиИОборотыМЦ.ДокументПередачи ССЫЛКА Документ.ПартияМатериаловВЭксплуатации
	|	И ХозрасчетныйОстаткиИОборотыМЦ.Счет В ИЕРАРХИИ(&СчетУчетаСпецоснасткиЗабалансовыйБУ)";
	
	ВыборкаМатериалов = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаМатериалов.Следующий() Цикл
		
		Если (ВыборкаМатериалов.СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию)
			ИЛИ (ВыборкаМатериалов.СтоимостьНачальныйОстаток <= 0) Тогда
			// Материал не имеет остаточной стоимости на начало месяца
			// или списан на затраты при передаче - дополнительная обработка не требуется.
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаМатериалов.СпособПогашенияСтоимости) Тогда
			Если ВыдаватьСообщения = Истина Тогда
				ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("Не указан способ погашения стоимости для материала <" + ВыборкаМатериалов.Номенклатура + "> в назначении использования <"+ВыборкаМатериалов.НазначениеИспользования+">. 
				|Рекомендуется проверить данные документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаМатериалов.СрокПолезногоИспользования)
			И (ВыборкаМатериалов.СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.Линейный) Тогда
			Если ВыдаватьСообщения = Истина Тогда
				ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("Не указан срок использования для материала <" + ВыборкаМатериалов.Номенклатура + "> в назначении использования <"+ВыборкаМатериалов.НазначениеИспользования+">.
				|Рекомендуется проверить данные документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		СуммаПогашения   = 0;
		СуммаПогашенияПР = 0;
		СуммаПогашенияВР = 0;
		
		Если ВыборкаМатериалов.СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.Линейный Тогда
			
			СуммаПогашения   = ВыборкаМатериалов.ПервоначальнаяСтоимостьКонечныйОстаток / ВыборкаМатериалов.СрокПолезногоИспользования;
			СуммаПогашенияПР = ВыборкаМатериалов.ПервоначальнаяСтоимостьПРКонечныйОстаток / ВыборкаМатериалов.СрокПолезногоИспользования;
			СуммаПогашенияВР = ВыборкаМатериалов.ПервоначальнаяСтоимостьВРКонечныйОстаток / ВыборкаМатериалов.СрокПолезногоИспользования;
			
		ИначеЕсли ВыборкаМатериалов.СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.ПропорциональноОбъемуПродукцииРабот Тогда
			
			Если НЕ ЗначениеЗаполнено(ВыборкаМатериалов.Выработка) Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ВыборкаМатериалов.ОбъемПродукцииРабот) Тогда
				Если ВыдаватьСообщения = Истина Тогда
				ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("Не определен объем продукции для вычисления погашения стоимости для материала <" + ВыборкаМатериалов.Номенклатура + "> в назначении использования <"+ВыборкаМатериалов.НазначениеИспользования+">.
				|Рекомендуется проверить данные документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			СуммаПогашения   = ВыборкаМатериалов.ПервоначальнаяСтоимостьКонечныйОстаток * ВыборкаМатериалов.Выработка / ВыборкаМатериалов.ОбъемПродукцииРабот;
			СуммаПогашенияПР = ВыборкаМатериалов.ПервоначальнаяСтоимостьПРКонечныйОстаток * ВыборкаМатериалов.Выработка / ВыборкаМатериалов.ОбъемПродукцииРабот;
			СуммаПогашенияВР = ВыборкаМатериалов.ПервоначальнаяСтоимостьВРКонечныйОстаток * ВыборкаМатериалов.Выработка / ВыборкаМатериалов.ОбъемПродукцииРабот;
			
		КонецЕсли;
		
		Количество = ВыборкаМатериалов.КоличествоКонечныйОстаток;
		
		Если НЕ (ТаблицаНоменклатуры = Неопределено) Тогда
			ТаблицаНоменклатуры.Индексы.Добавить("Номенклатура, ПартияМатериаловВЭксплуатации, ПодразделениеФизЛицо");
		КонецЕсли;
		
		// Сумма погашения рассчитывается пропорционально количеству материалов, по
		// которым погашается стоимость.
		СуммаПогашения = ?(ВыборкаМатериалов.КоличествоКонечныйОстаток > 0,
		                   СуммаПогашения / ВыборкаМатериалов.КоличествоКонечныйОстаток * ВыборкаМатериалов.Количество,
		                   0);
		
		СуммаПогашенияПР =  ?(ВыборкаМатериалов.КоличествоКонечныйОстаток > 0,
		                      СуммаПогашенияПР / ВыборкаМатериалов.КоличествоКонечныйОстаток * ВыборкаМатериалов.Количество,
		                      0);
		
		СуммаПогашенияВР =  ?(ВыборкаМатериалов.КоличествоКонечныйОстаток > 0,
		                      СуммаПогашенияВР / ВыборкаМатериалов.КоличествоКонечныйОстаток * ВыборкаМатериалов.Количество,
		                      0);
		
		// Сумма погашения не может быть меньше 0 и больше остаточной стоимости.
		СуммаПогашения   = Макс(0, Мин(ВыборкаМатериалов.СтоимостьКонечныйОстаток, СуммаПогашения));
		СуммаПогашенияПР = Макс(0, Мин(ВыборкаМатериалов.СтоимостьПРКонечныйОстаток, СуммаПогашенияПР));
		СуммаПогашенияВР = Макс(0, Мин(ВыборкаМатериалов.СтоимостьВРКонечныйОстаток, СуммаПогашенияВР));
		
		// Если установлен список номенклатуры, для которой рассчитывается сумма погашения,
		// дополним таблицу значений колонкой "СуммаПогашения".
		Если НЕ (ТаблицаНоменклатуры = Неопределено) Тогда
			
			Если ТипЗнч(ВыборкаМатериалов.ПодразделениеФизЛицо) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				
				МассивСтрок = ТаблицаНоменклатуры.НайтиСтроки(Новый Структура("Номенклатура, ПартияМатериаловВЭксплуатации, ПодразделениеФизЛицо" ,
				ВыборкаМатериалов.Номенклатура,
				ВыборкаМатериалов.ДокументПередачи,
				ВыборкаМатериалов.ПодразделениеФизЛицо));
				
			Иначе
				
				МассивСтрок = ТаблицаНоменклатуры.НайтиСтроки(Новый Структура("Номенклатура, ПартияМатериаловВЭксплуатации" ,
				ВыборкаМатериалов.Номенклатура,
				ВыборкаМатериалов.ДокументПередачи));
				
			КонецЕсли;
			
			Если МассивСтрок.Количество() > 0 Тогда
				МассивСтрок[0].СуммаПогашения = СуммаПогашения;
				МассивСтрок[0].СуммаПогашенияПР = СуммаПогашенияПР;
				МассивСтрок[0].СуммаПогашенияВР = СуммаПогашенияВР;
			КонецЕсли;  
			
		КонецЕсли;
		
		СтрокаПогашения = ТаблицаСумм.Добавить();
		
		СтрокаПогашения.Номенклатура             = ВыборкаМатериалов.Номенклатура;
		СтрокаПогашения.ДокументПередачи         = ВыборкаМатериалов.ДокументПередачи;
		СтрокаПогашения.ПодразделениеФизЛицо     = ВыборкаМатериалов.ПодразделениеФизЛицо;
		СтрокаПогашения.НаправлениеАмортизации   = ВыборкаМатериалов.СпособОтраженияРасходов;
		СтрокаПогашения.СчетУчета                = ВыборкаМатериалов.Счет;
		СтрокаПогашения.СчетАмортизации          = ВыборкаМатериалов.Счет;
		СтрокаПогашения.ПодразделениеОрганизации = ВыборкаМатериалов.ПодразделениеОрганизации;
		СтрокаПогашения.Сумма                    = СуммаПогашения;
		СтрокаПогашения.СуммаНУ                  = 0;
		СтрокаПогашения.СуммаПР                  = СуммаПогашенияПР;
		СтрокаПогашения.СуммаВР                  = СуммаПогашенияВР;
		СтрокаПогашения.КоличествоМЦ             = ВыборкаМатериалов.КоличествоМЦ;
		
	КонецЦикла;
	
	Возврат ТаблицаСумм;
	
КонецФункции // РасчетСуммыПогашенияСтоимостиМатериалов()

// Функция получает таблицу значений с указанием списка материалов и сумм 
// погашения стоимости и возвращает таблицу значений с распределенными 
// значениями погашения стоимости.
//
Функция ПолучитьРаспределениеПогашенияСтоимости(ДокументОбъект, Отказ, Заголовок, ТабАмортизации, СтруктураШапкиДокумента, ТекстПроводки = "Погашение стоимости спецодежды (спецоснастки)") Экспорт

	ДатаРасчета = ДокументОбъект.Дата;

	// Определим структуру выходной таблицы.
	ТабЗатрат = Новый ТаблицаЗначений;
	ТабЗатрат.Колонки.Добавить("Сумма", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	ТабЗатрат.Колонки.Добавить("СуммаНУ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	ТабЗатрат.Колонки.Добавить("СуммаПР", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	ТабЗатрат.Колонки.Добавить("СуммаВР", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	ТабЗатрат.Колонки.Добавить("СуммаМЦВР", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	ТабЗатрат.Колонки.Добавить("СуммаМЦПР", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 2));
	ТабЗатрат.Колонки.Добавить("Количество", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 3));
	ТабЗатрат.Колонки.Добавить("КоличествоМЦ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15, 3));
	ТабЗатрат.Колонки.Добавить("СчетЗатрат");
	ТабЗатрат.Колонки.Добавить("СчетАмортизации");
	ТабЗатрат.Колонки.Добавить("ПодразделениеОрганизации");
	ТабЗатрат.Колонки.Добавить("ПодразделениеЗатрат");
	ТабЗатрат.Колонки.Добавить("ОбъектУчета");
	ТабЗатрат.Колонки.Добавить("ДокументПередачи");
	ТабЗатрат.Колонки.Добавить("ПодразделениеФизЛицо");
	ТабЗатрат.Колонки.Добавить("Субконто1");
	ТабЗатрат.Колонки.Добавить("Субконто2");
	ТабЗатрат.Колонки.Добавить("Субконто3");

	МассивКоэф = Новый Массив;
	
	// цикл по Объектам из ТабАмортизации
	Для Каждого СтрокаТЗ Из ТабАмортизации Цикл

		Если СтрокаТЗ.КОличествоМЦ = 0
		   И СтрокаТЗ.Количество = 0
		   И СтрокаТЗ.Сумма = 0
		   И (СтрокаТЗ.СуммаНУ = 0
		      И СтрокаТЗ.СуммаПР = 0
		      И СтрокаТЗ.СуммаВР = 0) Тогда
			//Распределять погашение стоимости не надо, так как распределять нечего.
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЗ.НаправлениеАмортизации) Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("Не указаны способы отражения расходов по погашению стоимости для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
			Продолжить;
		КонецЕсли;

		Если СтрокаТЗ.НаправлениеАмортизации.Способы.Количество() = 0 Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("В способах отражения затрат по погашению стоимости нет записей для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
			Продолжить;
		КонецЕсли;

		МассивКоэф.Очистить();
		Для Каждого СтрокаНапр Из СтрокаТЗ.НаправлениеАмортизации.Способы Цикл
			МассивКоэф.Добавить(СтрокаНапр.Коэффициент);
		КонецЦикла;

		МассивСумм = ОбщегоНазначения.РаспределитьПропорционально(СтрокаТЗ.Сумма, МассивКоэф, 2);
		Если ЗначениеЗаполнено(СтрокаТЗ.Сумма) И (МассивСумм = Неопределено) Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("В способах отражения расходов по погашению стоимости не проставлены коэффициенты для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
			Продолжить;
		КонецЕсли;
		
		МассивСуммНУ = ОбщегоНазначения.РаспределитьПропорционально(СтрокаТЗ.СуммаНУ, МассивКоэф, 2);
		Если ЗначениеЗаполнено(СтрокаТЗ.СуммаНУ) И (МассивСуммНУ = Неопределено) Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("В способах отражения расходов по погашению стоимости не проставлены коэффициенты для НУ для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
			Продолжить;
		КонецЕсли;
		
		МассивСуммПР = ОбщегоНазначения.РаспределитьПропорционально(СтрокаТЗ.СуммаПР, МассивКоэф, 2);
		Если ЗначениеЗаполнено(СтрокаТЗ.СуммаПР) И (МассивСуммПР = Неопределено) Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("В способах отражения расходов по погашению стоимости не проставлены коэффициенты для НУ для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные в документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
			Продолжить;
		КонецЕсли;
		
		МассивСуммВР = ОбщегоНазначения.РаспределитьПропорционально(СтрокаТЗ.СуммаВР, МассивКоэф, 2);
		Если ЗначениеЗаполнено(СтрокаТЗ.СуммаВР) И (МассивСуммВР = Неопределено) Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("В способах отражения расходов по погашению стоимости не проставлены коэффициенты для НУ для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные в документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
			Продолжить;
		КонецЕсли;
		
		Если НЕ (МассивСумм = Неопределено) Тогда
			Если МассивСумм.Количество() <> МассивКоэф.Количество() Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("В способах отражения расходов по погашению стоимости есть нулевые коэффициенты для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные в документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ (МассивСуммНУ = Неопределено) Тогда
			Если МассивСуммНУ.Количество() <> МассивКоэф.Количество() Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("В способах отражения расходов по погашению стоимости для НУ есть нулевые коэффициенты для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные в документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ (МассивСуммПР = Неопределено) Тогда
			Если МассивСуммПР.Количество() <> МассивКоэф.Количество() Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("В способах отражения расходов по погашению стоимости для НУ есть нулевые коэффициенты для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные в документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ (МассивСуммВР = Неопределено) Тогда
			Если МассивСуммВР.Количество() <> МассивКоэф.Количество() Тогда
			ЗакрытиеМесяца.СообщитьОбОшибкеРегОперации("В способах отражения расходов по погашению стоимости для НУ есть нулевые коэффициенты для материала " + СтрокаТЗ.ОбъектУчета + ".
				|Рекомендуется проверить данные в документе передачи в эксплуатацию",, Отказ, ДокументОбъект);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		МассивКоличества = ?(НЕ ЗначениеЗаполнено(СтрокаТЗ.Количество), Неопределено, ОбщегоНазначения.РаспределитьПропорционально(СтрокаТЗ.Количество, МассивКоэф, 3));

		МассивКоличестваМЦ = ?(НЕ ЗначениеЗаполнено(СтрокаТЗ.КоличествоМЦ), Неопределено, ОбщегоНазначения.РаспределитьПропорционально(СтрокаТЗ.КоличествоМЦ, МассивКоэф, 3));
		
		Для Каждого СтрокаНапр Из СтрокаТЗ.НаправлениеАмортизации.Способы Цикл
			
			НоваяСтрока = ТабЗатрат.Добавить();
			
			НоваяСтрока.СчетЗатрат               = СтрокаНапр.СчетЗатрат;
			НоваяСтрока.ОбъектУчета              = СтрокаТЗ.ОбъектУчета;
			НоваяСтрока.ДокументПередачи         = СтрокаТЗ.ДокументПередачи;
			НоваяСтрока.ПодразделениеФизЛицо     = СтрокаТЗ.ПодразделениеФизЛицо;
			НоваяСтрока.ПодразделениеОрганизации = СтрокаТЗ.ПодразделениеОрганизации;
			НоваяСтрока.ПодразделениеЗатрат      = СтрокаНапр.ПодразделениеОрганизации;
			НоваяСтрока.СчетАмортизации          = СтрокаТЗ.СчетАмортизации;
			НоваяСтрока.Субконто1                = СтрокаНапр.Субконто1;
			НоваяСтрока.Субконто2                = СтрокаНапр.Субконто2;
			НоваяСтрока.Субконто3                = СтрокаНапр.Субконто3;
			НоваяСтрока.Сумма                    = ?(ЗначениеЗаполнено(МассивСумм), МассивСумм[СтрокаНапр.НомерСтроки - 1], 0);
			НоваяСтрока.СуммаНУ                  = ?(ЗначениеЗаполнено(МассивСуммНУ), МассивСуммНУ[СтрокаНапр.НомерСтроки - 1], 0);
			НоваяСтрока.СуммаПР                  = ?(ЗначениеЗаполнено(МассивСуммПР), МассивСуммПР[СтрокаНапр.НомерСтроки - 1], 0);
			НоваяСтрока.СуммаВР                  = ?(ЗначениеЗаполнено(МассивСуммВР), МассивСуммВР[СтрокаНапр.НомерСтроки - 1], 0);
			НоваяСтрока.Количество               = ?(ЗначениеЗаполнено(МассивКоличества), МассивКоличества[СтрокаНапр.НомерСтроки - 1], 0);
			НоваяСтрока.КоличествоМЦ             = ?(ЗначениеЗаполнено(МассивКоличестваМЦ), МассивКоличестваМЦ[СтрокаНапр.НомерСтроки - 1], 0);
			
		КонецЦикла;

	КонецЦикла;

	// Формирование проводок.
	Проводки = ДокументОбъект.Движения.Хозрасчетный;

	Для каждого СтрокаЗатрат из ТабЗатрат Цикл

		Если СтрокаЗатрат.Количество = 0
		   И СтрокаЗатрат.Сумма = 0
		   И (СтрокаЗатрат.СуммаНУ = 0
			  И СтрокаЗатрат.СуммаПР = 0
			  И СтрокаЗатрат.СуммаВР = 0) Тогда
			Продолжить;
		КонецЕсли;
		
		Проводка = Проводки.Добавить();

		Проводка.Период       = ДатаРасчета;
		Проводка.Содержание   = ТекстПроводки;
		Проводка.Организация  = СтруктураШапкиДокумента.Организация;
		Проводка.Сумма        = СтрокаЗатрат.Сумма;

		Проводка.СчетДт = СтрокаЗатрат.СчетЗатрат;
		БухгалтерскийУчет.УстановитьСубконто(СтрокаЗатрат.СчетЗатрат, Проводка.СубконтоДт, 1, СтрокаЗатрат.Субконто1);
		БухгалтерскийУчет.УстановитьСубконто(СтрокаЗатрат.СчетЗатрат, Проводка.СубконтоДт, 2, СтрокаЗатрат.Субконто2);
		БухгалтерскийУчет.УстановитьСубконто(СтрокаЗатрат.СчетЗатрат, Проводка.СубконтоДт, 3, СтрокаЗатрат.Субконто3);
		
		Проводка.СчетКт = СтрокаЗатрат.СчетАмортизации;
		БухгалтерскийУчет.УстановитьСубконто(СтрокаЗатрат.СчетАмортизации, Проводка.СубконтоКт, 1, СтрокаЗатрат.ОбъектУчета);
		БухгалтерскийУчет.УстановитьСубконто(СтрокаЗатрат.СчетАмортизации, Проводка.СубконтоКт, 2, СтрокаЗатрат.ДокументПередачи);
		БухгалтерскийУчет.УстановитьСубконто(СтрокаЗатрат.СчетАмортизации, Проводка.СубконтоКт, 3, СтрокаЗатрат.ПодразделениеФизЛицо);
		
		БухгалтерскийУчет.УстановитьПодразделенияПроводки(
			Проводка, СтрокаЗатрат.ПодразделениеЗатрат, СтрокаЗатрат.ПодразделениеОрганизации);
		
		Проводка.КоличествоКт    = СтрокаЗатрат.Количество;
		
		СуммаНУДт = СтрокаЗатрат.СуммаНУ;
		СуммаНУКт = СтрокаЗатрат.СуммаНУ;
		СуммаПРДт = СтрокаЗатрат.СуммаПР;
		СуммаПРКт = СтрокаЗатрат.СуммаПР;
		СуммаВРДт = СтрокаЗатрат.СуммаВР;
		СуммаВРКт = СтрокаЗатрат.СуммаВР;
		СуммаМЦВР = СтрокаЗатрат.СуммаВР;
        СуммаМЦПР = СтрокаЗатрат.СуммаПР;
        
        НеПринимаемыйРасход = НалоговыйУчет.ОпределитьНепринимаемыйРасход(
        	Проводка.СчетДт, СтрокаЗатрат.Субконто1, СтрокаЗатрат.Субконто1, СтрокаЗатрат.Субконто2, СтрокаЗатрат.Субконто3);
        	
        Если Проводка.Сумма = 0 Тогда
        		
   			СуммаПрДт = 0;
    		СуммаВрКт = 0;
    		СуммаПрКт = 0;
    		
    		Если НеПринимаемыйРасход Тогда
    			СуммаНУДт = 0;
    			СуммаВрДт = 0;
    			СуммаПрКт = - СуммаНУКт;
    		Иначе
    			СуммаВрДт = - СуммаНУКт;
    			СуммаВрКт = - СуммаНУКт;
    		КонецЕсли;
    		
    		СтрокаЗатрат.СуммаМЦПр = СуммаМЦПр - СуммаПРКт;
    		СтрокаЗатрат.СуммаМЦВр = СуммаМЦВр - СуммаВРКт;
    		
    	Иначе
    		
    		Если НеПринимаемыйРасход Тогда
    			СуммаПрДт = СуммаПрДт + СуммаНУДт;
    			СуммаНУДт = 0;
    		КонецЕсли;
    		
    		СтрокаЗатрат.СуммаМЦПр = 0;
    		СтрокаЗатрат.СуммаМЦВр = 0;
    		
    	КонецЕсли;
			
		НалоговыйУчет.ЗаполнитьНалоговыеСуммыПроводки(
			СуммаНУДт, СуммаНУКт, СуммаПрДт, СуммаПРКт, СуммаВрДт, СуммаВРКт, Проводка, СтруктураШапкиДокумента.ПрименениеПБУ18);

        СтрокаЗатрат.Сумма = ?(СтрокаЗатрат.Сумма = 0, СтрокаЗатрат.СуммаМЦВР + СтрокаЗатрат.СуммаМЦПР, СтрокаЗатрат.Сумма);
				
	КонецЦикла;

	Возврат ТабЗатрат;

КонецФункции // ПолучитьРаспределениеПогашенияСтоимости()

// Процедура формирует таблицу остатков спецодежды в незавершенном производстве.
//
Процедура ЗаполнитьСпецодеждуПоОстаткамВЭксплуатации(ДокОбъект, ТаблицаМатериалы, ВключатьЗабалансовые = Истина, СИстекшимСрокомПолезногоИспользования = Ложь) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("Дата",          ДокОбъект.Дата);
	Запрос.УстановитьПараметр("Период",        Новый Граница(ДокОбъект.МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация",   ДокОбъект.Организация);
	Запрос.УстановитьПараметр("Подразделение", ДокОбъект.ПодразделениеОрганизации);
	
	ТекстУсловия1 = ?(ВключатьЗабалансовые, "", "ГДЕ (ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) > 0) И (ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) > 0)");
	ТекстУсловия1 = ТекстУсловия1 + ?(СИстекшимСрокомПолезногоИспользования, 
	                                  ?(ТекстУсловия1 = "", "ГДЕ ", " И ") + " ДОБАВИТЬКДАТЕ(ХозрасчетныйОстаткиМЦ.Субконто2.Дата, МЕСЯЦ, ПередачаМатериаловВЭксплуатациюСпецодежда.НазначениеИспользования.СрокПолезногоИспользования) < &Дата", 
	                                  "");
				  
	ТекстУсловия2 = ?(ВключатьЗабалансовые, "", "ГДЕ (ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) > 0) И (ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0) > 0)");
	ТекстУсловия2 = ТекстУсловия2 + ?(СИстекшимСрокомПолезногоИспользования,
	                                  ?(ТекстУсловия2 = "", "ГДЕ ", " И ") + " ДОБАВИТЬКДАТЕ(ХозрасчетныйОстаткиМЦ.Субконто2.Дата, МЕСЯЦ, ХозрасчетныйОстаткиМЦ.Субконто2.НазначениеИспользования.СрокПолезногоИспользования) < &Дата",
	                                  "");
				  
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстаткиМЦ.Субконто1                                                              КАК Номенклатура,
	|	ХозрасчетныйОстаткиМЦ.Субконто2                                                              КАК ПартияМатериаловВЭксплуатации,
	|	ХозрасчетныйОстаткиМЦ.Субконто3                                                              КАК Физлицо,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, ХозрасчетныйОстаткиМЦ.КоличествоОстатокДт) КАК Количество,
	|   ВЫБОР КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) = 0 ТОГДА 
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный)
	|	ИНАЧЕ
	|		ХозрасчетныйОстатки.Счет
	|	КОНЕЦ                                                                                        КАК СчетПередачи
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию) КАК ХозрасчетныйОстаткиМЦ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет НЕ В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный)), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 В (ВЫБРАТЬ РАЗЛИЧНЫЕ Ссылка ИЗ Документ.ПередачаМатериаловВЭксплуатацию)) КАК ХозрасчетныйОстатки
	|		ПО ХозрасчетныйОстаткиМЦ.Субконто1 = ХозрасчетныйОстатки.Субконто1
	|		 И ХозрасчетныйОстаткиМЦ.Субконто2 = ХозрасчетныйОстатки.Субконто2
	|		 И ХозрасчетныйОстаткиМЦ.Субконто3 = ХозрасчетныйОстатки.Субконто3
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаМатериаловВЭксплуатацию.Спецодежда КАК ПередачаМатериаловВЭксплуатациюСпецодежда
	|		ПО ХозрасчетныйОстаткиМЦ.Субконто1 = ПередачаМатериаловВЭксплуатациюСпецодежда.Номенклатура
	|		 И ХозрасчетныйОстаткиМЦ.Субконто2 = ПередачаМатериаловВЭксплуатациюСпецодежда.Ссылка
	|		 И ХозрасчетныйОстаткиМЦ.Субконто3 = ПередачаМатериаловВЭксплуатациюСпецодежда.ФизЛицо
	|" + ТекстУсловия1 + "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиМЦ.Субконто1,
	|	ХозрасчетныйОстаткиМЦ.Субконто2,
	|	ХозрасчетныйОстаткиМЦ.Субконто3,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, ХозрасчетныйОстаткиМЦ.КоличествоОстатокДт),
	|   ВЫБОР КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) = 0 ТОГДА 
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный)
	|	ИНАЧЕ
	|		ХозрасчетныйОстатки.Счет
	|	КОНЕЦ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПартияМатериаловВЭксплуатации) КАК ХозрасчетныйОстаткиМЦ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет НЕ В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный)), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 В (ВЫБРАТЬ РАЗЛИЧНЫЕ Ссылка ИЗ Документ.ПартияМатериаловВЭксплуатации)) КАК ХозрасчетныйОстатки
	|		ПО ХозрасчетныйОстаткиМЦ.Субконто1 = ХозрасчетныйОстатки.Субконто1
	|		 И ХозрасчетныйОстаткиМЦ.Субконто2 = ХозрасчетныйОстатки.Субконто2
	|		 И ХозрасчетныйОстаткиМЦ.Субконто3 = ХозрасчетныйОстатки.Субконто3
	|" + ТекстУсловия2 + "
	|";
	
	ОстаткиМатериалов = Запрос.Выполнить().Выгрузить();
	
	ТаблицаМатериалы.Загрузить(ОстаткиМатериалов);
	
КонецПроцедуры // ЗаполнитьСпецодеждуПоОстаткамВЭксплуатации()

// Процедура формирует таблицу остатков спецоснастки в незавершенном производстве.
//
Процедура ЗаполнитьСпецоснасткуПоОстаткамВЭксплуатации(ДокОбъект, ТаблицаМатериалы, ВключатьЗабалансовые = Истина, СИстекшимСрокомПолезногоИспользования = Ложь) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("Дата",          ДокОбъект.Дата);
	Запрос.УстановитьПараметр("Период",        Новый Граница(ДокОбъект.МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация",   ДокОбъект.Организация);
	Запрос.УстановитьПараметр("Подразделение", ДокОбъект.ПодразделениеОрганизации);
	
	Запрос.УстановитьПараметр("СчетУчетаСпецодеждыЗабалансовыйБУ",   ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный);
	Запрос.УстановитьПараметр("СчетУчетаСпецоснасткиЗабалансовыйБУ", ПланыСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный);
	
	ТекстУсловия1 = ?(ВключатьЗабалансовые, "", "ГДЕ (ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) > 0)");
	ТекстУсловия1 = ТекстУсловия1 + ?(СИстекшимСрокомПолезногоИспользования, 
	                                  ?(ТекстУсловия1 = "", "ГДЕ ", " И ") + " ДОБАВИТЬКДАТЕ(ХозрасчетныйОстаткиМЦ.Субконто2.Дата, МЕСЯЦ, ПередачаМатериаловВЭксплуатациюСпецоснастка.НазначениеИспользования.СрокПолезногоИспользования) < &Дата", 
	                                  "");
				  
	ТекстУсловия2 = ?(ВключатьЗабалансовые, "", "ГДЕ (ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) > 0)");
	ТекстУсловия2 = ТекстУсловия2 + ?(СИстекшимСрокомПолезногоИспользования,
	                                  ?(ТекстУсловия2 = "", "ГДЕ ", " И ") + " ДОБАВИТЬКДАТЕ(ХозрасчетныйОстаткиМЦ.Субконто2.Дата, МЕСЯЦ, ХозрасчетныйОстаткиМЦ.Субконто2.НазначениеИспользования.СрокПолезногоИспользования) < &Дата",
	                                  "");
				  
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстаткиМЦ.Субконто1                                                              КАК Номенклатура,
	|	ХозрасчетныйОстаткиМЦ.Субконто2                                                              КАК ПартияМатериаловВЭксплуатации,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, ХозрасчетныйОстаткиМЦ.КоличествоОстатокДт) КАК Количество,
	|   ВЫБОР КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) = 0 ТОГДА 
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный)
	|	ИНАЧЕ
	|		ХозрасчетныйОстатки.Счет
	|	КОНЕЦ                                                                                        КАК СчетПередачи
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию) КАК ХозрасчетныйОстаткиМЦ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет НЕ В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный)), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 В (ВЫБРАТЬ РАЗЛИЧНЫЕ Ссылка ИЗ Документ.ПередачаМатериаловВЭксплуатацию)) КАК ХозрасчетныйОстатки
	|		ПО ХозрасчетныйОстаткиМЦ.Субконто1 = ХозрасчетныйОстатки.Субконто1
	|		 И ХозрасчетныйОстаткиМЦ.Субконто2 = ХозрасчетныйОстатки.Субконто2
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаМатериаловВЭксплуатацию.Спецоснастка КАК ПередачаМатериаловВЭксплуатациюСпецоснастка
	|		ПО ХозрасчетныйОстаткиМЦ.Субконто1 = ПередачаМатериаловВЭксплуатациюСпецоснастка.Номенклатура
	|		 И ХозрасчетныйОстаткиМЦ.Субконто2 = ПередачаМатериаловВЭксплуатациюСпецоснастка.Ссылка
	|" + ТекстУсловия1 + "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиМЦ.Субконто1,
	|	ХозрасчетныйОстаткиМЦ.Субконто2,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, ХозрасчетныйОстаткиМЦ.КоличествоОстатокДт),
	|   ВЫБОР КОГДА ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0) = 0 ТОГДА 
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный)
	|	ИНАЧЕ
	|		ХозрасчетныйОстатки.Счет
	|	КОНЕЦ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПартияМатериаловВЭксплуатации) КАК ХозрасчетныйОстаткиМЦ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет НЕ В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный)), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 В (ВЫБРАТЬ РАЗЛИЧНЫЕ Ссылка ИЗ Документ.ПартияМатериаловВЭксплуатации)) КАК ХозрасчетныйОстатки
	|		ПО ХозрасчетныйОстаткиМЦ.Субконто1 = ХозрасчетныйОстатки.Субконто1
	|		 И ХозрасчетныйОстаткиМЦ.Субконто2 = ХозрасчетныйОстатки.Субконто2
	|" + ТекстУсловия2 + "
	|";
	
	ОстаткиМатериалов = Запрос.Выполнить().Выгрузить();
	
	ТаблицаМатериалы.Загрузить(ОстаткиМатериалов);
	
КонецПроцедуры // ЗаполнитьСпецоснасткуПоОстаткамВЭксплуатации()

// Процедура формирует таблицу остатков инвентаря и хозяйственных 
// принадлежностей на забалансовом счете.
//
Процедура ЗаполнитьИнвентарьИХозяйственныеПринадлежностиПоОстаткамВЭксплуатации(ДокОбъект, ТаблицаМатериалы) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("Период",        Новый Граница(ДокОбъект.МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Организация",   ДокОбъект.Организация);
	Запрос.УстановитьПараметр("Подразделение", ДокОбъект.ПодразделениеОрганизации);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстаткиМЦ.Субконто1                                                              КАК Номенклатура,
	|	ХозрасчетныйОстаткиМЦ.Субконто2                                                              КАК ПартияМатериаловВЭксплуатации,
	|	ХозрасчетныйОстаткиМЦ.Субконто3                                                              КАК Физлицо,
	|	ХозрасчетныйОстаткиМЦ.КоличествоОстатокДт                                                    КАК Количество
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежностиВЭксплуатации), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию) КАК ХозрасчетныйОстаткиМЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстаткиМЦ.Субконто1,
	|	ХозрасчетныйОстаткиМЦ.Субконто2,
	|	ХозрасчетныйОстаткиМЦ.Субконто3,
	|	ХозрасчетныйОстаткиМЦ.КоличествоОстатокДт
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежностиВЭксплуатации), , Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПартияМатериаловВЭксплуатации) КАК ХозрасчетныйОстаткиМЦ
	|";
	
	ОстаткиМатериалов = Запрос.Выполнить().Выгрузить();
	
	ТаблицаМатериалы.Загрузить(ОстаткиМатериалов);
	
КонецПроцедуры // ЗаполнитьСпецоснасткуПоОстаткамВЭксплуатации()

