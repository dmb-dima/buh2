
// Процедура устанавливает в проводке суммы НУ,ПР,ВР в зависимости от признака ведения налогового учета на счете
//
Процедура ЗаполнитьНалоговыеСуммыПроводки(СуммаНУДт = 0, СуммаНУКт = 0, СуммаПРДт = 0, СуммаПРКт = 0, СуммаВРДт = 0, СуммаВРКт = 0, Проводка, ПрименениеПБУ18 = Истина) Экспорт
		
	Если Проводка.СчетДт.НалоговыйУчет Тогда
		Проводка.СуммаНУДт = СуммаНУДт;
		Проводка.СуммаПРДт = СуммаПРДт;
		Проводка.СуммаВРДт = СуммаВРДт;
	КонецЕсли;
	
	Если Проводка.СчетКт.НалоговыйУчет Тогда
		Проводка.СуммаНУКт = СуммаНУКт;
		Проводка.СуммаПРКт = СуммаПРКт;
		Проводка.СуммаВРКт = СуммаВРКт;
	КонецЕсли;
	
КонецПроцедуры

// Функция анализирует аналитику затрат по дебету проводки.
// Если статья затрат не принимаемая к НУ, то сумма НУ по дебету преобразуется в сумму ПР по дебету
//
Функция ОпределитьПостоянныеРазницыВРасходах(Проводка,ПоддержкаПБУ18) Экспорт
	СчетДт = Проводка.СчетДт;
	Если ЗначениеЗаполнено(СчетДт) И СчетДт.НалоговыйУчет Тогда
		
		ЭтоНепринимаемыеРасходы = ОпределитьНеПринимаемыеРасходы(Проводка);
		ЭтоАмортизация = ОпределитьРасходыНаАмортизацию(Проводка);
		
		Если ЭтоНепринимаемыеРасходы Тогда 
			Проводка.СуммаПРДт = ?(ПоддержкаПБУ18, Проводка.СуммаПРДт + Проводка.СуммаНУДт, 0);
			Проводка.СуммаНУДт = 0;
		ИначеЕсли НЕ ЭтоАмортизация И ПоддержкаПБУ18 Тогда
			Если Проводка.СуммаНУДт + Проводка.СуммаПРДт <> 0  Тогда
				Проводка.СуммаПРДт = Проводка.СуммаПРДт - Проводка.СуммаНУДт;
				Проводка.СуммаВРДт = Проводка.Сумма - Проводка.СуммаНУДт - Проводка.СуммаПРДт;
			КонецЕсли;
			Если Проводка.СуммаНУКт + Проводка.СуммаПРКт <> 0 Тогда
				Проводка.СуммаПРКт = Проводка.СуммаПРКт - Проводка.СуммаНУКт;
				Проводка.СуммаВРКт = Проводка.Сумма - Проводка.СуммаНУКт - Проводка.СуммаПРКт;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

// Функция анализирует аналитику затрат по кредиту проводки.
// Если статья затрат не принимаемая к НУ, то возвращаемое значение "истина"
//
Функция ОпределитьНеПринимаемыеДоходы(Проводка) Экспорт
	
	Если Не Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ПустаяСсылка() И (Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ПрочиеДоходы Или Проводка.СчетКт.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ПрочиеДоходы)) Тогда
		Для Каждого Субконто Из Проводка.СубконтоКт Цикл
			Если ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") И Не Субконто.Значение.ЭтоГруппа Тогда
				
				Если НЕ Субконто.Значение = Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка() И НЕ Субконто.Значение.ПринятиеКналоговомуУчету Тогда
					Возврат  Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

// Функция анализирует аналитику затрат по дебету проводки.
// Если статья затрат не принимаемая к НУ, то возвращаемое значение "истина"
//
Функция ОпределитьНеПринимаемыеРасходы(Проводка) Экспорт
	
	Для Каждого Субконто Из Проводка.СубконтоДт Цикл
		
		Если ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.СтатьиЗатрат") И Не Субконто.Значение.ЭтоГруппа Тогда
			
			
			Если Субконто.Значение.ВидРасходовНУ = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения Тогда
				
				// Расходы на строительство объектов основных средств, не относятся к расходам по производству и реализации
				Если Проводка.СчетДт = ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств Или Проводка.СчетДт.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств) Тогда
					Возврат Ложь;
				КонецЕсли;
				
				ОтнесениеКЕНВД = Субконто.Значение.ВидДеятельностиДляНалоговогоУчетаЗатрат;
				
				Если ОтнесениеКЕНВД = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД Тогда
					ОбщегоНазначения.СообщитьОбОшибке("" + Субконто.Значение + " - неправильно указана: для непринимаемых расходов (" + ОтнесениеКЕНВД + ").
					|Расходы, не учитываемые для целей налогообложения, не следует относить к распределяемым
					|- это не имеет смысла для расчета налоговой базы и не поддерживается программой.", Ложь, , СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
				
				Если ОтнесениеКЕНВД = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ВидДеятельностиОпределяетсяПоДоходам Тогда
					ОбщегоНазначения.СообщитьОбОшибке("" + Субконто.Значение + " - неправильно указана: для непринимаемых расходов (" + ОтнесениеКЕНВД + ").
					|Расходы, не учитываемые для целей налогообложения, не следует относить к распределяемым
					|- это не имеет смысла для расчета налоговой базы и не поддерживается программой.", Ложь, , СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
				
				Возврат Истина;
				Прервать;
			КонецЕсли; 
			
		ИначеЕсли ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") И Не Субконто.Значение.ЭтоГруппа Тогда
			
			Если НЕ Субконто.Значение = Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка() И НЕ Субконто.Значение.ПринятиеКналоговомуУчету Тогда
				
				ОтнесениеКЕНВД = Субконто.Значение.ВидДеятельностиДляНалоговогоУчетаЗатрат;
				
				Если ОтнесениеКЕНВД = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Проводка " + Проводка.Содержание + ": неправильно указана статья затрат для непринимаемых расходов (" + ОтнесениеКЕНВД + ").
					|Расходы, не учитываемые для целей налогообложения, не следует относить к деятельности, облагаемой ЕНВД,
					|- это не имеет смысла для расчета налоговой базы и не поддерживается программой.", Ложь, , СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
				
				Если ОтнесениеКЕНВД = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ВидДеятельностиОпределяетсяПоДоходам Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Проводка " + Проводка.Содержание + ": неправильно указана статья затрат для непринимаемых расходов (" + ОтнесениеКЕНВД + ").
					|Расходы, не учитываемые для целей налогообложения, не следует относить к распределяемым
					|- это не имеет смысла для расчета налоговой базы и не поддерживается программой.", Ложь, , СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
				
				Возврат Истина;
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

// Функция анализирует аналитику затрат по дебету проводки.
// Если статья затрат отличается от "Амортизация", то возвращаемое значение "истина"
//
Функция ОпределитьРасходыНаАмортизацию(Проводка) Экспорт
	
	Для каждого Субконто Из Проводка.СубконтоДт Цикл
		
		Если ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда 
			Возврат Истина;
		КонецЕсли;
		
		Если ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.СтатьиЗатрат") 
			И НЕ Субконто.Значение.ЭтоГруппа
			И Субконто.Значение.ВидРасходовНУ = Перечисления.ВидыРасходовНУ.Амортизация Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Формируется таблица строк в которой задано соответствие видов активов
// (обязательства) и счетов налогового учета, на которых отражаются разницы
// по этим видам активов.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица соответствия.
//
Функция ПолучитьТаблицуВидовАктивовИОбязательств() Экспорт
	
	Если  ПланыСчетов.Хозрасчетный.ТоварыНаСкладах.ВидыСубконто.Количество() = 1 ТОгда
		ЕстьСкладскойУчет = Ложь;
	ИНачеЕсли  ПланыСчетов.Хозрасчетный.ТоварыНаСкладах.ВидыСубконто.Количество() = 2 ТОгда
		ЕстьСкладскойУчет = ПланыСчетов.Хозрасчетный.ТоварыНаСкладах.ВидыСубконто[1].ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады
	Иначе
		ЕстьСкладскойУчет = ПланыСчетов.Хозрасчетный.ТоварыНаСкладах.ВидыСубконто[2].ВидСубконто = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады
	КонецЕсли;
	
	ТаблицаВидовАктивовИОбязательств = Новый ТаблицаЗначений;
	ТаблицаВидовАктивовИОбязательств.Колонки.Добавить("ВидАктивовОбязательств", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыАктивовИОбязательств, Строка"));
	ТаблицаВидовАктивовИОбязательств.Колонки.Добавить("Счета",      Новый ОписаниеТипов("Массив"));
	ТаблицаВидовАктивовИОбязательств.Колонки.Добавить("Субконто",   Новый ОписаниеТипов("Массив"));
	
	//Основные средства
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ОсновныеСредства;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияОС_01);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	
	//ДоходныеВложенияВ_МЦ
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ДоходныеВложенияВ_МЦ;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияОС_03);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ДоходныеВложенияВ_МЦ);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	
	//Нематериальные активы
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.НематериальныеАктивы;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияНематериальныхАктивов);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
	
	// Оборудование 
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.Оборудование;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке);
	Если ЕстьСкладскойУчет Тогда
		НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	// Внеоборотные активы   08.01
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ВнеоборотныеАктивы;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПриобретениеЗемельныхУчастков);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства);
	
	// Внеоборотные активы   08.02
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ВнеоборотныеАктивы;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПриобретениеОбъектовПриродопользования);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства);
	
	// Внеоборотные активы   08.03
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ВнеоборотныеАктивы;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства);
	
	// Внеоборотные активы 08.04
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ВнеоборотныеАктивы;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПриобретениеОбъектовОсновныхСредств);
	Если ЕстьСкладскойУчет Тогда
		НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	// Внеоборотные активы 08.05
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ВнеоборотныеАктивы;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПриобретениеНематериальныхАктивов);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
	
	// Внеоборотные активы 08.08
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ВнеоборотныеАктивы;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ВыполнениеНИОКР);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыНаНИОКР);
	
	// Материалы
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.Материалы;
	СчетаУчетаМатериалов = ПланыСчетов.Хозрасчетный.ВыбратьИерархически(ПланыСчетов.Хозрасчетный.Материалы);
	Пока СчетаУчетаМатериалов.Следующий() Цикл
		СчетУчетаМатериалов = СчетаУчетаМатериалов.Ссылка;
		КодСчета = СчетУчетаМатериалов.Код;
		Если Лев(КодСчета, 5) = "10.МЦ"
			Или Лев(КодСчета, 5) = "10.11" 
			Или Лев(КодСчета, 5) = "10.07" Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока.Счета.Добавить(СчетУчетаМатериалов);
	КонецЦикла;
	Если ЕстьСкладскойУчет Тогда
		НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
		// Материалы  в переработке
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.Материалы;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.МатериалыПереданныеВПереработку.Ссылка);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	
		// Материалы  в эксплуатации
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.Материалы;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатации.Ссылка);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.СпецоснасткаВЭксплуатации.Ссылка);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации);
	
	// Незавершенное производство
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.НезавершенноеПроизводство;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.БракВПроизводстве);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
	
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.НезавершенноеПроизводство;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПроизводствоИзДавальческогоСырья);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	// Готовая продукция
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ГотоваяПродукция;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);
	Если ЕстьСкладскойУчет Тогда
		НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	// Полуфабрикаты
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.Полуфабрикаты;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);
	Если ЕстьСкладскойУчет Тогда
		НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	// Расходы будущих периодов
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.РасходыБудущихПериодов;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов);
	
	// Товары
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.Товары;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.Товары);
	Если ЕстьСкладскойУчет Тогда
		НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	КонецЕсли;
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	// Товары отгруженные
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ТоварыОтгруженные;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	// Издержки обращения
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ИздержкиОбращения;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	
	// Финансовые вложения (счета 58.01.1)
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ФинансовыеВложения;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.Паи);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	
	// Финансовые вложения (счета 58.01.2 и Н58.02)
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ФинансовыеВложения;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.Акции);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ДолговыеЦенныеБумаги);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ЦенныеБумаги);
	
	// Финансовые вложения (счета 58.03, 58.04, 58.05)
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ФинансовыеВложения;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПредоставленныеЗаймы);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ВкладыПоДоговоруПростогоТоварищества);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПриобретенныеПрава);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	// Доходы будущих периодов
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ДоходыБудущихПериодов;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ДоходыБудущихПериодов);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДоходыБудущихПериодов);
	
	
	// Дебиторская задолженность
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ДебиторскаяЗадолженность;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателями);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученным);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСРозничнымиПокупателями);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчиками);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчиками);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	// Кредиторская задолженность
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.КредиторскаяЗадолженность;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамВыданным);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ВекселяВыданные);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоИмущественномуИЛичномуСтрахованию);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПлатежиПоДобровольномуСтрахованиюРаботников);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоПретензиям);
    НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоПричитающимсяДивидендам);
    НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоДепонированнымСуммам);
    НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПоставщикамиИПодрядчиками);
    НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторами);
    НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоИсполнительнымДокументамРаботников);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	// Курсовые разницы при оплате в рублях
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.КурсовыеРазницыПоРасчетамВУЕ;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиУЕ);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамВыданнымУЕ);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиУЕ);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымУЕ);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоПретензиямУЕ);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПоставщикамиИПодрядчикамиУЕ);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчикамиУЕ);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ);
	
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	// Курсовые разницы при оплате в валюте
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.КурсовыеРазницыПоРасчетамВВалюте;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамВыданнымВал);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиВал);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученнымВал);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоИмущественномуИЛичномуСтрахованиюВал);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоПретензиямВал);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПоставщикамиИПодрядчикамиВал);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПрочимиПокупателямиИЗаказчикамиВал);
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеРасчетыСРазнымиДебиторамиИКредиторамиУЕ);
	
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	НоваяСтрока.Субконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	// Убытки текущего периода
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.УбытокТекущегоПериода;
	
	// Недостачи и потери от порчи ценностей
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.НедостачиПотери;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.НедостачиИПотериОтПорчиЦенностей);
	
	// Оценочные обязательства
	НоваяСтрока = ТаблицаВидовАктивовИОбязательств.Добавить();
	НоваяСтрока.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.ОценочныеОбязательства;
	НоваяСтрока.Счета.Добавить(ПланыСчетов.Хозрасчетный.РезервыПредстоящихРасходов);
    
	Возврат ТаблицаВидовАктивовИОбязательств;
	
КонецФункции // ПолучитьТаблицуВидовАктивовИОбязательств()

// Определяются суммы временных разницы, которые отражены на плане счетов
// по конкретному виду ОНА (ОНО) и распределяются на вычитаемые и налогооблагаемые разницы.
//
// Параметры:
//		СтрокаВидАктиваОбязательства - строка таблицы в которой задано соответствие
//			вида актива (обязательства) и счетов налогового учета, на которых
//			отражаются разницы по этому виду активов.
//
Функция ОборотыВременныхРазницПоВидуАктивовОбязательств(СтрокаВидАктиваОбязательства, СтруктураДопПараметров, СтруктураШапкиДокумента, УчетПоПодразделениям, ЭтоОтчет = Ложь) Экспорт
	
	КоличествоОбъектовАналитики = СтрокаВидАктиваОбязательства.Субконто.Количество();
	//Если КоличествоОбъектовАналитики = 0 Тогда
	//	Возврат Новый ТаблицаЗначений;
	//КонецЕсли;
	
	МассивСубконто = СтрокаВидАктиваОбязательства.Субконто;
	Если НЕ КоличествоОбъектовАналитики = 0 Тогда
		ФлагОС_НМА = ?(МассивСубконто[0] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства, 1, 0);
		ФлагОС_НМА = ?(МассивСубконто[0] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы, 1, ФлагОС_НМА);
		ФлагОбъектовСтроительства = ?(МассивСубконто[0] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства, 1, 0);
		ФлагНЗП = ?(СтрокаВидАктиваОбязательства.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.НезавершенноеПроизводство, 1, 0);
	Иначе
		ФлагОС_НМА = Ложь;
		ФлагОбъектовСтроительства = Ложь;
		ФлагНЗП = Ложь;
	КонецЕсли;

	Запрос = Новый Запрос;                                           
	Запрос.УстановитьПараметр("НачалоМесяца",   СтруктураДопПараметров.НачГраница);
	Запрос.УстановитьПараметр("КонецМесяца",    СтруктураДопПараметров.КонГраница);
	Запрос.УстановитьПараметр("Организация",    СтруктураШапкиДокумента.СписокОП);
	Запрос.УстановитьПараметр("МассивСчетов",   СтрокаВидАктиваОбязательства.Счета);
	Запрос.УстановитьПараметр("ВидСубконто",    МассивСубконто);
	Запрос.УстановитьПараметр("СчетЕН",         ПолучитьМассивСчетовЕНВД());
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,
	               |	ХозрасчетныйОстаткиИОбороты.Подразделение КАК Подразделение,
	               |	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК Субконто1,
	               |	ХозрасчетныйОстаткиИОбороты.Субконто2 КАК Субконто2,
	               |	ХозрасчетныйОстаткиИОбороты.Субконто3 КАК Субконто3,
	               |	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаВРНачальныйОстатокДт) КАК НачОстатокДт,
	               |	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокДт) КАК КонОстатокДт,
	               |	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаВРНачальныйОстатокКт) КАК НачОстатокКт,
	               |	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокКт) КАК КонОстатокКт,
	               |	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаВРОборотДт) КАК ОборотДт,
	               |	СУММА(ХозрасчетныйОстаткиИОбороты.СуммаВРОборотКт) КАК ОборотКт,
	               |	СУММА(0) КАК ОборотЕН
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоМесяца, &КонецМесяца, , , Счет В ИЕРАРХИИ (&МассивСчетов), &ВидСубконто, Организация В ИЕРАРХИИ (&Организация)) КАК ХозрасчетныйОстаткиИОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОстаткиИОбороты.Счет,
	               |	ХозрасчетныйОстаткиИОбороты.Субконто1,
	               |	ХозрасчетныйОстаткиИОбороты.Субконто2,
	               |	ХозрасчетныйОстаткиИОбороты.Субконто3,
	               |	ХозрасчетныйОстаткиИОбороты.Подразделение
	               |
	               |ДЛЯ ИЗМЕНЕНИЯ
	               |	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.Подразделение,
	               |	ХозрасчетныйОбороты.Субконто1,
	               |	ХозрасчетныйОбороты.Субконто2,
	               |	ХозрасчетныйОбороты.Субконто3,
	               |	СУММА(0),
	               |	СУММА(0),
	               |	СУММА(0),
	               |	СУММА(0),
	               |	СУММА(0),
	               |	СУММА(0),
	               |	СУММА(ХозрасчетныйОбороты.СуммаВРОборотКт)
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоМесяца, &КонецМесяца, , Счет В ИЕРАРХИИ (&МассивСчетов), &ВидСубконто, Организация В ИЕРАРХИИ (&Организация), КорСчет В ИЕРАРХИИ (&СчетЕН), ) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Счет,
	               |	ХозрасчетныйОбороты.Субконто1,
	               |	ХозрасчетныйОбороты.Субконто2,
	               |	ХозрасчетныйОбороты.Субконто3,
	               |	ХозрасчетныйОбороты.Подразделение";
	
	
	Если ФлагОС_НМА = 1 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ХозрасчетныйОстаткиИОбороты.Счет КАК Счет,", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ХозрасчетныйОстаткиИОбороты.Счет,", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ХозрасчетныйОбороты.Счет,", "");
	КонецЕсли;
	
	Если КоличествоОбъектовАналитики < 3 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОстаткиИОбороты.Субконто3 КАК Субконто3", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОстаткиИОбороты.Субконто3", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОбороты.Субконто3", "");
	КонецЕсли;
	
	Если КоличествоОбъектовАналитики < 2 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОстаткиИОбороты.Субконто2 КАК Субконто2", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОстаткиИОбороты.Субконто2", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОбороты.Субконто2", "");
	КонецЕсли;
	
	Если КоличествоОбъектовАналитики < 1 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК Субконто1", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОстаткиИОбороты.Субконто1", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОбороты.Субконто1", "");
		КонецЕсли;
		
	Если СтрокаВидАктиваОбязательства.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.РасходыБудущихПериодов И Не ЭтоОтчет Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ХозрасчетныйОстаткиИОбороты.СуммаВРОборотДт", "ВЫБОР КОГДА Субконто1.ВидРБП = &УбыткиПрошлыхЛет ТОГДА 0 ИНАЧЕ ХозрасчетныйОстаткиИОбороты.СуммаВРОборотДт КОНЕЦ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокДт", "ХозрасчетныйОстаткиИОбороты.СуммаВРКонечныйОстатокДт - ВЫБОР КОГДА Субконто1.ВидРБП = &УбыткиПрошлыхЛет ТОГДА ХозрасчетныйОстаткиИОбороты.СуммаВРОборотДт ИНАЧЕ 0 КОНЕЦ");
		
		Запрос.УстановитьПараметр("УбыткиПрошлыхЛет",  Перечисления.ВидыРБП.УбыткиПрошлыхЛет);
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	ТаблицаРезультатов = Результат.Выгрузить();
	
	ТаблицаРезультатов.Свернуть("" + ?(ФлагОС_НМА = 1, "", "Счет") + ?(УчетПоПодразделениям,",Подразделение", "") + ?(КоличествоОбъектовАналитики = 0, "",",Субконто1") + ?(ФлагОбъектовСтроительства = 1, "", ?(КоличествоОбъектовАналитики = 2, ",Субконто2","")) + ?(ФлагНЗП = 1, "", ?(КоличествоОбъектовАналитики = 3, ",Субконто3","")),"НачОстатокКт,КонОстатокКт,НачОстатокДт,КонОстатокДт,ОборотДт,ОборотКт,ОборотЕН");
	
	 Если ТаблицаРезультатов.Количество() > 0 Тогда

	ТаблицаРезультатов.Колонки.Добавить("СуммаДт09");
	ТаблицаРезультатов.Колонки.Добавить("СуммаКт09");
	ТаблицаРезультатов.Колонки.Добавить("СуммаДт77");
	ТаблицаРезультатов.Колонки.Добавить("СуммаКт77");
	ТаблицаРезультатов.Колонки.Добавить("СуммаЕНВД");
	
	
	Запрос = Новый Запрос;                                           
	Запрос.УстановитьПараметр("НачалоМесяца",   СтруктураДопПараметров.НачГраница);
	Запрос.УстановитьПараметр("КонецМесяца",    СтруктураДопПараметров.КонГраница);
	Запрос.УстановитьПараметр("Организация",    СтруктураДопПараметров.Организация);
	НеРаспределяемыеСчета = Новый Массив;
	НеРаспределяемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство);
	НеРаспределяемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства);
	НеРаспределяемыеСчета.Добавить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы);
	Запрос.УстановитьПараметр("НеРаспределяемыеСчета",           НеРаспределяемыеСчета);
	
	Запрос.УстановитьПараметр("ВидСубконто",       ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВидСубконто91",     ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	Запрос.УстановитьПараметр("Распределяемые",    Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ВидДеятельностиОпределяетсяПоДоходам);
	Запрос.УстановитьПараметр("ЕНВД",              Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД);
	Запрос.УстановитьПараметр("Счет91",      ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы);
	Запрос.УстановитьПараметр("Счет97",      ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов);
	Запрос.УстановитьПараметр("СчетПрочихРасходов", ПланыСчетов.Хозрасчетный.ПрочиеРасходы);
	Запрос.УстановитьПараметр("КорСчет", СтрокаВидАктиваОбязательства.Счета);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА ХозрасчетныйОбороты.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат = &Распределяемые
	               |			ТОГДА ЕСТЬNULL(ХозрасчетныйОбороты.СуммаВРОборотДт, 0)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаЗатрат,
	               |	ВЫБОР
	               |		КОГДА ХозрасчетныйОбороты.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат = &ЕНВД
	               |			ТОГДА ЕСТЬNULL(ХозрасчетныйОбороты.СуммаВРОборотДт, 0)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаЗатратЕНВД,
	               |	ХозрасчетныйОбороты.КорСубконто1 КАК КорСубконто1
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоМесяца, &КонецМесяца, , (НЕ Счет В (&НеРаспределяемыеСчета)), &ВидСубконто, Организация В ИЕРАРХИИ (&Организация), КорСчет В ИЕРАРХИИ (&КорСчет), ) КАК ХозрасчетныйОбороты
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВЫБОР
	               |		КОГДА ХозрасчетныйОбороты.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат = &Распределяемые
	               |			ТОГДА ЕСТЬNULL(ХозрасчетныйОбороты.СуммаВРОборотДт, 0)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА ХозрасчетныйОбороты.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат = &ЕНВД
	               |			ТОГДА ЕСТЬNULL(ХозрасчетныйОбороты.СуммаВРОборотДт, 0)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ХозрасчетныйОбороты.КорСубконто1
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоМесяца, &КонецМесяца, , Счет В ИЕРАРХИИ (&СчетПрочихРасходов), &ВидСубконто91, Организация В ИЕРАРХИИ (&Организация), КорСчет В ИЕРАРХИИ (&КорСчет), ) КАК ХозрасчетныйОбороты
	               |ИТОГИ
	               |	СУММА(СуммаЗатрат),
	               |	СУММА(СуммаЗатратЕНВД)
	               |ПО
	               |	КорСубконто1";
	
	Результат = Запрос.Выполнить();
	ТаблицаРаспределяемыхРасходов = Результат.Выгрузить();
	
			
		Для Каждого Строка Из ТаблицаРезультатов Цикл
			СводноеСальдоКон = ?(Строка.КонОстатокДт = NULL, 0, Строка.КонОстатокДт) - ?(Строка.КонОстатокКт = NULL, 0, Строка.КонОстатокКт);
			СводноеСальдоНач = ?(Строка.НачОстатокДт = NULL, 0, Строка.НачОстатокДт) - ?(Строка.НачОстатокКт = NULL, 0, Строка.НачОстатокКт);
			
			Разница = ?(Строка.ОборотДт = NULL, 0, Строка.ОборотДт) - ?(Строка.ОборотКт = NULL, 0, Строка.ОборотКт);
			
			// Определим сумму временных разниц, приходящуюся на деятельность, облагаемую ЕНВД,
			// для распределяемых затрат, связанных с амортизацией и списанием РБП.
			СуммаРаспределяемогоРасхода = 0;
			СуммаЕНВДРасхода     = 0;
			СтрокаРаспределяемогоРасхода = ?(КоличествоОбъектовАналитики = 0, Неопределено, ТаблицаРаспределяемыхРасходов.Найти(Строка.Субконто1, "КорСубконто1"));
			
			Если СтрокаРаспределяемогоРасхода = Неопределено Тогда
				Строка.СуммаЕНВД = Строка.ОборотЕН;
			Иначе
				СуммаРаспределяемогоРасхода = СтрокаРаспределяемогоРасхода.СуммаЗатрат;
				СуммаЕНВДРасхода = СтрокаРаспределяемогоРасхода.СуммаЗатратЕНВД;
				Строка.СуммаЕНВД = СуммаРаспределяемогоРасхода * СтруктураДопПараметров.КоэффициентЕНВД + СуммаЕНВДРасхода;
			КонецЕсли;
			
			Если СводноеСальдоНач > 0 Тогда
				Если  СводноеСальдоКон - СводноеСальдоНач > 0 Тогда
					Строка.СуммаКт77 = Разница;
					Строка.СуммаДт77 = 0;
					Строка.СуммаКт09 = 0;
					Строка.СуммаДт09 = 0;
				ИначеЕсли  СводноеСальдоКон - СводноеСальдоНач = 0 Тогда
					Строка.СуммаКт77 = 0;
					Строка.СуммаДт77 = 0;
					Строка.СуммаКт09 = 0;
					Строка.СуммаДт09 = 0;
					Иначе  Если (СводноеСальдоКон < 0) И (СводноеСальдоНач >= 0) Тогда
						Строка.СуммаДт09 = - СводноеСальдоКон;
						Строка.СуммаДт77 = СводноеСальдоНач;
						Строка.СуммаКт09 = 0;
						Строка.СуммаКт77 = 0;
					Иначе
						Строка.СуммаКт77 = 0;
						Строка.СуммаДт77 = - Разница;
						Строка.СуммаКт09 = 0;
						Строка.СуммаДт09 = 0;
						
						
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если  СводноеСальдоКон - СводноеСальдоНач < 0 Тогда
					Строка.СуммаКт77 = 0;
					Строка.СуммаДт77 = 0;
					Строка.СуммаКт09 = 0;
					Строка.СуммаДт09 = - СводноеСальдоКон + СводноеСальдоНач;
				ИначеЕсли  СводноеСальдоКон - СводноеСальдоНач = 0 Тогда
					Строка.СуммаКт77 = 0;
					Строка.СуммаДт77 = 0;
					Строка.СуммаКт09 = 0;
					Строка.СуммаДт09 =  0;
					Иначе  Если (СводноеСальдоКон > 0) И (СводноеСальдоНач <= 0) Тогда
						Строка.СуммаКт77 = СводноеСальдоКон;
						Строка.СуммаДт77 = 0;
						Строка.СуммаКт09 = - СводноеСальдоНач;
						Строка.СуммаДт09 = 0;
					Иначе
						Строка.СуммаКт77 = 0;
						Строка.СуммаДт77 = 0;
						Строка.СуммаКт09 = Разница;
						Строка.СуммаДт09 = 0;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;	 	 
	
	Возврат  ТаблицаРезультатов;
КонецФункции // ОборотыВременныхРазницПоВидуАктивовОбязательств()

// Определяются суммы временных разницы, которые отражены на плане счетов
// по конкретному виду ОНА (ОНО) и распределяются на вычитаемые и налогооблагаемые разницы,
// числящиеся на остатках текущего периода.
//
// Параметры:
//		СтрокаВидАктиваОбязательства - строка таблицы в которой задано соответствие
//			вида актива (обязательства) и счетов налогового учета, на которых
//			отражаются разницы по этому виду активов.
//
Функция ОстаткиВременныхРазницПоВидуАктивовОбязательств(СтрокаВидАктиваОбязательства, Организация, НачалоГода, КонецГода) Экспорт
	
	КоличествоОбъектовАналитики = СтрокаВидАктиваОбязательства.Субконто.Количество();
	
	Если Не КоличествоОбъектовАналитики = 0 Тогда
		
		МассивСубконто = СтрокаВидАктиваОбязательства.Субконто;
		ФлагОС_НМА = ?(МассивСубконто[0] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства, 1, 0);
		ФлагОС_НМА = ?(МассивСубконто[0] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы, 1, ФлагОС_НМА);
		ФлагОбъектовСтроительства = ?(МассивСубконто[0] = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства, 1, 0);
		ФлагНЗП = ?(СтрокаВидАктиваОбязательства.ВидАктивовОбязательств = Перечисления.ВидыАктивовИОбязательств.НезавершенноеПроизводство, 1, 0);
	Иначе
		ФлагОС_НМА = Ложь;
		ФлагОбъектовСтроительства = Ложь;
		ФлагНЗП = Ложь;
	КонецЕсли;
 
	Запрос = Новый Запрос;  
	Запрос.УстановитьПараметр("НачалоГода",     НачалоГода);
	Запрос.УстановитьПараметр("КонецГода",      КонецГода);
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("МассивСчетов",   СтрокаВидАктиваОбязательства.Счета);
	Запрос.УстановитьПараметр("ВидУчета", 	    Перечисления.ВидыУчетаПоПБУ18.ВР);
	Запрос.УстановитьПараметр("ВидСубконто",    МассивСубконто);
	Запрос.УстановитьПараметр("Счет09",         ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеАктивы);
	Запрос.УстановитьПараметр("Счет77",         ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеОбязательства);
    Запрос.УстановитьПараметр("ВидАктивовОбязательств",СтрокаВидАктиваОбязательства.ВидАктивовОбязательств);
	Запрос.УстановитьПараметр("АктивыИОбязательства",ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ВидыАктивовИОбязательств);
    
    Массив = Новый Массив;
    Массив.Добавить(ПланыСчетов.Хозрасчетный.НераспределеннаяПрибыль);
    Массив.Добавить(ПланыСчетов.Хозрасчетный.ПрибылиИУбыткиОтПересчетаОНА_ОНО);
	Запрос.УстановитьПараметр("КорСчет",         Массив);
    
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОстаткиВР.Счет КАК Счет,
	               |	ОстаткиВР.Субконто1 КАК Субконто1,
	               |	ОстаткиВР.Субконто2 КАК Субконто2,
	               |	ОстаткиВР.Субконто3 КАК Субконто3,
	               |	&Счет09 КАК СчетОНАОНО,
	               |	ХозрасчетныйОстатки.СуммаОстатокДт - ХозрасчетныйОбороты.СуммаОборот КАК СтараяСумма,
	               |	СУММА(ВЫБОР
	               |			КОГДА ОстаткиВР.СуммаВРОстатокДт < 0
	               |					ИЛИ ОстаткиВР.СуммаВРОстатокКт > 0
	               |				ТОГДА -ОстаткиВР.СуммаВРОстаток
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК Сумма
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецГода, Счет В ИЕРАРХИИ (&МассивСчетов), &ВидСубконто, Организация В ИЕРАРХИИ (&Организация)) КАК ОстаткиВР,
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(
	               |			&КонецГода,
	               |			Счет В ИЕРАРХИИ (&Счет09),
	               |			&АктивыИОбязательства,
	               |			Организация В ИЕРАРХИИ (&Организация)
	               |				И Субконто1 = &ВидАктивовОбязательств) КАК ХозрасчетныйОстатки,
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоГода,
	               |			&КонецГода,
	               |			Период,
	               |			Счет В ИЕРАРХИИ (&Счет09),
	               |			&АктивыИОбязательства,
	               |			Организация В ИЕРАРХИИ (&Организация)
	               |				И Субконто1 = &ВидАктивовОбязательств,
	               |			КорСчет В ИЕРАРХИИ (&КорСчет),
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОстаткиВР.Счет,
	               |	ОстаткиВР.Субконто1,
	               |	ОстаткиВР.Субконто2,
	               |	ОстаткиВР.Субконто3,
	               |	ХозрасчетныйОстатки.СуммаОстатокДт - ХозрасчетныйОбороты.СуммаОборот
	               |
	               |ДЛЯ ИЗМЕНЕНИЯ
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ОстаткиВР.Счет,
	               |	ОстаткиВР.Субконто1,
	               |	ОстаткиВР.Субконто2,
	               |	ОстаткиВР.Субконто3,
	               |	&Счет77,
	               |	ХозрасчетныйОстатки.СуммаОстатокКт + ХозрасчетныйОбороты.СуммаОборот,
	               |	СУММА(ВЫБОР
	               |			КОГДА ОстаткиВР.СуммаВРОстатокДт > 0
	               |					ИЛИ ОстаткиВР.СуммаВРОстатокКт < 0
	               |				ТОГДА ОстаткиВР.СуммаВРОстаток
	               |			ИНАЧЕ 0
	               |		КОНЕЦ)
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецГода, Счет В ИЕРАРХИИ (&МассивСчетов), &ВидСубконто, Организация В ИЕРАРХИИ (&Организация)) КАК ОстаткиВР,
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(
	               |			&КонецГода,
	               |			Счет В ИЕРАРХИИ (&Счет77),
	               |			&АктивыИОбязательства,
	               |			Организация В ИЕРАРХИИ (&Организация)
	               |				И Субконто1 = &ВидАктивовОбязательств) КАК ХозрасчетныйОстатки,
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоГода,
	               |			&КонецГода,
	               |			Период,
	               |			Счет В ИЕРАРХИИ (&Счет77),
	               |			&АктивыИОбязательства,
	               |			Организация В ИЕРАРХИИ (&Организация)
	               |				И Субконто1 = &ВидАктивовОбязательств,
	               |			КорСчет В ИЕРАРХИИ (&КорСчет),
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОстаткиВР.Счет,
	               |	ОстаткиВР.Субконто1,
	               |	ОстаткиВР.Субконто2,
	               |	ОстаткиВР.Субконто3,
	               |	ХозрасчетныйОстатки.СуммаОстатокКт + ХозрасчетныйОбороты.СуммаОборот
	               |
	               |ДЛЯ ИЗМЕНЕНИЯ";
	
	
	Если ФлагОС_НМА = 1 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ОстаткиВР.Счет КАК Счет,", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ОстаткиВР.Счет,", "");
	КонецЕсли;
	
	Если КоличествоОбъектовАналитики < 3 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ОстаткиВР.Субконто3 КАК Субконто3", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ОстаткиВР.Субконто3", "");
	КонецЕсли;
	
	Если КоличествоОбъектовАналитики < 2 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ОстаткиВР.Субконто2 КАК Субконто2", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ОстаткиВР.Субконто2", "");
	КонецЕсли;
	
	Если КоличествоОбъектовАналитики < 1 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОстаткиИОбороты.Субконто1 КАК Субконто1", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОстаткиИОбороты.Субконто1", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ",
			|	ХозрасчетныйОбороты.Субконто1", "");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	ТаблицаРезультатов = Результат.Выгрузить();
	ТаблицаРезультатов.Свернуть("СчетОНАОНО,СтараяСумма," + ?(ФлагОС_НМА = 1, "", "Счет,") + ?(КоличествоОбъектовАналитики = 0, "","Субконто1") + ?(ФлагОбъектовСтроительства = 1, "", ?(КоличествоОбъектовАналитики = 2, ",Субконто2","")) + ?(ФлагНЗП = 1, "", ?(КоличествоОбъектовАналитики = 3, ",Субконто3","")),"Сумма");
	
	
	Возврат  ТаблицаРезультатов;
КонецФункции // ОборотыВременныхРазницПоВидуАктивовОбязательств()

Функция ОпределитьНеПринимаемыеДоходыРасходы(Проводка) Экспорт
	
	Если Не Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ПустаяСсылка() И (Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ПрочиеДоходы Или Проводка.СчетКт.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ПрочиеДоходы)) Тогда
		Для Каждого Субконто Из Проводка.СубконтоКт Цикл
			Если ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") И Не Субконто.Значение.ЭтоГруппа Тогда
				
				Если НЕ Субконто.Значение = Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка() И НЕ Субконто.Значение.ПринятиеКналоговомуУчету Тогда
					Возврат  Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Возврат Ложь;
	КонецЕсли;
			  	
	Для Каждого Субконто Из Проводка.СубконтоДт Цикл
		
		Если ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.СтатьиЗатрат") И Не Субконто.Значение.ЭтоГруппа Тогда
			
			
			Если Субконто.Значение.ВидРасходовНУ = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения Тогда
				
				// Расходы на строительство объектов основных средств, не относятся к расходам по производству и реализации
				Если Проводка.СчетДт = ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств Или Проводка.СчетДт.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств) Тогда
					Возврат Ложь;
				КонецЕсли;
				
				ОтнесениеКЕНВД = Субконто.Значение.ВидДеятельностиДляНалоговогоУчетаЗатрат;
				
				Если ОтнесениеКЕНВД = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД Тогда
					ОбщегоНазначения.СообщитьОбОшибке("" + Субконто.Значение + " - неправильно указана: для непринимаемых расходов (" + ОтнесениеКЕНВД + ").
					                 |Расходы, не учитываемые для целей налогообложения, не следует относить к распределяемым
									 |- это не имеет смысла для расчета налоговой базы и не поддерживается программой.", Ложь, , СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
				
				Если ОтнесениеКЕНВД = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ВидДеятельностиОпределяетсяПоДоходам Тогда
					ОбщегоНазначения.СообщитьОбОшибке("" + Субконто.Значение + " - неправильно указана: для непринимаемых расходов (" + ОтнесениеКЕНВД + ").
					                 |Расходы, не учитываемые для целей налогообложения, не следует относить к распределяемым
									 |- это не имеет смысла для расчета налоговой базы и не поддерживается программой.", Ложь, , СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
				
				Возврат Истина;
				Прервать;
			КонецЕсли; 
			
		ИначеЕсли ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") И Не Субконто.Значение.ЭтоГруппа Тогда
			
			Если НЕ Субконто.Значение = Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка() И НЕ Субконто.Значение.ПринятиеКналоговомуУчету Тогда
								
				ОтнесениеКЕНВД = Субконто.Значение.ВидДеятельностиДляНалоговогоУчетаЗатрат;
				
				Если ОтнесениеКЕНВД = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Проводка " + Проводка.Содержание + ": неправильно указана статья затрат для непринимаемых расходов (" + ОтнесениеКЕНВД + ").
					                 |Расходы, не учитываемые для целей налогообложения, не следует относить к деятельности, облагаемой ЕНВД,
									 |- это не имеет смысла для расчета налоговой базы и не поддерживается программой.", Ложь, , СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
				
				Если ОтнесениеКЕНВД = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ВидДеятельностиОпределяетсяПоДоходам Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Проводка " + Проводка.Содержание + ": неправильно указана статья затрат для непринимаемых расходов (" + ОтнесениеКЕНВД + ").
					                 |Расходы, не учитываемые для целей налогообложения, не следует относить к распределяемым
									 |- это не имеет смысла для расчета налоговой базы и не поддерживается программой.", Ложь, , СтатусСообщения.Важное);
					Возврат Ложь;
				КонецЕсли;
				
				Возврат Истина;
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Функция ОпределитьВнереализационныеДоходыРасходы(Проводка) Экспорт
	
	Если Не Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ПустаяСсылка() И Проводка.СчетКт.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ПрочиеДоходы) Тогда
		Для Каждого Субконто Из Проводка.СубконтоКт Цикл
			Если ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") И Не Субконто.Значение.ЭтоГруппа Тогда
				
				Если НЕ Субконто.Значение = Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка() И Субконто.Значение.ВидПрочихДоходовИРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы Тогда
					Возврат  Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Возврат Ложь;
	КонецЕсли;
			  	
	Для Каждого Субконто Из Проводка.СубконтоДт Цикл
		
		Если ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") И Не Субконто.Значение.ЭтоГруппа Тогда
			
			Если НЕ Субконто.Значение = Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка() И Субконто.Значение.ВидПрочихДоходовИРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы Тогда
				Возврат Истина;
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Функция ОпределитьЕНВДДоходыРасходы(Проводка) Экспорт
	
	Если Не Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ПустаяСсылка() И Проводка.СчетКт.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.ПрочиеДоходы) Тогда
	Для Каждого Субконто Из Проводка.СубконтоКт Цикл
		
		Если (ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.СтатьиЗатрат") И Не Субконто.Значение.ЭтоГруппа)
	    	Или (ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") И Не Субконто.Значение.ЭтоГруппа) Тогда
			
			Если Субконто.Значение.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
		Возврат Ложь;
	КонецЕсли;
			  	
	
	Для Каждого Субконто Из Проводка.СубконтоДт Цикл
		
		Если (ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.СтатьиЗатрат") И Не Субконто.Значение.ЭтоГруппа)
	    	Или (ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") И Не Субконто.Значение.ЭтоГруппа) Тогда
			
			Если Субконто.Значение.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

Функция ОпределитьРасходыЕНВД(Проводка) Экспорт
	
	Для Каждого Субконто Из Проводка.СубконтоДт Цикл
		
		Если (ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.СтатьиЗатрат") Или ТипЗнч(Субконто.Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы")) И Не Субконто.Значение.ЭтоГруппа Тогда
			// Расходы на строительство объектов основных средств, не относятся к расходам по производству и реализации
			Если Проводка.СчетДт = ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств Или Проводка.СчетДт.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств) Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ОтнесениеКЕНВД = Субконто.Значение.ВидДеятельностиДляНалоговогоУчетаЗатрат;
			
			Возврат (ОтнесениеКЕНВД = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД)
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции
	
Функция ПолучитьСтавкуНалогаНаПрибыль(СтруктураШапкиДокумента, СтавкаФБ = 0, СтавкаРФ = 0, СтавкаМестный = 0) Экспорт

	СтавкиНалога = РегистрыСведений.СтавкиНалогаНаПрибыльДляВсехОрганизаций.ПолучитьПоследнее(СтруктураШапкиДокумента.Дата);
	                            
	СтавкаФБ        = ?(ТипЗнч(СтавкиНалога.СтавкаФБ) = Тип("Число")       , СтавкиНалога.СтавкаФБ / 100       , 0);
	СтавкаРФ        = ?(ТипЗнч(СтавкиНалога.СтавкаСубъектРФ) = Тип("Число"), СтавкиНалога.СтавкаСубъектРФ / 100, 0);
	
	
	Если Константы.ПрименяютсяРазныеСтавкиНалогаНаПрибыль.Получить() Тогда
		
		Отбор = Новый Структура("Организация", СтруктураШапкиДокумента.Организация);
		Ставки = РегистрыСведений.СтавкиНалогаНаПрибыльВБюджетСубъектовРФ.ПолучитьПоследнее(СтруктураШапкиДокумента.Дата,Отбор);
		СтавкаРФ = ?(Ставки = Неопределено, 0, Ставки.СтавкаСубъектРФ / 100);
		
		// {ОбособленныеПодразделения
		Отбор = Новый Структура("Организация,ПериодРасчета", СтруктураШапкиДокумента.Организация,СтруктураШапкиДокумента.Дата);
		Ставки = РегистрыСведений.РасчетСтавкиНалогаНаПрибыльЗаМесяц.Получить(Отбор);
		РасчетнаяСтавкаНалога = ?(Ставки = Неопределено, 0, Ставки.Ставка / 100);
		СтавкаРФ = ?(РасчетнаяСтавкаНалога = 0, СтавкаРФ, РасчетнаяСтавкаНалога - СтавкаФБ);
		// }ОбособленныеПодразделения
		
		
	КонецЕсли;
	
	
	СтавкаНалога = СтавкаФБ + СтавкаРФ;
	
	Возврат СтавкаНалога;

КонецФункции // ПолучитьСтавкуНалогаНаПрибыль()

// Расчет транспортных расходов, которые необходимо списать.
// 
Процедура РасчетТранспортныхРасходов(НачГраница, КонГраница, ТекОрганизация, ПараметрыСтруктура) Экспорт

	Если ТипЗнч(ТекОрганизация) = Тип("СписокЗначений") Тогда
		ТекущаяОрганизация = ТекОрганизация[0].Значение;
	Иначе
		ТекущаяОрганизация = ТекОрганизация;
	КонецЕсли;
	Организация = ОбщегоНазначения.ГоловнаяОрганизация(ТекущаяОрганизация);
	
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(НачГраница, Ложь, Организация,,Ложь);
	Если УчетнаяПолитика.Количество() = 0 Тогда
		ИспользоватьДанныеБУ = Ложь;
	Иначе
		ИспользоватьДанныеБУ = УчетнаяПолитика.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная
			ИЛИ Организация.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
	КонецЕсли;
	
	СписокСчетов = Новый Массив;
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.Товары);
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.ПокупныеТоварыОтгруженные);
	СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.ТорговаяНаценка);
	
	СписокКорСчетов = Новый Массив;
	СписокКорСчетов.Добавить(ПланыСчетов.Хозрасчетный.СебестоимостьПродаж);
	
	// СКД41-СКК42+СКД45.01
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(РБОстатки.СуммаНУКонечныйОстаток, 0) КАК ОстатокТовараНУ,
	|	ЕСТЬNULL(РБОстатки.СуммаНУНачальныйОстаток, 0) КАК ОстатокТовараНачНУ,
	|	0 КАК ПриходТовараНУ,
	|	0 КАК РасходТовараНУ,
	|	ЕСТЬNULL(РБОстатки.СуммаКонечныйОстаток, 0) КАК ОстатокТовараБУ,
	|	ЕСТЬNULL(РБОстатки.СуммаНачальныйОстаток, 0) КАК ОстатокТовараНачБУ,
	|	0 КАК ПриходТовараБУ,
	|	0 КАК РасходТовараБУ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачГраница, &КонГраница, , , Счет В ИЕРАРХИИ (&Счет), , Организация В ИЕРАРХИИ (&Организация)) КАК РБОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотКт, 0),
	|	0,
	|	0,
	|	ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0)
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачГраница,
	|			&КонГраница,
	|			,
	|			Счет В ИЕРАРХИИ (&Счет),
	|			,
	|			Организация В ИЕРАРХИИ (&Организация),
	|			(НЕ КорСчет В ИЕРАРХИИ (&Счет))
	|				И (НЕ КорСчет = &ПустойСчет),
	|			) КАК ХозрасчетныйОбороты";
	
	Запрос.УстановитьПараметр("НачГраница",  НачГраница);
	Запрос.УстановитьПараметр("КонГраница",  КонГраница);
	Запрос.УстановитьПараметр("Организация", ТекОрганизация);
	Запрос.УстановитьПараметр("Счет",        СписокСчетов);
	Запрос.УстановитьПараметр("ПустойСчет",  ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Если ИспользоватьДанныеБУ Тогда
		Постфикс = "БУ";
	Иначе
		Постфикс = "НУ";
	КонецЕсли;
	
	ОстатокТовара    = 0;
	ОстатокТовараНач = 0;
	ПриходТовара     = 0;
	РасходТовара     = 0;
	Пока РезультатЗапроса.Следующий() Цикл
		ОстатокТовара    = ОстатокТовара    + РезультатЗапроса["ОстатокТовара" + Постфикс];
		ОстатокТовараНач = ОстатокТовараНач + РезультатЗапроса["ОстатокТовараНач" + Постфикс];
		ПриходТовара     = ПриходТовара     + РезультатЗапроса["ПриходТовара" + Постфикс];
		РасходТовара     = РасходТовара     + РезультатЗапроса["РасходТовара" + Постфикс];
	КонецЦикла;
	ПараметрыСтруктура.НачОстатокТоваров = ОстатокТовараНач;
	ПараметрыСтруктура.КонОстатокТоваров = ОстатокТовара;
	ПараметрыСтруктура.ПриходТовара      = ПриходТовара;
	
	// ДО(90.02,41)-ДО(90.02,42)+ДО(90.02,45.01);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(РБОбороты.СуммаНУОборот, 0) КАК СтоимостьРеализованныхТоваровНУ,
	|	0 КАК СтоимостьРеализованныхТоваровЕНВДНУ,
	|	ЕСТЬNULL(РБОбороты.СуммаОборот, 0) КАК СтоимостьРеализованныхТоваровБУ,
	|	0 КАК СтоимостьРеализованныхТоваровЕНВДБУ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачГраница, &КонГраница, , Счет В ИЕРАРХИИ (&Счет), , Организация В ИЕРАРХИИ (&Организация), КорСчет В ИЕРАРХИИ (&КорСчет), ) КАК РБОбороты";
	
	Запрос.УстановитьПараметр("НачГраница",  НачГраница);
	Запрос.УстановитьПараметр("КонГраница",  КонГраница);
	Запрос.УстановитьПараметр("Организация", ТекОрганизация);
	Запрос.УстановитьПараметр("Счет",        СписокКорСчетов);
	Запрос.УстановитьПараметр("КорСчет",     СписокСчетов);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	СуммаРеализованныхТоваров     = 0;
	СуммаРеализованныхТоваровЕНДВ = 0;
	
	Пока РезультатЗапроса.Следующий() Цикл
		СуммаРеализованныхТоваров     = СуммаРеализованныхТоваров     + РезультатЗапроса["СтоимостьРеализованныхТоваров" + Постфикс];
		СуммаРеализованныхТоваровЕНДВ = СуммаРеализованныхТоваровЕНДВ + РезультатЗапроса["СтоимостьРеализованныхТоваровЕНВД" + Постфикс];
	КонецЦикла;
	
	ПараметрыСтруктура.СуммаРеализованныхТоваров     = СуммаРеализованныхТоваров;
	ПараметрыСтруктура.СуммаРеализованныхТоваровЕНДВ = СуммаРеализованныхТоваровЕНДВ; 
	СтоимостьРеализованныхТоваров                    = СуммаРеализованныхТоваров + СуммаРеализованныхТоваровЕНДВ;
	Списано = РасходТовара - СтоимостьРеализованныхТоваров;
	
	Если СтоимостьРеализованныхТоваров + ОстатокТовара = 0 Тогда
		ПараметрыСтруктура.СуммаРасходовКСписанию = 0;
		ПараметрыСтруктура.СреднийПроцент         = 0;
		Возврат;
	КонецЕсли;
	
	ПараметрыСтруктура.СреднийПроцент =
		ОКР(ПараметрыСтруктура.СуммаРасходов / (СтоимостьРеализованныхТоваров + ОстатокТовара), 8);
	
	Если СтоимостьРеализованныхТоваров  = 0 Тогда
		
		ПараметрыСтруктура.СуммаРасходовКСписанию = 0;
	Иначе
		ПараметрыСтруктура.СуммаРасходовКсписанию =
			ПараметрыСтруктура.СуммаРасходов - Окр(ПараметрыСтруктура.СреднийПроцент * ОстатокТовара, 2);
	КонецЕсли;
	
	ПараметрыСтруктура.ДоляРасхода = ?(ПараметрыСтруктура.СуммаРасходов = 0, 0, ПараметрыСтруктура.СуммаРасходовКсписанию / ПараметрыСтруктура.СуммаРасходов);
	
КонецПроцедуры // РасчетТранспортныхРасходов()

Функция СписокПрямыхРасходовНУ(Организация, Дата, ДатаКон = неопределено) Экспорт
	
	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("ПериодДействия", НачалоГода(Дата));
	Запрос.УстановитьПараметр("НачДата",НачалоГода(Дата));
	Запрос.УстановитьПараметр("КонДата",?(ДатаКон=неопределено,КонецМесяца(Дата),КонецДня(ДатаКон)));
	Запрос.УстановитьПараметр("Организация",  Организация);
	Массив = Новый Массив;
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
	Запрос.УстановитьПараметр("СписокСчетов",  Массив);
	Массив = Новый Массив;
	Массив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВидСубконто",  Массив);
	Массив = Новый Массив;
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ДоходныеВложенияВ_МЦ);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.Материалы);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.Товары);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСРазнымиДебиторамиИКредиторами);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами_);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоПрочимОперациям);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоНалогам);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию);
	Запрос.УстановитьПараметр("СписокКорСчетов",  Массив);
	Запрос.УстановитьПараметр("ПустойВидРасходовНУ", Перечисления.ВидыРасходовНУ.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСтатьяЗатрат", Справочники.СтатьиЗатрат.ПустаяСсылка());
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	//Ищем дату действующих правил
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	Периоды.ПериодДействия КАК ПериодДействия
	               |ИЗ
	               |	РегистрСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ КАК Периоды
	               |ГДЕ
	               |	Периоды.Организация = &Организация
	               |	И Периоды.ПериодДействия >= &ПериодДействия
	               |	И Периоды.ПериодДействия <= &КонДата
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Периоды.ПериодДействия
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПериодДействия УБЫВ";
	Периоды = Запрос.Выполнить().Выгрузить();
	Если Периоды.Количество()>0 Тогда
		ДатаДействующихПравил = Периоды[0].ПериодДействия;
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Правила.Подразделение,
		|	Правила.Счет,
		|	Правила.КорСчет,
		|	Правила.СтатьяЗатрат,
		|	Правила.ВидРасходовНУ
		|ПОМЕСТИТЬ СписокВсехПравил
		|ИЗ
		|	РегистрСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ КАК Правила
		|ГДЕ
		|	Правила.Организация = &Организация
		|	И Правила.ПериодДействия = &ПериодДействия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Правила.ВидРасходовНУ КАК ВидЗатратНУ,
		|	Правила.Подразделение КАК Подразделение,
		|	Правила.Счет КАК СчетЗатрат,
		|	Правила.КорСчет КАК КорСчетЗатрат,
		|	Правила.СтатьяЗатрат КАК СтатьяЗатрат
		|ПОМЕСТИТЬ СписокПрямыхРасходовНУ
		|ИЗ
		|	СписокВсехПравил КАК Правила";
		
		Результат = Запрос.Выполнить();
		Возврат Запрос.МенеджерВременныхТаблиц;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ПериодДействия", ДатаДействующихПравил);
	
	//Определяем какие колонки будут выводиться
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	СУММА(ВЫБОР
	               |			КОГДА Правила.Подразделение = &ПустоеПодразделение
	               |				ТОГДА 0
	               |			ИНАЧЕ 1
	               |		КОНЕЦ) КАК Поле1,
	               |	СУММА(ВЫБОР
	               |			КОГДА Правила.Счет = &ПустойСчет
	               |				ТОГДА 0
	               |			ИНАЧЕ 1
	               |		КОНЕЦ) КАК Поле2,
	               |	СУММА(ВЫБОР
	               |			КОГДА Правила.КорСчет = &ПустойСчет
	               |				ТОГДА 0
	               |			ИНАЧЕ 1
	               |		КОНЕЦ) КАК Поле3,
	               |	СУММА(ВЫБОР
	               |			КОГДА Правила.СтатьяЗатрат = &ПустаяСтатьяЗатрат
	               |				ТОГДА 0
	               |			ИНАЧЕ 1
	               |		КОНЕЦ) КАК Поле4
	               |ИЗ
	               |	РегистрСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ КАК Правила
	               |ГДЕ
	               |	Правила.Организация = &Организация
	               |	И Правила.ПериодДействия = &ПериодДействия";
	ВидимыеКолонки = Запрос.Выполнить().Выгрузить();
	ЕстьПодразделение = ВидимыеКолонки[0].Поле1>0;
	ЕстьСчет = ВидимыеКолонки[0].Поле2>0;
	ЕстьКорСчет = ВидимыеКолонки[0].Поле3>0;
	ЕстьСтатьяЗатрат = ВидимыеКолонки[0].Поле4>0;
	
	//Собственно сам список прямых расходов
	Запрос.Текст =
	 "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	 |	Правила.Подразделение,
	 |	Правила.Счет,
	 |	Правила.КорСчет,
	 |	Правила.СтатьяЗатрат,
	 |	Правила.ВидРасходовНУ
	 |ПОМЕСТИТЬ СписокВсехПравил
	 |ИЗ
	 |	РегистрСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ КАК Правила
	 |ГДЕ
	 |	Правила.Организация = &Организация
	 |	И Правила.ПериодДействия = &ПериодДействия
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ РАЗРЕШЕННЫЕ"
	 +?(ЕстьПодразделение,	Символы.ПС+"	Правила.Подразделение,","")
	 +?(ЕстьСчет,			Символы.ПС+"	Правила.Счет,","")
	 +?(ЕстьКорСчет,		Символы.ПС+"	Правила.КорСчет,","")
	 +?(ЕстьСтатьяЗатрат,	Символы.ПС+"	Правила.СтатьяЗатрат,","")
	 +Символы.ПС+"	Правила.ВидРасходовНУ
	 |ПОМЕСТИТЬ СписокПравил
	 |ИЗ
	 |	СписокВсехПравил КАК Правила
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ"
	 +Символы.ПС+?(ЕстьПодразделение,	"	ХозрасчетныйОбороты.Подразделение КАК Подразделение,","НЕОПРЕДЕЛЕНО КАК Подразделение,")
	 +Символы.ПС+?(ЕстьСчет,			"	ХозрасчетныйОбороты.Счет КАК СчетЗатрат,","НЕОПРЕДЕЛЕНО КАК СчетЗатрат,")
	 +Символы.ПС+?(ЕстьКорСчет,			"	ХозрасчетныйОбороты.КорСчет КАК КорСчетЗатрат,","НЕОПРЕДЕЛЕНО КАК КорСчетЗатрат,")
	 +Символы.ПС+?(ЕстьСтатьяЗатрат,	"	ХозрасчетныйОбороты.Субконто1 КАК СтатьяЗатрат,","НЕОПРЕДЕЛЕНО КАК СтатьяЗатрат,")
	 +Символы.ПС+"	ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ КАК ВидЗатратНУ
	 |ПОМЕСТИТЬ СписокПрямыхРасходовНУ
	 |ИЗ
	 |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	 |			&НачДата,
	 |			&КонДата,
	 |			,
	 |			Счет В ИЕРАРХИИ (&СписокСчетов),
	 |			&ВидСубконто,
	 |			Организация = &Организация
	 |				И (ВЫРАЗИТЬ(Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ В (ВЫБРАТЬ РАЗЛИЧНЫЕ СписокПравил.ВидРасходовНУ ИЗ СписокПравил) ИЛИ ВЫРАЗИТЬ(Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ ЕСТЬ NULL),
	 |			,
	 |			) КАК ХозрасчетныйОбороты
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СписокПравил КАК Правила
	 |		ПО Выбор Когда ВЫРАЗИТЬ(Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ ЕСТЬ NULL ТОГДА ИСТИНА ИНАЧЕ
	 |		   (Правила.ВидРасходовНУ = ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ)"
	 +?(ЕстьПодразделение,	Символы.ПС+"			И (ВЫБОР КОГДА Правила.Подразделение <> &ПустоеПодразделение ТОГДА Правила.Подразделение = ХозрасчетныйОбороты.Подразделение ИНАЧЕ ИСТИНА КОНЕЦ)","")
	 +?(ЕстьСчет,			Символы.ПС+"			И (ВЫБОР КОГДА Правила.Счет          <> &ПустойСчет          ТОГДА Правила.Счет = ХозрасчетныйОбороты.Счет                   ИНАЧЕ ИСТИНА КОНЕЦ)","")
	 +?(ЕстьКорСчет,		Символы.ПС+"			И (ВЫБОР КОГДА Правила.КорСчет       <> &ПустойСчет          ТОГДА Правила.КорСчет = ХозрасчетныйОбороты.КорСчет             ИНАЧЕ ИСТИНА КОНЕЦ)","")
	 +?(ЕстьСтатьяЗатрат,	Символы.ПС+"			И (ВЫБОР КОГДА Правила.СтатьяЗатрат  <> &ПустаяСтатьяЗатрат  ТОГДА Правила.СтатьяЗатрат = ХозрасчетныйОбороты.Субконто1      ИНАЧЕ ИСТИНА КОНЕЦ)","")
	 +" КОНЕЦ
	 |СГРУППИРОВАТЬ ПО"
	 +?(ЕстьПодразделение,	Символы.ПС+"	ХозрасчетныйОбороты.Подразделение,","")
	 +?(ЕстьСчет,			Символы.ПС+"	ХозрасчетныйОбороты.Счет,","")
	 +?(ЕстьКорСчет,		Символы.ПС+"	ХозрасчетныйОбороты.КорСчет,","")
	 +?(ЕстьСтатьяЗатрат,	Символы.ПС+"	ХозрасчетныйОбороты.Субконто1,","")
	 +Символы.ПС+"	ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ";
	 
Запрос.Текст = Запрос.Текст +	" Объединить все 
	|ВЫБРАТЬ
	|	ВыпускПродукцииУслугОбороты.Подразделение,
	|	ВыпускПродукцииУслугОбороты.СчетЗатрат,
	|	ВыпускПродукцииУслугОбороты.СчетСписания,
	|	ВЫБОР КОГДА ВыпускПродукцииУслугОбороты.СубконтоСписания2 ССЫЛКА Справочник.СтатьиЗатрат ТОГДА ВыпускПродукцииУслугОбороты.СубконтоСписания2 ИНАЧЕ ВыпускПродукцииУслугОбороты.СубконтоСписания3 КОНЕЦ КАК СтатьяЗатрат,
	|	ВЫБОР КОГДА ВыпускПродукцииУслугОбороты.СубконтоСписания2 ССЫЛКА Справочник.СтатьиЗатрат ТОГДА ВыпускПродукцииУслугОбороты.СубконтоСписания2.ВидРасходовНУ ИНАЧЕ ВыпускПродукцииУслугОбороты.СубконтоСписания3.ВидРасходовНУ КОНЕЦ КАК ВидРасходовНУ
	|ИЗ
	|	РегистрНакопления.ВыпускПродукцииУслуг.Обороты(
	|			&НачДата,
	|			&КонДата,
	|			,
	|			Организация = &Организация
	|				И ПрямыеРасходыРаспределятьПоКоличеству) КАК ВыпускПродукцииУслугОбороты
	|СГРУППИРОВАТЬ ПО
	|	ВыпускПродукцииУслугОбороты.Подразделение,
	|	ВыпускПродукцииУслугОбороты.СчетЗатрат,
	|	ВыпускПродукцииУслугОбороты.СчетСписания,
	|	ВЫБОР КОГДА ВыпускПродукцииУслугОбороты.СубконтоСписания2 ССЫЛКА Справочник.СтатьиЗатрат ТОГДА ВыпускПродукцииУслугОбороты.СубконтоСписания2 ИНАЧЕ ВыпускПродукцииУслугОбороты.СубконтоСписания3 КОНЕЦ,
	|	ВЫБОР КОГДА ВыпускПродукцииУслугОбороты.СубконтоСписания2 ССЫЛКА Справочник.СтатьиЗатрат ТОГДА ВыпускПродукцииУслугОбороты.СубконтоСписания2.ВидРасходовНУ ИНАЧЕ ВыпускПродукцииУслугОбороты.СубконтоСписания3.ВидРасходовНУ КОНЕЦ";	
	
	Результат = Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
 КонецФункции
 
Функция ТипРасходаНУ_Прямой(Подразделение, СтатьяЗатрат, Счет, КорСчет) Экспорт
	
	Возврат Ложь;
	
КонецФункции

Функция ОпределитьВидПрочихДоходовИРасходов(Список) Экспорт
	
	Для Индекс = 0 По Список.Количество() - 1 Цикл
		ТекущееЗначение = Список.Получить(Индекс);
		Если ТипЗнч(ТекущееЗначение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") И Не ТекущееЗначение = Неопределено И Не ТекущееЗначение = Справочники.ПрочиеДоходыИРасходы.ПустаяСсылка() Тогда
			Возврат ТекущееЗначение.ВидПрочихДоходовИРасходов;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
КонецФункции

Функция ПолучитьМассивСчетовУчетаРасходов() Экспорт
	
	СчетаЗатрат = Новый Массив;
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство);
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства);
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы);
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства);
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
	
	Возврат СчетаЗатрат;
	
КонецФункции // ПолучитьСписокСчетовУчетаКосвенныхРасходов()

Функция ПолучитьМассивВнереализационныхРасходов() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ВозмещениеУбытковКПолучениюУплате);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДолевоеУчастиеВИностранныхОрганизациях);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДолевоеУчастиеВРоссийскихОрганизациях);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыПоОперациямСФинансовымиИнструментамиСрочныхСделок);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыПоОперациямСФинансовымиИнструментамиСрочныхСделокОР);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСБезвозмезднымПолучениемИмущества);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСЛиквидациейОсновныхСредств);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСоСдачейИмуществаВАренду);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСУчастиемВДругихОрганизациях);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.КурсовыеРазницы);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.КурсовыеРазницыПоРасчетамВУЕ);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.НДСПоСписаннойКредиторскойЗадолженности);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ОтчисленияВОценочныеРезервы);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПремияПокупателю);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПрибыльУбытокПрошлыхЛет);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыКПолучениюУплате);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыНачисленныеПоСт269);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыПоГосударственнымЦеннымБумагам);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПроцентыПоГосударственнымЦеннымБумагамПоСтавке0);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.РазницаМеждуПервоначальнойИНоминальнойСтоимостьюПоДолговымЦеннымБумагам);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.РасходыНаУслугиБанков);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.РасходыОтСниженияСтоимостиАктивов);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.СписаниеДебиторскойКредиторскойЗадолженности);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ШтрафыПениНеустойкиКПолучениюУплате);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыСвязанныеСВосстановлениемАмортизационнойПремии);
	Массив.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.РасходыПоПередачеТоваровБезвозмездноИДляСобственныхНужд);
	
	Возврат Массив;
	
КонецФункции // ПолучитьМассивВнереализационныхРасходов()

Функция ПолучитьМассивСчетовЕНВД() Экспорт
	
	СчетаЗатрат = Новый Массив;
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ВыручкаЕНВД);
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.СебестоимостьПродажЕНВД);
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажуЕНВД);
	СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходыЕНВД);
	
	Возврат СчетаЗатрат;
	
КонецФункции // ПолучитьСписокСчетовУчетаКосвенныхРасходов()

Функция ПолучитьТекстВидаДеятельности(ПрименяетсяЕНВД, ЕстьОборотЕНВД, ЭтоЕНВД) Экспорт
	
	
	Если ПрименяетсяЕНВД Тогда
		
			Если ЭтоЕНВД  Тогда
				Возврат " по видам деятельности с особым порядком налогообложения";
			Иначе
				Возврат " по видам деятельности с основной системой налогообложения";
			КонецЕсли;
		
		
		Иначе  // Если ЕНВД не применятся, но обороты по счетам - ЕНВД есть, то будем уточнять вид налогообложения
			   // у видов деятельности
		
		Если ЕстьОборотЕНВД Тогда
			
			Если ЭтоЕНВД  Тогда
				Возврат " по видам деятельности с особым порядком налогообложени";
			Иначе
				Возврат " по видам деятельности с основной системой налогообложения";
			КонецЕсли;
			
		Иначе
			
			Возврат "";
		КонецЕсли;
		
	КонецЕсли;
		
КонецФункции

// Функция выполняет пропорциональное распределение суммы в соответствии
// с заданными коэффициентами распределения
//
// Параметры:
//		ИсхСумма - распределяемая сумма
//		МассивКоэф - массив коэффициентов распределения
//		Точность - точность округления при распределении. Необязателен.
//
//	Возврат:
//		МассивСумм - массив размерностью равный массиву коэффициентов, содержит
//			суммы в соответствии с весом коэффициента (из массива коэффициентов)
//          В случае если распределить не удалось (сумма = 0, кол-во коэф. = 0,
//          или суммарный вес коэф. = 0), тогда возвращается значение Неопределено
//
Функция РаспределитьПропорциональноСписокСумм(Знач ИсхСумма, МассивКоэф, Знач Точность = 2) Экспорт

	Если МассивКоэф.Количество() = 0  Или ИсхСумма = Неопределено Или (ИсхСумма.Сумма = 0 И ИсхСумма.СуммаНУ = 0 И ИсхСумма.СуммаПР = 0 И ИсхСумма.СуммаВР = 0) Тогда
		Возврат Неопределено;
	КонецЕсли;

	ИндексМакс = Новый Структура("Индекс,ИндексНУ,ИндексПР,ИндексВР",0,0,0,0);
	МаксЗнач   = 0;
	РаспрСумма = Новый Структура("Сумма,СУммаНУ,СуммаПР,СуммаВР",0,0,0,0);
	СуммаКоэф  = 0;

	Для К = 0 По МассивКоэф.Количество() - 1 Цикл

		МодульЧисла = ?(МассивКоэф[К] > 0, МассивКоэф[К], - МассивКоэф[К]);

		Если МаксЗнач < МодульЧисла Тогда
			МаксЗнач   = МодульЧисла;
			ИндексМакс = К;
		КонецЕсли;

		СуммаКоэф = СуммаКоэф + МассивКоэф[К];

	КонецЦикла;


	Если СуммаКоэф = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	МассивСумм = Новый Массив(МассивКоэф.Количество(), 4);

	Для К = 0 По МассивКоэф.Количество() - 1 Цикл
		
		Сумма =  Окр(ИсхСумма.Сумма   * МассивКоэф[К] / СуммаКоэф, Точность, 1);
		СуммаНУ =  Окр(ИсхСумма.СуммаНУ * МассивКоэф[К] / СуммаКоэф, Точность, 1);
		СуммаПР =  Окр(ИсхСумма.СуммаПР * МассивКоэф[К] / СуммаКоэф, Точность, 1);
		СуммаВР =  Окр(ИсхСумма.СуммаВР * МассивКоэф[К] / СуммаКоэф, Точность, 1);
		МассивСумм[К] = Новый Структура("Сумма,СуммаНУ,СуммаПР,СУммаВР",Сумма,СуммаНУ,СуммаПР,СУммаВр);
		РаспрСумма.Сумма    = РаспрСумма.Сумма + Сумма;
		РаспрСумма.СуммаНУ  = РаспрСумма.СуммаНУ + СуммаНУ;
		РаспрСумма.СуммаПР  = РаспрСумма.СуммаПР + СуммаПР;
		РаспрСумма.СуммаВР  = РаспрСумма.СуммаВР + СуммаВР;
	КонецЦикла;

	// Погрешности округления отнесем на коэффициент с максимальным весом
	Если Не РаспрСумма.Сумма = ИсхСумма.Сумма Тогда
		МассивСумм[ИндексМакс].Сумма = МассивСумм[ИндексМакс].Сумма + ИсхСумма.Сумма - РаспрСумма.Сумма;
	КонецЕсли;
	
	Если Не РаспрСумма.СуммаНУ = ИсхСумма.СуммаНУ Тогда
		МассивСумм[ИндексМакс].СуммаНУ = МассивСумм[ИндексМакс].СуммаНУ + ИсхСумма.СуммаНУ - РаспрСумма.СуммаНУ;
	КонецЕсли;
	
	Если Не РаспрСумма.СуммаПР = ИсхСумма.СуммаПР Тогда
		МассивСумм[ИндексМакс].СуммаПР = МассивСумм[ИндексМакс].СуммаПР + ИсхСумма.СуммаПР - РаспрСумма.СуммаПР;
	КонецЕсли;

	Если Не РаспрСумма.СуммаВР = ИсхСумма.СуммаВР Тогда
		МассивСумм[ИндексМакс].СуммаВР = МассивСумм[ИндексМакс].СуммаВР + ИсхСумма.СуммаВР - РаспрСумма.СуммаВР;
	КонецЕсли;
	
	Возврат МассивСумм;

КонецФункции // РаспределитьПропорционально()


// Определяет коэффициент распределения
// расходов по видам деятельности (ЕНВД / не ЕНВД).
//
// Параметры
//  Организация - СправочникСсылка.Организации
//  Дата  – Дата – одна из дат того месяца,
//			в котором необходимо рассчитать коэффициент.
//
// Возвращаемое значение:
//   Число – коэффициент распределения расходов по видам деятельности.
//
Функция КоэффициентРаспределенияРасходовПоВидамДеятельности(Организация, Знач Дата, Знач НачДата = Неопределено, ПоВсемСчетам = Истина, ВыручкаНеЕНВД = 0, ВыручкаЕНВД = 0, СписокОрганизаций = Неопределено) Экспорт
	
	Если СписокОрганизаций = Неопределено Тогда
		СписокОрганизаций = Организация;
	КонецЕсли;
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, Ложь, Организация);
	Если ЗначениеЗаполнено(УчетнаяПолитика) Тогда
		УчитыватьВсеДоходы = ?(ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, Ложь, Организация).БазаРаспределенияКосвенныхРасходовПоВидамДеятельности = Перечисления.БазыРаспределенияКосвенныхРасходовПоВидамДеятельности.ДоходыОтРеализацииИВнереализационные, Истина, Ложь);
	Иначе 
		УчитыватьВсеДоходы = Ложь;
	КонецЕсли;
	
	Если НачДата = Неопределено Тогда
		НачДата = НачалоМесяца(Дата);
	КонецЕсли;
	КонДата = КонецМесяца(Дата);      // Доходы от реализации
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	СУММА(ВЫБОР
	               |			КОГДА ХозрасчетныйОбороты.Счет В (&СчетВыручкаЕНВД)
	               |				ТОГДА -ХозрасчетныйОбороты.СуммаОборот
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК ВыручкаЕНВД,
	               |	СУММА(ВЫБОР
	               |			КОГДА (НЕ ХозрасчетныйОбороты.Счет В (&СчетВыручкаЕНВД))
	               |				ТОГДА -ХозрасчетныйОбороты.СуммаОборот
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК ВыручкаНеЕНВД,
	               |	СУММА(0) КАК РасходыЕНВДПрошлыхПериодов,
	               |	СУММА(0) КАК РасходыСНачалаГода,
	               |	СУММА(0) КАК РасходыМесяца,
	               |	ХозрасчетныйОбороты.Счет КАК Счет
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачДата,
	               |			&КонДата,
	               |			Период,
	               |			Счет В ИЕРАРХИИ (&МассивСчетов),
	               |			,
	               |			Организация В ИЕРАРХИИ (&Организация),
	               |			(НЕ КорСчет В ИЕРАРХИИ (&МассивСчетов))
	               |				И (НЕ КорСчет В ИЕРАРХИИ (&Счет9009)),
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Счет";
	
	Если УчитыватьВсеДоходы Тогда   // Внереализационные доходы
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА ХозрасчетныйОбороты.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат = &ПрочиеДоходыЕНВД
		|				ТОГДА -ХозрасчетныйОбороты.СуммаОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ),
		|	СУММА(ВЫБОР
		|			КОГДА НЕ ХозрасчетныйОбороты.Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат = &ПрочиеДоходыЕНВД
		|				ТОГДА -ХозрасчетныйОбороты.СуммаОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ),
		|	СУММА(0),
		|	СУММА(0),
		|	СУММА(0),
		|	ХозрасчетныйОбороты.Счет
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачДата, &КонДата, Период, Счет В ИЕРАРХИИ (&ПрочиеДоходы), , Организация В ИЕРАРХИИ (&Организация), Не КорСчет В ИЕРАРХИИ (&ПрочиеДоходы), ) КАК ХозрасчетныйОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОбороты.Счет";
	КонецЕсли;
	
	//Массив счетов, по которым рассчитывается доход организации
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.Выручка);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.Продажи_НДС);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.Продажи_Акцизы);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.Продажи_ЭкспортныеПошлины);
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НачДата",              НачДата);
	Запрос.УстановитьПараметр("КонДата",              КонДата);
	Запрос.УстановитьПараметр("Организация",          СписокОрганизаций);
	Запрос.УстановитьПараметр("СчетВыручкаЕНВД",      НалоговыйУчетУСН.МассивСчетовВыручкиЕНВД());
	Запрос.УстановитьПараметр("Счет9009",             ПланыСчетов.Хозрасчетный.ПрибыльУбытокОтПродаж);
	Запрос.УстановитьПараметр("МассивСчетов",         МассивСчетов);
	Запрос.УстановитьПараметр("ПрочиеДоходы",         ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ПрочиеДоходыЕНВД",     Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Если результат запроса пустой, то считаем, что все
	// расходы относятся к деятельности не облагаемой ЕНВД.
	Если РезультатЗапроса.Пустой() Тогда
		Коэффициент = 0;
		
	Иначе
		
		ТаблицаРезультат = РезультатЗапроса.Выгрузить();
		ВыручкаЕНВД      = Макс(ТаблицаРезультат.Итог("ВыручкаЕНВД"),   0);
		ВыручкаНеЕНВД    = Макс(ТаблицаРезультат.Итог("ВыручкаНеЕНВД"), 0);
		
		Если ВыручкаНеЕНВД + ВыручкаЕНВД = 0 Тогда // нет дохода ни по одному из видов деятельности
			Коэффициент = 0;
			Возврат Коэффициент;
		КонецЕсли;
		
		

			
			Если ВыручкаНеЕНВД = 0 Тогда  // нет дохода виду деятельности не подпадающему под обложение ЕНВД
				Коэффициент = 1;
				
			Иначе
				Коэффициент = ВыручкаЕНВД / (ВыручкаНеЕНВД + ВыручкаЕНВД);
			КонецЕсли;
			
	КонецЕсли;
	
	
	Возврат Коэффициент;
	
КонецФункции // КоэффициентРаспределенияРасходовПоВидамДеятельности()

// Определяет наличие видов деятельности, облагаемых ЕНВД.
//
// Параметры:
//  Организация  - Справочник.Ссылка - Организация, по которой
//                 определяется наличие видов деятельности, облагаемых ЕНВД
//  Дата         - Дата - Дата, на которую получаются сведения
//
// Возвращаемое значение:
//  Булево.
//
Функция ПрименениеЕНВД(Организация, Знач Дата) Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;

	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, Ложь, Организация,,Ложь);
	Если УчетнаяПолитика.Количество() = 0 Тогда
		Возврат Истина;
	Иначе
		Возврат УчетнаяПолитика.ОрганизацияЯвляетсяПлательщикомЕНВД;
	КонецЕсли;

КонецФункции // ПоддержкаПБУ18()

// Определяет, установлена ли поддержка ПБУ 18/02.
//
// Параметры:
//  Организация  - Справочник.Ссылка - Организация, по которой
//                 определяется поддержка ПБУ 18/02
//  Дата         - Дата - Дата, на которую получаются сведения
//
// Возвращаемое значение:
//  Булево.
//
Функция ПрименениеПБУ18(Организация, Знач Дата) Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;

	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, Ложь, Организация,,Ложь);
	Если УчетнаяПолитика.Количество() = 0 Тогда
		Возврат Ложь;
	Иначе
		Возврат УчетнаяПолитика.ПоддержкаПБУ18;
	КонецЕсли;

КонецФункции

// Определяет доли списания нормируемых расходов, 
// транспортных расходов, расходов, распределяемых по видам деятельности
Функция ПолучитьДолюКосвенногоРасхода(ТаблицаДолейКосвенныхРасходов, ВидРасхода) Экспорт
		
	Если ТаблицаДолейКосвенныхРасходов = Неопределено Или ТаблицаДолейКосвенныхРасходов.Количество() = 0 Тогда
		Возврат 1;
	КонецЕсли;
	
	Строка = ТаблицаДолейКосвенныхРасходов[0];
	Если ВидРасхода = Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахование Тогда
		Возврат Строка.ДоляРасходовНаДобровольноеМедицинскоеСтрахование;
		               
	ИначеЕсли ВидРасхода = Перечисления.ВидыРасходовНУ.ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников Тогда
		Возврат Строка.ДоляРасходовНаДобровольноеСтрахованиеЖизни;
		
	ИначеЕсли ВидРасхода = Перечисления.ВидыРасходовНУ.РасходыНаВозмещениеЗатратРаботниковПоУплатеПроцентов Тогда
		Возврат Строка.ДоляРасходовНаВозмещениеПроцентовРаботникам;
		
	ИначеЕсли ВидРасхода = Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности Тогда
		Возврат Строка.ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев;
		
	ИначеЕсли ВидРасхода = Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы Тогда
		Возврат Строка.ДоляПредставительскихРасходов;
		
	ИначеЕсли ВидРасхода = Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые Тогда
		Возврат Строка.ДоляРасходовНаРекламу;
		
	ИначеЕсли ВидРасхода = Перечисления.ВидыРасходовНУ.ТранспортныеРасходы Тогда
		Возврат Строка.ДоляТранспортныхРасходов;
		
	ИначеЕсли ВидРасхода = Неопределено Тогда
		
		Возврат Строка.ДоляЕНВД;
	Иначе	
		
		Возврат 1;
	КонецЕсли;
	
	
КонецФункции

// Формирует проводки, отражающие списание косвенных расходов
// (включение косвенных расходов в уменьшение налогооблагаемой прибыли)
Процедура ПроводкиПоКосвеннымРасходам(мПроводки, Выборка, Сумма,СчетДт, СтруктураШапкиДокумента, СтруктураДопПараметров, АналитикаПоНомГруппам = Ложь) Экспорт
	
	Если СтруктураШапкиДокумента.ОрганизацияПрименяетУСН Или СтруктураШапкиДокумента.Предприниматель Тогда
		Сумма.СуммаНУ = 0; Сумма.СуммаПР = 0; Сумма.СуммаВР = 0;
	КонецЕсли;
	
	Если Не СтруктураШапкиДокумента.ПрименениеПБУ18 Тогда
		Сумма.СуммаПР = 0; Сумма.СуммаВР = 0;
	КонецЕсли;
	
	Если Сумма.СуммаБУ = 0 И Сумма.СуммаНУ = 0 И Сумма.СуммаПР = 0 И Сумма.СуммаВР = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Проводка = мПроводки.Добавить();
	
	Проводка.Период       = СтруктураШапкиДокумента.Дата;
	Проводка.Организация  = СтруктураШапкиДокумента.Организация;
	
	Проводка.СчетДт       = СчетДт; 
	Если АналитикаПоНомГруппам Тогда
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, "НоменклатурныеГруппы", Выборка.НоменклатурнаяГруппа);
	КонецЕсли;
	
	Проводка.СчетКт       = Выборка.Счет;
	БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "СтатьиЗатрат", Выборка.СтатьяЗатрат);
	Если АналитикаПоНомГруппам Тогда
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "НоменклатурныеГруппы", Выборка.НоменклатурнаяГруппа);
	КонецЕсли;
	
	БухгалтерскийУчет.УстановитьПодразделенияПроводки(
		Проводка, Выборка.Подразделение, Выборка.Подразделение);
	
	Проводка.Содержание   = "Закрытие счетов косвенных расходов";
	
	Проводка.Сумма = Сумма.СуммаБУ;
	СуммаНУ        = Сумма.СуммаНУ;
	СуммаПР        = Сумма.СуммаПР;
	СуммаВР        = Сумма.СуммаВР;
	
	ЗаполнитьНалоговыеСуммыПроводки(СуммаНУ,СуммаНУ,СуммаПР,СуммаПр,СуммаВР,СуммаВР,Проводка, СтруктураШапкиДокумента.ПрименениеПБУ18); 
	
КонецПроцедуры

// Создает таблицу долей списания косвенных расходов по видам расходов
// на основе записей регистра сведений "Доли списания косвенных расходов"
Функция ПолучитьТаблицуДолейСписанияКосвенныхРасходов(СтруктураШапкиДокумента) Экспорт
	
Запрос = Новый Запрос;
Запрос.Текст = 
"ВЫБРАТЬ
|	РасчетДолейСписанияКосвенныхРасходов.ДоляРасходовНаРекламу КАК ДоляРасходовНаРекламу,
|	РасчетДолейСписанияКосвенныхРасходов.ДоляПредставительскихРасходов КАК ДоляПредставительскихРасходов,
|	РасчетДолейСписанияКосвенныхРасходов.ДоляРасходовНаДобровольноеМедицинскоеСтрахование КАК ДоляРасходовНаДобровольноеМедицинскоеСтрахование,
|	РасчетДолейСписанияКосвенныхРасходов.ДоляРасходовНаДобровольноеСтрахованиеЖизни КАК ДоляРасходовНаДобровольноеСтрахованиеЖизни,
|	РасчетДолейСписанияКосвенныхРасходов.ДоляРасходовНаВозмещениеПроцентовРаботникам КАК ДоляРасходовНаВозмещениеПроцентовРаботникам,
|	РасчетДолейСписанияКосвенныхРасходов.ДоляЕНВД КАК ДоляЕНВД,
|	РасчетДолейСписанияКосвенныхРасходов.ДоляТранспортныхРасходов КАК ДоляТранспортныхРасходов,
|	РасчетДолейСписанияКосвенныхРасходов.ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев КАК ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев
|ИЗ
|	РегистрСведений.ДолиСписанияКосвенныхРасходов КАК РасчетДолейСписанияКосвенныхРасходов
|ГДЕ
|	РасчетДолейСписанияКосвенныхРасходов.Организация = &Организация
|	И РасчетДолейСписанияКосвенныхРасходов.ПериодРасчета МЕЖДУ &ДатаНач И &ДатаКон
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	0,
|	0,
|	0,
|	0,
|	0,
|	0,
|	0,
|	0
|ИЗ
|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
|ГДЕ
|	РегламентнаяОперация.Организация = &Организация
|	И РегламентнаяОперация.Дата МЕЖДУ &ДатаНач И &ДатаКон
|	И РегламентнаяОперация.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыРегламентныхОпераций.РасчетДолейСписанияКосвенныхРасходов)
|	И РегламентнаяОперация.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийРегламентныхОпераций.Пропущено)";

Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.ГоловноеПодразделение);
Запрос.УстановитьПараметр("ДатаНач",     НачалоМесяца(СтруктураШапкиДокумента.Дата));
Запрос.УстановитьПараметр("ДатаКон",     КонецМесяца(СтруктураШапкиДокумента.Дата));

Результат = Запрос.Выполнить();                      

Если Результат.Пустой() Тогда
	Возврат Неопределено;
Иначе
	Возврат Результат.Выгрузить();
КонецЕсли;


КонецФункции

Функция ПолучитьДолиЕНВД(СтруктураПараметров) Экспорт
	
	ЗапросВр = Новый Запрос;
	ЗапросВр.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗапросВр.УстановитьПараметр("ДатаНач",           СтруктураПараметров.ДатаНачалаПериода);
	ЗапросВр.УстановитьПараметр("ДатаКон",           СтруктураПараметров.ДатаКонцаПериода);
	ЗапросВр.УстановитьПараметр("СписокОрганизаций", СтруктураПараметров.СписокОрганизаций);
	ЗапросВр.Текст = 
	"ВЫБРАТЬ
	|	ДолиЕНВД.ДоляЕНВД КАК ДоляЕНВД,
	|	МЕСЯЦ(ДолиЕНВД.ПериодРасчета) КАК МесяцЕНВД
	|ПОМЕСТИТЬ ДолиЕНВД
	|ИЗ
	|	РегистрСведений.ДолиСписанияКосвенныхРасходов КАК ДолиЕНВД
	|ГДЕ
	|	ДолиЕНВД.Организация В(&СписокОрганизаций)
	|	И ДолиЕНВД.Активность
	|	И ДолиЕНВД.ПериодРасчета МЕЖДУ &ДатаНач И &ДатаКон";
	
	ЗапросВр.Выполнить();
	
	Возврат ЗапросВр.МенеджерВременныхТаблиц;

КонецФункции


// Процедура вызывается при проведении документов, содержащих отдельные колонки
// для отражения расходов БУ и НУ (Поступление товаров и услуг, Авансовый отчет, ГТД импорт, Поступление доп.расходов
// Процедура анализирует наличие разниц, определяет постоянные и временные разницы,
// создает проводки по счету из колонки "СчетУчетаБУ"
Процедура СоздатьПроводкиПоРазнымСчетамБУиНУ(СтруктураШапкиДокумента, Проводки, ПроводкаБУ, Заголовок, СуммаНУ, СчетДтНУ, СубконтоНУ1, СубконтоНУ2, СубконтоНУ3) Экспорт
	
	Если НЕ СтруктураШапкиДокумента.ОтражатьВНалоговомУчете Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСубконто1 = 1;
	ЗначениеСубконто1 = СубконтоНУ1;
	
	Если ТипЗнч(СубконтоНУ2) = Тип("Структура") Тогда
		ИмяСубконто2      = СубконтоНУ2.Тип;
		ЗначениеСубконто2 = СубконтоНУ2.Значение;
	Иначе
		ИмяСубконто2 = 2;
		ЗначениеСубконто2 = СубконтоНУ2;
	КонецЕсли;
	
	Если ТипЗнч(СубконтоНУ3) = Тип("Структура") Тогда
		ИмяСубконто3      = СубконтоНУ3.Тип;
		ЗначениеСубконто3 = СубконтоНУ3.Значение;
	Иначе
		ИмяСубконто3 = 3;
		ЗначениеСубконто3 = СубконтоНУ3;
	КонецЕсли;
	
	СуммаПР = ПроводкаБУ.Сумма - СуммаНУ;
	
	НеПринимаемыйРасход = ОпределитьНепринимаемыйРасход(
		ПроводкаБУ.СчетДт, ПроводкаБУ.СубконтоДт.РасходыБудущихПериодов, ЗначениеСубконто1, ЗначениеСубконто2, ЗначениеСубконто3);
	
	Если НеПринимаемыйРасход Тогда
			
		ЗаполнитьНалоговыеСуммыПроводки(0, СуммаНУ, ПроводкаБУ.Сумма, СуммаПР, 0, 0, ПроводкаБУ, СтруктураШапкиДокумента.ПрименениеПБУ18); 
		
	ИначеЕсли СчетДтНУ = ПроводкаБУ.СчетДт Тогда
		
		ЗаполнитьНалоговыеСуммыПроводки(СуммаНУ, СуммаНУ, СуммаПР, СуммаПР, 0, 0, ПроводкаБУ, СтруктураШапкиДокумента.ПрименениеПБУ18); 
		
	Иначе
		
		ЗаполнитьНалоговыеСуммыПроводки(0, СуммаНУ, СуммаПР, СуммаПР, СуммаНУ, 0, ПроводкаБУ, СтруктураШапкиДокумента.ПрименениеПБУ18); 
		
		// Если отличаются счета БУ и НУ и расход принимаемый - создаем вторую проводку со счетом НУ по дебету
		
		ПроводкаНУ = Проводки.Добавить();
		
		ПроводкаНУ.Период      = ПроводкаБУ.Период;
		ПроводкаНУ.Организация = ПроводкаБУ.Организация;
		ПроводкаНУ.Содержание  = ПроводкаБУ.Содержание;
		ПроводкаНУ.Сумма       = 0;
		
		ПроводкаНУ.СчетДт          = СчетДтНУ;
		
		БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт, ПроводкаНУ.СубконтоДт, ИмяСубконто1, ЗначениеСубконто1);
		БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт, ПроводкаНУ.СубконтоДт, ИмяСубконто2, ЗначениеСубконто2);
		БухгалтерскийУчет.УстановитьСубконто(ПроводкаНУ.СчетДт, ПроводкаНУ.СубконтоДт, ИмяСубконто3, ЗначениеСубконто3);
		
		Если ЗначениеЗаполнено(ПроводкаБУ.СчетКт) Тогда
			
			ПроводкаНУ.СчетКт          = ПроводкаБУ.СчетКт;
			
			Для каждого Элемент Из ПроводкаБУ.СубконтоКт Цикл
				ПроводкаНУ.СубконтоКт.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЦикла;
						
		КонецЕсли;
		
		БухгалтерскийУчет.УстановитьПодразделенияПроводки(
			ПроводкаНУ, ПроводкаБУ.ПодразделениеДт, ПроводкаБУ.ПодразделениеКт);
		
		ЗаполнитьНалоговыеСуммыПроводки(СуммаНУ, 0, 0, 0, -СуммаНУ, 0, ПроводкаНУ, СтруктураШапкиДокумента.ПрименениеПБУ18); 
		
	КонецЕсли;
	
КонецПроцедуры

// Функция анализирует данные проводки и выдает информацию о присутствии или отсутствии
// расходов не принимаемых для целей налогообложения прибыли
Функция ОпределитьНепринимаемыйРасход(Счет, Субконто1, СубконтоНУ1, СубконтоНУ2, СубконтоНУ3) Экспорт
	
	Если Лев(Счет.Код, 2) = "97" Тогда
		СтатьяЗатрат1 = Субконто1.СубконтоЗатрат1;
		СтатьяЗатрат2 = Субконто1.СубконтоЗатрат2;
		СтатьяЗатрат3 = Субконто1.СубконтоЗатрат3;
	Иначе
		СтатьяЗатрат1 = СубконтоНУ1;
		СтатьяЗатрат2 = СубконтоНУ2;
		СтатьяЗатрат3 = СубконтоНУ3;
	КонецЕсли;
	
	НеПринимаемыйРасход = Ложь;
	
	Если ТипЗнч(СтатьяЗатрат1) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
		НеПринимаемыйРасход = (СтатьяЗатрат1.ВидРасходовНУ = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения);
	ИначеЕсли ТипЗнч(СтатьяЗатрат2) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
		НеПринимаемыйРасход = (СтатьяЗатрат2.ВидРасходовНУ = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения);
	ИначеЕсли ТипЗнч(СтатьяЗатрат3) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
		НеПринимаемыйРасход = (СтатьяЗатрат3.ВидРасходовНУ = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения);
	ИначеЕсли ТипЗнч(СтатьяЗатрат1) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда
		НеПринимаемыйРасход = НЕ СтатьяЗатрат1.ПринятиеКналоговомуУчету;
	ИначеЕсли ТипЗнч(СтатьяЗатрат2) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда
		НеПринимаемыйРасход = НЕ СтатьяЗатрат2.ПринятиеКналоговомуУчету;
	ИначеЕсли ТипЗнч(СтатьяЗатрат3) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда
		НеПринимаемыйРасход = НЕ СтатьяЗатрат3.ПринятиеКналоговомуУчету;
	КонецЕсли;
	
	Возврат НеПринимаемыйРасход;
	
КонецФункции

/// Процедура расчета сумм списания расходов, по которым предусмотрены ограничения
// Нормируемые, транспортные, распределяемые
Процедура ПровестиРасчетСуммРасходовПоКоторымПредусмотреныОграничения(СтруктураШапкиДокумента) Экспорт
	
	ЗаписьДолей = СтруктураШапкиДокумента.ЗаписиДолей.Добавить();
	
	ЗаписьДолей.ДоляРасходовНаРекламу = НормированиеРасходовПоПроценту(СтруктураШапкиДокумента, Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые, ОпределитьНормуРасходовПоВыручке(СтруктураШапкиДокумента, 1),1);
	
	ЗаписьДолей.ДоляРасходовНаДобровольноеМедицинскоеСтрахование = НормированиеРасходовПоПроценту(СтруктураШапкиДокумента, Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахование, ОпределитьНормуПоРасходамНаОплатуТруда(СтруктураШапкиДокумента, 6, Ложь),6);
	
	ЗаписьДолей.ДоляРасходовНаДобровольноеСтрахованиеЖизни = НормированиеРасходовПоПроценту(СтруктураШапкиДокумента, Перечисления.ВидыРасходовНУ.ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников, ОпределитьНормуПоРасходамНаОплатуТруда(СтруктураШапкиДокумента, 12, Ложь),12);
	
	ЗаписьДолей.ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев = НормированиеРасходовПоРаботникуОрганизации(СтруктураШапкиДокумента, Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности);
    	
	ЗаписьДолей.ДоляПредставительскихРасходов = НормированиеРасходовПоПроценту(СтруктураШапкиДокумента, Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы, ОпределитьНормуПоРасходамНаОплатуТруда(СтруктураШапкиДокумента, 4, Истина),4);
	
	Если СтруктураШапкиДокумента.Дата < Дата("20120101") Тогда
		ЗаписьДолей.ДоляРасходовНаВозмещениеПроцентовРаботникам = НормированиеРасходовПоПроценту(СтруктураШапкиДокумента, Перечисления.ВидыРасходовНУ.РасходыНаВозмещениеЗатратРаботниковПоУплатеПроцентов, ОпределитьНормуПоРасходамНаОплатуТруда(СтруктураШапкиДокумента, 3, Ложь),3);
	КонецЕсли;
	
	ЗаписьДолей.ДоляЕНВД = РаспределениеРасходовПоВидамДеятельности(СтруктураШапкиДокумента);
	
	ЗаписьДолей.ДоляТранспортныхРасходов = СписаниеТранспортныхРасходов(СтруктураШапкиДокумента);
	
	
КонецПроцедуры

// Производит нормирование расходов, для которых норма задается процентом от суммового показателя
//
Функция НормированиеРасходовПоПроценту(СтруктураШапкиДокумента, ВидЗатрат, Норма, Процент)
	
	мКонДата    = КонецМесяца (СтруктураШапкиДокумента.Дата);
	мНачГода    = НачалоГода(СтруктураШапкиДокумента.Дата);
	мНачДата    = НачалоМесяца (СтруктураШапкиДокумента.Дата);
	
	мКонГраница = Новый Граница(мКонДата, ВидГраницы.Включая);
	
	ТекущаяНорма = Норма;
    СуммаБазы = 100 * Норма / Процент;
	// Формируем запрос по виду затрат
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоМесяца", мНачГода);
	Запрос.УстановитьПараметр("КонецМесяца",  мКонГраница);
	Запрос.УстановитьПараметр("НачалоТекМесяца",мНачДата);
	Запрос.УстановитьПараметр("ВидСубконто",  ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВидЗатрат",    ВидЗатрат);
	Запрос.УстановитьПараметр("Организация",  СтруктураШапкиДокумента.СписокОрганизаций);
	Запрос.УстановитьПараметр("СчетаЗатрат",  НалоговыйУчет.ПолучитьМассивСчетовУчетаРасходов());
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВложенныйЗапрос.Счет КАК Счет,
	               |	ВложенныйЗапрос.СтатьяЗатрат КАК СтатьяЗатрат,
	               |	СУММА(ВложенныйЗапрос.ОборотДт) КАК ОборотДт,
	               |	СУММА(ВложенныйЗапрос.ОборотКт) КАК ОборотКт,
	               |	СУММА(ВложенныйЗапрос.ОборотПР) КАК ОборотПР
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		ХозрасчетныйОбороты.Счет КАК Счет,
	               |		ХозрасчетныйОбороты.Субконто1 КАК СтатьяЗатрат,
	               |		ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотДт, 0) КАК ОборотДт,
	               |		ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотКт, 0) КАК ОборотКт,
	               |		ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборот, 0) КАК ОборотПР
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |				&НачалоМесяца,
	               |				&КонецМесяца,
	               |				Период,
	               |				Счет В ИЕРАРХИИ (&СчетаЗатрат),
	               |				&ВидСубконто,
	               |				Организация В ИЕРАРХИИ (&Организация)
	               |					И ВЫРАЗИТЬ(Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ = &ВидЗатрат,
	               |				(НЕ КорСчет В ИЕРАРХИИ (&СчетаЗатрат)),
	               |				) КАК ХозрасчетныйОбороты
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		ХозрасчетныйОбороты.Счет,
	               |		ХозрасчетныйОбороты.Субконто1,
	               |		0,
	               |		-ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотКт, 0),
	               |		-ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборот, 0)
	               |	ИЗ
	               |		РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |				&НачалоТекМесяца,
	               |				&КонецМесяца,
	               |				Период,
	               |				Счет В ИЕРАРХИИ (&СчетаЗатрат),
	               |				&ВидСубконто,
	               |				Организация В ИЕРАРХИИ (&Организация)
	               |					И ВЫРАЗИТЬ(Субконто1 КАК Справочник.СтатьиЗатрат).ВидРасходовНУ = &ВидЗатрат,
	               |				(НЕ КорСчет В ИЕРАРХИИ (&СчетаЗатрат)),
	               |				) КАК ХозрасчетныйОбороты) КАК ВложенныйЗапрос
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВложенныйЗапрос.СтатьяЗатрат,
	               |	ВложенныйЗапрос.Счет";
	Результат = Запрос.Выполнить();
	
	ТаблицаРасходов = Новый ТаблицаЗначений;
	ТаблицаРасходов = Результат.Выгрузить();
	ТаблицаРасходов.Колонки.Добавить("НеСписано");
	ТаблицаРасходов.Колонки.Добавить("МожноСписать");
	ТаблицаРасходов.Колонки.Добавить("СписатьПостоянныеРазницы");
	
	// Определяем не принятые расходы и учитываем ранее принятые
	Для каждого Расход из ТаблицаРасходов Цикл
		Расход.НеСписано = Расход.ОборотДт - Расход.ОборотКт;
		Норма            = Норма - Расход.ОборотКт;
	КонецЦикла;
	
	Норма = Макс(Норма, 0);
	
	// Определяем сумму расхода по нормам
	ТаблицаРасходов.Сортировать("НеСписано Возр");
	
	
	Для каждого Расход из ТаблицаРасходов Цикл
		
		Если Норма > Расход.НеСписано Тогда
			Расход.МожноСписать = Расход.НеСписано;
			Норма = Норма - Расход.МожноСписать;
			
		Иначе
			Расход.МожноСписать = Норма;
			Норма = 0;
			
		КонецЕсли;
		
		Расход.СписатьПостоянныеРазницы = Расход.ОборотДт - Расход.МожноСписать - Расход.ОборотКт - Расход.ОборотПР;
		
		СтрокаЗаписьНормированияРасходов              = СтруктураШапкиДокумента.ЗаписьНормированияРасходов.Добавить();
		СтрокаЗаписьНормированияРасходов.ВидОперации  = ВидЗатрат;
		СтрокаЗаписьНормированияРасходов.ВидРегОперации= "Закрытие " + Расход.Счет.Код;
		СтрокаЗаписьНормированияРасходов.ЗатратыГод   = Расход.ОборотДт;
		СтрокаЗаписьНормированияРасходов.РасходыГод   = Расход.ОборотКт;
		СтрокаЗаписьНормированияРасходов.СуммаБазы    = СуммаБазы;
		СтрокаЗаписьНормированияРасходов.РасходыМесяц = Расход.МожноСписать;
		СтрокаЗаписьНормированияРасходов.РасходыГод = СтрокаЗаписьНормированияРасходов.РасходыГод + Расход.МожноСписать;
		СтрокаЗаписьНормированияРасходов.РазницыМесяц = Расход.СписатьПостоянныеРазницы;
		СтрокаЗаписьНормированияРасходов.РазницыГод   = Расход.ОборотПР + Расход.СписатьПостоянныеРазницы;
			
	КонецЦикла;
	
	Остаток = ТаблицаРасходов.Итог("ОборотДт") - ТаблицаРасходов.Итог("ОборотКт");
	
	Возврат ?( Остаток = 0, 0, ТаблицаРасходов.Итог("МожноСписать") /  Остаток);
	
Конецфункции // НормированиеРасходовПоПроценту()

Функция СписаниеТранспортныхРасходов(СтруктураШапкиДокумента)
	
	мНачДата    = НачалоМесяца (СтруктураШапкиДокумента.Дата);
	мКонДата    = КонецМесяца (СтруктураШапкиДокумента.Дата);
	
	СуммаРасходовВсего = РасчетПризнанныхТранспортныхРасходов(СтруктураШапкиДокумента);
	
	ПараметрыТР = Новый Структура("СуммаРасходовКсписанию, НачОстатокТоваров, ПриходТовара, СуммаРеализованныхТоваров, СуммаРеализованныхТоваровЕНДВ, Списано, КонОстатокТоваров, СреднийПроцент, СуммаРасходов,ДоляРасхода",
		0, 0, 0, 0, 0, 0, 0, 0, СуммаРасходовВсего,0);
		
	НалоговыйУчет.РасчетТранспортныхРасходов(мНачДата, мКонДата, СтруктураШапкиДокумента.СписокОрганизаций, ПараметрыТР);
	
	Возврат ПараметрыТР.ДоляРасхода;
	
КонецФункции
	
Функция  РасчетПризнанныхТранспортныхРасходов(СтруктураШапкиДокумента)
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ХозрасчетныйОстатки.СуммаОстаток КАК Сумма
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки(
	               |			&КонГраница,
	               |			Счет В ИЕРАРХИИ (&СчетЗатрат),
	               |			&ВидыСубконто,
	               |			Организация В ИЕРАРХИИ (&Организация)
	               |				И Субконто1.ВидРасходовНУ = &ТранспортныеРасходы) КАК ХозрасчетныйОстатки
	               |
	               |ДЛЯ ИЗМЕНЕНИЯ
	               |	РегистрБухгалтерии.Хозрасчетный.Остатки";
	
				   
				   мКонДата    = КонецМесяца (СтруктураШапкиДокумента.Дата);
				   мКонГраница = Новый Граница(мКонДата, ВидГраницы.Включая);
				   
				   Запрос.УстановитьПараметр("КонГраница",  мКонГраница);
				   Запрос.УстановитьПараметр("Организация", СтруктураШапкиДокумента.СписокОрганизаций);
				   Запрос.УстановитьПараметр("СчетЗатрат",  ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
	Запрос.УстановитьПараметр("ВидыСубконто",ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ТранспортныеРасходы",  Перечисления.ВидыРасходовНУ.ТранспортныеРасходы);
    Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат.Итог("Сумма");
		
КонецФункции
	

Функция РаспределениеРасходовПоВидамДеятельности(СтруктураШапкиДокумента)
	
	Возврат НалоговыйУчет.КоэффициентРаспределенияРасходовПоВидамДеятельности(СтруктураШапкиДокумента.Организация, СтруктураШапкиДокумента.Дата,,,,, СтруктураШапкиДокумента.СписокОрганизаций)
	
КонецФункции

// Возвращает значение нормы, соответствующее проценту от выручки
//
Функция ОпределитьНормуРасходовПоВыручке(СтруктураШапкиДокумента, Процент)
	
	// Формируем массив счетов учета выручки
	СписокВидовДоходов = Новый Массив;
	СписокВидовДоходов.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСРеализациейОсновныхСредств);
	СписокВидовДоходов.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСРеализациейНематериальныхАктивов);
	СписокВидовДоходов.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСРеализациейОбъектовСтроительства);
	СписокВидовДоходов.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСРеализациейПрочегоИмущества);
	СписокВидовДоходов.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСРеализациейИмущественныхПравКромеПраваТребования);
	СписокВидовДоходов.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСРеализациейИмущественныхПравПоОбъектамОбслуживающихПроизводств);
	СписокВидовДоходов.Добавить(Перечисления.ВидыПрочихДоходовИРасходов.ДоходыРасходыСвязанныеСРеализациейЦенныхБумаг);
	
	мКонДата    = КонецМесяца (СтруктураШапкиДокумента.Дата);
	мНачГода    = НачалоГода(СтруктураШапкиДокумента.Дата);
	мКонГраница = Новый Граница(мКонДата, ВидГраницы.Включая);
	
	// Формируем запрос по счетам учета выручки
	ЗапросПоВыручке = Новый Запрос;
	ЗапросПоВыручке.УстановитьПараметр("НачалоМесяца", мНачГода);
	ЗапросПоВыручке.УстановитьПараметр("КонецМесяца",  мКонГраница);
	ЗапросПоВыручке.УстановитьПараметр("Организация",  СтруктураШапкиДокумента.СписокОрганизаций);
	ЗапросПоВыручке.УстановитьПараметр("Выручка90", ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД);
	ЗапросПоВыручке.УстановитьПараметр("Выручка91", ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	ЗапросПоВыручке.УстановитьПараметр("ПрочиеДоходы", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы);
	ЗапросПоВыручке.УстановитьПараметр("СписокВидовДоходов", СписокВидовДоходов);
	ЗапросПоВыручке.УстановитьПараметр("НеЕНВД" , Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьНеОблагаемаяЕНВД);
	
	ЗапросПоВыручке.Текст = "ВЫБРАТЬ
	                        |	ХозрасчетныйОбороты.СуммаНУОборотКт КАК ОборотКт
	                        |ИЗ
	                        |	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоМесяца, &КонецМесяца, , Счет В (&Выручка90), , Организация В ИЕРАРХИИ (&Организация), , ) КАК ХозрасчетныйОбороты
	                        |
	                        |ОБЪЕДИНИТЬ ВСЕ
	                        |
	                        |ВЫБРАТЬ
	                        |	ХозрасчетныйОбороты.СуммаНУОборотКт
	                        |ИЗ
	                        |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	                        |			&НачалоМесяца,
	                        |			&КонецМесяца,
	                        |			,
	                        |			Счет В (&Выручка91),
	                        |			&ПрочиеДоходы,
	                        |			Организация В ИЕРАРХИИ (&Организация)
	                        |				И Субконто1.ВидПрочихДоходовИРасходов В (&СписокВидовДоходов)
	                        |				И Субконто1.ВидДеятельностиДляНалоговогоУчетаЗатрат = &НеЕНВД,
	                        |			,
	                        |			) КАК ХозрасчетныйОбороты";
	ВыборкаВыручки = ЗапросПоВыручке.Выполнить().Выбрать();
	
	// Собираем выручку
	ВыручкаНУ = 0;
	Пока ВыборкаВыручки.Следующий() Цикл
		ВыручкаНУ = ВыручкаНУ + ?(ВыборкаВыручки.ОборотКт = Null, 0, ВыборкаВыручки.ОборотКт);
	КонецЦикла;
	
	// Определяем норму
	Возврат Окр((ВыручкаНУ * Процент / 100), 2, 1);
	
КонецФункции // ОпределитьНормуРасходовПоВыручке()

// Возвращает значение нормы, соответствующее проценту от расходов на оплату труда
// с учетом или без учета расходов на добровольное страхование работников
Функция ОпределитьНормуПоРасходамНаОплатуТруда(СтруктураШапкиДокумента, Процент, ВключатьДобровольноеСтрахование = Ложь)
	
	мКонДата    = КонецМесяца (СтруктураШапкиДокумента.Дата);
	мНачГода    = НачалоГода(СтруктураШапкиДокумента.Дата);
	
	мСчетаЕНВД = Новый Массив;
	мСчетаЕНВД.Добавить(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажуЕНВД);
	мСчетаЕНВД.Добавить(ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходыЕНВД);
	
	// Определяем сумму расходов по виду затрат "Оплата труда"
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоМесяца", мНачГода);
	Запрос.УстановитьПараметр("КонецМесяца",  мКонДата);
	Запрос.УстановитьПараметр("ВидСубконто",  ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВидЗатрат",    Перечисления.ВидыРасходовНУ.ОплатаТруда);
	Запрос.УстановитьПараметр("Организация",  СтруктураШапкиДокумента.СписокОрганизаций);
	Запрос.УстановитьПараметр("СчетЕНВД",     мСчетаЕНВД);
	Запрос.УстановитьПараметр("Счет70",       ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);
	Запрос.УстановитьПараметр("Счет97",        ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов);
	Запрос.УстановитьПараметр("ОплатаТрудаРБП",Справочники.РасходыБудущихПериодов.РБПНаОплатуТруда);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотДт, 0)) КАК СуммаРасходовНаОплатуТруда,
	               |	СУММА(ЕСТЬNULL(СписаноНаЕНВД.СуммаНУОборотКт, 0)) КАК СписаноНаЕНВД,
	               |	ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоМесяца, &КонецМесяца, , , &ВидСубконто, Организация В ИЕРАРХИИ (&Организация), КорСчет В ИЕРАРХИИ (&Счет70), ) КАК ХозрасчетныйОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоМесяца, &КонецМесяца, , , &ВидСубконто, Организация В ИЕРАРХИИ (&Организация), КорСчет В ИЕРАРХИИ (&СчетЕНВД), ) КАК СписаноНаЕНВД
	               |		ПО ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ = СписаноНаЕНВД.Субконто1.ВидРасходовНУ
	               |ГДЕ
	               |	ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ = &ВидЗатрат
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СУММА(ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотДт, 0)),
	               |	СУММА(ЕСТЬNULL(СписаноНаЕНВД.СуммаНУОборотКт, 0)),
	               |	ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоМесяца,
	               |			&КонецМесяца,
	               |			,
	               |			,
	               |			&ВидСубконто,
	               |			Организация В ИЕРАРХИИ (&Организация)
	               |				И КорСубконто1 = &ОплатаТрудаРБП,
	               |			КорСчет В ИЕРАРХИИ (&Счет97),
	               |			) КАК ХозрасчетныйОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоМесяца, &КонецМесяца, , , &ВидСубконто, Организация В ИЕРАРХИИ (&Организация), КорСчет В ИЕРАРХИИ (&СчетЕНВД), ) КАК СписаноНаЕНВД
	               |		ПО ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ = СписаноНаЕНВД.Субконто1.ВидРасходовНУ
	               |ГДЕ
	               |	ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ = &ВидЗатрат
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ";
	
	
	ВыборкаНаОплатуТруда = Запрос.Выполнить().Выбрать();
	
	НаОплатуТруда = 0;
	Пока ВыборкаНаОплатуТруда.Следующий() Цикл
		НаОплатуТруда = НаОплатуТруда + ВыборкаНаОплатуТруда.СуммаРасходовНаОплатуТруда - ВыборкаНаОплатуТруда.СписаноНаЕНВД;
	КонецЦикла;
	
	Если ВключатьДобровольноеСтрахование Тогда
		
		// Определяем сумму расходов по виду затрат добровольное страхование работников
		СписокКоррСчетов = Новый Массив;
		СписокКоррСчетов.Добавить(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажу);
		СписокКоррСчетов.Добавить(ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходы);
		
		СписокВидовЗатрат = Новый Массив;
		СписокВидовЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахование);
		СписокВидовЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности);
		СписокВидовЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("НачалоМесяца",     мНачГода);
		Запрос.УстановитьПараметр("КонецМесяца",      мКонДата);
		Запрос.УстановитьПараметр("ВидСубконто",      ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
		Запрос.УстановитьПараметр("ВидЗатрат",        СписокВидовЗатрат);
		Запрос.УстановитьПараметр("СписокКоррСчетов", СписокКоррСчетов);
		Запрос.УстановитьПараметр("Организация",      СтруктураШапкиДокумента.СписокОрганизаций);
		Запрос.Текст = "ВЫБРАТЬ
		               |	ХозрасчетныйОбороты.СуммаНУОборотДт КАК ОборотКт
		               |ИЗ
		               |	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоМесяца, &КонецМесяца, , , &ВидСубконто, Организация В ИЕРАРХИИ (&Организация), КорСчет В (&СписокКоррСчетов), ) КАК ХозрасчетныйОбороты
		               |ГДЕ
		               |	ХозрасчетныйОбороты.Субконто1.ВидРасходовНУ В(&ВидЗатрат)";
		ВыборкаНаОплатуТруда = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаНаОплатуТруда.Следующий() Цикл
			НаОплатуТруда = НаОплатуТруда + ?(ВыборкаНаОплатуТруда.ОборотКт = Null, 0, ВыборкаНаОплатуТруда.ОборотКт);
		КонецЦикла;
		
	КонецЕсли;
	
	// Определяем норму
	Возврат Окр((НаОплатуТруда * Процент / 100), 2, 1);
	
КонецФункции //ОпределитьНормуПоРасходамНаОплатуТруда()

// Производит нормирование расходов, для которых норма задана фиксированной суммой на работника
//
Функция НормированиеРасходовПоРаботникуОрганизации(СтруктураШапкиДокумента, ВидЗатрат)
	
	// найдем период для анализа ранее нормированных расходов
	
	мКонДата    = КонецМесяца (СтруктураШапкиДокумента.Дата);
	мКонГраница = Новый Граница(мКонДата, ВидГраницы.Включая);
	мНачГода    = НачалоГода(СтруктураШапкиДокумента.Дата);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоМесяца",   мНачГода);
	Запрос.УстановитьПараметр("КонецМесяца",    мКонГраница);
	Запрос.УстановитьПараметр("ВидСубконто",    ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов);
	Запрос.УстановитьПараметр("ВидКорСубконто", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВидЗатрат",      ВидЗатрат);
	Запрос.УстановитьПараметр("Организация",    СтруктураШапкиДокумента.СписокОрганизаций);
	
	Массив = Новый Массив;
	Массив.Добавить(ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов);
	Массив.Добавить(ПланыСчетов.Хозрасчетный.ПлатежиПоДобровольномуСтрахованиюРаботников);
	Запрос.УстановитьПараметр("СчетРБП",  Массив);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	МИНИМУМ(ЕСТЬNULL(ХозрасчетныйОбороты.СубконтоКт1.ДатаНачалаСписания, &НачалоМесяца)) КАК Субконто1ДатаНачалаСписания
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|		&НачалоМесяца,
	|		&КонецМесяца,
	|		,
	|		,
	|		&ВидКорСубконто,
	|		СчетКт В ИЕРАРХИИ (&СчетРБП),
	|		&ВидСубконто,
	|		Организация В ИЕРАРХИИ (&Организация)
	|		    И СубконтоДт1.ВидРасходовНУ = &ВидЗатрат) КАК ХозрасчетныйОбороты";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат 0 ;
	КонецЕсли;
	
	НачалоПериодаСтрахования = Результат.Выбрать();
	Пока НачалоПериодаСтрахования.Следующий() Цикл
		ДатаНачалаПериодаСтрахования = НачалоПериодаСтрахования.Субконто1ДатаНачалаСписания;
	КонецЦикла;
	
	мНачДата    = НачалоМесяца (СтруктураШапкиДокумента.Дата);
	мКонДата    = КонецМесяца (СтруктураШапкиДокумента.Дата);
	Запрос.УстановитьПараметр("КонецМесяца",    мКонГраница);
	
	ДатаНачалаПериодаСтрахования = ?(НЕ ЗначениеЗаполнено(ДатаНачалаПериодаСтрахования), мНачДата, ДатаНачалаПериодаСтрахования);
	// Формируем запрос по затратам в разрезе работников
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоМесяца",   ДатаНачалаПериодаСтрахования);
	Запрос.УстановитьПараметр("КонецМесяца",    мКонГраница);
	Запрос.УстановитьПараметр("НачалоТекМесяца",мНачДата);
	Запрос.УстановитьПараметр("ВидСубконто",    ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Массив = Новый Массив;
	Массив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
	Запрос.УстановитьПараметр("ВидКорСубконто", Массив);
	Запрос.УстановитьПараметр("СчетаКосвенныхРасходов",  НалоговыйУчет.ПолучитьМассивСчетовУчетаРасходов());
	Запрос.УстановитьПараметр("ВидЗатрат",      ВидЗатрат);
	Запрос.УстановитьПараметр("Организация",    СтруктураШапкиДокумента.СписокОрганизаций);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СУММА(ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотДт, 0)) КАК ОборотДт,
	               |	СУММА(ВЫБОР
	               |			КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.РегламентнаяОперация
	               |					И ХозрасчетныйОбороты.Период > &НачалоТекМесяца
	               |				ТОГДА 0
	               |			ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОбороты.СуммаНУОборотКт, 0)
	               |		КОНЕЦ) КАК ОборотКт,
	               |	СУММА(ВЫБОР
	               |			КОГДА ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.РегламентнаяОперация
	               |					И ХозрасчетныйОбороты.Период > &НачалоТекМесяца
	               |				ТОГДА 0
	               |			ИНАЧЕ ЕСТЬNULL(ХозрасчетныйОбороты.СуммаПРОборот, 0)
	               |		КОНЕЦ) КАК ОборотКтПР,
	               |	0 КАК КоличествоЗастрахованных
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоМесяца,
	               |			&КонецМесяца,
	               |			Регистратор,
	               |			Счет В ИЕРАРХИИ (&СчетаКосвенныхРасходов),
	               |			&ВидСубконто,
	               |			Организация В ИЕРАРХИИ (&Организация)
	               |				И Субконто1.ВидРасходовНУ = &ВидЗатрат,
	               |			,
	               |			) КАК ХозрасчетныйОбороты
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	СУММА(0),
	               |	СУММА(0),
	               |	СУММА(0),
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ХозрасчетныйОбороты.КорСубконто1)
	               |ИЗ
	               |	РегистрБухгалтерии.Хозрасчетный.Обороты(
	               |			&НачалоМесяца,
	               |			&КонецМесяца,
	               |			,
	               |			Счет В ИЕРАРХИИ (&СчетаКосвенныхРасходов),
	               |			&ВидСубконто,
	               |			Организация В ИЕРАРХИИ (&Организация)
	               |				И Субконто1.ВидРасходовНУ = &ВидЗатрат,
	               |			,
	               |			&ВидКорСубконто) КАК ХозрасчетныйОбороты";
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат 0;
	КонецЕсли;
	
	ТаблицаРасходов = Новый ТаблицаЗначений;
	ТаблицаРасходов = Результат.Выгрузить();
	СуммаБазы = ТаблицаРасходов.Итог("КоличествоЗастрахованных") * 15000;
	
	
	ЗатратыГод   = ТаблицаРасходов.Итог("ОборотДт");
	РасходыГод = ?(СуммаБазы > ЗатратыГод, ЗатратыГод, СуммаБазы);
	РасходыМесяц = РасходыГод - ТаблицаРасходов.Итог("ОборотКт");
	РазницыГод = ЗатратыГод - РасходыГод;
	РазницыМесяц =  РазницыГод  - ТаблицаРасходов.Итог("ОборотКтПР");
	
	Если НЕ ЗатратыГод = 0 Или Не РасходыГод = 0 Или Не РасходыМесяц = 0 Или Не РазницыМесяц = 0 Или Не РазницыГод = 0 Тогда
		
		СтрокаЗаписьНормированияРасходов              = СтруктураШапкиДокумента.ЗаписьНормированияРасходов.Добавить();
		СтрокаЗаписьНормированияРасходов.ВидОперации  = ВидЗатрат;
		СтрокаЗаписьНормированияРасходов.ВидРегОперации= "Страхование";
		СтрокаЗаписьНормированияРасходов.СуммаБазы    = СуммаБазы;
		СтрокаЗаписьНормированияРасходов.ЗатратыГод   = ЗатратыГод;
		СтрокаЗаписьНормированияРасходов.РасходыГод = РасходыГод;
		СтрокаЗаписьНормированияРасходов.РасходыМесяц = РасходыМесяц;
		СтрокаЗаписьНормированияРасходов.РазницыМесяц = РазницыМесяц;
		СтрокаЗаписьНормированияРасходов.РазницыГод   = РазницыГод;
		
	КонецЕсли;
	
	Остаток = ТаблицаРасходов.Итог("ОборотДт") - ТаблицаРасходов.Итог("ОборотКт");
	
	Возврат ?(Остаток = 0, 0, РасходыМесяц / Остаток);	
	
КонецФункции // НормированиеРасходовПоРаботникуОрганизации()

// Производится проверка заполнения обязательных реквизитов
// регистров налогового учета
Функция ПроверитьЗаполнениеОбязательныхРеквизитов(НачалоПериода,КонецПериода,Организация) Экспорт
	
	Отказ = Ложь;
	
	Если НЕ ЗначениеЗаполнено(НачалоПериода) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Укажите дату начала периода");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КонецПериода) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Укажите дату окончания периода");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(КонецПериода) И НачалоПериода > КонецПериода Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Дата начала периода не может быть больше даты конца периода");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьОбОшибке("Укажите организацию");
		
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

// формируется сообщение о необходимости проведения рег.операции "Реформация баланса
// в случае изменения ставки налога на прибыль
//
Процедура СообщитьОбИзмененииСтавки(ТекущиеДанные, ИспользоватьОрганизацию = Ложь) Экспорт
    
    Если ТекущиеДанные = Неопределено Тогда
        Возврат;
    КонецЕсли;
    
    ДатаРеформации = НачалоДня(ТекущиеДанные.Период)-1;
    
    Запрос = Новый Запрос;
    Запрос.УстановитьПараметр("ДатаКон",     ДатаРеформации);
    
    Массив = Новый Массив;
    Массив.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеАктивы);
    Массив.Добавить(ПланыСчетов.Хозрасчетный.ОтложенныеНалоговыеОбязательства);
    Запрос.УстановитьПараметр("Счета", Массив);
    
    Запрос.Текст = 	
    
    "ВЫБРАТЬ РАЗРЕШЕННЫЕ
    |   ХозрасчетныйОстатки.Организация КАК Организация
    |ИЗ
    |   РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаКон, Счет В ИЕРАРХИИ (&Счета), , Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(перечисление.ЮрФизЛицо.ЮрЛицо)) КАК ХозрасчетныйОстатки
    |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&ДатаКон, ) КАК УчетнаяПолитикаОрганизацийСрезПоследних
    |       ПО ХозрасчетныйОстатки.Организация = УчетнаяПолитикаОрганизацийСрезПоследних.Организация
    |ГДЕ
    |   ХозрасчетныйОстатки.СуммаОстаток <> 0
    |   И УчетнаяПолитикаОрганизацийСрезПоследних.ПоддержкаПБУ18
    |   И УчетнаяПолитикаОрганизацийСрезПоследних.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
    |
    |УПОРЯДОЧИТЬ ПО
    |   ХозрасчетныйОстатки.Организация.Наименование";
    
    Если ИспользоватьОрганизацию Тогда
        Запрос.УстановитьПараметр("Организация", ТекущиеДанные.Организация);
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация.ЮрФизЛицо","Организация = &Организация И Организация.ЮрФизЛицо");
    КонецЕсли;
    
    Результат = Запрос.Выполнить();
    
    Если Результат.Пустой() Тогда
        Возврат;
    КонецЕсли;
    
    ТекстСообщения = "Изменена ставка налога на прибыль.
    |Рекомендуется выполнить регламентную операцию ""Реформация баланса"" закрытия месяца
    |за декабрь " + формат(Год(ДатаРеформации),"ЧГ=") + " года (меню ""Операции - Закрытие месяца"") для следующих организаций:";
    
    Выборка = Результат.Выбрать();
    
    Пока Выборка.Следующий() Цикл
        ТекстСообщения = ТекстСообщения + "
        |" + Выборка.Организация.ПолноеНаименование();
    КонецЦикла;
    
    Сообщить(ТекстСообщения);
КонецПроцедуры
