
Процедура ЗаписатьУзелДереваXMLВXML(СтрокаДерева, ПотокXML, ПрефиксПространстваИмен = "xsi", URIПространстваИмен = "http://www.w3.org/2001/XMLSchema") Экспорт
	
	Если ТипЗнч(СтрокаДерева) = Тип("СтрокаДереваЗначений") Тогда
		
		Если НЕ ПустаяСтрока(СтрокаДерева.Имя) Тогда
			
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Имя);
			ПотокXML.ЗаписатьСоответствиеПространстваИмен(ПрефиксПространстваИмен, URIПространстваИмен);
			СписокАтрибутов = ПроцедурыПерсонифицированногоУчета.НайтиАтрибуты(СтрокаДерева);
			
			Если СписокАтрибутов <> Неопределено Тогда
				Для каждого СтрокаСАтрибутом Из СписокАтрибутов.Строки Цикл
					ПотокXML.ЗаписатьАтрибут(СтрокаСАтрибутом.Имя, СтрокаСАтрибутом.Значение);
				КонецЦикла;
			КонецЕсли;
			
			ПотокXML.ЗаписатьТекст(СтрокаДерева.Значение);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого Лист из СтрокаДерева.Строки Цикл
		Если ПроцедурыПерсонифицированногоУчета.СтрокаСАтрибутами(Лист) Тогда
			Продолжить;
		КонецЕсли;
		ЗаписатьУзелДереваXMLВXML(Лист, ПотокXML, ПрефиксПространстваИмен, URIПространстваИмен);
	КонецЦикла;
	
	Если ТипЗнч(СтрокаДерева) = Тип("СтрокаДереваЗначений") Тогда
		Если НЕ ПустаяСтрока(СтрокаДерева.Имя) Тогда
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////

Функция СуммаОКАТОиКПП(КодПоОКАТО, КПП) Экспорт 

	Возврат РегламентированнаяОтчетность.ДополнитьСтроку(КодПоОКАТО, 11) + "/" + РегламентированнаяОтчетность.ДополнитьСтроку(КПП, 9)

КонецФункции // СуммаОКАТОиКПП()

Функция КодОКАТОизСуммыОКАТОиКПП(ОКАТО_КПП) Экспорт

	Возврат СокрЛП(Лев(ОКАТО_КПП,11))

КонецФункции // ОКАТОизСуммыОКАТОиКПП()

Функция КППизСуммыОКАТОиКПП(ОКАТО_КПП) Экспорт

	Возврат СокрЛП(Прав(ОКАТО_КПП,9))

КонецФункции // КППизСуммыОКАТОиКПП()

// Проверяет, написана ли строка только русскими и латинскими буквами 
//
// Параметры:
//  СтрокаПараметр - строка, проверяемая строка.
//
// Возвращаемое значение:
//		Истина - строка состоит из допустимых символов
//		Ложь - в строке встречаются недопустимые символы
// 
// Описание:
//		Строка проверяется на наличие только русских и латинских букв;
//		кроме того,	допускаются дефис, "Ё", "ё".
//
Функция СтрокаНаписанаРусскимиИлиЛатинскими(Знач СтрокаПараметр) Экспорт
	
	СтрокаПараметр = СокрЛП(СтрокаПараметр);	

	КоличествоСимволов = СтрДлина(СтрокаПараметр);
	
	Если  КоличествоСимволов > 0 Тогда
		
		ПервыйСимвол = КодСимвола(Лев(СтрокаПараметр,1)); 
		Русские = (ПервыйСимвол >= 192) или (ПервыйСимвол = 184) или (ПервыйСимвол = 168);
		
		СписокДопустимыхЗначений = Новый Соответствие;
		СписокДопустимыхЗначений.Вставить(184,Истина);   // ё
		СписокДопустимыхЗначений.Вставить(168,Истина);   // Ё
		СписокДопустимыхЗначений.Вставить(45,Истина);   //  "-"

		Для Сч = 1 По КоличествоСимволов Цикл
			
			Код = КодСимвола(Сред(СтрокаПараметр,Сч));   
			
			// Большие латинские буквы: 65 - 90
			// Маленькие латинские буквы: 97 - 122
			// Русские буквы: 192 и больше
			
			//русские:
			Если Русские Тогда
				
				Если (СписокДопустимыхЗначений[Код] = Неопределено) и (Код < 192)  Тогда
					Возврат Ложь;
				КонецЕсли;

			// латинские:	
			ИначеЕсли (Код <> 45) и ((Код < 65) или	(Код > 90) и (Код < 97) или (Код > 122))  Тогда    
				Возврат Ложь;
			КонецЕсли;
				
		КонецЦикла;
		
		Возврат Истина;

	Иначе 	

		Возврат Истина;

	КонецЕсли;
	
КонецФункции // СтрокаНаписанаРусскимиИлиЛатинскими()

////////////////////////////////////////////////////////////////////////////////

Процедура ПрочитатьДанныеСотрудника(ОписаниеСправки, Дата, НалоговыйПериод) Экспорт

	ФизЛицо = ОписаниеСправки.Сотрудник;
	
	Если ЗначениеЗаполнено(ФизЛицо) Тогда
		ТаблицаСотрудников = Новый ТаблицаЗначений;
		ТаблицаСотрудников.Колонки.Добавить("Сотрудник",Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
		ТаблицаСотрудников.Колонки.Добавить("Ставка",Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавки"));
		ЗаполнитьЗначенияСвойств(ТаблицаСотрудников.Добавить(), ОписаниеСправки);
		ЗаполнитьЗначенияСвойств(ОписаниеСправки, СправкиПоНДФЛ.ДанныеСотрудников(ТаблицаСотрудников, Дата, НалоговыйПериод)[0]);
	Иначе
		ОписаниеСправки.АдресЗарубежом = "";
		ОписаниеСправки.АдресРФ = "";
		ОписаниеСправки.ВидДокумента = "";
		ОписаниеСправки.Гражданство = "";
		ОписаниеСправки.ДатаРождения = "";
		ОписаниеСправки.Имя = "";
		ОписаниеСправки.Отчество = "";
		ОписаниеСправки.Фамилия = "";
		ОписаниеСправки.ИНН = "";
		ОписаниеСправки.НомерДокумента = "";
		ОписаниеСправки.СерияДокумента = "";
		ОписаниеСправки.СтатусНалогоплательщика = "";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьДанныеСправки(ОписаниеСправки, СведенияОДоходах, СведенияОВычетах, НалоговыйПериод, Организация, ГоловнаяОрганизация, ОКАТО_КПП, Дата) Экспорт

	ФизЛицо = ОписаниеСправки.Сотрудник;
	Ставка = ОписаниеСправки.Ставка;
	
	Если Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(ФизЛицо) Или Не ЗначениеЗаполнено(Ставка) Тогда
		Возврат
	КонецЕсли;
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаСотрудников.Колонки.Добавить("Ставка", Новый ОписаниеТипов("ПеречислениеСсылка.НДФЛСтавки"));
	ТаблицаСотрудников.Колонки.Добавить("НомерСправки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8, 0)));
	ЗаполнитьЗначенияСвойств(ТаблицаСотрудников.Добавить(), ОписаниеСправки);
	СтруктураДанных = ДанныеОДоходах(ТаблицаСотрудников, НалоговыйПериод, Организация, ГоловнаяОрганизация, ОКАТО_КПП, Дата);
	Для каждого СтрокаТЗ Из СтруктураДанных.Доходы Цикл
		ЗаполнитьЗначенияСвойств(СведенияОДоходах.Добавить(),СтрокаТЗ);
	КонецЦикла; 
	Для каждого СтрокаТЗ Из СтруктураДанных.Вычеты Цикл
		ЗаполнитьЗначенияСвойств(СведенияОВычетах.Добавить(),СтрокаТЗ);
	КонецЦикла; 
	ЗаполнитьЗначенияСвойств(ОписаниеСправки, СтруктураДанных.Итоги[0]);
	
КонецПроцедуры

Функция ДанныеОДоходах(ТаблицаСотрудников, НалоговыйПериод, Организация, ГоловнаяОрганизация, ОКАТО_КПП, Дата) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылки", ТаблицаСотрудников);
	Запрос.УстановитьПараметр("ДатаЗакона229ФЗ", ПроведениеРасчетов.ДатаЗакона229ФЗ());
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ГодНП",НалоговыйПериод);
	Запрос.УстановитьПараметр("КонецНП",КонецГода(Дата(НалоговыйПериод,1,1)));
	Запрос.УстановитьПараметр("НачалоНП", НачалоГода(Дата(НалоговыйПериод,1,1)));
	Запрос.УстановитьПараметр("НачалоСледующегоНП", КонецГода(Дата(НалоговыйПериод,1,1)) + 1);// начало следующего налогового периода
	Запрос.УстановитьПараметр("ДатаПодачиСведений", Дата);
	Запрос.УстановитьПараметр("ДатаДоПодачиСведений", Дата - 1);
	ЧитатьСводныеДанные = ОКАТО_КПП = Неопределено;
	Запрос.УстановитьПараметр("ЧитатьСводныеДанные", ЧитатьСводныеДанные);
	Запрос.УстановитьПараметр("ОКАТО_КПП", ОКАТО_КПП);
	Если ЧитатьСводныеДанные Тогда
		Запрос.УстановитьПараметр("КодПоОКАТО", Неопределено);
		Запрос.УстановитьПараметр("КПП", Неопределено);
	Иначе
		Запрос.УстановитьПараметр("КодПоОКАТО",КодОКАТОизСуммыОКАТОиКПП(ОКАТО_КПП));
		Запрос.УстановитьПараметр("КПП",КППизСуммыОКАТОиКПП(ОКАТО_КПП));
	КонецЕсли;
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.УстановитьПараметр("ПустойКПП","");
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.Сотрудник КАК Физлицо,
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.Ставка
	|ПОМЕСТИТЬ ВТФизлица
	|ИЗ
	|	&Ссылки КАК СотрудникиОрганизаций";
	Запрос.Выполнить();
	НачМесяца = НачалоГода(Дата(НалоговыйПериод,1,1));
	ДатыПоМесяцамТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	ДатыПоМесяцамТекст = ДатыПоМесяцамТекст + "
	|ПОМЕСТИТЬ ВТПериодыНалогаНаДоходы";
	Пока НачМесяца < НачалоМесяца(КонецГода(Дата(НалоговыйПериод,1,1))) Цикл
		НачМесяца = НачалоМесяца(КонецМесяца(НачМесяца) + 1);
		ДатыПоМесяцамТекст = ДатыПоМесяцамТекст +"
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(НачМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	Запрос.Текст = ДатыПоМесяцамТекст;
	Запрос.Выполнить();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Статусы.ФизЛицо,
	|	Статусы.Статус
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаКонецГода
	|ИЗ
	|	РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ.СрезПоследних(
	|			&КонецНП,
	|			ФизЛицо В
	|				(ВЫБРАТЬ
	|					ФизическиеЛица.ФизЛицо
	|				ИЗ
	|					ВТФизлица КАК ФизическиеЛица)) КАК Статусы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыРегистра.ФизЛицо КАК ФизЛицо,
	|	ПериодыРегистра.МесяцНалоговогоПериода,
	|	ЕСТЬNULL(СтатусНалогоплательщика.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) КАК Статус
	|ПОМЕСТИТЬ ВТРезидентствоФизлицНаКонецМесяца
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Физлица.ФизЛицо КАК ФизЛицо,
	|		Периоды.Период КАК МесяцНалоговогоПериода,
	|		МАКСИМУМ(СтатусНалогоплательщика.Период) КАК Период
	|	ИЗ
	|		ВТФизлица КАК Физлица
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПериодыНалогаНаДоходы КАК Периоды
	|			ПО (ИСТИНА)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|			ПО Физлица.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|				И (СтатусНалогоплательщика.Период <= КОНЕЦПЕРИОДА(Периоды.Период, МЕСЯЦ))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Физлица.ФизЛицо,
	|		Периоды.Период) КАК ПериодыРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ КАК СтатусНалогоплательщика
	|		ПО ПериодыРегистра.ФизЛицо = СтатусНалогоплательщика.ФизЛицо
	|			И ПериодыРегистра.Период = СтатусНалогоплательщика.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Физлица.ФизЛицо,
	|	Периоды.Период КАК МесяцНалоговогоПериода,
	|	ВЫБОР
	|		КОГДА Периоды.Период < &ДатаЗакона229ФЗ
	|			ТОГДА ЕСТЬNULL(РезидентствоФизлицНаКонецГода.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент))
	|		КОГДА ЕСТЬNULL(РезидентствоФизлицНаКонецГода.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|		ИНАЧЕ РезидентствоФизлицНаКонецМесяца.Статус
	|	КОНЕЦ КАК Статус
	|ПОМЕСТИТЬ ВТСтатусы
	|ИЗ
	|	ВТФизлица КАК Физлица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПериодыНалогаНаДоходы КАК Периоды
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецМесяца КАК РезидентствоФизлицНаКонецМесяца
	|		ПО Физлица.ФизЛицо = РезидентствоФизлицНаКонецМесяца.ФизЛицо
	|			И (Периоды.Период = РезидентствоФизлицНаКонецМесяца.МесяцНалоговогоПериода)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРезидентствоФизлицНаКонецГода КАК РезидентствоФизлицНаКонецГода
	|		ПО Физлица.ФизЛицо = РезидентствоФизлицНаКонецГода.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо,
	|	МИНИМУМ(НДФЛИмущественныеВычетыФизлиц.Регистратор) КАК Регистратор
	|ПОМЕСТИТЬ ВТДокументыУведомлений
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ СотрудникиОрганизаций.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И (НДФЛИмущественныеВычетыФизлиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = &ГодНП)
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|ГДЕ
	|	НДФЛИмущественныеВычетыФизлиц.Регистратор ССЫЛКА Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	СотрудникиОрганизаций.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыУведомлений.НомерСправки,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ДатаУведомления, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.КодНалоговогоОргана, """") КАК КодНалоговогоОрганаУведомления,
	|	ЕСТЬNULL(НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.НомерВходящегоДокумента, """") КАК НомерУведомления
	|ПОМЕСТИТЬ ВТУведомления
	|ИЗ
	|	ВТДокументыУведомлений КАК ДокументыУведомлений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.НДФЛПодтверждениеПраваНаИмущественныйВычет.РаботникиОрганизации КАК НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации
	|		ПО ДокументыУведомлений.Регистратор = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.Ссылка
	|			И ДокументыУведомлений.ФизЛицо = НДФЛПодтверждениеПраваНаИмущественныйВычетРаботникиОрганизации.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
	|	НДФЛСведенияОДоходах.КодДохода,
	|	НДФЛСведенияОДоходах.КодВычета,
	|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) КАК СуммаДохода,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛСведенияОДоходах.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ НДФЛСведенияОДоходах.СуммаВычета
	|		КОНЕЦ) КАК СуммаВычета
	|ПОМЕСТИТЬ ВТДоходы
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|			ПО НДФЛСведенияОДоходах.ФизЛицо = Статусы.ФизЛицо
	|				И (НАЧАЛОПЕРИОДА(НДФЛСведенияОДоходах.Период, МЕСЯЦ) = Статусы.МесяцНалоговогоПериода)
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛСведенияОДоходах.ФизЛицо
	|			И (НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛСведенияОДоходах.ОбособленноеПодразделение = &Организация)
	|			И (ГОД(НДФЛСведенияОДоходах.Период) = &ГодНП)
	|			И (НДФЛСведенияОДоходах.ПериодРегистрации < &ДатаПодачиСведений)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛСведенияОДоходах.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ = &КодПоОКАТО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛСведенияОДоходах.КПП <> &ПустойКПП
	|						ТОГДА НДФЛСведенияОДоходах.КПП
	|					КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ СотрудникиОрганизаций.Ставка = ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ВЫБОР
	|								КОГДА НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|							КОНЕЦ
	|					КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ)
	|			И ((НЕ НДФЛСведенияОДоходах.КодДохода.НеОтражаетсяВОтчетности2010))
	|ГДЕ
	|	НДФЛСведенияОДоходах.КодДохода ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	СотрудникиОрганизаций.НомерСправки,
	|	НДФЛСведенияОДоходах.КодДохода,
	|	НДФЛСведенияОДоходах.КодВычета,
	|	МЕСЯЦ(НДФЛСведенияОДоходах.Период)
	|
	|ИМЕЮЩИЕ
	|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки КАК НомерСправки,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета КАК КодВычета,
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПримененныйВычет КАК СуммаВычета
	|ПОМЕСТИТЬ ВТВычеты
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛПредоставленныеСтандартныеВычетыФизЛиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ФизЛицо
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ СотрудникиОрганизаций.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение = &Организация)
	|			И (ГОД(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.МесяцНалоговогоПериода) = &ГодНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ = &КодПоОКАТО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КПП <> &ПустойКПП
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КПП
	|					КОГДА ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|ГДЕ
	|	НДФЛПредоставленныеСтандартныеВычетыФизЛиц.КодВычета ЕСТЬ НЕ NULL 
	|	И НДФЛПредоставленныеСтандартныеВычетыФизЛиц.ПримененныйВычет <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки,
	|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
	|	НДФЛИмущественныеВычетыФизлиц.Размер
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|		ПО СотрудникиОрганизаций.ФизЛицо = НДФЛИмущественныеВычетыФизлиц.ФизЛицо
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ СотрудникиОрганизаций.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13))
	|			И (НДФЛИмущественныеВычетыФизлиц.Период < &ДатаПодачиСведений)
	|			И (НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение = &Организация)
	|			И (НДФЛИмущественныеВычетыФизлиц.Год = &ГодНП)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ = &КодПоОКАТО)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ ВЫБОР
	|					КОГДА НДФЛИмущественныеВычетыФизлиц.КПП <> &ПустойКПП
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КПП
	|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ = &КПП)
	|			И (НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|ГДЕ
	|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный ЕСТЬ НЕ NULL 
	|	И НДФЛИмущественныеВычетыФизлиц.Размер <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыИВычеты.НомерСправки,
	|	СУММА(ДоходыИВычеты.ОбщаяСуммаДохода) КАК ОбщаяСуммаДохода,
	|	СУММА(ДоходыИВычеты.ОблагаемаяСуммаДохода) КАК ОблагаемаяСуммаДохода
	|ПОМЕСТИТЬ ВТИтогиДоходов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Доходы.НомерСправки КАК НомерСправки,
	|		Доходы.СуммаДохода КАК ОбщаяСуммаДохода,
	|		Доходы.СуммаДохода - Доходы.СуммаВычета КАК ОблагаемаяСуммаДохода
	|	ИЗ
	|		ВТДоходы КАК Доходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Вычеты.НомерСправки,
	|		0,
	|		-Вычеты.СуммаВычета
	|	ИЗ
	|		ВТВычеты КАК Вычеты) КАК ДоходыИВычеты
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыИВычеты.НомерСправки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СправкиОДоходах.НомерСправки КАК НомерСправки,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
	|						И (НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК НачисленоЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
	|						И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода < &НачалоСледующегоНП
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК НачисленоЗаВсеГоды,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
	|						И (НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК УдержаноЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
	|						И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода < &НачалоСледующегоНП
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК УдержаноЗаВсеГоды,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
	|						И (НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|					ТОГДА -НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ВозвращеноЗаТекущийГод,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
	|						И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода < &НачалоСледующегоНП
	|					ТОГДА -НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ВозвращеноЗаВсеГоды,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
	|						И НДФЛРасчетыСБюджетом.Период < &НачалоСледующегоНП
	|					ТОГДА НДФЛРасчетыСБюджетом.Налог
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК ПереданоНаВзысканиеЗаВсеГоды
	|ПОМЕСТИТЬ ВТНалоги
	|ИЗ
	|	ВТФизлица КАК СправкиОДоходах
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатусы КАК Статусы
	|			ПО НДФЛРасчетыСБюджетом.ФизЛицо = Статусы.ФизЛицо
	|				И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода = Статусы.МесяцНалоговогоПериода
	|		ПО СправкиОДоходах.ФизЛицо = НДФЛРасчетыСБюджетом.ФизЛицо
	|			И (НДФЛРасчетыСБюджетом.Период < &ДатаПодачиСведений)
	|			И (НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &Организация)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ &КодПоОКАТО = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
	|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
	|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|				КОНЕЦ)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ &КПП = ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.КПП <> &ПустойКПП
	|						ТОГДА НДФЛРасчетыСБюджетом.КПП
	|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КПП
	|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|				КОНЕЦ)
	|			И (&ЧитатьСводныеДанные
	|				ИЛИ СправкиОДоходах.Ставка = ВЫБОР
	|					КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ВЫБОР
	|								КОГДА НДФЛРасчетыСБюджетом.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
	|									ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15)
	|								ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка30)
	|							КОНЕЦ
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка09)
	|					КОГДА НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка35)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка35)
	|					ИНАЧЕ """"
	|				КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	СправкиОДоходах.НомерСправки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.Ставка,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.ФизЛицо,
	|	РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты.СуммаРасход КАК Сумма
	|ПОМЕСТИТЬ ВТПеречисленоНДФЛ
	|ИЗ
	|	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Обороты(
	|			&НачалоНП,
	|			&ДатаДоПодачиСведений,
	|			,
	|			Организация = &Организация
	|				И ФизЛицо В
	|					(ВЫБРАТЬ
	|						ФизическиеЛица.ФизЛицо
	|					ИЗ
	|						ВТФизлица КАК ФизическиеЛица)
	|				И (МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП)
	|				И (&ЧитатьСводныеДанные
	|					ИЛИ ОКАТО_КПП = &ОКАТО_КПП)) КАК РасчетыНалоговыхАгентовСБюджетомПоНДФЛОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Доходы.НомерСправки КАК НомерСправки,
	|	Доходы.КодДохода,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода,
	|	СУММА(Доходы.СуммаВычета) КАК СуммаВычета,
	|	ВЫБОР
	|		КОГДА Доходы.СуммаВычета = 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		ИНАЧЕ Доходы.КодВычета
	|	КОНЕЦ КАК КодВычета,
	|	Доходы.Месяц КАК Месяц,
	|	Доходы.КодДохода.КодДляОтчетности КАК КодДоходаКодДляОтчетности
	|ИЗ
	|	ВТДоходы КАК Доходы
	|ГДЕ
	|	Доходы.Месяц ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.Месяц,
	|	Доходы.НомерСправки,
	|	Доходы.КодДохода,
	|	Доходы.КодДохода.КодДляОтчетности,
	|	ВЫБОР
	|		КОГДА Доходы.СуммаВычета = 0
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		ИНАЧЕ Доходы.КодВычета
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки,
	|	Месяц,
	|	КодДоходаКодДляОтчетности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Вычеты.НомерСправки КАК НомерСправки,
	|	Вычеты.КодВычета,
	|	СУММА(Вычеты.СуммаВычета) КАК СуммаВычета,
	|	Вычеты.КодВычета.КодДляОтчетности2010 КАК КодВычетаКодДляОтчетности2010
	|ИЗ
	|	ВТВычеты КАК Вычеты
	|ГДЕ
	|	Вычеты.КодВычета ЕСТЬ НЕ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Вычеты.НомерСправки,
	|	Вычеты.КодВычета,
	|	Вычеты.КодВычета.КодДляОтчетности2010
	|
	|ИМЕЮЩИЕ
	|	СУММА(Вычеты.СуммаВычета) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки,
	|	КодВычетаКодДляОтчетности2010
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СотрудникиОрганизаций.НомерСправки КАК НомерСправки,
	|	ИтогиДоходов.ОбщаяСуммаДохода,
	|	ИтогиДоходов.ОблагаемаяСуммаДохода,
	|	Налоги.НачисленоЗаТекущийГод КАК Исчислено,
	|	Налоги.УдержаноЗаТекущийГод - Налоги.ВозвращеноЗаТекущийГод КАК Удержано,
	|	ЕСТЬNULL(ПеречисленоНДФЛ.Сумма, 0) КАК Перечислено,
	|	ВЫБОР
	|		КОГДА Налоги.НачисленоЗаВсеГоды - Налоги.УдержаноЗаВсеГоды + Налоги.ВозвращеноЗаВсеГоды - Налоги.ПереданоНаВзысканиеЗаВсеГоды > 0
	|			ТОГДА Налоги.НачисленоЗаВсеГоды - Налоги.УдержаноЗаВсеГоды + Налоги.ВозвращеноЗаВсеГоды - Налоги.ПереданоНаВзысканиеЗаВсеГоды
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Задолженность,
	|	ВЫБОР
	|		КОГДА Налоги.НачисленоЗаВсеГоды - Налоги.УдержаноЗаВсеГоды + Налоги.ВозвращеноЗаВсеГоды - Налоги.ПереданоНаВзысканиеЗаВсеГоды < 0
	|			ТОГДА -(Налоги.НачисленоЗаВсеГоды - Налоги.УдержаноЗаВсеГоды + Налоги.ВозвращеноЗаВсеГоды - Налоги.ПереданоНаВзысканиеЗаВсеГоды)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ИзлишнеУдержано,
	|	Уведомления.ДатаУведомления,
	|	Уведомления.КодНалоговогоОрганаУведомления,
	|	Уведомления.НомерУведомления
	|ИЗ
	|	ВТФизлица КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУведомления КАК Уведомления
	|		ПО СотрудникиОрганизаций.НомерСправки = Уведомления.НомерСправки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогиДоходов КАК ИтогиДоходов
	|		ПО СотрудникиОрганизаций.НомерСправки = ИтогиДоходов.НомерСправки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНалоги КАК Налоги
	|		ПО СотрудникиОрганизаций.НомерСправки = Налоги.НомерСправки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПеречисленоНДФЛ КАК ПеречисленоНДФЛ
	|		ПО СотрудникиОрганизаций.ФизЛицо = ПеречисленоНДФЛ.ФизЛицо
	|			И СотрудникиОрганизаций.Ставка = ПеречисленоНДФЛ.Ставка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСправки";

	Результаты = Запрос.ВыполнитьПакет();
	ВсегоЗапросов = Результаты.Количество();
	
	Возврат Новый Структура("Доходы, Вычеты, Итоги", Результаты[ВсегоЗапросов - 3].Выгрузить(), Результаты[ВсегоЗапросов - 2].Выгрузить(), Результаты[ВсегоЗапросов - 1].Выгрузить());
	
КонецФункции


Функция ДанныеПодписывающихЛиц(Организация, ДатаАктуальности) Экспорт

	СтруктураДанных = Новый Структура("ГлавныйБухгалтер, Телефон, СправкуПодписал, ДолжностьПодписавшегоЛица");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если ОбщегоНазначенияПереопределяемый.ЭтоЮрЛицо(Организация) Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК ДолжностьПодписавшегоЛица,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо КАК ГлавныйБухгалтер,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо КАК СправкуПодписал,
		|	ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование
		|ИЗ
		|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
		|			&Дата,
		|			СтруктурнаяЕдиница = &Организация
		|				И ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер)) КАК ОтветственныеЛицаОрганизацийСрезПоследних";
		Запрос.УстановитьПараметр("Дата", ДатаАктуальности - 1);
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий(); 
		ЗаполнитьЗначенияСвойств(СтруктураДанных,Выборка);
	Иначе
		ИндивидуальныйПредприниматель = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Организация,"ИндивидуальныйПредприниматель");
		СтруктураДанных.ГлавныйБухгалтер = ИндивидуальныйПредприниматель;
		СтруктураДанных.СправкуПодписал = ИндивидуальныйПредприниматель;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(КонтактнаяИнформация.Представление КАК СТРОКА(300)) КАК ТелефонОрганизации
	|ИЗ
	|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Объект = &Организация
	|	И КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	|	И КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонОрганизации)";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураДанных.Телефон = СокрЛП(Выборка.ТелефонОрганизации);
	КонецЕсли;
	
	Возврат СтруктураДанных

КонецФункции // ДанныеРуководителяОрганизации()

Функция ДанныеСотрудников(ТаблицаСотрудников, Дата, НалоговыйПериод) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылки", ТаблицаСотрудников);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("КонецНП",КонецГода(Дата(НалоговыйПериод,1,1)));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СотрудникиОрганизаций.Сотрудник КАК Физлицо,
	|	СотрудникиОрганизаций.Ставка
	|ПОМЕСТИТЬ ВТФизлица
	|ИЗ
	|	&Ссылки КАК СотрудникиОрганизаций";
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФИОФизЛицСрезПоследних.ФизЛицо,
	|	ФИОФизЛицСрезПоследних.Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя,
	|	ФИОФизЛицСрезПоследних.Отчество
	|ПОМЕСТИТЬ ВТФИОФизЛиц
	|ИЗ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|			&Дата,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					ВТФизлица КАК Физлица)) КАК ФИОФизЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГражданствоФизЛицСрезПоследних.ФизЛицо,
	|	ГражданствоФизЛицСрезПоследних.Страна
	|ПОМЕСТИТЬ ВТГражданство
	|ИЗ
	|	РегистрСведений.ГражданствоФизЛиц.СрезПоследних(
	|			&Дата,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					ВТФизлица КАК Физлица)) КАК ГражданствоФизЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатусФизЛиц.ФизЛицо,
	|	СтатусФизЛиц.Статус
	|ПОМЕСТИТЬ ВТСтатусы
	|ИЗ
	|	РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ.СрезПоследних(
	|			&КонецНП,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					ВТФизлица КАК Физлица)) КАК СтатусФизЛиц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер
	|ПОМЕСТИТЬ ВТПаспортныеДанные
	|ИЗ
	|	РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(
	|			&Дата,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					ВТФизлица КАК Физлица)) КАК ПаспортныеДанныеФизЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Физлица.Физлицо КАК Сотрудник,
	|	Физлица.Ставка,
	|	ФИОФизЛицСрезПоследних.Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя,
	|	ФИОФизЛицСрезПоследних.Отчество,
	|	"","" + АдресаРегистрации.Поле1 + "","" + АдресаРегистрации.Поле2 + "","" + АдресаРегистрации.Поле3 + "","" + АдресаРегистрации.Поле4 + "","" + АдресаРегистрации.Поле5 + "","" + АдресаРегистрации.Поле6 + "","" + АдресаРегистрации.Поле7 + "","" + АдресаРегистрации.Поле8 + "","" + АдресаРегистрации.Поле9 КАК АдресРФ,
	|	АдресаИностранцев.Представление КАК АдресЗарубежом,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид КАК ВидДокумента,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия КАК СерияДокумента,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер КАК НомерДокумента,
	|	ФизическиеЛица.ДатаРождения,
	|	ФизическиеЛица.ИНН,
	|	ФизическиеЛица.Наименование КАК Наименование,
	|	ЕСТЬNULL(Гражданство.Страна, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) КАК Гражданство,
	|	ЕСТЬNULL(Статусы.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) КАК СтатусНалогоплательщика
	|ИЗ
	|	ВТФизлица КАК Физлица
	|		ЛЕВОЕ СОЕДИНеНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО Физлица.Физлицо = ФизическиеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНеНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаИностранцев
	|		ПО Физлица.Физлицо = АдресаИностранцев.Объект
	|			И (АдресаИностранцев.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ИнострАдресФизЛица))
	|		ЛЕВОЕ СОЕДИНеНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаРегистрации
	|		ПО Физлица.Физлицо = АдресаРегистрации.Объект
	|			И (АдресаРегистрации.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица))
	|			И (АдресаРегистрации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|		ЛЕВОЕ СОЕДИНеНИЕ ВТПаспортныеДанные КАК ПаспортныеДанныеФизЛицСрезПоследних
	|		ПО Физлица.Физлицо = ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо
	|		ЛЕВОЕ СОЕДИНеНИЕ ВТФИОФизЛиц КАК ФИОФизЛицСрезПоследних
	|		ПО Физлица.Физлицо = ФИОФизЛицСрезПоследних.ФизЛицо
	|		ЛЕВОЕ СОЕДИНеНИЕ ВТГражданство КАК Гражданство
	|		ПО Физлица.Физлицо = Гражданство.ФизЛицо
	|		ЛЕВОЕ СОЕДИНеНИЕ ВТСтатусы КАК Статусы
	|		ПО Физлица.Физлицо = Статусы.ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	Возврат Запрос.Выполнить().Выгрузить()
	
КонецФункции

Функция ОписаниеДоходовПоКодам(Организация, ГоловнаяОрганизация, ОрганизацияЭтоЮрЛицо, НалоговыйПериод, ДатаСведений, ФизЛицо = Неопределено, Ставка = Неопределено) Экспорт

	ОтбиратьПоФизлицу = ЗначениеЗаполнено(ФизЛицо);
	ОтбиратьПоСтавке = ЗначениеЗаполнено(Ставка);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ГодНП",НалоговыйПериод);
	Запрос.УстановитьПараметр("ДатаПодачиСведений", ДатаСведений);
	Запрос.УстановитьПараметр("НеОтбиратьПоФизлицу", Не ОтбиратьПоФизлицу);
	Запрос.УстановитьПараметр("НеОтбиратьПоСтавке", Не ОтбиратьПоСтавке);
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Запрос.УстановитьПараметр("Ставка", Ставка);
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.УстановитьПараметр("ПустойКПП","");
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
	|			ТОГДА НДФЛСведенияОДоходах.КодПоОКАТО
	|		КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
	|			ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО
	|		ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
	|	КОНЕЦ КАК КодПоОКАТО,
	|	ВЫБОР
	|		КОГДА НДФЛСведенияОДоходах.КПП <> &ПустойКПП
	|			ТОГДА НДФЛСведенияОДоходах.КПП
	|		КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП, &ПустойКПП) <> &ПустойКПП
	|			ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КПП
	|		ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КПП, &ПустойКПП)
	|	КОНЕЦ КАК КПП
	|ИЗ
	|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|ГДЕ
	|	НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
	|	И НДФЛСведенияОДоходах.ОбособленноеПодразделение = &Организация
	|	И ГОД(НДФЛСведенияОДоходах.Период) = &ГодНП
	|	И НДФЛСведенияОДоходах.ПериодРегистрации < &ДатаПодачиСведений
	|	И НДФЛСведенияОДоходах.СуммаДохода <> 0
	|	И (&НеОтбиратьПоФизлицу
	|			ИЛИ НДФЛСведенияОДоходах.ФизЛицо = &ФизЛицо)
	|	И (&НеОтбиратьПоСтавке
	|			ИЛИ НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = &Ставка)";
	Выборка = Запрос.Выполнить().Выбрать();
    ТекстКодов = "";
	Пока Выборка.Следующий() Цикл
		ТекстКодов = ТекстКодов + ?(ЗначениеЗаполнено(ТекстКодов),", ","") + ?(ОрганизацияЭтоЮрЛицо, СокрЛП(СправкиПоНДФЛ.СуммаОКАТОиКПП(Выборка.КодПоОКАТО, Выборка.КПП)), СокрЛП(Выборка.КодПоОКАТО))
	КонецЦикла;
	
	Возврат "За " + Формат(НалоговыйПериод,"ЧЦ=4; ЧДЦ=0; ЧГ=0") + " год " + ?(ЗначениеЗаполнено(ТекстКодов), "зарегистрированы доходы по следующим кодам ОКАТО" + ?(ОрганизацияЭтоЮрЛицо, " и КПП","") + ": " + ТекстКодов, "не зарегистрировано доходов")

КонецФункции // ОписаниеДоходов()

Функция СписокОКАТОиКПП(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Наименование + "": "" + Организации.КодПоОКАТО + "", "" + Организации.КПП КАК Представление,
	|	Организации.КодПоОКАТО,
	|	Организации.КПП,
	|	1 КАК Порядок
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПодразделенияОрганизаций.Владелец.Наименование + "", "" + ПодразделенияОрганизаций.Наименование + "": "" + ПодразделенияОрганизаций.КодПоОКАТО + "", "" + ПодразделенияОрганизаций.КПП,
	|	ПодразделенияОрганизаций.КодПоОКАТО,
	|	ПодразделенияОрганизаций.КПП,
	|	2
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
	|ГДЕ
	|	ПодразделенияОрганизаций.Владелец = &Организация
	|	И ПодразделенияОрганизаций.КодПоОКАТО <> &ПустойКодПоОКАТО
	|	И (ПодразделенияОрганизаций.КодПоОКАТО <> ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КодПоОКАТО, &ПустойКодПоОКАТО)
	|			ИЛИ ПодразделенияОрганизаций.КПП <> ЕСТЬNULL(ПодразделенияОрганизаций.Родитель.КПП, &ПустойКодПоОКАТО))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	НаборыДанных = Запрос.Выполнить().Выгрузить(); 
	
	СписокОКАТОиКПП = Новый СписокЗначений;
	
	Для Каждого СтрокаЗапроса Из НаборыДанных Цикл
		СписокОКАТОиКПП.Добавить(СуммаОКАТОиКПП(СтрокаЗапроса.КодПоОКАТО, СтрокаЗапроса.КПП), СтрокаЗапроса.Представление);
	КонецЦикла;
	
	Возврат СписокОКАТОиКПП;
	
КонецФункции // СписокОКАТОиКПП

////////////////////////////////////////////////////////////////////////////////

#Если Клиент Тогда
	
Процедура НастроитьПолеВводаОКАТО(ПолеВвода, Организация, СписокОКАТОиКПП) Экспорт 

	СписокОКАТОиКПП = СписокОКАТОиКПП(Организация);
 	
	ПолеВвода.КнопкаВыбора = Ложь;
	
	// Если не нашли данных - ничего не делаем
	Если СписокОКАТОиКПП.Количество() > 0 Тогда
		
		МаксимальнаяДлина = 40;
		Для Каждого ЭлементСпискаЗначений Из СписокОКАТОиКПП Цикл
			МаксимальнаяДлина = Макс(МаксимальнаяДлина, СтрДлина(ЭлементСпискаЗначений.Представление));
		КонецЦикла;
		
		ПолеВвода.КнопкаВыбора = Истина;
		ПолеВвода.ШиринаСпискаВыбора = МаксимальнаяДлина;
		
	КонецЕсли; 

КонецПроцедуры

Функция СписокСтандартныхВычетов() Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВычетыНДФЛ.Ссылка,
	|	ВычетыНДФЛ.Код
	|ИЗ
	|	Справочник.ВычетыНДФЛ КАК ВычетыНДФЛ
	|ГДЕ
	|	ВычетыНДФЛ.Ссылка В(&Ссылка)");

	СпрСтандартныеВычеты = Справочники.ВычетыНДФЛ;
	МассивСтандартныеВычеты = Новый Массив;
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код103);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код104);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код105);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код101);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код102);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код106);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код107);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код111);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код113);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код311);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код312);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код318);
	МассивСтандартныеВычеты.Добавить(СпрСтандартныеВычеты.Код319);
	
	Запрос.УстановитьПараметр("Ссылка", МассивСтандартныеВычеты);
	Выборка = Запрос.Выполнить().Выбрать();		
	
	СписокВыбораСтандартныхВычетов = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		СписокВыбораСтандартныхВычетов.Добавить(Выборка.Ссылка,СокрЛП(Выборка.Код));	
	КонецЦикла;

	Возврат СписокВыбораСтандартныхВычетов
	
КонецФункции // СписокСтандартныхВычетов()

Функция СписокВычетовДляДохода(КодДохода, СоответствиеДоходовИВычетов, АвторскиеВознаграждения, СтатусНалогоплательщика) Экспорт

	Если Не ЗначениеЗаполнено(КодДохода) Тогда
		Возврат Новый СписокЗначений;	// Пустой список
	КонецЕсли; 

	// Всем подряд могут предоставляться только вычеты 3хх на доходы по ценным бумагам, 5хх на доходы 27X0 и вычет по дивидендам
	// остальные вычеты - только резидентам
	Если КодДохода = Справочники.ДоходыНДФЛ.Код2010 Или АвторскиеВознаграждения.Найти(КодДохода) <> Неопределено Тогда // Договора подряда

		Если СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент Тогда 
			Возврат Новый СписокЗначений;
		КонецЕсли;
		
	КонецЕсли;
	
	Вычеты = СоответствиеДоходовИВычетов[КодДохода];
	
	Если Вычеты = Неопределено Тогда
		Возврат Новый СписокЗначений;
	ИначеЕсли ТипЗнч(Вычеты) = Тип("СправочникСсылка.ВычетыНДФЛ") Тогда
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.Добавить(Вычеты);
		Возврат СписокВычетов;
	Иначе	
		Возврат Вычеты;
	КонецЕсли;

КонецФункции 

////////////////////////////////////////////////////////////////////////////////

Процедура ПроверитьДанныеСотрудника(Режим, ДанныеФизЛица, Доходы, Вычеты, Дата, Отказ, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеИменСтран) Экспорт
	
	СтрОшибки = "";
	СтрПредупрСводнойСправки = "";
	
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.НомерСправки) Тогда
		СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- не указан номер справки!";
	КонецЕсли;
	
	Если Режим <> "СводнаяСправка" И Не ЗначениеЗаполнено(ДанныеФизЛица.Ставка) Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- не указана ставка налогообложения!";
	КонецЕсли;
	
	// ИНН
	Если Не ПустаяСтрока(ДанныеФизЛица.ИНН) И НЕ РегламентированнаяОтчетность.ИННСоответствуетТребованиям(ДанныеФизЛица.ИНН, Перечисления.ЮрФизЛицо.ФизЛицо) Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- указан неверный ИНН!";	
	КонецЕсли;
	
	// ФИО
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.Фамилия) Или Не ЗначениеЗаполнено(ДанныеФизЛица.Имя) Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- должны быть указаны, как минимум, фамилия и имя физического лица!";
	КонецЕсли; 
	Если ДанныеФизЛица.Гражданство = Справочники.КлассификаторСтранМира.Россия Тогда
		Если НЕ РегламентированнаяОтчетность.СтрокаНаписанаПоРусски(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- ФИО физического лица должно быть введено русскими буквами!";
		КонецЕсли; 
	ИначеЕсли ЗначениеЗаполнено(ДанныеФизЛица.Гражданство) Тогда
		Если НЕ СтрокаНаписанаРусскимиИлиЛатинскими(СтрЗаменить(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество, " ", "")) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- ФИО физического лица нерезидента должно быть введено только русскими или только латинскими буквами!";
		КонецЕсли; 
	КонецЕсли;
	
	// Гражданство
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.Гражданство) Тогда
		СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- не указано гражданство!";
	КонецЕсли;
	
	// СтатусНалогоплательщика
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.СтатусНалогоплательщика) Тогда
		СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- не указан статус налогоплательщика!";
	КонецЕсли;
	
	// Удостоверение личности
	КодДокумента = СоответствиеДокументовУдостоверяющиеЛичность[ДанныеФизЛица.ВидДокумента];
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.ВидДокумента) Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- не указан документ, удостоверяющий личность!";
	Иначе
		Если НЕ ЗначениеЗаполнено(КодДокумента) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- указан неверный код документа, удостоверяющего личность!";
		Иначе
			Если Число(КодДокумента) < 1 или Число(КодДокумента)>99 Тогда
				СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- указан неверный код документа, удостоверяющего личность!";
			ИначеЕсли	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС05
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС10
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС11
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС12
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС13
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС14
				И	ДанныеФизЛица.ВидДокумента <> Справочники.ДокументыУдостоверяющиеЛичность.ИМНС91 Тогда
				// Справа от последнего пробела расположен номер документа, слева - серия
				СерияДок = ДанныеФизЛица.СерияДокумента;
				НомерДок = ДанныеФизЛица.НомерДокумента;
				
				СтрОшибкиПроверки = "";
				Если НЕ ПроцедурыПерсонифицированногоУчета.ПроверитьСериюУдостоверенияЛичности(ДанныеФизЛица.ВидДокумента, СерияДок, СтрОшибкиПроверки) Тогда
					СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- Ошибка в серии удостоверения личности: " + НРег(СтрОшибкиПроверки);
				КонецЕсли; 
				Если НЕ ПроцедурыПерсонифицированногоУчета.ПроверитьНомерУдостоверенияЛичности(ДанныеФизЛица.ВидДокумента, НомерДок, СтрОшибкиПроверки) Тогда
					СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- Ошибка в номере удостоверения личности: " + НРег(СтрОшибкиПроверки);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
	// Дата рождения
	Если Не ЗначениеЗаполнено(ДанныеФизЛица.ДатаРождения) Тогда 
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- не указана дата рождения!";
	ИначеЕсли ДанныеФизЛица.ДатаРождения > Дата тогда	
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- неверно указана дата рождения!";
	КонецЕсли;	
		
	// Адреса
	Если ДанныеФизЛица.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.Резидент Или ДанныеФизЛица.Гражданство = Справочники.КлассификаторСтранМира.Россия И ЗначениеЗаполнено(ДанныеФизЛица.АдресЗарубежом) Тогда 			
		Если Не ЗначениеЗаполнено(ДанныеФизЛица.АдресРФ) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- не указан адрес регистрации!";
		КонецЕсли;
	ИначеЕсли Не ЗначениеЗаполнено(ДанныеФизЛица.АдресРФ) Тогда // Адрес нерезидента
		Если ПустаяСтрока(ДанныеФизЛица.АдресЗарубежом) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - для физического лица нерезидента или иностранца должен быть заполнен ""Адрес физ. лица за пределами РФ""!";
		КонецЕсли;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ДанныеФизЛица.АдресРФ) Тогда
		СтруктураАдресаДляПроверки = РегламентированнаяОтчетность.РазложитьАдрес(ДанныеФизЛица.АдресРФ);
		Если ПустаяСтрока(СтруктураАдресаДляПроверки.Индекс) тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - в адресе регистрации не указан индекс!";
			СтруктураАдресаДляПроверки.Индекс = "000000";
		КонецЕсли;
		Если НЕ РегламентированнаяОтчетность.АдресСоответствуетТребованиям(СтруктураАдресаДляПроверки) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - указанный адрес регистрации не соответствует требованиям ФНС! Возможно, в адресе присутствуют латинские или недопустимые символы";
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеФизЛица.АдресЗарубежом) Тогда
		СтруктураАдреса = УправлениеКонтактнойИнформацией.ПолучитьСтруктуруАдресаИзСтроки(ДанныеФизЛица.АдресЗарубежом);
		Если Не ОбщегоНазначения.ТолькоЦифрыВСтроке(СоответствиеИменСтран[СтруктураАдреса.Страна]) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - неверно указана страна проживания нерезидента (возможно, не заполнен справочник ""Классификатор стран мира"")!";
		КонецЕсли;
	КонецЕсли;
	
	// доходы и вычеты
	Если Доходы.НайтиСтроки(Новый Структура("КодДохода", Справочники.ДоходыНДФЛ.ПустаяСсылка())).Количество() > 0 Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- в таблице доходов присутствуют строки, в которых не указан код дохода!";
	КонецЕсли;
	
	Если Вычеты.НайтиСтроки(Новый Структура("КодВычета", Справочники.ВычетыНДФЛ.ПустаяСсылка())).Количество() > 0 Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- в таблице вычетов присутствуют строки, в которых не указан код вычета!";
	КонецЕсли;
	
	Если Вычеты.НайтиСтроки(Новый Структура("СуммаВычета", 0)).Количество() > 0 Тогда
		СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + "- в таблице вычетов присутствуют строки, в которых не указана сумма вычета!";
	КонецЕсли;
	
	Если Режим <> "СводнаяСправка" И (Вычеты.Найти(Справочники.ВычетыНДФЛ.Код311, "КодВычета") <> Неопределено Или Вычеты.Найти(Справочники.ВычетыНДФЛ.Код312, "КодВычета") <> Неопределено Или Вычеты.Найти(Справочники.ВычетыНДФЛ.Код318, "КодВычета") <> Неопределено) Тогда
		
		Если Не ЗначениеЗаполнено(ДанныеФизЛица.КодНалоговогоОрганаУведомления) Или Не ЗначениеЗаполнено(ДанныеФизЛица.НомерУведомления) Или Не ЗначениеЗаполнено(ДанныеФизЛица.ДатаУведомления) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - не указаны данные уведомления о праве на имущественный вычет!";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеФизЛица.КодНалоговогоОрганаУведомления) И Не ОбщегоНазначения.ТолькоЦифрыВСтроке(ДанныеФизЛица.КодНалоговогоОрганаУведомления) Тогда
			СтрОшибки = СтрОшибки + Символы.ПС + Символы.Таб + " - неверно указан код налогового органа, выдавшего уведомление о праве на имущественный вычет!";
		КонецЕсли;
	
	КонецЕсли;
	
	// итоги
	Если ДанныеФизЛица.ОбщаяСуммаДохода <> Доходы.Итог("СуммаДохода") Тогда
		СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- общая сумма дохода не совпадает с итогом таблицы доходов!";
	КонецЕсли;
	
	Если ДанныеФизЛица.ОблагаемаяСуммаДохода <> Доходы.Итог("СуммаДохода") - Доходы.Итог("СуммаВычета") - Вычеты.Итог("СуммаВычета") Тогда
		СтрПредупрСводнойСправки = СтрПредупрСводнойСправки + Символы.ПС + Символы.Таб + "- сумма налоговой базы не совпадает с итогами таблиц доходов и вычетов!";
	КонецЕсли;
	
	// выводим сообщения об ошибках
	Если ЗначениеЗаполнено(СтрОшибки + СтрПредупрСводнойСправки) Тогда
		
		Если Режим <> "СводнаяСправка" Тогда
			СтрОшибки =  "В сведениях о доходах сотрудника (справка № " + ДанныеФизЛица.НомерСправки + ")" + ?(Не ПустаяСтрока(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество), " " + ДанныеФизЛица.Фамилия + " " + ДанныеФизЛица.Имя + " " + ДанныеФизЛица.Отчество, " <ФИО не указано>") + " обнаружены ошибки:" + СтрОшибки + СтрПредупрСводнойСправки;
			ОбщегоНазначения.СообщитьОбОшибке(СтрОшибки, Отказ);
		Иначе
			СтрОшибки =  "В сведениях о доходах сотрудника (справка № " + ДанныеФизЛица.НомерСправки + ")" + ?(Не ПустаяСтрока(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество), " " + ДанныеФизЛица.Фамилия + " " + ДанныеФизЛица.Имя + " " + ДанныеФизЛица.Отчество, " <ФИО не указано>") + " обнаружены ошибки:" + СтрОшибки;
			ОбщегоНазначения.СообщитьОбОшибке(СтрОшибки, Отказ);
			Если Не ЗначениеЗаполнено(СтрОшибки) Тогда
				СтрПредупрСводнойСправки =  "В сведениях о доходах сотрудника (справка № " + ДанныеФизЛица.НомерСправки + ")" + ?(Не ПустаяСтрока(ДанныеФизЛица.Фамилия+ДанныеФизЛица.Имя+ДанныеФизЛица.Отчество), " " + ДанныеФизЛица.Фамилия + " " + ДанныеФизЛица.Имя + " " + ДанныеФизЛица.Отчество, " <ФИО не указано>") + " обнаружены ошибки:" + СтрПредупрСводнойСправки;
			КонецЕсли;
			ОбщегоНазначения.СообщитьОбОшибке(СтрПредупрСводнойСправки, Отказ)
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьОбщиеДанныеСправки(СтруктураДанных, Отказ) Экспорт

	Если НЕ ЗначениеЗаполнено(СтруктураДанных.НалоговыйПериод) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан налоговый период, за который формируются сведения!", Отказ);
	ИначеЕсли СтруктураДанных.НалоговыйПериод < 2010 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("В документе поддерживаются налоговые периоды с 2010 года!", Отказ);
	КонецЕсли;
	
	ЭтоЮрЛицо = СтруктураДанных.ЭтоЮрЛицо;
	Если НЕ ЗначениеЗаполнено(СтруктураДанных.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"), Отказ);
	Иначе
		
		Если Не ЗначениеЗаполнено(СтруктураДанных.НазваниеОрганизации) Тогда
			Если ЭтоЮрЛицо Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указаны полное и сокращенное наименования " + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации") + "!", Отказ);
			ИначеЕсли ПустаяСтрока(СтруктураДанных.Фамилия) Или ПустаяСтрока(СтруктураДанных.Имя) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Для физического лица-работодателя должны быть указаны, как минимум, фамилия и имя!", Отказ);
			КонецЕсли;
		ИначеЕсли Не ЭтоЮрЛицо Тогда
			Если Не РегламентированнаяОтчетность.СтрокаНаписанаПоРусски(СтруктураДанных.Фамилия + СтруктураДанных.Имя + СтруктураДанных.Отчество) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Для физического лица-работодателя Фамилия, Имя, Отчество должно быть введены русскими буквами!", Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если СтруктураДанных.Признак <> "СводнаяСправка" Тогда
			Если НЕ ЗначениеЗаполнено(СтруктураДанных.ИННорг) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указан ИНН " + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации") + "!", Отказ);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СтруктураДанных.КодИФНС) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указан код ИФНС " + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации") + "!", Отказ);
			КонецЕсли;
		Иначе 	
			Если НЕ ЗначениеЗаполнено(СтруктураДанных.ИННорг) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указан ИНН " + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации") + "!", Отказ);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтруктураДанных.ИННорг) Тогда
			Если ЭтоЮрЛицо Тогда
				Если НЕ РегламентированнаяОтчетность.ИННСоответствуетТребованиям(СтруктураДанных.ИННорг, Перечисления.ЮрФизЛицо.ЮрЛицо ) Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Для " + ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("организации") + " неверно введен ИНН!", Отказ);
				КонецЕсли; 
			Иначе
				Если НЕ РегламентированнаяОтчетность.ИННСоответствуетТребованиям(СтруктураДанных.ИННорг, Перечисления.ЮрФизЛицо.ФизЛицо) Тогда
					ОбщегоНазначения.СообщитьОбОшибке("Для физического лица-работодателя неверно введен ИНН!", Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураДанных.Признак <> "СводнаяСправка" Тогда
		Если НЕ ЗначениеЗаполнено(СтруктураДанных.ОКАТО) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан код по ОКАТО, по которому формируются сведения!", Отказ);
		КонецЕсли;
		Если ЭтоЮрЛицо И Не ЗначениеЗаполнено(СтруктураДанных.КПП) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан КПП, по которому формируются сведения!", Отказ);
		КонецЕсли;
	КонецЕсли;
		
	Если ЭтоЮрЛицо И НЕ ЗначениеЗаполнено(СтруктураДанных.ДолжностьПодписавшегоЛица) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указано лицо, подписавшее справку!", Отказ);
	КонецЕсли;
	
	Если ЭтоЮрЛицо И  НЕ ЗначениеЗаполнено(СтруктураДанных.ДолжностьПодписавшегоЛица) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана должность подписавшего лица!", Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьКлассификаторыВСоответствия(СоответствиеКодовСтран, СоответствиеИменСтран, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеКодовРегионов, СоответствиеКодовДоходов, СоответствиеКодовВычетов) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторСтранМира.Ссылка,
	|	КлассификаторСтранМира.Код,
	|	КлассификаторСтранМира.Наименование
	|ИЗ
	|	Справочник.КлассификаторСтранМира КАК КлассификаторСтранМира";
	Выборка = Запрос.Выполнить().Выбрать();
	СоответствиеКодовСтран = Новый Соответствие;
	СоответствиеИменСтран = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		СоответствиеКодовСтран.Вставить(Выборка.Ссылка,Выборка.Код);
		СоответствиеИменСтран.Вставить(Выборка.Наименование,Выборка.Код)
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыУдостоверяющиеЛичность.Ссылка,
	|	ДокументыУдостоверяющиеЛичность.КодИМНС
	|ИЗ
	|	Справочник.ДокументыУдостоверяющиеЛичность КАК ДокументыУдостоверяющиеЛичность";
	Выборка = Запрос.Выполнить().Выбрать();
	СоответствиеДокументовУдостоверяющиеЛичность = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		СоответствиеДокументовУдостоверяющиеЛичность.Вставить(Выборка.Ссылка,Выборка.КодИМНС)
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	АдресныйКлассификатор.Наименование,
	|	АдресныйКлассификатор.Сокращение,
	|	АдресныйКлассификатор.КодРегионаВКоде
	|ИЗ
	|	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
	|ГДЕ
	|	АдресныйКлассификатор.ТипАдресногоЭлемента = 1";
	Выборка = Запрос.Выполнить().Выбрать();
	СоответствиеКодовРегионов = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		СоответствиеКодовРегионов.Вставить(СокрЛП(Выборка.Наименование) + " " + СокрЛП(Выборка.Сокращение), Формат(Выборка.КодРегионаВКоде,"ЧЦ=2; ЧДЦ=0; ЧВН="))
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоходыНДФЛ.Ссылка,
	|	ДоходыНДФЛ.КодДляОтчетности
	|ИЗ
	|	Справочник.ДоходыНДФЛ КАК ДоходыНДФЛ";
	Выборка = Запрос.Выполнить().Выбрать();
	СоответствиеКодовДоходов = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		СоответствиеКодовДоходов.Вставить(Выборка.Ссылка, Выборка.КодДляОтчетности)
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВычетыНДФЛ.Ссылка,
	|	ВычетыНДФЛ.КодДляОтчетности2010
	|ИЗ
	|	Справочник.ВычетыНДФЛ КАК ВычетыНДФЛ";
	Выборка = Запрос.Выполнить().Выбрать();
	СоответствиеКодовВычетов = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		СоответствиеКодовВычетов.Вставить(Выборка.Ссылка, Выборка.КодДляОтчетности2010)
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеНалоговогоАгентаДля2НДФЛ(Организация, НалоговыйПериод, ОКАТО_КПП, Дата, Телефон, ДолжностьПодписавшегоЛица, СправкуПодписал) Экспорт

	СтруктураДанныхНА = Новый Структура("Организация, НалоговыйПериод, НалоговыйПериодСтрокой, Дата, Телефон, Признак, ЭтоЮрЛицо, НазваниеОрганизации, Фамилия, Имя, Отчество, КодИФНС, ИННорг, КПП, ОКАТО, ДолжностьПодписавшегоЛица, ФИОПодписавшего", Организация, НалоговыйПериод, Формат(НалоговыйПериод,"ЧЦ=4; ЧДЦ=0; ЧГ=0"), Дата, Телефон);
	ЭтоЮрЛицо = ОбщегоНазначенияПереопределяемый.ЭтоЮрЛицо(Организация);
	СтруктураДанныхНА.ЭтоЮрЛицо = ЭтоЮрЛицо;
	ДанныеОрганизации = ОбщегоНазначения.ПолучитьЗначенияРеквизитов(Организация, "ИНН, КодИМНС, НаименованиеПолное, НаименованиеСокращенное" + ?(ЭтоЮрЛицо,"", ",ИндивидуальныйПредприниматель"));
	СтруктураДанныхНА.КодИФНС = ДанныеОрганизации.КодИМНС;
	СтруктураДанныхНА.ИННорг = ДанныеОрганизации.ИНН;
	СтруктураДанныхНА.ОКАТО = СправкиПоНДФЛ.КодОКАТОизСуммыОКАТОиКПП(ОКАТО_КПП);
	СтруктураДанныхНА.КПП = СправкиПоНДФЛ.КППизСуммыОКАТОиКПП(ОКАТО_КПП);
	Если ЭтоЮрЛицо Тогда
		СтруктураДанныхНА.НазваниеОрганизации = ?(ЗначениеЗаполнено(ДанныеОрганизации.НаименованиеСокращенное), ДанныеОрганизации.НаименованиеСокращенное, ДанныеОрганизации.НаименованиеПолное);
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИндивидуальныйПредприниматель", ДанныеОрганизации.ИндивидуальныйПредприниматель);
		Запрос.УстановитьПараметр("ДатаКадровыхСведений", КонецДня(Дата));
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ФИОФизЛицСрезПоследних.Фамилия,
		|	ФИОФизЛицСрезПоследних.Имя,
		|	ФИОФизЛицСрезПоследних.Отчество
		|ИЗ
		|	РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаКадровыхСведений, ФизЛицо = &ИндивидуальныйПредприниматель) КАК ФИОФизЛицСрезПоследних";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Фамилия = Выборка.Фамилия;
			Имя = Выборка.Имя;
			Отчество = Выборка.Отчество;
		Иначе
			Фамилия = "";
			Имя = "";
			Отчество = "";
			ОбщегоНазначения.ФамилияИнициалыФизЛица(ОбщегоНазначения.ПолучитьЗначениеРеквизита(ДанныеОрганизации.ИндивидуальныйПредприниматель,"Наименование"),Фамилия,Имя,Отчество);
		КонецЕсли;
		СтруктураДанныхНА.НазваниеОрганизации = Фамилия + " " + Имя + " " + Отчество;
		СтруктураДанныхНА.Фамилия = Фамилия;
		СтруктураДанныхНА.Имя = Имя;
		СтруктураДанныхНА.Отчество = Отчество;
	КонецЕсли;
	СтруктураДанныхНА.ДолжностьПодписавшегоЛица = ОбщегоНазначения.ПолучитьПредставленияОбъектов(ДолжностьПодписавшегоЛица);
	Если ЗначениеЗаполнено(СправкуПодписал) Тогда
		СтруктураДанныхНА.ФИОПодписавшего = ОбщегоНазначения.ФамилияИнициалыФизЛица(СправкуПодписал)
	КонецЕсли;
	
    Возврат СтруктураДанныхНА
	
КонецФункции // ДанныеНалоговогоАгентаДля2НДФЛ()

////////////////////////////////////////////////////////////////////////////////

Процедура ВывестиТабличныеДанные(МассивСтрок, ДокументРезультат, ОбластиМакета, Знач МинимумСтрок, КоличествоКолонок = 2)

	ВсегоСтрок = МассивСтрок.Количество();
	
	Для каждого Область Из ОбластиМакета Цикл
		ОчиститьПараметрыОбластиТабличногоДокумента(Область);
	КонецЦикла;
	
	ЛеваяЧастьМакета = ОбластиМакета[0];
	
	// Вычислим высоту таблицы
	ВысотаТаблицы = ?(ВсегоСтрок < МинимумСтрок, ВсегоСтрок, ?(ВсегоСтрок > МинимумСтрок * КоличествоКолонок, ОбщегоНазначения.ЦелМаксимальное(ВсегоСтрок / КоличествоКолонок), МинимумСтрок));
	Для Сч = 1 По ВысотаТаблицы Цикл
		
		// перенесем данные
		ЛеваяЧастьМакета.Параметры.Заполнить(МассивСтрок[Сч - 1]);
		Если ВсегоСтрок > МинимумСтрок Тогда  // правая часть
			Для СчКолонок = 2 По КоличествоКолонок Цикл
				Если ВысотаТаблицы * (СчКолонок - 1) + Сч <= ВсегоСтрок Тогда
					ОбластиМакета[СчКолонок - 1].Параметры.Заполнить(МассивСтрок[ВысотаТаблицы * (СчКолонок - 1) + Сч - 1]);
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
		
		// выведем области макета
		ДокументРезультат.Вывести(ЛеваяЧастьМакета);	
		Для СчКолонок = 2 По КоличествоКолонок Цикл
			ДокументРезультат.Присоединить(ОбластиМакета.Получить(СчКолонок - 1));	
		КонецЦикла;
		
		Для каждого Область Из ОбластиМакета Цикл
			ОчиститьПараметрыОбластиТабличногоДокумента(Область);
		КонецЦикла;
		
	КонецЦикла;
	
	// довыведем пустые строки раздела
	Если ВысотаТаблицы < МинимумСтрок Тогда
		Для Сч = ВысотаТаблицы + 1 По МинимумСтрок Цикл
			ДокументРезультат.Вывести(ЛеваяЧастьМакета);	
			Для СчКолонок = 2 По КоличествоКолонок Цикл
				ДокументРезультат.Присоединить(ОбластиМакета[СчКолонок - 1]);	
			КонецЦикла; 
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ВывестиТабличныеДанные()

Процедура ОчиститьПараметрыОбластиТабличногоДокумента(Область) Экспорт
	
	Для Параметр = 0 По Область.Параметры.Количество() -1 Цикл
		Область.Параметры[Параметр] = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

Функция ПечатьСправок2НДФЛ(Ссылка, СтруктураДанныхНА, ТаблицаДанныхСотрудников, ТаблицаДанныхОДоходах, ТаблицаДанныхОВычетах) Экспорт
	
	ПустаяСтруктураАдреса = Новый Структура("Индекс,КодРегиона,Район,Город,НаселенныйПункт,Улица,Дом,Корпус,Квартира");
	СтруктураОтбора = Новый Структура("НомерСправки");
	
	Отказ = Ложь;
	
	ТекущаяДата = ОбщегоНазначения.ПолучитьРабочуюДату();
	
	ПроверитьОбщиеДанныеСправки(СтруктураДанныхНА, Отказ);
	
	СоответствиеКодовСтран = Новый Соответствие;
	СоответствиеИменСтран = Новый Соответствие;
	СоответствиеДокументовУдостоверяющиеЛичность = Новый Соответствие;
	СоответствиеКодовРегионов = Новый Соответствие;
	СоответствиеКодовДоходов = Новый Соответствие;
	СоответствиеКодовВычетов = Новый Соответствие;
	ПрочитатьКлассификаторыВСоответствия(СоответствиеКодовСтран, СоответствиеИменСтран, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеКодовРегионов, СоответствиеКодовДоходов, СоответствиеКодовВычетов);
	
	Если Не Отказ Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;
		ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаПоНДФЛВНалоговыйОрган_Справка2_НДФЛ";
		
		Справка = ПолучитьОбщийМакет("Форма2НДФЛс2010");
		
		ОбластьМакетаШапка = Справка.ПолучитьОбласть("Шапка"); // Шапка документа.
		ОбластьМакетаСередина = Справка.ПолучитьОбласть("Середина"); // Середина документа.
		ОбластьМакетаПодвал	= Справка.ПолучитьОбласть("Подвал");// Подвал документа
		ОбластьМакетаСтрокаВычетов = Справка.ПолучитьОбласть("СтрокаВычетов"); 
		ОбластьМакетаСтрокаДоходов = Справка.ПолучитьОбласть("СтрокаДоходов"); 
		ОбластиМакетаДляДоходов = Новый Массив;
		ОбластиМакетаДляДоходов.Добавить(Справка.ПолучитьОбласть("СтрокаДоходов|ЛеваяЧасть"));
		ОбластиМакетаДляДоходов.Добавить(Справка.ПолучитьОбласть("СтрокаДоходов|ПраваяЧасть"));
		ОбластиМакетаДляВычетов = Новый Массив;
		Для Сч = 1 По 4 Цикл
			ОбластиМакетаДляВычетов.Добавить(Справка.ПолучитьОбласть("СтрокаВычетов|Вычеты"+Сч));
		КонецЦикла;
		
		ОбластьМакетаШапка.Параметры.Заполнить(СтруктураДанныхНА);  // Раздел 1
		Если СтруктураДанныхНА.Признак = "СводнаяСправка" Тогда
			ОбластьМакетаШапка.Параметры.Признак = ""
		КонецЕсли;;
		
		ОбластьМакетаПодвал.Параметры.Заполнить(СтруктураДанныхНА); // подписи
		
	КонецЕсли;
	
	Для каждого СтрокаДанных Из ТаблицаДанныхСотрудников Цикл 
		
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаДанных);
		ДоходыПоВидам = ТаблицаДанныхОДоходах.Скопировать(ТаблицаДанныхОДоходах.НайтиСтроки(СтруктураОтбора));
		ВычетыПоВидам = ТаблицаДанныхОВычетах.Скопировать(ТаблицаДанныхОВычетах.НайтиСтроки(СтруктураОтбора));
		
		// Проверка корректности выводимых данных
		ПроверитьДанныеСотрудника(СтруктураДанныхНА.Признак, СтрокаДанных, ДоходыПоВидам, ВычетыПоВидам, ТекущаяДата, Отказ, СоответствиеДокументовУдостоверяющиеЛичность, СоответствиеИменСтран);
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ДоходыПоВидам.Колонки.Добавить("КодДоходаСтрокой", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(4)));
		ДоходыПоВидам.Колонки.Добавить("КодВычетаСтрокой", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(3)));
		ДоходыПоВидам.Сортировать("Месяц, КодДохода, КодВычета");
		СводныеДоходы = Новый ТаблицаЗначений;
		СводныеДоходы = ДоходыПоВидам.Скопировать();
		СводныеДоходы.Свернуть("Месяц, КодДохода", "СуммаДохода");
		Для Каждого СтрокаДоходыПоВидам Из ДоходыПоВидам Цикл
			МассивНайденныхСтрок = СводныеДоходы.НайтиСтроки(Новый Структура("Месяц, КодДохода", СтрокаДоходыПоВидам.Месяц, СтрокаДоходыПоВидам.КодДохода));
			Если МассивНайденныхСтрок.Количество() > 0 Тогда
				СтрокаДоходыПоВидам.СуммаДохода = МассивНайденныхСтрок[0].СуммаДохода;
				СводныеДоходы.Удалить(МассивНайденныхСтрок[0]);
				СтрокаДоходыПоВидам.КодДоходаСтрокой = СоответствиеКодовДоходов[СтрокаДоходыПоВидам.КодДохода];
			Иначе
				СтрокаДоходыПоВидам.СуммаДохода = 0;
				СтрокаДоходыПоВидам.Месяц = 0;
				
			КонецЕсли;
			
			СтрокаДоходыПоВидам.КодВычетаСтрокой = СоответствиеКодовВычетов[СтрокаДоходыПоВидам.КодВычета];
		КонецЦикла;
		
		
		ВычетыПоВидам.Колонки.Добавить("КодВычетаСтрокой", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(3)));
		Для каждого СтрокаТЗ Из ВычетыПоВидам Цикл
			СтрокаТЗ.КодВычетаСтрокой = СоответствиеКодовВычетов[СтрокаТЗ.КодВычета];
		КонецЦикла;
		
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда  // если справок несколько - разобьем на отдельные страницы
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		// Заполнение данных в параметрах макета
		
		ОбластьМакетаШапка.Параметры.Заполнить(СтрокаДанных);  // Раздел 2
		ОбластьМакетаШапка.Параметры.Статус = ?(СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент Или СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.УчастникПрограммыПоПереселениюСоотечественников,2,?(СтрокаДанных.СтатусНалогоплательщика = Перечисления.СтатусыНалогоплательщиковПоНДФЛ.ВысококвалифицированныйИностранныйСпециалист,3,1));
		ОбластьМакетаШапка.Параметры.Гражданство = СоответствиеКодовСтран[СтрокаДанных.Гражданство];
		ОбластьМакетаШапка.Параметры.КодУдЛичн = СоответствиеДокументовУдостоверяющиеЛичность[СтрокаДанных.ВидДокумента];
		
		Если ЗначениеЗаполнено(СтрокаДанных.АдресРФ) Тогда
			СтруктураАдреса = РегламентированнаяОтчетность.РазложитьАдрес(СтрокаДанных.АдресРФ);
			ОбластьМакетаШапка.Параметры.Заполнить(СтруктураАдреса);  
			ОбластьМакетаШапка.Параметры.КодРегиона = СоответствиеКодовРегионов[СтруктураАдреса.Регион]
		Иначе // очищаем адресные данные
			ОбластьМакетаШапка.Параметры.Заполнить(ПустаяСтруктураАдреса);  
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаДанных.АдресЗарубежом) Тогда
			СтруктураАдреса = УправлениеКонтактнойИнформацией.ПолучитьСтруктуруАдресаИзСтроки(СтрокаДанных.АдресЗарубежом);
			ОбластьМакетаШапка.Параметры.КодСтр = СоответствиеИменСтран[СтруктураАдреса.Страна];
			ОбластьМакетаШапка.Параметры.АдрТекст = СтруктураАдреса.Представление;
		Иначе // очищаем адресные данные
			ОбластьМакетаШапка.Параметры.КодСтр = "";
			ОбластьМакетаШапка.Параметры.АдрТекст = "";
		КонецЕсли;
		
		ОбластьМакетаПодвал.Параметры.Заполнить(СтрокаДанных); // реквизиты Уведомления об имущ.вычете Раздела 4 и суммы Раздела 5
		
		// Вывод подготовленной справки в табличный документ
		
		ТабДокумент.Вывести(ОбластьМакетаШапка);
		
		// Раздел 3
		Если ДоходыПоВидам.Количество() = 0 Тогда
			Для Сч = 1 По 9 Цикл
				ТабДокумент.Вывести(ОбластьМакетаСтрокаДоходов);
			КонецЦикла;
		Иначе
			ВывестиТабличныеДанные(ДоходыПоВидам, ТабДокумент, ОбластиМакетаДляДоходов, 9, 2);
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакетаСередина);
		
		// Раздел 4
		ВычетыПоВидам.Сортировать("КодВычета");
		Если ВычетыПоВидам.Количество() = 0 Тогда
			ТабДокумент.Вывести(ОбластьМакетаСтрокаВычетов);
		Иначе
			ВывестиТабличныеДанные(ВычетыПоВидам, ТабДокумент, ОбластиМакетаДляВычетов, 1, 4);
		КонецЕсли;
		
		ТабДокумент.Вывести(ОбластьМакетаПодвал);
		
	КонецЦикла;
	
	Если Отказ Тогда
	
		Возврат Неопределено	
		
	КонецЕсли;
		
	Возврат ТабДокумент;

КонецФункции // ПечатьСправки()


#КонецЕсли


Функция СоответствиеДоходовИВычетов(НалоговыйПериод) Экспорт
	
	// Все виды авторских 2201-2209
	СписокАвторскихВознаграждений = Новый Массив;
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2201);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2202);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2203);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2204);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2205);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2206);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2207);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2208);
	СписокАвторскихВознаграждений.Добавить(Справочники.ДоходыНДФЛ.Код2209);
	
	СоответствиеДоходовИВычетов = Новый Соответствие;
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2761, Справочники.ВычетыНДФЛ.Код506); // Суммы материальной помощи, оказываемой инвалидам общественными организациями инвалидов, п.28 ст.217
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2720, Справочники.ВычетыНДФЛ.Код501); // Стоимость подарков
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2730, Справочники.ВычетыНДФЛ.Код502); // Стоимость призов на соревнованиях, определяемая согласно п.28 ст.217 НК
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2740, Справочники.ВычетыНДФЛ.Код505); // Стоимость выигрышей и призов, полученных в мероприятиях в целях рекламы, согл. п.28 ст.217 НК РФ
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2760, Справочники.ВычетыНДФЛ.Код503); //Суммы материальной помощи
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.МатпомощьПриРожденииРебенка, Справочники.ВычетыНДФЛ.МатпомощьПриРожденииРебенка); //Суммы материальной помощи
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2770, Справочники.ВычетыНДФЛ.Код504); //Суммы возмещения работникам стоимости медикаментов
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2790, Справочники.ВычетыНДФЛ.Код507); //Суммы помощи и подарков ветеранам ВОВ, п.33 ст.217
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1010, Справочники.ВычетыНДФЛ.Код601); //Дивиденды
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1211, Справочники.ВычетыНДФЛ.Код607); //Взносы в пенс.фонды
	
	Если НалоговыйПериод > 2010 Тогда
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код307);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код204);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код208);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код209);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1530, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код308);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код309);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1531, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1533_2010, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код206);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код205);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код305);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1532, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1535, Справочники.ВычетыНДФЛ.Код305); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1536, Справочники.ВычетыНДФЛ.Код309);
	ИначеЕсли НалоговыйПериод > 2009 Тогда
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код307);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код204);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код208);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код209);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1530, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код308);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код309);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1531, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1533_2010, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код206);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код205);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код305);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1532, СписокВычетов); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1533, Справочники.ВычетыНДФЛ.Код306); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1535, Справочники.ВычетыНДФЛ.Код305); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1536, Справочники.ВычетыНДФЛ.Код309);
	Иначе
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1530, Справочники.ВычетыНДФЛ.Код307); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1531, Справочники.ВычетыНДФЛ.Код308); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1532, Справочники.ВычетыНДФЛ.Код305); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1535, Справочники.ВычетыНДФЛ.Код305); // Доходы от реализации акций или иных ценных бумаг
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1533, Справочники.ВычетыНДФЛ.Код306); // Доходы от реализации акций или иных ценных бумаг
		СписокВычетов = Новый СписокЗначений;
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код309);
		СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код310);
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код1536,СписокВычетов);
	КонецЕсли;
	
	СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код2010, Справочники.ВычетыНДФЛ.Код403); // Договора подряда
	Вычет620 = Справочники.ВычетыНДФЛ.НайтиПоКоду("620");
	Если Вычет620 <> Неопределено Тогда
		СоответствиеДоходовИВычетов.Вставить(Справочники.ДоходыНДФЛ.Код4800, Вычет620); // Иные доходы
	КонецЕсли;
	
	СписокВычетов = Новый СписокЗначений;
	СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код404);
	СписокВычетов.Добавить(Справочники.ВычетыНДФЛ.Код405);
	Для каждого КодДоходаАвторскихВознаграждений Из СписокАвторскихВознаграждений Цикл
		СоответствиеДоходовИВычетов.Вставить(КодДоходаАвторскихВознаграждений,СписокВычетов);
	КонецЦикла;
	
	Возврат СоответствиеДоходовИВычетов
	
КонецФункции // СоответствиеДоходовИВычетов()
