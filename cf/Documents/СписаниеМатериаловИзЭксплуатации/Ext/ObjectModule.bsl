////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда
	
// Функция формирует табличный документ унифицированной формы МБ-8
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Табличный документ по форме МБ-8 (акт на списание).
//
Функция ПечатьМБ8()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент",                               ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("СпособСписанияВДебетСчетаУказанногоВДокументе", Перечисления.СпособыСписанияРасходов.ВДебетСчетаУказанногоВДокументе);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеМатериаловИзЭксплуатации.Номер КАК Номер,
	|	СписаниеМатериаловИзЭксплуатации.Дата КАК ДатаСоставления,
	|	СписаниеМатериаловИзЭксплуатации.Дата КАК ДатаДокумента,
	|	СписаниеМатериаловИзЭксплуатации.Организация,
	|	СписаниеМатериаловИзЭксплуатации.Организация КАК ЮрФизЛицо,
	|	СписаниеМатериаловИзЭксплуатации.Организация КАК Руководители,
	|	СписаниеМатериаловИзЭксплуатации.ПодразделениеОрганизации КАК Подразделение,
	|	ВЫБОР
	|		КОГДА СписаниеМатериаловИзЭксплуатации.СпособСписанияРасходов = &СпособСписанияВДебетСчетаУказанногоВДокументе
	|			ТОГДА СписаниеМатериаловИзЭксплуатации.СчетДт.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Счет
	|ИЗ
	|	Документ.СписаниеМатериаловИзЭксплуатации КАК СписаниеМатериаловИзЭксплуатации
	|ГДЕ
	|	СписаниеМатериаловИзЭксплуатации.Ссылка = &ТекущийДокумент";

	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();

	ЗапросПоТоварам = Новый Запрос();
	ЗапросПоТоварам.УстановитьПараметр("ТекущийДокумент", ЭтотОбъект.Ссылка);
	ЗапросПоТоварам.УстановитьПараметр("ДатаДокумента",   Шапка.ДатаДокумента);
	
	ЗапросПоТоварам.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВЫРАЗИТЬ(ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК ТоварНаименование,
	|	ВложенныйЗапрос.Номенклатура.Код КАК НоменклатурныйНомер,
	|	ВложенныйЗапрос.Номенклатура.БазоваяЕдиницаИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ВложенныйЗапрос.Номенклатура.БазоваяЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
	|	ЕСТЬNULL(ВложенныйЗапрос.ПартияМатериаловВЭксплуатации.Дата, НЕОПРЕДЕЛЕНО) КАК ДатаВводаВЭксплуатацию,
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.Количество, 0)) КАК Количество,
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.ПервоначальнаяСтоимость, 0)) КАК СуммаПервоначальнойСтоимости,
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.ОстаточнаяСтоимость, 0)) КАК СуммаОстаточнойСтоимости
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура КАК Номенклатура,
	|		СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации КАК ПартияМатериаловВЭксплуатации,
	|		СУММА(СписаниеМатериаловИзЭксплуатацииСпецодежда.Количество) КАК Количество,
	|		СУММА(ХозрасчетныйДвиженияССубконтоЗБ.ПервоначальнаяСтоимость) КАК ПервоначальнаяСтоимость,
	|		СУММА(ХозрасчетныйДвиженияССубконто.ОстаточнаяСтоимость) КАК ОстаточнаяСтоимость
	|	ИЗ
	|		(ВЫБРАТЬ
	|			СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура КАК Номенклатура,
	|			СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации КАК ПартияМатериаловВЭксплуатации,
	|			СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо КАК ФизЛицо,
	|			СУММА(СписаниеМатериаловИзЭксплуатацииСпецодежда.Количество) КАК Количество
	|		ИЗ
	|			Документ.СписаниеМатериаловИзЭксплуатации.Спецодежда КАК СписаниеМатериаловИзЭксплуатацииСпецодежда
	|		ГДЕ
	|			СписаниеМатериаловИзЭксплуатацииСпецодежда.Ссылка = &ТекущийДокумент
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура,
	|			СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо,
	|			СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации) КАК СписаниеМатериаловИзЭксплуатацииСпецодежда
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				СУММА(ХозрасчетныйДвиженияССубконтоЗБ.Сумма) КАК ПервоначальнаяСтоимость,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт1 КАК СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт2 КАК СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт3 КАК СубконтоКт3
	|			ИЗ
	|				РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|					,
	|					,
	|					Регистратор = &ТекущийДокумент
	|						И СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный)) КАК ХозрасчетныйДвиженияССубконтоЗБ
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт3) КАК ХозрасчетныйДвиженияССубконтоЗБ
	|			ПО ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт1 = СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура
	|				И ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт2 = СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации
	|				И ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт3 = СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				СУММА(ХозрасчетныйДвиженияССубконто.Сумма) КАК ОстаточнаяСтоимость,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК СубконтоКт3
	|			ИЗ
	|				РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|					,
	|					,
	|					Регистратор = &ТекущийДокумент
	|						И (НЕ СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный))
	|						И (НЕ КоличествоКт = 0)) КАК ХозрасчетныйДвиженияССубконто
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт3) КАК ХозрасчетныйДвиженияССубконто
	|			ПО ХозрасчетныйДвиженияССубконто.СубконтоКт1 = СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура
	|				И ХозрасчетныйДвиженияССубконто.СубконтоКт2 = СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации
	|				И ХозрасчетныйДвиженияССубконто.СубконтоКт3 = СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура,
	|		СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура,
	|		СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации,
	|		СУММА(СписаниеМатериаловИзЭксплуатацииСпецоснастка.Количество),
	|		СУММА(ХозрасчетныйДвиженияССубконтоЗБ.ПервоначальнаяСтоимость),
	|		СУММА(ХозрасчетныйДвиженияССубконто.ОстаточнаяСтоимость)
	|	ИЗ
	|		(ВЫБРАТЬ
	|			СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура КАК Номенклатура,
	|			СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации КАК ПартияМатериаловВЭксплуатации,
	|			СУММА(СписаниеМатериаловИзЭксплуатацииСпецоснастка.Количество) КАК Количество
	|		ИЗ
	|			Документ.СписаниеМатериаловИзЭксплуатации.Спецоснастка КАК СписаниеМатериаловИзЭксплуатацииСпецоснастка
	|		ГДЕ
	|			СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка = &ТекущийДокумент
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура,
	|			СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации) КАК СписаниеМатериаловИзЭксплуатацииСпецоснастка
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				СУММА(ХозрасчетныйДвиженияССубконтоЗБ.Сумма) КАК ПервоначальнаяСтоимость,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт1 КАК СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт2 КАК СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт3 КАК СубконтоКт3
	|			ИЗ
	|				РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|					,
	|					,
	|					Регистратор = &ТекущийДокумент
	|						И СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный)) КАК ХозрасчетныйДвиженияССубконтоЗБ
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт3) КАК ХозрасчетныйДвиженияССубконтоЗБ
	|			ПО ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт1 = СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура
	|				И ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт2 = СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				СУММА(ХозрасчетныйДвиженияССубконто.Сумма) КАК ОстаточнаяСтоимость,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК СубконтоКт3
	|			ИЗ
	|				РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|					,
	|					,
	|					Регистратор = &ТекущийДокумент
	|						И (НЕ СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный))
	|						И (НЕ КоличествоКт = 0)) КАК ХозрасчетныйДвиженияССубконто
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконто.СубконтоКт3) КАК ХозрасчетныйДвиженияССубконто
	|			ПО ХозрасчетныйДвиженияССубконто.СубконтоКт1 = СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура
	|				И ХозрасчетныйДвиженияССубконто.СубконтоКт2 = СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура,
	|		СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Номенклатура,
	|		СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ПартияМатериаловВЭксплуатации,
	|		СУММА(СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Количество),
	|		СУММА(ВложенныйЗапрос.ПервоначальнаяСтоимость),
	|		0
	|	ИЗ
	|		(ВЫБРАТЬ
	|			СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Номенклатура КАК Номенклатура,
	|			СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ПартияМатериаловВЭксплуатации КАК ПартияМатериаловВЭксплуатации,
	|			СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ФизЛицо КАК ФизЛицо,
	|			СУММА(СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Количество) КАК Количество
	|		ИЗ
	|			Документ.СписаниеМатериаловИзЭксплуатации.ИнвентарьИХозяйственныеПринадлежности КАК СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности
	|		ГДЕ
	|			СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Ссылка = &ТекущийДокумент
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Номенклатура,
	|			СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ФизЛицо,
	|			СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ПартияМатериаловВЭксплуатации) КАК СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				СУММА(ХозрасчетныйДвиженияССубконтоЗБ.Сумма) КАК ПервоначальнаяСтоимость,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт1 КАК СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт2 КАК СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт3 КАК СубконтоКт3
	|			ИЗ
	|				РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|					,
	|					,
	|					Регистратор = &ТекущийДокумент
	|						И СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежностиВЭксплуатации)) КАК ХозрасчетныйДвиженияССубконтоЗБ
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт1,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт2,
	|				ХозрасчетныйДвиженияССубконтоЗБ.СубконтоКт3) КАК ВложенныйЗапрос
	|			ПО ВложенныйЗапрос.СубконтоКт1 = СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Номенклатура
	|				И ВложенныйЗапрос.СубконтоКт2 = СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ПартияМатериаловВЭксплуатации
	|				И ВложенныйЗапрос.СубконтоКт3 = СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ФизЛицо
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Номенклатура,
	|		СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ПартияМатериаловВЭксплуатации) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Номенклатура,
	|	ВЫРАЗИТЬ(ВложенныйЗапрос.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)),
	|	ВложенныйЗапрос.Номенклатура.Код,
	|	ВложенныйЗапрос.Номенклатура.БазоваяЕдиницаИзмерения.Наименование,
	|	ВложенныйЗапрос.Номенклатура.БазоваяЕдиницаИзмерения.Код,
	|	ЕСТЬNULL(ВложенныйЗапрос.ПартияМатериаловВЭксплуатации.Дата, НЕОПРЕДЕЛЕНО)
	|";
	
	Если Константы.ДополнительнаяКолонкаПечатныхФормДокументов.Получить() = Перечисления.ДополнительнаяКолонкаПечатныхФормДокументов.Артикул Тогда
		ЗапросПоТоварам.Текст = СтрЗаменить(ЗапросПоТоварам.Текст, "Номенклатура.Код", "Номенклатура.Артикул");
	КонецЕсли;
	
	ВыборкаСтрокТовары = ЗапросПоТоварам.Выполнить().Выгрузить();
	
	ТаблицаСуммСписания = БухгалтерскийУчет.ПолучитьСуммуСписанияАктивов(ЭтотОбъект);
	
	Макет = ПолучитьМакет("МБ8");

	ТабДокумент = Новый ТабличныйДокумент;
	
	// Зададим параметры макета
	ТабДокумент.ПолеСверху         = 0;
	ТабДокумент.ПолеСлева          = 0;
	ТабДокумент.ПолеСнизу          = 0;
	ТабДокумент.ПолеСправа         = 0;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеМатериаловИзЭксплуатации_МБ8";

	// Выводим общие реквизиты шапки
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Шапка.ЮрФизЛицо, Шапка.ДатаСоставления);

	ШапкаТаблицы     = Макет.ПолучитьОбласть("Шапка");
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");	
	Итого            = Макет.ПолучитьОбласть("Итого");
	Подвал           = Макет.ПолучитьОбласть("Подвал");
	
	ШапкаТаблицы.Параметры.Заполнить(Шапка);
	ШапкаТаблицы.Параметры.ПредставлениеОрганизации   = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации);
	ШапкаТаблицы.Параметры.ОрганизацияПоОКПО          = СведенияОбОрганизации.КодПоОКПО;
	ШапкаТаблицы.Параметры.НомерДокумента             = ОбщегоНазначения.ПолучитьНомерНаПечать(Шапка);
	ШапкаТаблицы.Параметры.ПредставлениеПодразделения = Шапка.Подразделение;
	
	ТабДокумент.Вывести(ШапкаТаблицы);

	// Выводим заголовок таблицы
	ТабДокумент.Вывести(ЗаголовокТаблицы);

	// Инициализация номера страницы
	НомерСтраницы = 1;

	// Инициализация итогов в документе
	ИтогоКоличество 				  = 0;
	ИтогоСуммаПервоначальнойСтоимости = 0;
	ИтогоСуммаПогашеннойСтоимости     = 0;
	
	НомерСтроки = 0;

	// Выводим многострочную часть документа
	Строка   = Макет.ПолучитьОбласть("Строка");
	ПоследняяСтрока	= Макет.ПолучитьОбласть("ПоследняяСтрока");
	
	Для Каждого СтрокаТовар Из ВыборкаСтрокТовары Цикл
		
		НомерСтроки = НомерСтроки + 1;

		// Проверим, помещается ли строка с данными и последняя строка (или итоги) на странице.
		СтрокаТаблицы = Новый Массив;
		СтрокаТаблицы.Добавить(Строка);
		Если НЕ (НомерСтроки = ВыборкаСтрокТовары.Количество()) Тогда
			СтрокаТаблицы.Добавить(ПоследняяСтрока);
		Иначе 
			СтрокаТаблицы.Добавить(Итого);
		КонецЕсли;
		
		Если НЕ ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаТаблицы) Тогда

			НомерСтраницы = НомерСтраницы + 1;
			
			ТабДокумент.Вывести(ПоследняяСтрока);
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
			ТабДокумент.Вывести(ЗаголовокТаблицы);
			
		КонецЕсли;

		Строка.Параметры.Заполнить(СтрокаТовар);

		Строка.Параметры.ТоварНаименование        = СокрЛП(СтрокаТовар.ТоварНаименование);
		Строка.Параметры.Цена                     = ?(СтрокаТовар.Количество = 0, 0, СтрокаТовар.СуммаПервоначальнойСтоимости / СтрокаТовар.Количество);
        Строка.Параметры.СуммаПогашеннойСтоимости = СтрокаТовар.СуммаПервоначальнойСтоимости - СтрокаТовар.СуммаОстаточнойСтоимости;

		Если НЕ (Шапка.ДатаДокумента = Неопределено) И НЕ (СтрокаТовар.ДатаВводаВЭксплуатацию = Неопределено) Тогда
		
			КоличествоЛет     = Год(Шапка.ДатаДокумента) - Год(СтрокаТовар.ДатаВводаВЭксплуатацию);
			КоличествоМесяцев = Месяц(Шапка.ДатаДокумента) - Месяц(СтрокаТовар.ДатаВводаВЭксплуатацию);
			
			Строка.Параметры.СрокСлужбы = Строка(КоличествоЛет * 12 + КоличествоМесяцев) + " мес.";
			
		КонецЕсли;
		
		ТабДокумент.Вывести(Строка);

		ИтогоКоличество                   = ИтогоКоличество + Строка.Параметры.Количество;
		ИтогоСуммаПервоначальнойСтоимости = ИтогоСуммаПервоначальнойСтоимости + Строка.Параметры.СуммаПервоначальнойСтоимости;
		ИтогоСуммаПогашеннойСтоимости     = ИтогоСуммаПогашеннойСтоимости + Строка.Параметры.СуммаПогашеннойСтоимости;

	КонецЦикла;

	// Выводим итоги по документу
	Итого.Параметры.ИтогоСуммаПервоначальнойСтоимости          = ИтогоСуммаПервоначальнойСтоимости;
	Итого.Параметры.ИтогоСуммаПогашеннойСтоимости              = ИтогоСуммаПогашеннойСтоимости;
	Итого.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ЧислоПрописью(ВыборкаСтрокТовары.Итог("Количество"), ,",,,,,,,,0");
	ТабДокумент.Вывести(Итого);
	
	// Проверим, помещается ли подвал
	Если НЕ ФормированиеПечатныхФорм.ПроверитьВыводТабличногоДокумента(ТабДокумент, Подвал) Тогда
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	// Выводим подвал
	Подвал.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(Подвал);

	Возврат ТабДокумент;

КонецФункции // ПечатьМБ8()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь, НепосредственнаяПечать = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	Если      ИмяМакета = "МБ8" Тогда
		
		// Унифицированная форма М-15 (накладная).
		ТабДокумент = ПечатьМБ8();
		
	КонецЕсли;
	
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект), НепосредственнаяПечать);
	
КонецПроцедуры // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("МБ8", "МБ-8 (Акт на списание)");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

// Процедура проверяет правильность заполнения реквизитов документа
//
Функция ПроверкаРеквизитов(Отказ, Заголовок) Экспорт

	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	// Документ должен принадлежать хотя бы к одному виду учета (управленческий, бухгалтерский, налоговый)
	ОбщегоНазначения.ПроверитьПринадлежностьКВидамУчета(СтруктураШапкиДокумента, Отказ, Заголовок);

	РеквизитыШапки = "Организация"
	               + ?(Спецоснастка.Количество() = 0, "", ", ПодразделениеОрганизации")
	               + ?((Спецодежда.Количество() = 0 И Спецоснастка.Количество() = 0), "", ", СпособСписанияРасходов");
	
	Если СпособСписанияРасходов = Перечисления.СпособыСписанияРасходов.ВДебетСчетаУказанногоВДокументе Тогда
		РеквизитыШапки = РеквизитыШапки + ", СчетДт";
	КонецЕсли;

	РеквизитыТЧСпецодежда   = "Номенклатура, Количество, ФизЛицо, ПартияМатериаловВЭксплуатации, СчетПередачи";
	РеквизитыТЧСпецоснастка = "Номенклатура, Количество, ПартияМатериаловВЭксплуатации, СчетПередачи";
	
	УправлениеПроизводством.НепроверятьРеквизитыПоТипуУчета(ЭтотОбъект, РеквизитыШапки, , , "");
	УправлениеПроизводством.НепроверятьРеквизитыПоТипуУчета(ЭтотОбъект, РеквизитыТЧСпецодежда, , , "", "Спецодежда");
	УправлениеПроизводством.НепроверятьРеквизитыПоТипуУчета(ЭтотОбъект, РеквизитыТЧСпецоснастка, , , "", "Спецоснастка");

	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект, Новый Структура(РеквизитыШапки), Отказ, Заголовок);
	
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Спецодежда", Новый Структура(РеквизитыТЧСпецодежда), Отказ, Заголовок);
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Спецоснастка", Новый Структура(РеквизитыТЧСпецоснастка), Отказ, Заголовок);	
	
	Возврат СтруктураШапкиДокумента;

КонецФункции // ПроверкаРеквизитов()

// Процедура заполняет счета в строке табличной части документа.
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабЧасти(СтрокаТЧ) Экспорт

	МетаданныеДокумента = Метаданные();
	ИмяТабличнойЧасти = ОбщегоНазначения.ПолучитьИмяТабличнойЧастиПоСсылкеНаСтроку(СтрокаТЧ);
	
	Склад = Неопределено;
	
	Если ЗначениеЗаполнено(СтрокаТЧ.ПартияМатериаловВЭксплуатации) Тогда
		
		Если ТипЗнч(СтрокаТЧ.ПартияМатериаловВЭксплуатации) = Тип("ДокументСсылка.ПередачаМатериаловВЭксплуатацию") Тогда
		
			Склад = СтрокаТЧ.ПартияМатериаловВЭксплуатации.Склад;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СчетаУчета = БухгалтерскийУчет.ПолучитьСчетаУчетаНоменклатуры(Организация, СтрокаТЧ.Номенклатура, Склад);

	Если ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СчетПередачи", МетаданныеДокумента, ИмяТабличнойЧасти) Тогда
		СтрокаТЧ.СчетПередачи = СчетаУчета.СчетПередачи;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСчетаУчетаВСтрокеТабЧасти()

// Заполняет счета в табличной части документа.
//
Процедура ЗаполнитьСчетаУчетаВТабЧасти(ТабличнаяЧасть) Экспорт

	Для Каждого СтрокаТабЧасти Из ТабличнаяЧасть Цикл
		ЗаполнитьСчетаУчетаВСтрокеТабЧасти(СтрокаТабЧасти)
	КонецЦикла;

КонецПроцедуры // ЗаполнитьСчетаУчетаВТабЧасти()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// По результату запроса по шапке документа формируем движения по регистрам.
//
// Параметры: 
//  РежимПроведения           - режим проведения документа (оперативный или неоперативный),
//  СтруктураШапкиДокумента   - выборка из результата запроса по шапке документа,
//  ТаблицаПоТоварам          - таблица значений, содержащая данные для проведения и проверки ТЧ Товары
//  ТаблицаПоТаре             - таблица значений, содержащая данные для проведения и проверки ТЧ "Возвратная тара",
//  Отказ                     - флаг отказа в проведении,
//  Заголовок                 - строка, заголовок сообщения об ошибке проведения.
//
Процедура ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоМатериалам, Отказ, Заголовок)

	УСН = НалоговыйУчетУСН.ПрименениеУСН(Организация, Дата);
	
	ТаблицаПоМатериаламРегл = ТаблицаПоМатериалам.Скопировать();				
					
	ДвиженияПоРегистрамБухРегл(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоМатериаламРегл, Отказ, Заголовок);
	
КонецПроцедуры // ДвиженияПоРегистрам()

// Формирование движений по регистрам бухгалтерского учета (регламентированный учет).
//
Процедура ДвиженияПоРегистрамБухРегл(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоМатериалам, Отказ, Заголовок)
	
	Проводки = Движения.Хозрасчетный;
	
	ТЗДанныеМатериала = Новый ТаблицаЗначений;
	ТЗДанныеМатериала.Колонки.Добавить("ОбъектУчета");
	ТЗДанныеМатериала.Колонки.Добавить("ДокументПередачи");
	ТЗДанныеМатериала.Колонки.Добавить("ПодразделениеФизЛицо");
	ТЗДанныеМатериала.Колонки.Добавить("Сумма");
	ТЗДанныеМатериала.Колонки.Добавить("СуммаНУ");
	ТЗДанныеМатериала.Колонки.Добавить("СуммаПР");
	ТЗДанныеМатериала.Колонки.Добавить("СуммаВР");
	ТЗДанныеМатериала.Колонки.Добавить("Количество");
	ТЗДанныеМатериала.Колонки.Добавить("КоличествоМЦ");
	ТЗДанныеМатериала.Колонки.Добавить("ПодразделениеОрганизации");
	ТЗДанныеМатериала.Колонки.Добавить("НаправлениеАмортизации");
	ТЗДанныеМатериала.Колонки.Добавить("СчетАмортизации");
	ТЗДанныеМатериала.Колонки.Добавить("Субконто1");
	ТЗДанныеМатериала.Колонки.Добавить("Субконто2");
	ТЗДанныеМатериала.Колонки.Добавить("Субконто3");
	
	// Добавляем колонку "Сумма.." для хранения списываемых сумм.
	Если ТаблицаПоМатериалам.Колонки.Найти("Сумма") = Неопределено Тогда
		ТаблицаПоМатериалам.Колонки.Добавить("Сумма");
	КонецЕсли;
	
	Если ТаблицаПоМатериалам.Колонки.Найти("СуммаПР") = Неопределено Тогда
		ТаблицаПоМатериалам.Колонки.Добавить("СуммаПР");
	КонецЕсли;
	
	Если ТаблицаПоМатериалам.Колонки.Найти("СуммаВР") = Неопределено Тогда
		ТаблицаПоМатериалам.Колонки.Добавить("СуммаВР");
	КонецЕсли;

	// Рассчитываем сумму погашения стоимости за текущий месяц.
	ТабАмортизации  = СпецодеждаИСпецоснастка.РасчетСуммыПогашенияСтоимостиМатериалов(ЭтотОбъект, СтруктураШапкиДокумента.Организация, ТаблицаПоМатериалам,,, Отказ);
		
	ТабАмортизации.Колонки.Номенклатура.Имя  = "ОбъектУчета";
	ТабЗатрат = СпецодеждаИСпецоснастка.ПолучитьРаспределениеПогашенияСтоимости(ЭтотОбъект, Отказ, Заголовок, ТабАмортизации, СтруктураШапкиДокумента);
	
	Для Каждого СтрокаМатериал Из ТаблицаПоМатериалам Цикл
		
		Если СтрокаМатериал.ТабличнаяЧасть = "Спецодежда" ИЛИ СтрокаМатериал.ТабличнаяЧасть = "ИнвентарьИХозяйственныеПринадлежности" Тогда
			НадписьПодразделениеФизЛицо = "Работник";
		ИначеЕсли СтрокаМатериал.ТабличнаяЧасть = "Спецоснастка" Тогда
			НадписьПодразделениеФизЛицо = "Подразделение";
		КонецЕсли;
		
		// Проверяем, достаточный ли остаток материалов учтен на забалансовом счете
		Если СтрокаМатериал.Количество > СтрокаМатериал.КоличествоОстатокЗБ Тогда
		
			ОбщегоНазначения.СообщитьОбОшибке("Бух. учет. Строка: " + СтрокаМатериал.НомерСтроки + "
			                 |Не списано " + СтрокаМатериал.Количество + " " + СтрокаМатериал.Номенклатура.БазоваяЕдиницаИзмерения + 
							 " материала " + СтрокаМатериал.Номенклатура + " со счета " + СтрокаМатериал.СчетЗабалансовый + ".
			                 |", , , СтатусСообщения.Важное);
							 
			Отказ = Истина;
			
			Продолжить;
			
		КонецЕсли;
		
		// Рассчитываем суммы списания (забаланс.)
		ДоляСписанияЗБ = ?(СтрокаМатериал.КоличествоОстатокЗБ = 0, 0, СтрокаМатериал.Количество / СтрокаМатериал.КоличествоОстатокЗБ);
		
		СуммаСписанияЗБ   = СтрокаМатериал.СуммаОстатокЗБ * ДоляСписанияЗБ;
		СуммаСписанияПРЗБ = СтрокаМатериал.СуммаОстатокПРЗБ * ДоляСписанияЗБ;
		СуммаСписанияВРЗБ = СтрокаМатериал.СуммаОстатокВРЗБ * ДоляСписанияЗБ;
						
		// Если стоимость материала не была погашена при передаче в эксплуатацию, списываем его с бух.учета
		Если НЕ (СтрокаМатериал.СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию) Тогда
						  
			// Проверяем, достаточный ли остаток по количеству (бух. учет)
			Если СтрокаМатериал.Количество > СтрокаМатериал.КоличествоОстаток Тогда
			
				ОбщегоНазначения.СообщитьОбОшибке("Бух. учет. Строка: " + СтрокаМатериал.НомерСтроки + "
				                 |Не списано " + СтрокаМатериал.Количество + " " + СтрокаМатериал.Номенклатура.БазоваяЕдиницаИзмерения + 
								 " материала " + СтрокаМатериал.Номенклатура + ", счет учета " + СтрокаМатериал.СчетПередачи + ".
				                 |", , , СтатусСообщения.Важное);
							 
				Отказ = Истина;
			
				Продолжить;
				
			КонецЕсли;
			
			// Рассчитываем суммы списания.
			ДоляСписания = ?(СтрокаМатериал.КоличествоОстаток = 0, 0, СтрокаМатериал.Количество / СтрокаМатериал.КоличествоОстаток);
			
			СтрокаМатериал.Сумма   = СтрокаМатериал.СуммаОстаток * ДоляСписания;
			СтрокаМатериал.СуммаПР = СтрокаМатериал.СуммаОстатокПР * ДоляСписания;
			СтрокаМатериал.СуммаВР = СтрокаМатериал.СуммаОстатокВР * ДоляСписания;
								 
			Если СтруктураШапкиДокумента.СпособСписанияРасходов = Перечисления.СпособыСписанияРасходов.ВДебетСчетаУказанногоВДокументе Тогда
				
				Проводка = Проводки.Добавить();
				
				Проводка.Период      = СтруктураШапкиДокумента.Дата;
				Проводка.Организация = СтруктураШапкиДокумента.Организация;
				Проводка.Содержание  = "Списание " + БухгалтерскийУчет.ПолучитьНазваниеОбъекта(СтрокаМатериал.СчетПередачи);
				Проводка.Сумма       = СтрокаМатериал.Сумма - СтрокаМатериал.СуммаПогашения;
				
				Проводка.СчетДт = СчетДт;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 1, СубконтоДт1);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 2, СубконтоДт2);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетДт, Проводка.СубконтоДт, 3, СубконтоДт3);
				
				Проводка.СчетКт = СтрокаМатериал.СчетПередачи;
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Номенклатура",                  СтрокаМатериал.Номенклатура);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ПартииМатериаловВЭксплуатации", СтрокаМатериал.ПартияМатериаловВЭксплуатации);
				БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3,                               СтрокаМатериал.ПодразделениеФизЛицо);
				Проводка.КоличествоКт    = СтрокаМатериал.Количество;
				
				БухгалтерскийУчет.УстановитьПодразделенияПроводки(
					Проводка, СтруктураШапкиДокумента.ПодразделениеОрганизации, СтруктураШапкиДокумента.ПодразделениеОрганизации);
				
				Проводка.СуммаПРКт = СтрокаМатериал.СуммаПР - СтрокаМатериал.СуммаПогашенияПР;
				Проводка.СуммаПРДт = СтрокаМатериал.СуммаПР - СтрокаМатериал.СуммаПогашенияПР;
				
				Проводка.СуммаВРКт = СтрокаМатериал.СуммаВР - СтрокаМатериал.СуммаПогашенияВР;
				Проводка.СуммаВРДт = СтрокаМатериал.СуммаВР - СтрокаМатериал.СуммаПогашенияВР;
				
			Иначе
				
				НоваяСтрока = ТЗДанныеМатериала.Добавить();
				НоваяСтрока.ОбъектУчета              = СтрокаМатериал.Номенклатура; 
				НоваяСтрока.ДокументПередачи         = СтрокаМатериал.ПартияМатериаловВЭксплуатации; 
				НоваяСтрока.ПодразделениеФизЛицо     = СтрокаМатериал.ПодразделениеФизЛицо; 
				НоваяСтрока.Сумма                    = СтрокаМатериал.Сумма - СтрокаМатериал.СуммаПогашения;
				НоваяСтрока.СуммаНУ                  = 0;
				НоваяСтрока.СуммаПР                  = СтрокаМатериал.СуммаПР - СтрокаМатериал.СуммаПогашенияПР;
				НоваяСтрока.СуммаВР                  = СтрокаМатериал.СуммаВР - СтрокаМатериал.СуммаПогашенияВР;
				НоваяСтрока.Количество               = СтрокаМатериал.Количество;
				НоваяСтрока.КоличествоМЦ             = СтрокаМатериал.Количество;
				НоваяСтрока.ПодразделениеОрганизации = СтруктураШапкиДокумента.ПодразделениеОрганизации;
				НоваяСтрока.НаправлениеАмортизации   = СтрокаМатериал.СпособОтраженияРасходов;
				НоваяСтрока.СчетАмортизации          = СтрокаМатериал.СчетПередачи;
				НоваяСтрока.Субконто1                = СтрокаМатериал.Номенклатура;
				НоваяСтрока.Субконто2                = СтрокаМатериал.ПартияМатериаловВЭксплуатации;
				НоваяСтрока.Субконто3                = СтрокаМатериал.ПодразделениеФизЛицо;
				
			КонецЕсли;
			
		КонецЕсли;
			
		// Списываем стоимость и количество со счета МЦ.
		Проводка = Проводки.Добавить();
		
		Проводка.Период      = СтруктураШапкиДокумента.Дата;
		Проводка.Организация = СтруктураШапкиДокумента.Организация;
		Проводка.Сумма       = СуммаСписанияЗБ;
		
		Если СтрокаМатериал.ТабличнаяЧасть = "Спецодежда" Тогда
			Проводка.СчетКт = ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный;
	    	Проводка.Содержание  = "Списание спецодежды";
		ИначеЕсли СтрокаМатериал.ТабличнаяЧасть = "Спецоснастка" Тогда
			Проводка.СчетКт = ПланыСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный;
			Проводка.Содержание  = "Списание спецоснатки";
		ИначеЕсли СтрокаМатериал.ТабличнаяЧасть = "ИнвентарьИХозяйственныеПринадлежности" Тогда
			Проводка.СчетКт = ПланыСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежностиВЭксплуатации;
			Проводка.Содержание  = "Списание инвентаря";
		КонецЕсли;
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "Номенклатура",                  СтрокаМатериал.Номенклатура);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, "ПартииМатериаловВЭксплуатации", СтрокаМатериал.ПартияМатериаловВЭксплуатации);
		БухгалтерскийУчет.УстановитьСубконто(Проводка.СчетКт, Проводка.СубконтоКт, 3,                               СтрокаМатериал.ПодразделениеФизЛицо);
		Проводка.КоличествоКт    = СтрокаМатериал.Количество;
		
		БухгалтерскийУчет.УстановитьПодразделениеПроводки(Проводка, СтруктураШапкиДокумента.ПодразделениеОрганизации, "Кт");
		
		Если НЕ СтрокаМатериал.ТабличнаяЧасть = "ИнвентарьИХозяйственныеПринадлежности"
		   И НЕ СтрокаМатериал.СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию Тогда
			
			// Отражение первоначальной суммы постоянных и временных разниц.
			Проводка.СуммаПРКт = СуммаСписанияПРЗБ;
			Проводка.СуммаВРКт = СуммаСписанияВРЗБ;
			
		КонецЕсли;
		
	КонецЦикла;

	Если ТЗДанныеМатериала.Количество() > 0 Тогда
	
		ТабЗатрат = СпецодеждаИСпецоснастка.ПолучитьРаспределениеПогашенияСтоимости(ЭтотОбъект, Отказ, Заголовок, ТЗДанныеМатериала, СтруктураШапкиДокумента, "Списание спецодежды (спецоснастки) из эксплуатации");
		
	КонецЕсли;
	
КонецПроцедуры // ДвиженияПоРегистрамБухРегл()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события ОбработкаПроведения
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Проверка ручной корректировки
	Если ОбщегоНазначения.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект) Тогда
		Возврат
	КонецЕсли;

	СтруктураШапкиДокумента = ПроверкаРеквизитов(Отказ, Заголовок);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Описание типов реквизита "ПодразделениеФизЛицо"
	Массив = Новый Массив; 
	Массив.Добавить(Тип("СправочникСсылка.ПодразделенияОрганизаций"));
	Массив.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	ОписаниеТиповПодразделениеФизЛицо = Новый ОписаниеТипов(Массив);
	
	// Описание типов реквизита "ПартияМатериаловВЭксплуатации"
	Массив = Новый Массив; 
	Массив.Добавить(Тип("ДокументСсылка.ПередачаМатериаловВЭксплуатацию"));
	Массив.Добавить(Тип("ДокументСсылка.ПартияМатериаловВЭксплуатации"));
	ОписаниеТиповПартияМатериаловВЭксплуатации = Новый ОписаниеТипов(Массив);
	
	ТаблицаПоМатериалам = Новый ТаблицаЗначений;
	ТаблицаПоМатериалам.Колонки.Добавить("ТабличнаяЧасть", ОбщегоНазначения.ПолучитьОписаниеТиповСтроки(50));
	ТаблицаПоМатериалам.Колонки.Добавить("Ссылка");
	ТаблицаПоМатериалам.Колонки.Добавить("НомерСтроки");
	ТаблицаПоМатериалам.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаПоМатериалам.Колонки.Добавить("СпособПогашенияСтоимости");
	ТаблицаПоМатериалам.Колонки.Добавить("СпособОтраженияРасходов");
	ТаблицаПоМатериалам.Колонки.Добавить("ПартияМатериаловВЭксплуатации", ОписаниеТиповПартияМатериаловВЭксплуатации);
	ТаблицаПоМатериалам.Колонки.Добавить("ПодразделениеФизЛицо", ОписаниеТиповПодразделениеФизЛицо);
	ТаблицаПоМатериалам.Колонки.Добавить("Количество", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,3));
	ТаблицаПоМатериалам.Колонки.Добавить("ПодразделениеОрганизации", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаПоМатериалам.Колонки.Добавить("СчетПередачи");
	ТаблицаПоМатериалам.Колонки.Добавить("СчетЗабалансовый");
	ТаблицаПоМатериалам.Колонки.Добавить("СуммаОстаток", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаПоМатериалам.Колонки.Добавить("СуммаОстатокНУ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаПоМатериалам.Колонки.Добавить("СуммаОстатокПР", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаПоМатериалам.Колонки.Добавить("СуммаОстатокВР", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаПоМатериалам.Колонки.Добавить("КоличествоОстаток", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,3));
	ТаблицаПоМатериалам.Колонки.Добавить("СуммаОстатокЗБ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаПоМатериалам.Колонки.Добавить("СуммаОстатокНУЗБ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаПоМатериалам.Колонки.Добавить("СуммаОстатокПРЗБ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаПоМатериалам.Колонки.Добавить("СуммаОстатокВРЗБ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,2));
	ТаблицаПоМатериалам.Колонки.Добавить("КоличествоОстатокЗБ", ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,3));
	ТаблицапоМатериалам.Индексы.Добавить("ТабличнаяЧасть, НомерСтроки");
	
	// Подготовим таблицу материалов для проведения.
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка",        Ссылка);
	Запрос.УстановитьПараметр("Период",        СтруктураШапкиДокумента.Дата);
	Запрос.УстановитьПараметр("Организация",   СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("Подразделение", СтруктураШапкиДокумента.ПодразделениеОрганизации);
	
	ОграничениеСубконтоСпецодежда = Новый Массив();
	ОграничениеСубконтоСпецодежда.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ОграничениеСубконтоСпецодежда.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации);	
	ОграничениеСубконтоСпецодежда.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
	Запрос.УстановитьПараметр("ОграничениеСубконтоСпецодежда", ОграничениеСубконтоСпецодежда);
	
	ОграничениеСубконтоСпецоснастка = Новый Массив();
	ОграничениеСубконтоСпецоснастка.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	ОграничениеСубконтоСпецоснастка.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПартииМатериаловВЭксплуатации);	
	Запрос.УстановитьПараметр("ОграничениеСубконтоСпецоснастка", ОграничениеСубконтоСпецоснастка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Ссылка,
	|	МИНИМУМ(СписаниеМатериаловИзЭксплуатацииСпецодежда.НомерСтроки) КАК НомерСтроки,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо,
	|	СУММА(СписаниеМатериаловИзЭксплуатацииСпецодежда.Количество) КАК Количество,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.СчетПередачи
	|ПОМЕСТИТЬ
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда
	|ИЗ
	|	Документ.СписаниеМатериаловИзЭксплуатации.Спецодежда КАК СписаниеМатериаловИзЭксплуатацииСпецодежда
	|ГДЕ
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Ссылка,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.СчетПередачи
	|;
	|
    |ВЫБРАТЬ
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка,
	|	МИНИМУМ(СписаниеМатериаловИзЭксплуатацииСпецоснастка.НомерСтроки) КАК НомерСтроки,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации,
	|	СУММА(СписаниеМатериаловИзЭксплуатацииСпецоснастка.Количество) КАК Количество,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.СчетПередачи
	|ПОМЕСТИТЬ
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка
	|ИЗ
	|	Документ.СписаниеМатериаловИзЭксплуатации.Спецоснастка КАК СписаниеМатериаловИзЭксплуатацииСпецоснастка
	|ГДЕ
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.СчетПередачи
	|;
	|
	|ВЫБРАТЬ
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Ссылка,
	|	МИНИМУМ(СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.НомерСтроки) КАК НомерСтроки,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Номенклатура,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ФизЛицо,
	|	СУММА(СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Количество) КАК Количество
	|ПОМЕСТИТЬ
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности
	|ИЗ
	|	Документ.СписаниеМатериаловИзЭксплуатации.ИнвентарьИХозяйственныеПринадлежности КАК СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности
	|ГДЕ
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Ссылка,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Номенклатура,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ФизЛицо
	|;
	|
	|ВЫБРАТЬ
	|	""Спецодежда""                                                                             КАК ТабличнаяЧасть,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Ссылка                                          КАК Ссылка,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.НомерСтроки                                     КАК НомерСтроки,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура                                    КАК Номенклатура,
	|	ПередачаМатериаловВЭксплуатациюСпецодежда.НазначениеИспользования.СпособПогашенияСтоимости КАК СпособПогашенияСтоимости,
	|	ПередачаМатериаловВЭксплуатациюСпецодежда.НазначениеИспользования.СпособОтраженияРасходов  КАК СпособОтраженияРасходов,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации                   КАК ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо                                         КАК ПодразделениеФизЛицо,
	|	ЕСТЬNULL(СписаниеМатериаловИзЭксплуатацииСпецодежда.Количество, 0)                         КАК Количество,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.СчетПередачи                                    КАК СчетПередачи,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный)                   КАК СчетЗабалансовый,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0)                                            КАК СуммаОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаНУОстатокДт, 0)                                          КАК СуммаОстатокНУ,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаПРОстатокДт, 0)                                          КАК СуммаОстатокПР,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаВРОстатокДт, 0)                                          КАК СуммаОстатокВР,
	|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0)                                       КАК КоличествоОстаток,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаОстатокДт, 0)                                          КАК СуммаОстатокЗБ,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаНУОстатокДт, 0)                                        КАК СуммаОстатокНУЗБ,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаПРОстатокДт, 0)                                        КАК СуммаОстатокПРЗБ,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаВРОстатокДт, 0)                                        КАК СуммаОстатокВРЗБ,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.КоличествоОстатокДт, 0)                                     КАК КоличествоОстатокЗБ
	|ИЗ
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаМатериаловВЭксплуатацию.Спецодежда КАК ПередачаМатериаловВЭксплуатациюСпецодежда
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура                  = ПередачаМатериаловВЭксплуатациюСпецодежда.Номенклатура
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации = ПередачаМатериаловВЭксплуатациюСпецодежда.Ссылка
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо                       = ПередачаМатериаловВЭксплуатациюСпецодежда.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет В (ВЫБРАТЬ РАЗЛИЧНЫЕ СчетПередачи ИЗ Документ.СписаниеМатериаловИзЭксплуатации.Спецодежда ГДЕ Ссылка = &Ссылка), &ОграничениеСубконтоСпецодежда, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию) КАК ХозрасчетныйОстатки
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецодежда.СчетПередачи                  = ХозрасчетныйОстатки.Счет
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура                  = ХозрасчетныйОстатки.Субконто1
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации = ХозрасчетныйОстатки.Субконто2
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо                       = ХозрасчетныйОстатки.Субконто3
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный), &ОграничениеСубконтоСпецодежда, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию) КАК ХозрасчетныйОстаткиЗБ
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура                  = ХозрасчетныйОстаткиЗБ.Субконто1
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации = ХозрасчетныйОстаткиЗБ.Субконто2
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо                       = ХозрасчетныйОстаткиЗБ.Субконто3
	|ГДЕ
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Ссылка = &Ссылка
	|	И СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию
	|ДЛЯ ИЗМЕНЕНИЯ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Спецодежда"",
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Ссылка,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.НомерСтроки,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации.НазначениеИспользования.СпособПогашенияСтоимости,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации.НазначениеИспользования.СпособОтраженияРасходов,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо,
	|	ЕСТЬNULL(СписаниеМатериаловИзЭксплуатацииСпецодежда.Количество, 0),
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.СчетПередачи,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаНУОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаПРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаВРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаНУОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаПРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаВРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.КоличествоОстатокДт, 0)
	|ИЗ
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет В (ВЫБРАТЬ РАЗЛИЧНЫЕ СчетПередачи ИЗ Документ.СписаниеМатериаловИзЭксплуатации.Спецодежда ГДЕ Ссылка = &Ссылка), &ОграничениеСубконтоСпецодежда, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПартияМатериаловВЭксплуатации) КАК ХозрасчетныйОстатки
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецодежда.СчетПередачи                  = ХозрасчетныйОстатки.Счет
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура                  = ХозрасчетныйОстатки.Субконто1
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации = ХозрасчетныйОстатки.Субконто2
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо                       = ХозрасчетныйОстатки.Субконто3
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный), &ОграничениеСубконтоСпецодежда, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПартияМатериаловВЭксплуатации) КАК ХозрасчетныйОстаткиЗБ
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецодежда.Номенклатура                  = ХозрасчетныйОстаткиЗБ.Субконто1
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации = ХозрасчетныйОстаткиЗБ.Субконто2
	|		 И СписаниеМатериаловИзЭксплуатацииСпецодежда.ФизЛицо                       = ХозрасчетныйОстаткиЗБ.Субконто3
	|ГДЕ
	|	СписаниеМатериаловИзЭксплуатацииСпецодежда.Ссылка = &Ссылка
	|	И СписаниеМатериаловИзЭксплуатацииСпецодежда.ПартияМатериаловВЭксплуатации ССЫЛКА Документ.ПартияМатериаловВЭксплуатации
	|ДЛЯ ИЗМЕНЕНИЯ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Спецоснастка"",
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.НомерСтроки,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура,
	|	ПередачаМатериаловВЭксплуатациюСпецоснастка.НазначениеИспользования.СпособПогашенияСтоимости,
	|	ПередачаМатериаловВЭксплуатациюСпецоснастка.НазначениеИспользования.СпособОтраженияРасходов,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка.ПодразделениеОрганизации,
	|	ЕСТЬNULL(СписаниеМатериаловИзЭксплуатацииСпецоснастка.Количество, 0),
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.СчетПередачи,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаНУОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаПРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаВРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаНУОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаПРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаВРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.КоличествоОстатокДт, 0)
	|ИЗ
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаМатериаловВЭксплуатацию.Спецоснастка КАК ПередачаМатериаловВЭксплуатациюСпецоснастка
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура                    = ПередачаМатериаловВЭксплуатациюСпецоснастка.Номенклатура
	|		 И СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации   = ПередачаМатериаловВЭксплуатациюСпецоснастка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет В (ВЫБРАТЬ РАЗЛИЧНЫЕ СчетПередачи ИЗ Документ.СписаниеМатериаловИзЭксплуатации.Спецоснастка ГДЕ Ссылка = &Ссылка), &ОграничениеСубконтоСпецоснастка, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию) КАК ХозрасчетныйОстатки
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецоснастка.СчетПередачи                    = ХозрасчетныйОстатки.Счет
	|		 И СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура                    = ХозрасчетныйОстатки.Субконто1
	|		 И СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации   = ХозрасчетныйОстатки.Субконто2
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный), &ОграничениеСубконтоСпецоснастка, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию) КАК ХозрасчетныйОстаткиЗБ
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура                    = ХозрасчетныйОстаткиЗБ.Субконто1
	|		 И СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации   = ХозрасчетныйОстаткиЗБ.Субконто2
	|ГДЕ
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка = &Ссылка
	|	И СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации ССЫЛКА Документ.ПередачаМатериаловВЭксплуатацию
	|ДЛЯ ИЗМЕНЕНИЯ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Спецоснастка"",
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.НомерСтроки,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации.НазначениеИспользования.СпособПогашенияСтоимости,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации.НазначениеИспользования.СпособОтраженияРасходов,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка.ПодразделениеОрганизации,
	|	ЕСТЬNULL(СписаниеМатериаловИзЭксплуатацииСпецоснастка.Количество, 0),
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.СчетПередачи,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаНУОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаПРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.СуммаВРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстатки.КоличествоОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаНУОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаПРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаВРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.КоличествоОстатокДт, 0)
	|ИЗ
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет В (ВЫБРАТЬ РАЗЛИЧНЫЕ СчетПередачи ИЗ Документ.СписаниеМатериаловИзЭксплуатации.Спецоснастка ГДЕ Ссылка = &Ссылка), &ОграничениеСубконтоСпецоснастка, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПартияМатериаловВЭксплуатации) КАК ХозрасчетныйОстатки
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецоснастка.СчетПередачи                    = ХозрасчетныйОстатки.Счет
	|		 И СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура                    = ХозрасчетныйОстатки.Субконто1
	|		 И СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации   = ХозрасчетныйОстатки.Субконто2
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный), &ОграничениеСубконтоСпецоснастка, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто2 ССЫЛКА Документ.ПартияМатериаловВЭксплуатации) КАК ХозрасчетныйОстаткиЗБ
	|		ПО СписаниеМатериаловИзЭксплуатацииСпецоснастка.Номенклатура                    = ХозрасчетныйОстаткиЗБ.Субконто1
	|		 И СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации   = ХозрасчетныйОстаткиЗБ.Субконто2
	|ГДЕ
	|	СписаниеМатериаловИзЭксплуатацииСпецоснастка.Ссылка = &Ссылка
	|	И СписаниеМатериаловИзЭксплуатацииСпецоснастка.ПартияМатериаловВЭксплуатации ССЫЛКА Документ.ПартияМатериаловВЭксплуатации
	|ДЛЯ ИЗМЕНЕНИЯ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ИнвентарьИХозяйственныеПринадлежности"",
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Ссылка,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.НомерСтроки,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Номенклатура,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию),
	|	НЕОПРЕДЕЛЕНО,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ПартияМатериаловВЭксплуатации,
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ФизЛицо,
	|	ЕСТЬNULL(СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Количество, 0),
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежностиВЭксплуатации),
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаНУОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаПРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.СуммаВРОстатокДт, 0),
	|	ЕСТЬNULL(ХозрасчетныйОстаткиЗБ.КоличествоОстатокДт, 0)
	|ИЗ
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(&Период, Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ИнвентарьИХозяйственныеПринадлежностиВЭксплуатации), &ОграничениеСубконтоСпецодежда, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL)) КАК ХозрасчетныйОстаткиЗБ
	|		ПО СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Номенклатура                  = ХозрасчетныйОстаткиЗБ.Субконто1
	|		 И СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ПартияМатериаловВЭксплуатации = ХозрасчетныйОстаткиЗБ.Субконто2
	|		 И СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.ФизЛицо                       = ХозрасчетныйОстаткиЗБ.Субконто3
	|ГДЕ
	|	СписаниеМатериаловИзЭксплуатацииИнвентарьИХозяйственныеПринадлежности.Ссылка = &Ссылка
	|ДЛЯ ИЗМЕНЕНИЯ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки
	|";
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из РезультатЗапроса Цикл
		НоваяСтрока = ТаблицаПоМатериалам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	
	// Заполнение подразделения.
	ТаблицаПоМатериалам.ЗаполнитьЗначения(СтруктураШапкиДокумента.ПодразделениеОрганизации, "ПодразделениеОрганизации");
	
	// Движения по документу.
	Если НЕ Отказ Тогда
		ДвиженияПоРегистрам(РежимПроведения, СтруктураШапкиДокумента, ТаблицаПоМатериалам, Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры	// ОбработкаПроведения()

// Процедура - обработчик события ОбработкаЗаполнения
//
Процедура ОбработкаЗаполнения(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.ПередачаМатериаловВЭксплуатацию") Тогда
		
		// Заполнение шапки
		Организация                  = Основание.Организация;
		ПодразделениеОрганизации     = Основание.Местонахождение;
		Комментарий                  = Основание.Комментарий;
		Ответственный                = Основание.Ответственный;
		Склад                        = Основание.Склад;
		
		// Заполнение табличной части "Спецодежда"
		Для Каждого ТекСтрокаСпецодежда Из Основание.Спецодежда Цикл
			
			НоваяСтрока = Спецодежда.Добавить();
			
			НоваяСтрока.Номенклатура                  = ТекСтрокаСпецодежда.Номенклатура;
			НоваяСтрока.ФизЛицо 				      = ТекСтрокаСпецодежда.ФизЛицо;
			НоваяСтрока.ПартияМатериаловВЭксплуатации = Основание;
			НоваяСтрока.Количество                    = ТекСтрокаСпецодежда.Количество;
			Если ТекСтрокаСпецодежда.НазначениеИспользования.СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию Тогда
				НоваяСтрока.СчетПередачи            = ПланыСчетов.Хозрасчетный.СпецодеждаВЭксплуатацииВспомогательный;
			Иначе
				НоваяСтрока.СчетПередачи            = ТекСтрокаСпецодежда.СчетПередачи;
			КонецЕсли;
			
		КонецЦикла;
		
		// Заполнение табличной части "Спецоснастка"
		Для Каждого ТекСтрокаСпецоснастка Из Основание.Спецоснастка Цикл
			
			НоваяСтрока = Спецоснастка.Добавить();
			
			НоваяСтрока.Номенклатура                  = ТекСтрокаСпецоснастка.Номенклатура;
			НоваяСтрока.ПартияМатериаловВЭксплуатации = Основание;
			НоваяСтрока.Количество                    = ТекСтрокаСпецоснастка.Количество;
			Если ТекСтрокаСпецоснастка.НазначениеИспользования.СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию Тогда
				НоваяСтрока.СчетПередачи            = ПланыСчетов.Хозрасчетный.СпецоснасткаВЭксплуатацииВспомогательный;
			Иначе
				НоваяСтрока.СчетПередачи            = ТекСтрокаСпецоснастка.СчетПередачи;
			КонецЕсли;
			
		КонецЦикла;
		
		// Заполнение табличной части "Инвентарь и хозяйственные принадлежности"
		Для Каждого ТекСтрокаИнвентарьИХозяйственныеПринадлежности Из Основание.ИнвентарьИХозяйственныеПринадлежности Цикл
			
			НоваяСтрока = ИнвентарьИХозяйственныеПринадлежности.Добавить();
			
			НоваяСтрока.Номенклатура                  = ТекСтрокаИнвентарьИХозяйственныеПринадлежности.Номенклатура;
			НоваяСтрока.ПартияМатериаловВЭксплуатации = Основание;
			НоваяСтрока.ФизЛицо 				      = ТекСтрокаИнвентарьИХозяйственныеПринадлежности.ФизЛицо;
			НоваяСтрока.Количество                    = ТекСтрокаИнвентарьИХозяйственныеПринадлежности.Количество;
			
		КонецЦикла;
		
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ОбработкаУдаленияПроведения(Отказ)
		
	ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ, РучнаяКорректировка, Ложь);

КонецПроцедуры

