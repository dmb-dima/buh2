////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента; 

// Хранит валюту взаиморасчетов, установленную в текущем договоре взаиморасчетов,
// используется для определения необходимости пересчетов при изменении договора.
Перем мТекущаяВалютаВзаиморасчетов; 

// {ОбособленныеПодразделения
// Хранит текущее подразделение организации, используется для определения необходимости 
// установки нового номера документа, если документы по подразделению нумеруются отдельно.
Перем мТекущееПодразделениеОрганизации;
// }ОбособленныеПодразделения

// Хранит текущую форму подбора номенклатуры
Перем мФормаПодбораНоменклатуры; 

// Хранит список видов договоров, доступных для данного документа
Перем мСписокВидовДоговоров; 

// Хранит дерево макетов печатных форм
Перем мДеревоМакетов;

// Хранит элемент управления подменю печати
Перем мПодменюПечати;

// Хранит элемент управления кнопку печать по умолчанию
Перем мПечатьПоУмолчанию;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

//Используется для определения необходимости открытия настроек движения после сохранения
Перем ОткрыватьНастройкуДвижений;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ 

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.Товары,ЭлементыФормы.КоманднаяПанельТовары.Кнопки.ПодменюЗаполнить);
	СоответствиеТЧ.Вставить(ЭлементыФормы.ДенежныеСредства,ЭлементыФормы.КоманднаяПанельДенежныеСредства);
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧ(Ссылка, СоответствиеТЧ, Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
КонецПроцедуры

// Процедура устанавливает подменю "Печать" и кнопку "Печать по умолчанию" при необходимости
//
Процедура УстановитьКнопкиПечати()
	
	мДеревоМакетов = УниверсальныеМеханизмы.ПолучитьДеревоМакетовПечати(Ссылка, ПолучитьСтруктуруПечатныхФорм(), Новый Действие("ОсновныеДействияФормыПечать"), Новый Действие("ОсновныеДействияФормыУстановитьПечатьПоУмолчанию"));

	УниверсальныеМеханизмы.УстановитьПодменюПечати    (мПодменюПечати, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов.Строки.Количество() > 0);
	УниверсальныеМеханизмы.УстановитьПечатьПоУмолчанию(мПечатьПоУмолчанию, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов, Метаданные().Имя,Новый Действие("ОсновныеДействияФормыПечатьПоУмолчанию"));

	Если Не мПодменюПечати = Неопределено Тогда
		УниверсальныеМеханизмы.СформироватьПодменю(мДеревоМакетов, мПодменюПечати,Истина,Истина);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет текст про счет-фактуру в форме документа.
//   Вызывается из обработчика ПриОткрытии этой формы и из обработчика ПослеЗаписи
// формы счета-фактуры.
//
// Параметры:
//  Нет.
//
Процедура ЗаполнитьТекстПроСчетФактуру() Экспорт

	ЭлементыФормы.ГиперссылкаСчетФактура.Заголовок = РаботаСДиалогами.ПолучитьТекстСчетаФактуры(
													 УчетНДС.НайтиПодчиненныйСчетФактуру(Ссылка, "СчетФактураВыданный"));

КонецПроцедуры // ЗаполнитьТекстПроСчетФактуру()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура выполняет заполнение реквизитов связанных с комиссионным вознаграждением.
//
Процедура ЗаполнитьСпособРасчетаКомиссионногоВознаграждения()

	Если НЕ ЗначениеЗаполнено(СпособРасчетаКомиссионногоВознаграждения) Тогда
		СпособРасчетаКомиссионногоВознаграждения = ДоговорКонтрагента.СпособРасчетаКомиссионногоВознаграждения;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПроцентКомиссионногоВознаграждения) Тогда
		ПроцентКомиссионногоВознаграждения = ДоговорКонтрагента.ПроцентКомиссионногоВознаграждения;
	КонецЕсли;

КонецПроцедуры // ЗаполнитьСпособРасчетаКомиссионногоВознаграждения()

// Рассчитывает комиссионное вознаграждение в строке табличной части документа
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа,
//
Процедура РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти)

	Если СпособРасчетаКомиссионногоВознаграждения
		 = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается Тогда

	ИначеЕсли СпособРасчетаКомиссионногоВознаграждения
		 = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.ПроцентОтРазностиСуммПродажиИПоступления Тогда

		СтрокаТабличнойЧасти.СуммаВознаграждения = ПроцентКомиссионногоВознаграждения / 100
			* (СтрокаТабличнойЧасти.Сумма + ?(СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС) - СтрокаТабличнойЧасти.СуммаПоступления);

	ИначеЕсли СпособРасчетаКомиссионногоВознаграждения
		 = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.ПроцентОтСуммыПродажи Тогда

		СтрокаТабличнойЧасти.СуммаВознаграждения = ПроцентКомиссионногоВознаграждения / 100
		    * (СтрокаТабличнойЧасти.Сумма + ?(СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС));

	Иначе
		СтрокаТабличнойЧасти.СуммаВознаграждения = 0;
	КонецЕсли;

	СтрокаТабличнойЧасти.СуммаВознаграждения = Ценообразование.ПересчитатьЦенуПриИзмененииФлаговНалогов(СтрокаТабличнойЧасти.СуммаВознаграждения, Неопределено, Истина, УчитыватьНДС, 
											СуммаВключаетНДС, УчетНДС.ПолучитьСтавкуНДС(СтавкаНДСВознаграждения));
	
	СтрокаТабличнойЧасти.СуммаНДСВознаграждения = УчетНДС.РассчитатьСуммуНДС(СтрокаТабличнойЧасти.СуммаВознаграждения,
	        УчитыватьНДС, СуммаВключаетНДС, УчетНДС.ПолучитьСтавкуНДС(СтавкаНДСВознаграждения));
	
КонецПроцедуры // РассчитатьКомиссионноеВознаграждение()

// Процедура выполняет необходимые действия при изменении договора взаиморасчетов
// с контрагентом.
//
Процедура ПриИзмененииДоговора()

	Если НЕ ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Возврат;
	КонецЕсли;


	СпособЗаполненияЦен = Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатурыКонтрагентов;

	// Зададим, какие реквизиты надо редактировать.
	СтруктураРеквизитовДокумента = Ценообразование.ПолучитьСтруктуруРеквизитовДокументаДляЦенообразования(ЭтотОбъект);

	СтараяСумма            = Товары.Итог("Сумма");
	СтараяСуммаПоступления = Товары.Итог("СуммаПоступления");

	ЗаполнениеДокументов.ПриИзмененииЗначенияДоговора(ЭтотОбъект, мТекущаяВалютаВзаиморасчетов, мВалютаРегламентированногоУчета, СпособЗаполненияЦен, СтруктураРеквизитовДокумента, "Покупка", "Товары", ДоговорКонтрагента);

	НоваяСумма            = Товары.Итог("Сумма");
	НоваяСуммаПоступления = Товары.Итог("СуммаПоступления");

	СпособРасчетаКомиссионногоВознаграждения = ДоговорКонтрагента.СпособРасчетаКомиссионногоВознаграждения;
	ПроцентКомиссионногоВознаграждения       = ДоговорКонтрагента.ПроцентКомиссионногоВознаграждения;

	Если СпособРасчетаКомиссионногоВознаграждения
		<> Перечисления.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается Тогда

		// Если в табличной части отсутствуют строки - пересчитывать ничего не нужно.
		Если (НоваяСумма <> СтараяСумма ИЛИ НоваяСуммаПоступления <> СтараяСуммаПоступления)
		   И Товары.Количество() > 0 Тогда

			// Надо предложить пересчитать комиссионное вознаграждение.
			Ответ = Вопрос("Пересчитать комиссионное вознаграждение?", 
			               РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Да Тогда

				Для каждого СтрокаТабличнойЧасти Из Товары Цикл
					РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);
				КонецЦикла;

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	мТекущаяВалютаВзаиморасчетов = ДоговорКонтрагента.ВалютаВзаиморасчетов;

	УстановитьАвтоотметку();
	
	СчетаУчета = БухгалтерскийУчетРасчетовСКонтрагентами.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
		
	//Проверим какие же счета будем изменять
	ЗаполнятьСчетаРасчета = Ложь; //Заполнять ли счета расчетов
	ЗадаватьВопрос	   	  = Ложь; //Если заполнять то Задавать ли вопрос о его перезаполнении 
		
	Если ПоказыватьВДокументахСчетаУчета Тогда
			
		СтруктураОбязательныхПолей = Новый Структура;
		СтруктураНеОбязательныхПолей = Новый Структура;
		СтруктураОбязательныхПолей.Вставить("СчетУчетаРасчетовСКонтрагентом", СчетаУчета.СчетРасчетовПокупателя);			
		
		Если УдержатьВознаграждение Тогда
			СтруктураНеОбязательныхПолей.Вставить("СчетУчетаРасчетовПоАвансам", СчетаУчета.СчетРасчетовСКомитентом);
		Иначе
			СтруктураНеОбязательныхПолей.Вставить("СчетУчетаРасчетовПоАвансам", СчетаУчета.СчетАвансовПокупателя);
		КонецЕсли;
		
		БухгалтерскийУчет.ПроверитьНеобходимостьПерезаполненияСчетовУчета(ЭтотОбъект, СтруктураОбязательныхПолей,СтруктураНеОбязательныхПолей, ЗадаватьВопрос, ЗаполнятьСчетаРасчета);		
		
		Если ЗадаватьВопрос  Тогда
			
			Ответ = Вопрос("Установить счета учета расчетов в соответствии со значениями по умолчанию?", 
			РежимДиалогаВопрос.ДаНет, , , "Изменения счетов учета при смене договора");
			
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				ЗаполнятьСчетаРасчета = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ЗаполнятьСчетаРасчета     = Истина;
		
	КонецЕсли;
	



	Если ЗаполнятьСчетаРасчета Тогда

		СчетУчетаРасчетовСКонтрагентом = СчетаУчета.СчетРасчетовПокупателя;
		
		Если УдержатьВознаграждение Тогда
			СчетУчетаРасчетовПоАвансам = СчетаУчета.СчетРасчетовСКомитентом;
		Иначе
			СчетУчетаРасчетовПоАвансам = СчетаУчета.СчетАвансовПокупателя;
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // ПриИзмененииДоговора()

// {ОбособленныеПодразделения
// Проверка изменения подразделения организации. 
// Необходима для корректной нумерации документов в подразделениях.
//
Процедура ПриИзмененииПодразделенияОрганизации()
	
	Если НЕ ПустаяСтрока(Номер) 
	   И РаботаСДиалогами.ИзменилсяПрефиксПодразделенияОрганизации(мТекущееПодразделениеОрганизации, ПодразделениеОрганизации) Тогда
	   
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
		
	КонецЕсли;
	
	мТекущееПодразделениеОрганизации = ПодразделениеОрганизации;
	
КонецПроцедуры // ПриИзмененииПодразделенияОрганизации()
// }ОбособленныеПодразделения

// Функция формирует список запросов для передачи в форму подбора.
//
// Возвращаемое значение:
//  Список значений - список запросов.
//
Функция СформироватьСписокЗапросовДляПодбора()

	СписокЗапросов = Новый СписокЗначений();
	СписокЗапросов.Добавить(,"По справочнику");
	СписокЗапросов.Добавить("ОстаткиТоваровКомитентов", "Остатки товаров комитентов");

	Возврат СписокЗапросов;

КонецФункции // СформироватьСписокЗапросовДляПодбора()

Процедура УстановитьОчиститьСтавкуНДС()
	
	Если мТекущаяДатаДокумента >= '20060101000000' И Дата < '20060101000000' Тогда
		
		Для Каждого СтрокаТабличнойЧасти Из Товары Цикл
			СтрокаТабличнойЧасти.Цена = Ценообразование.ПересчитатьЦенуПриИзмененииФлаговНалогов(СтрокаТабличнойЧасти.Цена, Неопределено, СуммаВключаетНДС,
																				УчитыватьНДС, Истина, УчетНДС.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));
			ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
			СтрокаТабличнойЧасти.СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
			СтрокаТабличнойЧасти.СуммаНДС = 0;
			РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);
		КонецЦикла;
		
	ИначеЕсли мТекущаяДатаДокумента < '20060101000000' И Дата >= '20060101000000' Тогда
		
		Для Каждого СтрокаТабличнойЧасти Из Товары Цикл
			ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
			СтрокаТабличнойЧасти.Цена = Ценообразование.ПересчитатьЦенуПриИзмененииФлаговНалогов(СтрокаТабличнойЧасти.Цена, Неопределено, Истина,
																				УчитыватьНДС, СуммаВключаетНДС, УчетНДС.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));
			ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
			ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
			РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура установки аналитики в зависимости от выбранного счета
//
Процедура УстановитьАналитику(Счет, Субконто)

	Если Счет.ВидыСубконто.Количество() > 0 Тогда
		Если Счет.ВидыСубконто[0].ВидСубконто.ТипЗначения.СодержитТип(ТипЗнч(Субконто)) тогда
			//Не надо менять, значение подходит
		Иначе
			Субконто = Новый(Счет.ВидыСубконто[0].ВидСубконто.ТипЗначения.Типы()[0])
		КонецЕсли;
	Иначе
		Субконто = Неопределено;
	КонецЕсли;

КонецПроцедуры // УстановитьАналитику()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Процедура устанавливает видимость для тех колонок в табличной части 
// "Товары", видимость которых определяется реквизитами документа.
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()

	УСН = НалоговыйУчетУСН.ПрименениеУСН(организация, Дата);
	УСНДоходы = НалоговыйУчетУСН.ПрименениеУСНДоходы(Организация, Дата);
	ОСН_ЮрЛицо = (НЕ ОбщегоНазначения.Предприниматель(Организация, Дата) И НЕ УСН);
	
	Колонки = ЭлементыФормы.Товары.Колонки;
	УчетАгентскогоНДС = ДоговорКонтрагента.УчетАгентскогоНДС;
	
	Колонки.СуммаНДС.Видимость  = мЗаконодательство2006 И УчетАгентскогоНДС;
	Колонки.СтавкаНДС.Видимость = мЗаконодательство2006 И УчетАгентскогоНДС;
	
	ЭлементыФормы.НадписьНДСВБюджет.Видимость = мЗаконодательство2006 И УчетАгентскогоНДС;
	ЭлементыФормы.СуммаНДСВБюджет.Видимость = мЗаконодательство2006 И УчетАгентскогоНДС;
	
	ДоступностьПроцента = Истина;

	Если СпособРасчетаКомиссионногоВознаграждения = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается Тогда

		ДоступностьПроцента = Ложь;

		Если ПроцентКомиссионногоВознаграждения <> 0 Тогда
			ПроцентКомиссионногоВознаграждения = 0;
		КонецЕсли;

	КонецЕсли;

	ЭлементыФормы.ПроцентКомиссионногоВознаграждения.Доступность = ДоступностьПроцента;

	ЭлементыФормы.Субконто.Видимость   = (СчетДоходов.ВидыСубконто.Количество() > 0);
	ЭлементыФормы.НадписьСуб.Видимость = (СчетДоходов.ВидыСубконто.Количество() > 0);

	ЭлементыФормы.ДействияФормы.Кнопки.НастройкаУСН.Доступность = УСН И УСНДоходы;
	
	ЭлементыФормы.ОсновнаяПанель.Страницы.СчетРасчетов.Видимость = ПоказыватьВДокументахСчетаУчета;
	
	Если УдержатьВознаграждение Тогда

		ЭлементыФормы.НадписьСчетРасчетовСКомитентом.Заголовок = "Счет учета расчетов с комитентом:";

	Иначе

		ЭлементыФормы.НадписьСчетРасчетовСКомитентом.Заголовок = "Счет учета расчетов по авансам:";

	КонецЕсли;

КонецПроцедуры // УстановитьВидимость()

// Процедура устанавливает автоотметку незаполненного для тех колонок в табличной части 
// "Товары", обязательность заполнения которых определяется реквизитами документа.
//
// Параметры:
//  Нет.
//
Процедура УстановитьАвтоотметку()

	// Процент и ставка НДС с комиссионного вознаграждения
	Автоотметка = Ложь;
	Если СпособРасчетаКомиссионногоВознаграждения = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.ПроцентОтРазностиСуммПродажиИПоступления
	 ИЛИ СпособРасчетаКомиссионногоВознаграждения = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.ПроцентОтСуммыПродажи Тогда
		Автоотметка = Истина;
	КонецЕсли;

	Если ЭлементыФормы.ПроцентКомиссионногоВознаграждения.АвтоОтметкаНезаполненного <> Автоотметка Тогда
		ЭлементыФормы.ПроцентКомиссионногоВознаграждения.АвтоОтметкаНезаполненного = Автоотметка;
	КонецЕсли;

	Если ЭлементыФормы.ПроцентКомиссионногоВознаграждения.ОтметкаНезаполненного <> Автоотметка И НЕ ЗначениеЗаполнено(ПроцентКомиссионногоВознаграждения) Тогда
		ЭлементыФормы.ПроцентКомиссионногоВознаграждения.ОтметкаНезаполненного = Автоотметка;
	КонецЕсли;

	Если ЭлементыФормы.СтавкаНДСВознаграждения.АвтоОтметкаНезаполненного <> Автоотметка Тогда
		ЭлементыФормы.СтавкаНДСВознаграждения.АвтоОтметкаНезаполненного = Автоотметка;
	КонецЕсли;

	Если ЭлементыФормы.СтавкаНДСВознаграждения.ОтметкаНезаполненного <> Автоотметка И НЕ ЗначениеЗаполнено(СтавкаНДСВознаграждения) Тогда
		ЭлементыФормы.СтавкаНДСВознаграждения.ОтметкаНезаполненного = Автоотметка;
	КонецЕсли;

КонецПроцедуры // УстановитьАвтоотметку()

// Процедура формирует текст в информационной надписи об итогах документа.
//
// Параметры:
//  Нет.
//
Процедура ОбновитьПодвал()

	ЭлементыФормы.Всего.Значение               = ОбщегоНазначения.ФорматСумм(УчетНДС.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "Товары"));
	ЭлементыФормы.СуммаВознаграждения.Значение = ОбщегоНазначения.ФорматСумм(Товары.Итог("СуммаВознаграждения")+ ?(СуммаВключаетНДС,0,Товары.Итог("СуммаНДСВознаграждения")));
	ЭлементыФормы.СуммаНДС.Значение            = ОбщегоНазначения.ФорматСумм(Товары.Итог("СуммаНДСВознаграждения"));
 	ЭлементыФормы.СуммаНДСВБюджет.Значение     = ОбщегоНазначения.ФорматСумм(Товары.Итог("СуммаНДС"));

	Если НЕ ЗначениеЗаполнено(ДокументОбъект.ВалютаДокумента) Тогда
		ЭлементыФормы.НадписьВсего.Заголовок = "Всего реализовано (<>):";
		ЭлементыФормы.НадписьСуммаВознаграждения.Заголовок = "Сумма вознаграждения (<>):";
	Иначе
		ЭлементыФормы.НадписьВсего.Заголовок = "Всего реализовано (" + СокрЛП(ДокументОбъект.ВалютаДокумента) +"):";
		ЭлементыФормы.НадписьСуммаВознаграждения.Заголовок = "Сумма вознаграждения (" + СокрЛП(ДокументОбъект.ВалютаДокумента) +"):";
	КонецЕсли;
	
	Если СуммаВключаетНДС Тогда
		ЭлементыФормы.НадписьНДСВБюджет.Заголовок = "НДС к уплате в бюджет (в т. ч.):";
		ЭлементыФормы.НадписьСуммаНДСВознаграждения.Заголовок = "Сумма НДС (в т. ч.):";
	Иначе
		ЭлементыФормы.НадписьНДСВБюджет.Заголовок = "НДС к уплате в бюджет (сверху):";
		ЭлементыФормы.НадписьСуммаНДСВознаграждения.Заголовок = "Сумма НДС (сверху):";
	КонецЕсли;

КонецПроцедуры // ОбновитьПодвал()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок печати
	УстановитьКнопкиПечати();
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	      	
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"), мВалютаРегламентированногоУчета, "Покупка",ПараметрОбъектКопирования);

		СтавкаНДСВознаграждения = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяСтавкаНДС");
		ЗаполнитьСпособРасчетаКомиссионногоВознаграждения();

		УдержатьВознаграждение = Истина;

		СчетаУчета = БухгалтерскийУчетРасчетовСКонтрагентами.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
		СчетУчетаРасчетовСКонтрагентом = СчетаУчета.СчетРасчетовПокупателя;
			Если УдержатьВознаграждение Тогда
				СчетУчетаРасчетовПоАвансам = СчетаУчета.СчетРасчетовСКомитентом;
			Иначе
				СчетУчетаРасчетовПоАвансам = СчетаУчета.СчетАвансовПокупателя;
			КонецЕсли;

		СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;

		ЭлементыФормы.Товары.Колонки.КоличествоМест.Видимость   = Ложь;
		ЭлементыФормы.Товары.Колонки.ЕдиницаИзмерения.Видимость = Ложь;
		ЭлементыФормы.Товары.Колонки.Коэффициент.Видимость      = Ложь;

	КонецЕсли;
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
    
	//Устанавливает необходимость открытия настроек движения после сохранения
	ОткрыватьНастройкуДвижений = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ПоказыватьДвиженияПриПроведении");

	мЗаконодательство2006 = Дата >= '20060101000000';
	
	СтруктураКолонок = Новый Структура();

	// Установить колонки, видимостью которых пользователь управлять не может.
	СтруктураКолонок.Вставить("ЕдиницаХранения");

	ОбработкаТабличныхЧастей.УстановитьИзменятьВидимостьКолонокТабЧасти(ЭлементыФормы.Товары.Колонки, СтруктураКолонок);

	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);

	ЗаполнитьТекстПроСчетФактуру();
	
	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента        = Дата;
	мТекущаяВалютаВзаиморасчетов = ДоговорКонтрагента.ВалютаВзаиморасчетов;
	// {ОбособленныеПодразделения
	мТекущееПодразделениеОрганизации = ПодразделениеОрганизации;
    // }ОбособленныеПодразделения
	
	// Управление видимостью счетов
	ПоказыватьВДокументахСчетаУчета = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ПоказыватьВДокументахСчетаУчета");
	ЭлементыФормы.ДействияФормы.Кнопки.ПоказатьСкрытьСчетаУчета.Пометка = ПоказыватьВДокументахСчетаУчета;
	
	// Установить видимость реквизитов и заголовков колонок.
	УстановитьВидимость();

	// Установить автоотметку незаполненного для тех реквизитов, обязательность заполнения которых зависит
	// от значений других реквизитов 
	УстановитьАвтоотметку();

	// Установка текущей страницы
	Страницы = Новый СписокЗначений;
	Страницы.Добавить("Товары",	"Товары");
	Страницы.Добавить("ДенежныеСредства",	"ДенежныеСредства");
	РаботаСДиалогами.УстановкаТекущейСтраницы(ЭтотОбъект, ЭлементыФормы.ОсновнаяПанель, Страницы,);

	// Установить активный реквизит.
	СтруктураРеквизитов = Новый Структура("Дата,Организация,ПодразделениеОрганизации,СпособРасчетаКомиссионногоВознаграждения,Контрагент,ДоговорКонтрагента,ПроцентКомиссионногоВознаграждения,СтавкаНДСВознаграждения");
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма,СтруктураРеквизитов,ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница,Страницы);
	
	// Установить доступность формы с учетом даты запрета редактирования	
	РаботаСДиалогами.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, ЭтаФорма);
	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(Ссылка));

КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ПриЗакрытии" формы.
//
Процедура ПриЗакрытии()
			
	ОбщегоНазначения.ОткрытьЖурналДокумента(ЭтаФорма);

КонецПроцедуры

// Процедура - обработчик события "ОбработкаОповещения" формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеЗначенийСвойств" И Источник = Ссылка Тогда
		РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, Параметр);
	ИначеЕсли ИмяСобытия = "ОбновлениеТекстаПроСчетФактуру" Тогда
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			Если Параметр.Найти(Ссылка) <> Неопределено Тогда
				ЗаполнитьТекстПроСчетФактуру();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ОбновлениеОтображения" формы.
//
Процедура ОбновлениеОтображения()

	ОбновитьПодвал();

	// Подсчитаем количество строк в табличных частях.
	Для каждого СтраницаПанели из ЭлементыФормы.ОсновнаяПанель.Страницы Цикл

		Если СтраницаПанели.Имя = "Товары" Тогда
			СтраницаПанели.Заголовок = "Товары и услуги (" + ДокументОбъект.Товары.Количество() + " поз.)";
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ОбновлениеОтображения()

// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи()

	// Установка кнопок печати
	УстановитьКнопкиПечати();

	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
    
	//Открыть настройку движений
	ОбщегоНазначения.РучнаяКорректировкаОсновнаяФормаПослеЗаписи(ЭтотОбъект.Проведен,ОткрыватьНастройкуДвижений,Ссылка,ЭтотОбъект);

КонецПроцедуры // ПослеЗаписи()

// Производит заполнение документа переданными из формы подбора данными.
//
// Параметры:
//  ТабличнаяЧасть    - табличная часть, в которую надо добавлять подобранную позицию номенклатуры;
//  ЗначениеВыбора    - структура, содержащая параметры подбора.
//
Процедура ОбработкаПодбора(ТабличнаяЧасть, ЗначениеВыбора) Экспорт

	Перем СпособЗаполненияЦен, ВалютаЦены;
	Перем Номенклатура, ЕдиницаИзмерения, Количество, Цена;

	// Получим параметры подбора из структуры подбора.
	ЗначениеВыбора.Свойство("СпособЗаполненияЦен", СпособЗаполненияЦен);
	ЗначениеВыбора.Свойство("ВалютаЦены"         , ВалютаЦены);

	ЗначениеВыбора.Свойство("Номенклатура"    , Номенклатура);
	ЗначениеВыбора.Свойство("ЕдиницаИзмерения", ЕдиницаИзмерения);
	ЗначениеВыбора.Свойство("Количество"      , Количество);
	ЗначениеВыбора.Свойство("Цена"            , Цена);

	Если Номенклатура.Услуга Тогда
		Предупреждение("В данном контексте услуги не подбираются!");
		Возврат;
	КонецЕсли;

	// Ищем выбранную позицию в таблице подобранной номенклатуры.
	//  Если найдем - увеличим количество; не найдем - добавим новую строку.
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("Номенклатура",     Номенклатура);
	СтруктураОтбора.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Если ЗначениеЗаполнено(Цена) Тогда
		СтруктураОтбора.Вставить("Цена",         Цена);
	КонецЕсли;

	СтрокаТабличнойЧасти = ОбработкаТабличныхЧастей.НайтиСтрокуТабЧасти(ТабличнаяЧасть, СтруктураОтбора);
	Если СтрокаТабличнойЧасти <> Неопределено Тогда

		// Нашли, увеличиваем количество в первой найденной строке.
		СтрокаТабличнойЧасти.Количество       = СтрокаТабличнойЧасти.Количество + Количество;
		СтрокаТабличнойЧасти.СуммаПоступления = СтрокаТабличнойЧасти.ЦенаПоступления * СтрокаТабличнойЧасти.Количество;
		СтрокаТабличнойЧасти.Сумма            = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
		РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);

	Иначе

		// Не нашли - добавляем новую строку.
		СтрокаТабличнойЧасти = ТабличнаяЧасть.Добавить();
		СтрокаТабличнойЧасти.Номенклатура     = Номенклатура;
		СтрокаТабличнойЧасти.Количество       = Количество;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения = ЕдиницаИзмерения;
		СтрокаТабличнойЧасти.Коэффициент      = 1;

		СтрокаТабличнойЧасти.ЦенаПоступления  = Ценообразование.ПересчитатьЦенуПриИзмененииВалюты(Цена, ВалютаЦены, ВалютаДокумента,
		                                                                          ЗаполнениеДокументов.КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета), ЗаполнениеДокументов.КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета), Дата);
		СтрокаТабличнойЧасти.СуммаПоступления = Цена * СтрокаТабличнойЧасти.Количество;

		СтрокаТабличнойЧасти.Цена             = СтрокаТабличнойЧасти.ЦенаПоступления;
		СтрокаТабличнойЧасти.Сумма            = СтрокаТабличнойЧасти.СуммаПоступления;

		Если СпособРасчетаКомиссионногоВознаграждения = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.ПроцентОтРазностиСуммПродажиИПоступления Тогда
			СтрокаТабличнойЧасти.СуммаВознаграждения = ПроцентКомиссионногоВознаграждения / 100
			                                         * (СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.СуммаПоступления);
		ИначеЕсли СпособРасчетаКомиссионногоВознаграждения = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.ПроцентОтСуммыПродажи Тогда
			СтрокаТабличнойЧасти.СуммаВознаграждения = ПроцентКомиссионногоВознаграждения / 100
			                                         * СтрокаТабличнойЧасти.Сумма;
		КонецЕсли;

	КонецЕсли;

	ТабличнаяЧастьИмя = "Товары";
	ЭлементыФормы[ТабличнаяЧастьИмя].ТекущаяСтрока = СтрокаТабличнойЧасти;
	ЭлементыФормы[ТабличнаяЧастьИмя].ТекущаяКолонка = ЭлементыФормы[ТабличнаяЧастьИмя].Колонки["Количество"];

КонецПроцедуры // ТоварыОбработкаПодбора()

// Процедура - обработчик события "ОбработкаВыбора" формы.
//
Процедура ОбработкаВыбора(ЗначениеВыбора, Источник)
	Перем Команда;

	Если ТипЗнч(ЗначениеВыбора) = Тип("Структура") Тогда
		ЗначениеВыбора.Свойство("Команда", Команда);

		Если Команда = "ПодборВТабличнуюЧастьТовары" Тогда
			ОбработкаПодбора(Товары, ЗначениеВыбора);
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

 
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Обработчик события "Действие" кнопки "ПоказатьСкрытьСчетаУчета" 
// командной панели формы. Устанавливает видимость счетов в форме.
//
Процедура ДействияФормыПоказатьСкрытьСчетаУчета(Кнопка)
	
	ПоказыватьВДокументахСчетаУчета = НЕ ПоказыватьВДокументахСчетаУчета;
	
	Кнопка.Пометка = ПоказыватьВДокументахСчетаУчета;
	
	УстановитьВидимость();
	
КонецПроцедуры // ДействияФормыПоказатьСкрытьСчетаУчета()

// Процедура вызывается при нажатии кнопки "Заполнить" командной панели 
// табличного поля "Товары", вызывает сервисный механизм для заполнения табличной части
//
Процедура КоманднаяПанельТоварыДействиеЗаполнитьПоДоговору(Кнопка)

	ЗаполнятьТабличнуюЧасть = Ложь;
	Если НЕ ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ТекстПредупреждения = "Не выбран договор. Заполнение невозможно.";

	Иначе
		ЗаполнятьТабличнуюЧасть = Истина;

	КонецЕсли;

	Если Не ЗаполнятьТабличнуюЧасть Тогда
		Предупреждение(ТекстПредупреждения, , Метаданные().Имя);
		Возврат;
	КонецЕсли;

	Если Товары.Количество() > 0 Тогда

		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Метаданные().Имя);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;

		Товары.Очистить();

	КонецЕсли;

	Счет004   = ПланыСчетов.Хозрасчетный.ТоварыПринятыеНаКомиссию;
	МассивВидов = Новый Массив();
	МассивВидов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	МассивВидов.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Партии);
    	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Счет004",    Счет004);
	Запрос.УстановитьПараметр("ПустаяСсылка", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.УстановитьПараметр("МассивВидов",  МассивВидов);
	Запрос.УстановитьПараметр("Договор",      ДоговорКонтрагента);
	Запрос.УстановитьПараметр("НаДату", 		  Дата);
	Запрос.Текст = "ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1 КАК Номенклатура,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1.БазоваяЕдиницаИзмерения КАК Единица,
	|	1 КАК Коэффициент,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт2 КАК Партия,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт2.ДоговорКонтрагента КАК Договор,
	|	ВЫБОР КОГДА ХозрасчетныйОборотыДтКт.КоличествоОборотДт > 0 ТОГДА ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.СуммаОборот / ХозрасчетныйОборотыДтКт.КоличествоОборотДт КАК ЧИСЛО(15, 2)) ИНАЧЕ ХозрасчетныйОборотыДтКт.СуммаОборот КОНЕЦ КАК Цена,
	|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК Сумма,
	|	ХозрасчетныйОборотыДтКт.КоличествоОборотДт КАК Количество
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(, &НаДату, , СчетДт В ИЕРАРХИИ (&Счет004), &МассивВидов, СчетКт = &ПустаяСсылка, , Организация = &Организация И СубконтоДт2.ДоговорКонтрагента = &Договор) КАК ХозрасчетныйОборотыДтКт";
	Выборка = Запрос.Выполнить().Выбрать();
    	   	
	Пока Выборка.Следующий() Цикл

		СтрокаТабличнойЧасти = Товары.Добавить();
		СтрокаТабличнойЧасти.Номенклатура     = Выборка.Номенклатура;
		СтрокаТабличнойЧасти.Количество       = Выборка.Количество;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения = Выборка.Единица;
		СтрокаТабличнойЧасти.Коэффициент      = Выборка.Коэффициент;
		СтрокаТабличнойЧасти.КоличествоМест   = Выборка.Количество;

		СтараяВалютаДокумента   = Выборка.Партия.ВалютаДокумента;
		ДанныеОКурсе            = МодульВалютногоУчета.ПолучитьКурсВалюты(СтараяВалютаДокумента, Выборка.Партия.Дата);
		СтарыйКурс              = ДанныеОКурсе.Курс;
		СтараяКратность         = ДанныеОКурсе.Кратность;

		Сумма = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(Выборка.Сумма,
											мВалютаРегламентированногоУчета, ВалютаДокумента,
											1, СтарыйКурс,
											1, СтараяКратность);

		СтрокаТабличнойЧасти.СуммаПоступления = Сумма;
		СтрокаТабличнойЧасти.ЦенаПоступления  = Сумма / Выборка.Количество;

		СтрокаТабличнойЧасти.Цена             = СтрокаТабличнойЧасти.ЦенаПоступления;
		СтрокаТабличнойЧасти.Сумма            = СтрокаТабличнойЧасти.СуммаПоступления;

		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект); 
        ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
		
		РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);
		
	КонецЦикла;

КонецПроцедуры // КоманднаяПанельТоварыДействиеЗаполнить()

// Процедура вызывается при нажатии кнопки "Заполнить" командной панели 
// табличного поля "Товары", вызывает сервисный механизм для заполнения табличной части
//
Процедура КоманднаяПанельТоварыДействиеЗаполнитьПоПоступлению(Кнопка)

	ЗаполнятьТабличнуюЧасть = Ложь;
	Если НЕ ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ТекстПредупреждения = "Не выбран договор. Заполнение невозможно.";
	Иначе
		ЗаполнятьТабличнуюЧасть = Истина;
	КонецЕсли;

	Если Не ЗаполнятьТабличнуюЧасть Тогда
		Предупреждение(ТекстПредупреждения, , Метаданные().Имя);
		Возврат;
	КонецЕсли;

	Если Товары.Количество() > 0 Тогда
		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Метаданные().Имя);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли; 

		Товары.Очистить();
	КонецЕсли;

	ФормаВыбора = Документы.ПоступлениеТоваровУслуг.ПолучитьФормуВыбора();
	ФормаВыбора.Заголовок = "Выберите документ поступления для заполнения отчета комитенту";
	ФормаВыбора.РежимВыбора = Истина;

	// По умолчанию поставим отбор по договору и виду операции
	ФормаВыбора.Отбор.ДокументыПоДоговоруКонтрагента.Значение = ДоговорКонтрагента;
	ФормаВыбора.Отбор.ДокументыПоДоговоруКонтрагента.Использование = Истина;

	Поступление = ФормаВыбора.ОткрытьМодально();
	Если НЕ ЗначениеЗаполнено(Поступление) Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Поступление", Поступление);
	Запрос.Текст = "ВЫБРАТЬ
	|	ПоступлениеТоваровУслугТовары.Номенклатура,
	|	ПоступлениеТоваровУслугТовары.КоличествоМест,
	|	ПоступлениеТоваровУслугТовары.ЕдиницаИзмерения,
	|	ПоступлениеТоваровУслугТовары.Коэффициент,
	|	ПоступлениеТоваровУслугТовары.Количество,
	|	ПоступлениеТоваровУслугТовары.Цена,
	|	ВЫБОР КОГДА ПоступлениеТоваровУслугТовары.Ссылка.СуммаВключаетНДС ТОГДА ПоступлениеТоваровУслугТовары.Сумма ИНАЧЕ ПоступлениеТоваровУслугТовары.Сумма + ПоступлениеТоваровУслугТовары.СуммаНДС КОНЕЦ КАК Сумма
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
	|
	|ГДЕ
	|	ПоступлениеТоваровУслугТовары.Ссылка = &Поступление";
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл

		СтрокаТабличнойЧасти = Товары.Добавить();
		СтрокаТабличнойЧасти.Номенклатура     = Выборка.Номенклатура;
		СтрокаТабличнойЧасти.Количество       = Выборка.Количество;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
		СтрокаТабличнойЧасти.Коэффициент      = Выборка.Коэффициент;
		СтрокаТабличнойЧасти.КоличествоМест   = Выборка.Количество;

		СтрокаТабличнойЧасти.СуммаПоступления = Выборка.Сумма;
		СтрокаТабличнойЧасти.ЦенаПоступления  = Выборка.Сумма / Выборка.Количество;

		СтрокаТабличнойЧасти.Цена             = СтрокаТабличнойЧасти.ЦенаПоступления;
		СтрокаТабличнойЧасти.Сумма            = СтрокаТабличнойЧасти.СуммаПоступления;
		
		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект); 
        ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);

		РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);
	КонецЦикла;

КонецПроцедуры


// Процедура вызывается при нажатии кнопки "Заполнить" командной панели 
// табличного поля "Товары", вызывает сервисный механизм для заполнения табличной части
//
Процедура КоманднаяПанельТоварыДействиеЗаполнитьРеализованнымиПоДоговору(Кнопка)

	ЗаполнятьТабличнуюЧасть = Ложь;
	Если НЕ ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ТекстПредупреждения = "Не выбран договор. Заполнение невозможно.";

	Иначе
		ЗаполнятьТабличнуюЧасть = Истина;

	КонецЕсли;

	Если Не ЗаполнятьТабличнуюЧасть Тогда
		Предупреждение(ТекстПредупреждения, , Метаданные().Имя);
		Возврат;
	КонецЕсли;

	Если Товары.Количество() > 0 Тогда

		ТекстВопроса = "Перед заполнением табличная часть будет очищена. Заполнить?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Метаданные().Имя);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;

		Товары.Очистить();

	КонецЕсли;

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.УстановитьПараметр("Контрагент",   Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(Ссылка), Новый МоментВремени(Ссылка.Дата, Ссылка), КонецДня(Дата)));
		
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	РеализованныеТоварыКомитентовОстатки.Номенклатура,
	|	РеализованныеТоварыКомитентовОстатки.Номенклатура.БазоваяЕдиницаИзмерения КАК Единица,
	|	СУММА(РеализованныеТоварыКомитентовОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	|	СУММА(РеализованныеТоварыКомитентовОстатки.ВыручкаОстаток) КАК ВыручкаОстаток,
	|	СУММА(РеализованныеТоварыКомитентовОстатки.СуммаПоступленияОстаток) КАК СуммаПоступленияОстаток
	|ИЗ
	|	РегистрНакопления.РеализованныеТоварыКомитентов.Остатки(&Период
	|		,
	|		Организация = &Организация 
	|		    И Контрагент = &Контрагент 
	|		    И ДоговорКонтрагента = &ДоговорКонтрагента) КАК РеализованныеТоварыКомитентовОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализованныеТоварыКомитентовОстатки.Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		СтрокаТабличнойЧасти = Товары.Добавить();
		СтрокаТабличнойЧасти.Номенклатура     = Выборка.Номенклатура;
		СтрокаТабличнойЧасти.Количество       = Выборка.КоличествоОстаток;
		СтрокаТабличнойЧасти.ЕдиницаИзмерения = Выборка.Единица;
		СтрокаТабличнойЧасти.Коэффициент      = 1;
		СтрокаТабличнойЧасти.КоличествоМест	  = Выборка.КоличествоОстаток;
		
		СтрокаТабличнойЧасти.СуммаПоступления = Выборка.СуммаПоступленияОстаток;
		СтрокаТабличнойЧасти.ЦенаПоступления  = ?(Выборка.КоличествоОстаток = 0, 0, Выборка.СуммаПоступленияОстаток / Выборка.КоличествоОстаток);

		СтрокаТабличнойЧасти.Сумма            = Выборка.ВыручкаОстаток;
		СтрокаТабличнойЧасти.Цена             = ?(Выборка.КоличествоОстаток = 0, 0, Выборка.ВыручкаОстаток / Выборка.КоличествоОстаток);

		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект); 
        ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
		
		РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);
		
	КонецЦикла;

КонецПроцедуры

// Процедура вызывается при нажатии кнопки "ЦеныВалюта" командной панели
// табличного поля "Товары", вызывает сервисный механизм для изменения
// общих для всей табличной части "Товары" реквизитов - таких, например,
// как тип цен, валюта и т. д.
//
Процедура КоманднаяПанельТоварыДействиеЦеныВалюта(Кнопка)

	СпособЗаполненияЦен = Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатурыКонтрагентов;

	// Задать набор реквизитов для редактирования.
	СтруктураРеквизитовДокумента = Ценообразование.ПолучитьСтруктуруРеквизитовДокументаДляЦенообразования(ЭтотОбъект);

	// Вызов общей формы "Цены и валюта" и пересчеты по результатам выбора в этой форме.
	СтруктураЗначений = Ценообразование.ОткрытьФормуЦеныИВалюта(ДокументОбъект, СтруктураРеквизитовДокумента, мВалютаРегламентированногоУчета,
	                                            , "Товары");
	Если СтруктураЗначений <> Неопределено Тогда

		ЗаполнениеДокументов.ИзменитьЦеныВалюту(ЭтотОбъект, СпособЗаполненияЦен, СтруктураРеквизитовДокумента, , "Товары", мВалютаРегламентированногоУчета, , СтруктураЗначений);

		Для каждого СтрокаТабличнойЧасти Из ДокументОбъект.Товары Цикл
			РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);
		КонецЦикла;

	КонецЕсли;

	УстановитьВидимость();

КонецПроцедуры // КоманднаяПанельТоварыКнопкаЦеныВалюта()

// Процедура вызывается при нажатии кнопки "Изменить" командной панели 
// табличного поля "Товары", вызывает сервисный механизм для заполнения табличной части
//
Процедура КоманднаяПанельТоварыДействиеИзменить(Кнопка)

	// Получим контекст обработки
	ИзменениеТабличнойЧастиТовары      = Обработки.ОбработкаТабличнойЧастиТовары.Создать();
	ФормаИзменениеТабличнойЧастиТовары = ИзменениеТабличнойЧастиТовары.ПолучитьФорму(,ЭтаФорма);

	//Установим реквизиты и переменные формы.
	ФормаИзменениеТабличнойЧастиТовары.ДокументОбъект       = ЭтотОбъект;
	ФормаИзменениеТабличнойЧастиТовары.мФормаДокумента      = ЭтаФорма;
	ФормаИзменениеТабличнойЧастиТовары.мЕстьНДС             = Ложь;
	ФормаИзменениеТабличнойЧастиТовары.мЕстьЦенаВРознице    = Ложь;
	ФормаИзменениеТабличнойЧастиТовары.мСпособЗаполненияЦен = Неопределено;

	//Перенесем табличную часть
	ИзменениеТабличнойЧастиТовары.Товары.Загрузить(Товары.Выгрузить());

	// Открываем форму обработки
	ФормаИзменениеТабличнойЧастиТовары.Открыть();

КонецПроцедуры

// Процедура вызывается при нажатии кнопки "Подбор" командной панели
// табличного поля "Товары", вызывает сервисный механизм для
// подбора номенклатуры в табличную часть "Товары".
//
Процедура КоманднаяПанельТоварыПодбор(Кнопка)

	ДействиеПодбор(Товары);

КонецПроцедуры


// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры
        

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ОсновныеДействияФормыНастройка(Кнопка)

	//Открывает форму движений документа для ручного редактирования
	ОбщегоНазначения.РучнаяКорректировкаОсновнаяФорма(ЭтоНовый(),Ссылка,ЭтотОбъект);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура обновляет параметры в форме подбора, если она открыта.
//
// Параметры:
//  Реквизит - измененный реквизит.
//
Процедура ОбновитьФормуПодбора(Реквизит)

	РаботаСДиалогами.ОбновитьПараметрыИФормуПодбора(ЭтотОбъект, мФормаПодбораНоменклатуры, Реквизит);

КонецПроцедуры // ОбновитьФормуПодбора()

// Процедура вызывает сервисный механизм для подбора номенклатуры в табличную часть.
//
// Параметры:
//  ТабличнаяЧасть - табличная часть, в которую осуществляется подбор.
//
Процедура ДействиеПодбор(ТабличнаяЧасть)

	ЕстьЦена  = Истина;

	СписокВидовПодбора = СформироватьСписокЗапросовДляПодбора();
	ПредставлениеДок = Метаданные().Представление();
	ВременнаяДатаРасчетов = ?(НачалоДня(Дата) = НачалоДня(ТекущаяДата()), Неопределено, Дата);

	СтруктураПараметровПодбора = Новый Структура();
	СтруктураПараметровПодбора.Вставить("Команда",            "ПодборВТабличнуюЧастьТовары");
	СтруктураПараметровПодбора.Вставить("СписокВидовПодбора", СписокВидовПодбора);
	СтруктураПараметровПодбора.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
	СтруктураПараметровПодбора.Вставить("Организация",        Организация);
	// {ОбособленныеПодразделения
	СтруктураПараметровПодбора.Вставить("Подразделение"      , ПодразделениеОрганизации);
	// }ОбособленныеПодразделения
	СтруктураПараметровПодбора.Вставить("Контрагент",         Контрагент);
	СтруктураПараметровПодбора.Вставить("Склад",              Неопределено);
	СтруктураПараметровПодбора.Вставить("ДатаРасчетов",       ВременнаяДатаРасчетов);
	СтруктураПараметровПодбора.Вставить("ТипЦен",             ТипЦен);
	СтруктураПараметровПодбора.Вставить("ЕстьЦена",           ЕстьЦена);
	СтруктураПараметровПодбора.Вставить("ВалютаДокумента",    ВалютаДокумента);
	СтруктураПараметровПодбора.Вставить("Заголовок",          "Подбор номенклатуры в документ " + ПредставлениеДок + " № " + Номер);

	РаботаСДиалогами.ОткрытьПодборНоменклатуры(ЭтаФорма, СтруктураПараметровПодбора, мФормаПодбораНоменклатуры);

КонецПроцедуры // ДействиеПодбор()

Процедура ДействияФормыНастройкаУСН(Кнопка)
	
	ЗаполнитьНастройкуКнигиУСН(Ложь);
	
	ФормаНастройки = ПолучитьОбщуюФорму("ФормаНастройкиПлатежаУСН");
	
	ФормаНастройки.Графа4 		= Графа4_УСН;
	ФормаНастройки.Графа5 		= Графа5_УСН;
	ФормаНастройки.Графа6 		= 0;
	ФормаНастройки.Графа7 		= 0;
	ФормаНастройки.НДС 			= 0;
	ФормаНастройки.ДоходЕНВД 	= ДоходыЕНВД_УСН;
	ФормаНастройки.РасходЕНВД 	= Ложь;
	ФормаНастройки.Содержание 	= Содержание_УСН;
	ФормаНастройки.ТолькоДоходы = Истина;
	ФормаНастройки.АвтоРежим 	= НЕ РучнаяНастройка_УСН;
	
	СтруктураПараметров = ФормаНастройки.ОткрытьМодально();
	
	Если СтруктураПараметров = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Графа4_УСН	 		= СтруктураПараметров.Графа4;
	Графа5_УСН	 		= СтруктураПараметров.Графа5;
	ДоходыЕНВД_УСН	 	= СтруктураПараметров.ДоходыЕНВД;
	Содержание_УСН	 	= СтруктураПараметров.Содержание;
	РучнаяНастройка_УСН = НЕ СтруктураПараметров.АвтоРежим;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "ПриИзменении" поля ввода даты документа.
//
Процедура ДатаПриИзменении(Элемент)

	Если мТекущаяДатаДокумента >= '20060101000000' И Дата < '20060101000000' Тогда
		Если ДоговорКонтрагента.УчетАгентскогоНДС Тогда
			ОтветНаВопрос = Вопрос("При смене даты будет очищена ставка НДС и сумма НДС к уплате в бюджет табличной части ""Товары"". Сменить дату документа?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
			Если ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	РаботаСДиалогами.ПриИзмененииЗначенияДатыДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета);
	УстановитьОчиститьСтавкуНДС();

	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа
	НалоговыйУчетУСН.УстановитьВидимостьРеквизитовУСН(ЭлементыФормы, Организация, Дата);
    ЭлементыФормы.ДействияФормы.Кнопки.НастройкаУСН.Доступность = НалоговыйУчетУСН.ПрименениеУСНДоходы(Организация, Дата);
	
	мЗаконодательство2006 = Дата >= '20060101000000';
	
	УстановитьВидимость();
	
КонецПроцедуры // ДатаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода организации.
//
Процедура ОрганизацияПриИзменении(Элемент)
	
	// Выполняем общие действия для всех документов при изменении Организация.
	ЗаполнениеДокументов.ПриИзмененииЗначенияОрганизации(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"));
    	
	ЭлементыФормы.ДействияФормы.Кнопки.НастройкаУСН.Доступность = НалоговыйУчетУСН.ПрименениеУСНДоходы(Организация, Дата);
	
	Если Не ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	КонецЕсли;
    НалоговыйУчетУСН.УстановитьВидимостьРеквизитовУСН(ЭлементыФормы, Организация, Дата);
	
	КонтрагентПриИзменении(Элемент);
		
КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода способа
// расчета комиссионного вознаграждения.
//
Процедура СпособРасчетаКомиссионногоВознагражденияПриИзменении(Элемент)

	Если СпособРасчетаКомиссионногоВознаграждения
		<> Перечисления.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается Тогда
		// Если в табличной части отсутствуют строки - пересчитывать ничего не нужно.
		Если Товары.Количество() > 0 Тогда

			// Надо предложить пересчитать комиссионное вознаграждение.
			Ответ = Вопрос("Изменился способ расчета. Пересчитать комиссионное вознаграждение?", 
			               РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				Для каждого СтрокаТабличнойЧасти Из Товары Цикл
					РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);
				КонецЦикла;
				ЗаполнитьНастройкуКнигиУСН();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	УстановитьВидимость();
	УстановитьАвтоотметку();

КонецПроцедуры // СпособРасчетаКомиссионногоВознагражденияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода процента
// комиссионного вознаграждения.
//
Процедура ПроцентКомиссионногоВознагражденияПриИзменении(Элемент)

	// Если в табличной части отсутствуют строки - пересчитывать ничего не нужно.
	Если Товары.Количество() > 0 Тогда

		// Надо предложить пересчитать комиссионное вознаграждение.
		Ответ = Вопрос("Изменился процент вознаграждения. Пересчитать комиссионное вознаграждение", 
		               РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Для каждого СтрокаТабличнойЧасти Из Товары Цикл
				РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);
			КонецЦикла;
			ЗаполнитьНастройкуКнигиУСН();
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры // ПроцентКомиссионногоВознагражденияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода контрагента.
//
Процедура КонтрагентПриИзменении(Элемент)

	// Выполняем общие действия для всех документов при изменении Контрагент.
	ЗаполнениеДокументов.ПриИзмененииЗначенияКонтрагента(ЭтотОбъект,мСписокВидовДоговоров);

	// Могли поменять договор.
	ПриИзмененииДоговора();

КонецПроцедуры // КонтрагентПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода
// "ДоговорКонтрагента". Процедура выполняет действия при изменении договора
// взаиморасчетов с контрагентом.
//
Процедура ДоговорКонтрагентаПриИзменении(Элемент)

	ПриИзмененииДоговора();

КонецПроцедуры // ДоговорКонтрагентаПриИзменении()

// {ОбособленныеПодразделения
// Процедура - обработчик события "ПриИзменении" поля ввода "Подразделение".
// 
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	ПриИзмененииПодразделенияОрганизации();
	
КонецПроцедуры // ПодразделениеОрганизацииПриИзменении()
// }ОбособленныеПодразделения

// Процедура - обработчик события "НачалоВыбора" поля ввода ДоговорКонтрагента
//
Процедура ДоговорКонтрагентаНачалоВыбора(Элемент, СтандартнаяОбработка)

	РаботаСДиалогами.НачалоВыбораЗначенияДоговораКонтрагента(ЭтотОбъект, ЭтаФорма, Элемент, Контрагент, ДоговорКонтрагента,
											мСписокВидовДоговоров, СтандартнаяОбработка);

КонецПроцедуры // ДоговорКонтрагентаНачалоВыбора()

// Процедура - обработчик события "Нажатие" надписи о счете-фактуре.
//   Процедура выполняет либо ввод нового счета-фактуры, либо открывает
// форму уже существующего счета-фактуры, если тот был введен на основании
// текущего документа.
//
Процедура ГиперссылкаСчетФактураНажатие(Элемент)

	РаботаСДиалогами.ВвестиСчетФактуру(ЭтотОбъект, ЭтаФорма, "СчетФактураВыданный");

КонецПроцедуры // ГиперссылкаСчетФактураНажатие()

// Процедура - обработчик события "ОбработкаВыбора" поля ввода СчетУчетаРасчетовСКонтрагентом
//
Процедура СчетУчетаРасчетовСКонтрагентомОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);

КонецПроцедуры

// Процедура - обработчик события "ОбработкаВыбора" поля ввода СчетУчетаРасчетовПоАвансамПолученным
//
Процедура СчетУчетаРасчетовПоАвансамПолученнымОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение);

КонецПроцедуры

// Процедура - обработчик события "НачалоВыбора" поля ввода Услуга
//
Процедура УслугаПоВознаграждениюНачалоВыбора(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ФормаВыбора = Справочники.Номенклатура.ПолучитьФормуВыбора(, Элемент,);
	ФормаВыбора.Отбор.Услуга.Использование = Истина;
	ФормаВыбора.Отбор.Услуга.ВидСравнения  = ВидСравнения.Равно;
	ФормаВыбора.Отбор.Услуга.Значение      = Истина;
	
	ФормаВыбора.ЭлементыФормы.СправочникСписок.НастройкаОтбора.Услуга.Доступность = Ложь;

	ФормаВыбора.Открыть();

КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода Услуга
//
Процедура УслугаПоВознаграждениюПриИзменении(Элемент)

	СчетаУчета = БухгалтерскийУчет.ПолучитьСчетаУчетаНоменклатуры(Организация, УслугаПоВознаграждению);

	СчетУчетаНДСПоРеализации = СчетаУчета.СчетУчетаНДСПродажи;
	СчетДоходов            = СчетаУчета.СчетДоходов;

	СчетДоходовПриИзменении(Элемент);

КонецПроцедуры // УслугаПоВознаграждениюПриИзменении

// Процедура - обработчик события "ПриИзменении" поля ввода СчетДоходов
//
Процедура СчетДоходовПриИзменении(Элемент)

	УстановитьАналитику(СчетДоходов, Субконто);	
	
	Если ТипЗнч(Субконто) = Тип("СправочникСсылка.НоменклатурныеГруппы") 
	   И ЗначениеЗаполнено(УслугаПоВознаграждению) 
	   И ЗначениеЗаполнено(УслугаПоВознаграждению.НоменклатурнаяГруппа) Тогда
		
		Субконто = УслугаПоВознаграждению.НоменклатурнаяГруппа;
		
	КонецЕсли;
	
	УстановитьВидимость();

КонецПроцедуры // СчетДоходовПриИзменении()

// Процедура - обработчик события "ОбработкаВыбора" поля ввода СчетУчетаНДСПоРеализации
//
Процедура СчетУчетаНДСПоРеализацииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение)

КонецПроцедуры

// Процедура - обработчик события "ОбработкаВыбора" поля ввода СчетДоходов
//
Процедура СчетДоходовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение)

КонецПроцедуры

// Процедура обработчик события "ПриИзменении" поля "СтавкаНДСВознаграждения"
//
Процедура СтавкаНДСВознагражденияПриИзменении(Элемент)

	// Нужно спросить об обновлении сумм НДС вознаграждения в ТЧ
	Если Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Ответ = Вопрос("Пересчитать суммы НДС вознаграждения?", 
	               РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);

	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	Для каждого СтрокаТЧ Из Товары Цикл
		СтрокаТЧ.СуммаНДСВознаграждения = УчетНДС.РассчитатьСуммуНДС(СтрокаТЧ.СуммаВознаграждения,
		                                                     УчитыватьНДС, СуммаВключаетНДС,
		                                                     УчетНДС.ПолучитьСтавкуНДС(СтавкаНДСВознаграждения));
	КонецЦикла;

КонецПроцедуры

// Процедура обработчик события "ПриИзменении" флага "УдержатьВознаграждение"
//
Процедура УдержатьВознаграждениеПриИзменении(Элемент)

	УстановитьВидимость();

	Ответ = Вопрос("Изменить счета учета расчетов?", РежимДиалогаВопрос.ДаНет);

	Если НЕ УдержатьВознаграждение Тогда
		Графа4_УСН = 0;
		Графа5_УСН = 0;
		ДоходыЕНВД_УСН  = Ложь;
		Содержание_УСН  = "";
		РучнаяНастройка_УСН = Ложь;
	Иначе
		ЗаполнитьНастройкуКнигиУСН();
	КонецЕсли;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;

	СчетаУчета = БухгалтерскийУчетРасчетовСКонтрагентами.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);

	Если УдержатьВознаграждение Тогда
		СчетУчетаРасчетовПоАвансам = СчетаУчета.СчетРасчетовСКомитентом;
	Иначе
		СчетУчетаРасчетовПоАвансам = СчетаУчета.СчетАвансовПокупателя;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ ТОВАРЫ

// Процедура - обработчик события "ПриИзменении" поля ввода номенклатуры
// в строке табличной части "Товары".
//
Процедура ТоварыНоменклатураПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;

	// Выполнить общие действия для всех документов при изменении номенклатуры.
	ОбработкаТабличныхЧастей.ПриИзмененииНоменклатурыТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);

	// Берем из самого реквизита Номенклатура
	СтрокаТабличнойЧасти.Коэффициент      = 1;
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтрокаТабличнойЧасти.Номенклатура.БазоваяЕдиницаИзмерения;

	СтрокаТабличнойЧасти.СуммаПоступления = СтрокаТабличнойЧасти.ЦенаПоступления * СтрокаТабличнойЧасти.Количество;
	СтрокаТабличнойЧасти.Сумма            = СтрокаТабличнойЧасти.Цена            * СтрокаТабличнойЧасти.Количество;
	
	ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект); 
    ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);

	РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);

КонецПроцедуры // ТоварыНоменклатураПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода единицы
// в строке табличной части "Товары".
//
Процедура ТоварыЕдиницаПриИзменении(Элемент)

	// Выполнить общие действия для всех документов при изменении Единица.
	ОбработкаТабличныхЧастей.ПриИзмененииЕдиницыТабЧасти(ЭлементыФормы.Товары.ТекущиеДанные, ЭтотОбъект);

КонецПроцедуры // ТоварыЕдиницаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода цены
// в строке табличной части "Товары".
//
Процедура ТоварыЦенаПоступленияПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;

	СтрокаТабличнойЧасти.СуммаПоступления = СтрокаТабличнойЧасти.ЦенаПоступления * СтрокаТабличнойЧасти.Количество;
	РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);

КонецПроцедуры // ТоварыЦенаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода цены
// в строке табличной части "Товары".
//
Процедура ТоварыЦенаПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;

	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
    ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
	РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);

КонецПроцедуры // ТоварыЦенаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода количества
// в строке табличной части "Товары".
//
Процедура ТоварыКоличествоПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;

	СтрокаТабличнойЧасти.СуммаПоступления = СтрокаТабличнойЧасти.ЦенаПоступления * СтрокаТабличнойЧасти.Количество;
	СтрокаТабличнойЧасти.Сумма            = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
    ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
	РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);

КонецПроцедуры // ТоварыКоличествоПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода количества мест
// в строке табличной части "Товары".
//
Процедура ТоварыКоличествоМестПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;

	СтрокаТабличнойЧасти.СуммаПоступления = СтрокаТабличнойЧасти.ЦенаПоступления * СтрокаТабличнойЧасти.Количество;
	СтрокаТабличнойЧасти.Сумма            = СтрокаТабличнойЧасти.Цена * СтрокаТабличнойЧасти.Количество;
	РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);

КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода суммы
// в строке табличной части "Товары".
//
Процедура ТоварыСуммаПоступленияПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;

	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Количество) Тогда
		СтрокаТабличнойЧасти.ЦенаПоступления = 0;
	Иначе
		СтрокаТабличнойЧасти.ЦенаПоступления = СтрокаТабличнойЧасти.СуммаПоступления / СтрокаТабличнойЧасти.Количество;
	КонецЕсли; 

	РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);

КонецПроцедуры // ТоварыСуммаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода суммы
// в строке табличной части "Товары".
//
Процедура ТоварыСуммаПриИзменении(Элемент)

	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;

	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Количество) Тогда
		СтрокаТабличнойЧасти.Цена = 0;
	Иначе
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.Количество;
	КонецЕсли;

    ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
	РассчитатьКомиссионноеВознаграждение(СтрокаТабличнойЧасти);

КонецПроцедуры // ТоварыСуммаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода ставки НДС
// в строке табличной части "Товары".
//
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = ЭлементыФормы.Товары.ТекущиеДанные;
    ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ЭтотОбъект);
	
КонецПроцедуры

// Процедура обработчик события "ПриИзменении" поля "СуммаВознаграждения"
// табличной части "Товары"
//
Процедура ТоварыСуммаВознагражденияПриИзменении(Элемент)

	СтрокаТЧ = ЭлементыФормы.Товары.ТекущиеДанные;

	СтрокаТЧ.СуммаНДСВознаграждения = УчетНДС.РассчитатьСуммуНДС(СтрокаТЧ.СуммаВознаграждения,
	                                                     УчитыватьНДС, СуммаВключаетНДС,
	                                                     УчетНДС.ПолучитьСтавкуНДС(СтавкаНДСВознаграждения));
    ЗаполнитьНастройкуКнигиУСН();
КонецПроцедуры

Процедура ТоварыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	РаботаСДиалогами.ПоказатьКодАртикул(ЭлементыФормы.Товары.Колонки, ОформлениеСтроки.Ячейки, ДанныеСтроки.Номенклатура);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ Денежные средства

Процедура ДенежныеСредстваСуммаПриИзменении(Элемент)

	// Рассчитать реквизиты табличной части.
	СтрокаТЧ = ЭлементыФормы.ДенежныеСредства.ТекущиеДанные;

	СтрокаТЧ.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(СтрокаТЧ.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(СтрокаТЧ.СтавкаНДС));

КонецПроцедуры

Процедура ДенежныеСредстваСтавкаНДСПриИзменении(Элемент)

	СтрокаТЧ = ЭлементыФормы.ДенежныеСредства.ТекущиеДанные;

	СтрокаТЧ.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(СтрокаТЧ.Сумма, Истина, Истина, УчетНДС.ПолучитьСтавкуНДС(СтрокаТЧ.СтавкаНДС));

КонецПроцедуры

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать"
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Установить печать по умолчанию"
//
Процедура ОсновныеДействияФормыУстановитьПечатьПоУмолчанию(Кнопка)
	
	Если УниверсальныеМеханизмы.НазначитьКнопкуПечатиПоУмолчанию(мДеревоМакетов, Метаданные().Имя) Тогда
		
		УстановитьКнопкиПечати();
		
	КонецЕсли; 
	
	
КонецПроцедуры

// Процедура разрешения/запрещения редактирования номера документа
Процедура ДействияФормыРедактироватьНомер(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
			
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//

мСписокВидовДоговоров = Новый СписокЗначений;
мСписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомитентом);

