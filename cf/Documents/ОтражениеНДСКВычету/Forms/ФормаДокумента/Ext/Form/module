
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента; 

// Хранит текущий договор контрагента
// используется для восстановления старого договора в случае некорректного выбора нового
Перем мТекущийДоговорКонтрагента; 

// Хранит дерево макетов печатных форм
Перем мДеревоМакетов;

// Хранит элемент управления подменю печати
Перем мПодменюПечати;

// Хранит элемент управления кнопку печать по умолчанию
Перем мПечатьПоУмолчанию;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

// Хранит значение параметра учетной политики УпрощенныйУчетНДС
Перем мУпрощенныйУчетНДС;

//Используется для определения необходимости открытия настроек движения после сохранения
Перем ОткрыватьНастройкуДвижений;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.ТоварыИУслуги,ЭлементыФормы.КоманднаяПанельТовары.Кнопки.ПодменюЗаполнить);
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧ(Ссылка, СоответствиеТЧ, Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
КонецПроцедуры

// Процедура устанавливает подменю "Печать" и кнопку "Печать по умолчанию" при необходимости
//
Процедура УстановитьКнопкиПечати()
	
	мДеревоМакетов = УниверсальныеМеханизмы.ПолучитьДеревоМакетовПечати(Ссылка, Неопределено, Новый Действие("ОсновныеДействияФормыПечать"), Новый Действие("ОсновныеДействияФормыУстановитьПечатьПоУмолчанию"));

	УниверсальныеМеханизмы.УстановитьПодменюПечати    (мПодменюПечати, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов.Строки.Количество() > 0);
	УниверсальныеМеханизмы.УстановитьПечатьПоУмолчанию(мПечатьПоУмолчанию, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов, Метаданные().Имя,Новый Действие("ОсновныеДействияФормыПечатьПоУмолчанию"));

	Если Не мПодменюПечати = Неопределено Тогда
		УниверсальныеМеханизмы.СформироватьПодменю(мДеревоМакетов, мПодменюПечати,Истина,Истина);
	КонецЕсли;
	
КонецПроцедуры

// Процедура выполняет необходимые действия при изменении договора взаиморасчетов
// с контрагентом.
//
Процедура ПриИзмененииДоговора()

	Если Не ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком
		Или ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом
		Тогда
		СпособЗаполненияЦен = Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатурыКонтрагентов;
	ИначеЕсли ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем
			 Или ДоговорКонтрагента.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером
			 Тогда
		СпособЗаполненияЦен = Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатуры;
	КонецЕсли;

	// Зададим, какие реквизиты надо редактировать.
	СтруктураРеквизитовДокумента = Ценообразование.ПолучитьСтруктуруРеквизитовДокументаДляЦенообразования(ЭтотОбъект);

	ЗаполнениеДокументов.ПриИзмененииЗначенияДоговора(ЭтотОбъект, мВалютаРегламентированногоУчета, мВалютаРегламентированногоУчета, СпособЗаполненияЦен, СтруктураРеквизитовДокумента, , "ТоварыИУслуги", мТекущийДоговорКонтрагента);

	мТекущаяВалютаВзаиморасчетов = ДоговорКонтрагента.ВалютаВзаиморасчетов;
	
	УстановитьВидимость();
	
КонецПроцедуры // ПриИзмененииДоговора()

// Процедура управляет доступностью колонок субконто в табличной части услуги
Процедура УстановитьДоступностьСубконто()

	Колонки  = ЭлементыФормы.ТоварыИУслуги.Колонки;
	ТекущиеДанные = ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные;

	Если НЕ ЗначениеЗаполнено(ТекущиеДанные) Тогда
		Возврат;
	КонецЕсли;

	РаботаСДиалогами.ПриВыбореСчетаВТабличномПоле(ТекущиеДанные.СчетЗатрат,
		ТекущиеДанные.Субконто1, Колонки.Субконто1,
		ТекущиеДанные.Субконто2, Колонки.Субконто2,
		ТекущиеДанные.Субконто3, Колонки.Субконто3,
		ТекущиеДанные.ПодразделениеЗатрат, Колонки.ПодразделениеЗатрат,
		Колонки.СчетЗатрат, Ложь);

КонецПроцедуры // УстановитьДоступностьСубконто()

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры
         

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ОсновныеДействияФормыНастройка(Кнопка)
	
	//Открывает форму движений документа для ручного редактирования
	ОбщегоНазначения.РучнаяКорректировкаОсновнаяФорма(ЭтоНовый(),Ссылка,ЭтотОбъект);
	
КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать"
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Установить печать по умолчанию"
//
Процедура ОсновныеДействияФормыУстановитьПечатьПоУмолчанию(Кнопка)
	
	Если УниверсальныеМеханизмы.НазначитьКнопкуПечатиПоУмолчанию(мДеревоМакетов, Метаданные().Имя) Тогда
		
		УстановитьКнопкиПечати();
		
	КонецЕсли; 
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Процедура формирует текст в информационной надписи об итогах документа.
//
// Параметры:
//  Нет.
//
Процедура ОбновитьПодвал()

	// При изменении данных обновим суммы в подвале.
	ЭлементыФормы.Всего.Значение    = ОбщегоНазначения.ФорматСумм(ПолучитьСуммуДокументаСНДС());
	ЭлементыФормы.ВсегоНДС.Значение = ОбщегоНазначения.ФорматСумм(ПолучитьНДСДокумента());

	ВременнаяСтрока = "";

	Если (ЗначениеЗаполнено(ДокументОбъект.ВалютаДокумента))
	   И (ДокументОбъект.ВалютаДокумента <> мВалютаРегламентированногоУчета) Тогда

		ВременнаяСтрока = ВременнаяСтрока + РаботаСДиалогами.ПолучитьИнформациюКурсаВалютыСтрокой(ВалютаДокумента, 
	                                                                               ЗаполнениеДокументов.КурсДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета),
	                                                                               ЗаполнениеДокументов.КратностьДокумента(ЭтотОбъект, мВалютаРегламентированногоУчета),
	                                                                               мВалютаРегламентированногоУчета) + ", ";

	КонецЕсли;

	Если ЗначениеЗаполнено(ДокументОбъект.ТипЦен) Тогда
		ВременнаяСтрока = ВременнаяСтрока + "Тип цен: " + ДокументОбъект.ТипЦен;
	Иначе
		ВременнаяСтрока = ВременнаяСтрока + "Тип цен: Не заполнено!";
	КонецЕсли;

	ЭлементыФормы.ИнфНадписьТоварыИтоги.Заголовок = ВременнаяСтрока;

	Если НЕ ЗначениеЗаполнено(ДокументОбъект.ВалютаДокумента) Тогда
		ЭлементыФормы.НадписьВсего.Заголовок = "Всего (<>):";
	Иначе
		ЭлементыФормы.НадписьВсего.Заголовок = "Всего (" + СокрЛП(ДокументОбъект.ВалютаДокумента) +"):";
	КонецЕсли;

	Если СуммаВключаетНДС Тогда
		ЭлементыФормы.НадписьВсегоНДС.Заголовок = "НДС (в т. ч.):";
	Иначе
		ЭлементыФормы.НадписьВсегоНДС.Заголовок = "НДС (сверху):";
	КонецЕсли;

КонецПроцедуры // ОбновитьПодвал()

// Заполняет текст про счет-фактуру в форме документа.
//   Вызывается из обработчика ПриОткрытии этой формы и из обработчика ПослеЗаписи
// формы счета-фактуры.
//
// Параметры:
//  Нет.
//
Процедура ЗаполнитьТекстПроСчетФактуру() Экспорт

	Если ИспользоватьДокументРасчетовКакСчетФактуру <> Ссылка.ИспользоватьДокументРасчетовКакСчетФактуру Тогда 
		ЭлементыФормы.ГиперссылкаСчетФактура.Заголовок = "Для ввода счета-фактуры требуется записать документ";
		ЭлементыФормы.ГиперссылкаСчетФактура.ГиперСсылка = Ложь;
	ИначеЕсли ИспользоватьДокументРасчетовКакСчетФактуру
		И ТипЗнч(РасчетныйДокумент) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") 
		И Не РасчетныйДокумент.ПокупателемВыставляетсяСчетФактураНаВозврат Тогда
		ЭлементыФормы.ГиперссылкаСчетФактура.Заголовок = РаботаСДиалогами.ПолучитьТекстСчетаФактуры(УчетНДС.НайтиПодчиненныйСчетФактуру(Ссылка, "СчетФактураВыданный"),
																									ЭтотОбъект, ЭлементыФормы.ГиперссылкаСчетФактура.ГиперСсылка);
	Иначе
		ЭлементыФормы.ГиперссылкаСчетФактура.Заголовок = РаботаСДиалогами.ПолучитьТекстСчетаФактуры(УчетНДС.НайтиПодчиненныйСчетФактуру(Ссылка, "СчетФактураПолученный"),
																									ЭтотОбъект, ЭлементыФормы.ГиперссылкаСчетФактура.ГиперСсылка);
	КонецЕсли;																						

КонецПроцедуры // ЗаполнитьТекстПроСчетФактуру()

// Процедура устанавливает видимость для тех колонок в табличной части 
// "Товары", видимость которых определяется реквизитами документа.
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()
	
	ЭлементыФормы.ПрямаяЗаписьВКнигу.Доступность = Не мУпрощенныйУчетНДС;
	
	ЭлементыФормы.Контрагент.ОтметкаНезаполненного = Не ЗначениеЗаполнено(Контрагент) И Не (ИспользоватьДокументРасчетовКакСчетФактуру И УчетНДС.ДляСчетаФактурыНеТребуетсяКонтрагент(РасчетныйДокумент));
	ЭлементыФормы.ДоговорКонтрагента.ОтметкаНезаполненного = Не ЗначениеЗаполнено(ДоговорКонтрагента) И Не (ИспользоватьДокументРасчетовКакСчетФактуру И УчетНДС.ДляСчетаФактурыНеТребуетсяКонтрагент(РасчетныйДокумент));
	
	ЭлементыФормы.ФормироватьПроводки.Видимость = ПрямаяЗаписьВКнигу;
	ЭлементыФормы.РасчетныйДокумент.ОтметкаНезаполненного = Не ЗначениеЗаполнено(РасчетныйДокумент) И ИспользоватьДокументРасчетовКакСчетФактуру;
	ЭлементыФормы.ЗаписьДополнительногоЛиста.Видимость = ПрямаяЗаписьВКнигу;
	ЭлементыФормы.КорректируемыйПериод.Видимость = ПрямаяЗаписьВКнигу;
	
	ЭлементыФормы.КорректируемыйПериод.ОтметкаНезаполненного = ЗаписьДополнительногоЛиста И Не ЗначениеЗаполнено(КорректируемыйПериод);
	
	// Табличная часть Товары и услуги
	ЭлементыФормы.ТоварыИУслуги.Колонки.Цена.Видимость = Не ПрямаяЗаписьВКнигу;
	ЭлементыФормы.ТоварыИУслуги.Колонки.Количество.Видимость = Не ПрямаяЗаписьВКнигу;
	ЭлементыФормы.ТоварыИУслуги.Колонки.СчетЗатрат.Видимость = Не ПрямаяЗаписьВКнигу;
	ЭлементыФормы.ТоварыИУслуги.Колонки.Субконто1.Видимость = Не ПрямаяЗаписьВКнигу;
	ЭлементыФормы.ТоварыИУслуги.Колонки.Субконто2.Видимость = Не ПрямаяЗаписьВКнигу;
	ЭлементыФормы.ТоварыИУслуги.Колонки.Субконто3.Видимость = Не ПрямаяЗаписьВКнигу;
	ЭлементыФормы.ТоварыИУслуги.Колонки.Номенклатура.Видимость = Не ПрямаяЗаписьВКнигу;
	ЭлементыФормы.ТоварыИУслуги.Колонки.ВидЦенности.Видимость = ПрямаяЗаписьВКнигу;
	ЭлементыФормы.ТоварыИУслуги.Колонки.Событие.Видимость = ПрямаяЗаписьВКнигу;
	
	ЭлементыФормы.ТоварыИУслуги.Колонки.ВидЦенности.АвтоОтметкаНезаполненного = ПрямаяЗаписьВКнигу;
	ЭлементыФормы.ТоварыИУслуги.Колонки.СчетЗатрат.АвтоОтметкаНезаполненного = Не ПрямаяЗаписьВКнигу;
	
	// Закладка Счет-фактура
	ЭлементыФормы.ПредъявленСчетФактура.Видимость = Не ИспользоватьДокументРасчетовКакСчетФактуру;
	ЭлементыФормы.НадписьДатаВходящегоСчетаФактуры.Видимость = ПредъявленСчетФактура;
	ЭлементыФормы.НадписьНомерВходящегоСчетаФактуры.Видимость = ПредъявленСчетФактура;
	ЭлементыФормы.ДатаВходящегоСчетаФактуры.Видимость = ПредъявленСчетФактура;
	ЭлементыФормы.НомерВходящегоСчетаФактуры.Видимость = ПредъявленСчетФактура;
	ЭлементыФормы.НДСПредъявленКВычету.Видимость = Не НДСВключенВСтоимость И ПредъявленСчетФактура И мУпрощенныйУчетНДС;
	
	Для Каждого КнопкаЗаполнить Из ЭлементыФормы.КоманднаяПанельТовары.Кнопки.ПодменюЗаполнить.Кнопки Цикл
		КнопкаЗаполнить.Доступность = Не ТолькоПросмотр;
	КонецЦикла;
	
КонецПроцедуры // УстановитьВидимость()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ
 
// Процедура вызывается при нажатии кнопки "ЦеныВалюта" командной панели
// табличного поля "Товары", вызывает сервисный механизм для изменения
// общих для всей табличной части "Товары" реквизитов - таких, например,
// как тип цен, валюта и т. д.
//
Процедура КоманднаяПанельТоварыДействиеЦеныВалюта(Кнопка)

	СпособЗаполненияЦен = Перечисления.СпособыЗаполненияЦен.ПоЦенамНоменклатурыКонтрагентов;

	// Задать набор реквизитов для редактирования.
	СтруктураРеквизитовДокумента = Ценообразование.ПолучитьСтруктуруРеквизитовДокументаДляЦенообразования(ЭтотОбъект);
	СтруктураРеквизитовДокумента.Вставить("НДСВключенВСтоимость");
	СтруктураЗапрещенных = Неопределено;
	Если ПрямаяЗаписьВКнигу Тогда
		СтруктураЗапрещенных = Новый Структура("НДСВключенВСтоимость");
	КонецЕсли;

	// Вызов общей формы "Цены и валюта" и пересчеты по результатам выбора в этой форме.
	СтруктураЗначений = Ценообразование.ОткрытьФормуЦеныИВалюта(ДокументОбъект, СтруктураРеквизитовДокумента, мВалютаРегламентированногоУчета,
	                                            СтруктураЗапрещенных, "ТоварыИУслуги", , Истина);
	Если СтруктураЗначений <> Неопределено Тогда
		ЗаполнениеДокументов.ИзменитьЦеныВалюту(ЭтотОбъект, СпособЗаполненияЦен, СтруктураРеквизитовДокумента, СтруктураЗапрещенных, "ТоварыИУслуги", мВалютаРегламентированногоУчета, , СтруктураЗначений);
	КонецЕсли;

	УстановитьВидимость();

КонецПроцедуры // КоманднаяПанельТоварыКнопкаЦеныВалюта()

// Процедура разрешения/запрещения редактирования номера документа
Процедура ДействияФормыРедактироватьНомер(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
			
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок печати
	УстановитьКнопкиПечати();
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	   
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"), мВалютаРегламентированногоУчета, "Покупка");

	Иначе
		
		// Определить счет-фактуру, введенный на основании этого документа, если такой 
		// счет-фактура существует.
		ЗаполнитьТекстПроСчетФактуру();

	КонецЕсли;
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
    
	//Устанавливает необходимость открытия настроек движения после сохранения
	ОткрыватьНастройкуДвижений = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ПоказыватьДвиженияПриПроведении");

	мТекущаяДатаДокумента = Дата;

	мУпрощенныйУчетНДС = УчетНДС.ПолучитьУПУпрощенныйУчетНДС(Организация, Дата);
	Если мУпрощенныйУчетНДС И Не ПрямаяЗаписьВКнигу Тогда
		ПрямаяЗаписьВКнигу = Истина;
	КонецЕсли;
	
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента("", ЭтотОбъект, ЭтаФорма);

	ОграничениеТиповДляДокументаРасчетов(, Ложь);
	
	// Установить доступность формы с учетом даты запрета редактирования	
	РаботаСДиалогами.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, ЭтаФорма);
	
	УстановитьВидимость();
	
	Страницы = Новый СписокЗначений;
	Страницы.Добавить("ТоварыИУслуги",		"ТоварыИУслуги");
	Страницы.Добавить("ДокументыОплаты",	"ДокументыОплаты");
	РаботаСДиалогами.УстановкаТекущейСтраницы(ЭтотОбъект, ЭлементыФормы.ОсновнаяПанель, Страницы);

	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(Ссылка));

КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ПриЗакрытии" формы.
//
Процедура ПриЗакрытии()
			
	ОбщегоНазначения.ОткрытьЖурналДокумента(ЭтаФорма);

КонецПроцедуры

// Процедура - обработчик события "ОбработкаОповещения" формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеЗначенийСвойств" И Источник = Ссылка Тогда
		РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, Параметр);
	ИначеЕсли ИмяСобытия = "ОбновлениеТекстаПроСчетФактуру" Тогда
		Если ТипЗнч(Параметр) = Тип("Массив") Тогда
			Если Параметр.Найти(Ссылка) <> Неопределено Тогда
				ЗаполнитьТекстПроСчетФактуру();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи()

	// Установка кнопок печати
	УстановитьКнопкиПечати();

	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента("", ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	
	ЗаполнитьТекстПроСчетФактуру();
	  
	//Открыть настройку движений
	ОбщегоНазначения.РучнаяКорректировкаОсновнаяФормаПослеЗаписи(ЭтотОбъект.Проведен,ОткрыватьНастройкуДвижений,Ссылка,ЭтотОбъект);

КонецПроцедуры // ПослеЗаписи()

Процедура ОбновлениеОтображения()
	
	ОбновитьПодвал();

	// Подсчитаем количество строк в табличной части.
	СтраницаПанели  = ЭлементыФормы.ОсновнаяПанель.Страницы;
	СтраницаПанели.ТоварыИУслуги.Заголовок = "Товары и услуги ("   + ДокументОбъект.ТоварыИУслуги.Количество() + " поз.)";
	СтраницаПанели  = ЭлементыФормы.ОсновнаяПанель.Страницы;
	СтраницаПанели.ДокументыОплаты.Заголовок = "Документы оплаты ("   + ДокументОбъект.ДокументыОплаты.Количество() + " поз.)";
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "ПриИзменении" поля ввода даты документа.
//
Процедура ДатаПриИзменении(Элемент)

	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);

	Если НачалоМесяца(мТекущаяДатаДокумента) <> НачалоМесяца(Дата) Тогда
		
		мУпрощенныйУчетНДС = УчетНДС.ПолучитьУПУпрощенныйУчетНДС(Организация, Дата);

		Если мУпрощенныйУчетНДС И Не ПрямаяЗаписьВКнигу Тогда
			ПрямаяЗаписьВКнигу = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа

КонецПроцедуры // ДатаПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода организации.
//
Процедура ОрганизацияПриИзменении(Элемент)

	Если Не ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	КонецЕсли;

	мУпрощенныйУчетНДС = УчетНДС.ПолучитьУПУпрощенныйУчетНДС(Организация, Дата);
	Если мУпрощенныйУчетНДС И Не ПрямаяЗаписьВКнигу Тогда
		ПрямаяЗаписьВКнигу = Истина;
	КонецЕсли;
	
	// Выполняем общие действия для всех документов при изменении Организация.
	ЗаполнениеДокументов.ПриИзмененииЗначенияОрганизации(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"));

	УстановитьВидимость();
	
	КонтрагентПриИзменении(Элемент);
	
КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события "ПриИзменении" поля ввода контрагента.
//
Процедура КонтрагентПриИзменении(Элемент)

	// Выполняем общие действия для всех документов при изменении Контрагент.
	ЗаполнениеДокументов.ПриИзмененииЗначенияКонтрагента(ЭтотОбъект);

	ПриИзмененииДоговора();
	
КонецПроцедуры // КонтрагентПриИзменении()

// Процедура - обработчик события "НачалоВыбора" поля ввода ДоговорКонтрагента
//
Процедура ДоговорКонтрагентаНачалоВыбора(Элемент, СтандартнаяОбработка)

	// сохраним договор контрагента, возможно понадобится к нему вернуться
	мТекущийДоговорКонтрагента = ДоговорКонтрагента;

	РаботаСДиалогами.НачалоВыбораЗначенияДоговораКонтрагента(ЭтотОбъект, ЭтаФорма, Элемент, Контрагент, ДоговорКонтрагента,
	                                        Неопределено, СтандартнаяОбработка);

КонецПроцедуры // ДоговорКонтрагентаНачалоВыбора()

Процедура ДоговорКонтрагентаПриИзменении(Элемент)

	ПриИзмененииДоговора();
	
КонецПроцедуры

// Ограничение типов для документа расчетов
//
Функция ОграничениеТиповДляДокументаРасчетов(ИзменятьДокументРасчетов = Ложь, КонтролироватьДокументРасчетов = Истина)
	
	ИсключаемыеТипы = Новый Массив();
	ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.ВводНачальныхОстатков"));

	ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.СписаниеСРасчетногоСчета"));
	ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.КорректировкаДолга"));
	
	ОграничениеТипа = Новый ОписаниеТипов(ЭлементыФормы.РасчетныйДокумент.ТипЗначения,,ИсключаемыеТипы);
	ЭлементыФормы.РасчетныйДокумент.ОграничениеТипа = ОграничениеТипа;
	
	Если КонтролироватьДокументРасчетов 
		И Не РасчетныйДокумент = Неопределено И Не ОграничениеТипа.СодержитТип(ТипЗнч(РасчетныйДокумент)) Тогда
		// Выбран документ 
		ОбщегоНазначения.СообщитьОбОшибке(СтрЗаменить(Строка(типзнч(РасчетныйДокумент)), "Документ ссылка: ","Документ вида <")+"> не может использоваться в качестве "+?(ПрямаяЗаписьВКнигу,"документа оплаты", "документа расчетов")+".",,,СтатусСообщения.Информация);
		Если ИзменятьДокументРасчетов Тогда
			РасчетныйДокумент = Неопределено;
		КонецЕсли; 
	КонецЕсли; 

КонецФункции // ()

// Процедура - обработчик события "НачалоВыбора" поля ввода РасчетныйДокумент
//
Процедура РасчетныйДокументНачалоВыбора(Элемент, СтандартнаяОбработка)

	Отбор = Новый Структура("Контрагент, ДокументыПоДоговоруКонтрагента", Контрагент, ДоговорКонтрагента);
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "",Элемент.ОграничениеТипа.Типы());

КонецПроцедуры // РасчетныйДокументНачалоВыбора()

Процедура ПредъявленСчетФактураПриИзменении(Элемент)
	
	Если ПредъявленСчетФактура Тогда
		СчетФактура = УчетНДС.НайтиПодчиненныйСчетФактуру(Ссылка, "СчетФактураПолученный");
		Если ЗначениеЗаполнено(СчетФактура) И ТипЗнч(СчетФактура) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			
			Ответ = Вопрос("Для указания данных счета-фактуры требуется пометить на удаление подчиненный документ <" + СчетФактура + ">. Пометить документ на удаление?", РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				СчетФактураОбъект = СчетФактура.ПолучитьОбъект();
				СчетФактураОбъект.ДокументОснование = Неопределено;
				СчетФактураОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				СчетФактураОбъект.УстановитьПометкуУдаления(Истина);
			Иначе
				ПредъявленСчетФактура = Ложь;			
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если мУпрощенныйУчетНДС Тогда
		НДСПредъявленКВычету = ПредъявленСчетФактура;
	КонецЕсли;
	
	ЗаполнитьТекстПроСчетФактуру();
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ПрямаяЗаписьВКнигуПриИзменении(Элемент)
	
	ОграничениеТиповДляДокументаРасчетов(Истина);
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ИспользоватьДокументРасчетовКакСчетФактуруПриИзменении(Элемент)
	
	Если ИспользоватьДокументРасчетовКакСчетФактуру И Не Ссылка.ИспользоватьДокументРасчетовКакСчетФактуру Тогда
		СчетФактура = УчетНДС.НайтиПодчиненныйСчетФактуру(Ссылка, "СчетФактураПолученный");
		Если ЗначениеЗаполнено(СчетФактура) И ТипЗнч(СчетФактура) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
			
			Ответ = Вопрос("Требуется пометить на удаление подчиненный документ <" + СчетФактура + ">. Пометить документ на удаление?", РежимДиалогаВопрос.ДаНет);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				СчетФактураОбъект = СчетФактура.ПолучитьОбъект();
				СчетФактураОбъект.ДокументОснование = Неопределено;
				СчетФактураОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				СчетФактураОбъект.УстановитьПометкуУдаления(Истина);
			Иначе
				ИспользоватьДокументРасчетовКакСчетФактуру = Ложь;			
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ИспользоватьДокументРасчетовКакСчетФактуру Тогда
		ПредъявленСчетФактура = Ложь;
	КонецЕсли;
	
	ЗаполнитьТекстПроСчетФактуру();
	УстановитьВидимость();
	
КонецПроцедуры

Процедура КорректируемыйПериодПриИзменении(Элемент)
	
	ЭлементыФормы.КорректируемыйПериод.ОтметкаНезаполненного = ЗаписьДополнительногоЛиста И Не ЗначениеЗаполнено(КорректируемыйПериод);

КонецПроцедуры

Процедура ЗаписьДополнительногоЛистаПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура РасчетныйДокументПриИзменении(Элемент)
	
	ЭлементыФормы.РасчетныйДокумент.ОтметкаНезаполненного = Не ЗначениеЗаполнено(РасчетныйДокумент) И ИспользоватьДокументРасчетовКакСчетФактуру;
	УстановитьВидимость();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ "ТоварыИУслуги"

// Процедура - обработчик события "Нажатие" кнопки "Заполнить"
//
Процедура КоманднаяПанельТоварыДействиеЗаполнить(Кнопка)

	ЗаполнитьПоРасчетномуДокументу(Ложь);

КонецПроцедуры // КоманднаяПанельТоварыДействиеЗаполнить()

// Процедура - обработчик события "Нажатие" кнопки "Заполнить"
//
Процедура КоманднаяПанельТоварыДействиеЗаполнить1(Кнопка)

	ЗаполнитьПоРасчетномуДокументу(Истина);

КонецПроцедуры

// Процедура - обработчик события "ПриАктивизацииКолонки" ТЧ "ТоварыИУслуги"
//
Процедура ТоварыИУслугиПриАктивизацииКолонки(Элемент)

	УстановитьДоступностьСубконто();

КонецПроцедуры // ТоварыИУслугиПриАктивизацииКолонки()

Процедура ТоварыИУслугиСтавкаНДСПриИзменении(Элемент)

	ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные, ЭтотОбъект);

КонецПроцедуры

// Процедура - обработчик события "Нажатие" надписи о счете-фактуре.
//   Процедура выполняет либо ввод нового счета-фактуры, либо открывает
// форму уже существующего счета-фактуры, если тот был введен на основании
// текущего документа.
//
Процедура ГиперссылкаСчетФактураНажатие(Элемент)

	Если ИспользоватьДокументРасчетовКакСчетФактуру Тогда
		Если ТипЗнч(РасчетныйДокумент) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") 
			И Не РасчетныйДокумент.ПокупателемВыставляетсяСчетФактураНаВозврат Тогда
			РаботаСДиалогами.ВвестиСчетФактуру(РасчетныйДокумент.Сделка.ПолучитьОбъект(), ЭтаФорма, "СчетФактураВыданный");
		Иначе
			РаботаСДиалогами.ВвестиСчетФактуру(РасчетныйДокумент.ПолучитьОбъект(), ЭтаФорма, "СчетФактураПолученный");
		КонецЕсли;
	Иначе
		РаботаСДиалогами.ВвестиСчетФактуру(ЭтотОбъект, ЭтаФорма, "СчетФактураПолученный");
	КонецЕсли;

КонецПроцедуры // ГиперссылкаСчетФактураНажатие()

Процедура ТоварыИУслугиСчетЗатратОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение)

КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода "СчетЗатрат"
// табличной части "ТоварыИУслуги".
//
Процедура ТоварыИУслугиСчетЗатратПриИзменении(Элемент)
	
	Колонки = ЭлементыФормы.ТоварыИУслуги.Колонки;
	ТекущиеДанные = ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные;
	
	РаботаСДиалогами.ПриВыбореСчетаВТабличномПоле(Элемент.Значение,
		ТекущиеДанные.Субконто1, Колонки.Субконто1,
		ТекущиеДанные.Субконто2, Колонки.Субконто2,
		ТекущиеДанные.Субконто3, Колонки.Субконто3,
		ТекущиеДанные.ПодразделениеЗатрат, Колонки.ПодразделениеЗатрат);
	
КонецПроцедуры // ТоварыИУслугиСчетЗатратПриИзменении()

Процедура ТоварыИУслугиСчетУчетаНДСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = БухгалтерскийУчет.СчетМожноИспользоватьВПроводках(ВыбранноеЗначение)

КонецПроцедуры

Процедура ТоварыИУслугиСуммаПриИзменении(Элемент)
	
	// Выполнить общие действия для всех документов при изменении Суммы.
	ОбработкаТабличныхЧастей.ПриИзмененииСуммыТабЧасти(ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные, ЭтотОбъект);
	ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные, ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода номенклатуры
// в строке табличной части "Товары".
//
Процедура ТоварыИУслугиНоменклатураПриИзменении(Элемент)
	
	СтрокаТЧ = ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные;
	
	Если ТипЗнч(СтрокаТЧ.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
		// Выполнить общие действия для всех документов при изменении номенклатуры.
		ОбработкаТабличныхЧастей.ЗаполнитьНомерГТДИСтрануПроисхождения(СтрокаТЧ);

		// Заполняем реквизиты табличной части.
		ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСТабЧасти(СтрокаТЧ, ЭтотОбъект); 

		СтруктураШапкиДокумента = Новый Структура("Контрагент, ТипЦен, ДоговорКонтрагента, ДатаДокумента, ВалютаДокумента, УчитыватьНДС, СуммаВключаетНДС",
		                                           Контрагент, ТипЦен, ДоговорКонтрагента, Дата,          ВалютаДокумента, Истина, СуммаВключаетНДС);

		ОбработкаТабличныхЧастей.ЗаполнитьЕдиницуЦенуПокупкиТабЧасти(СтрокаТЧ, ЭтотОбъект, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета); 

		// Рассчитываем реквизиты табличной части.
		ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(СтрокаТЧ, ЭтотОбъект);
		ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, ЭтотОбъект);

		ЗаполнитьСчетаУчетаВСтрокеТабЧастиРегл(СтрокаТЧ, "ТоварыИУслуги", Истина, Ложь);
		
		СтрокаТЧ = ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные;
		ОбработкаТабличныхЧастей.ЗаполнитьНомерГТДИСтрануПроисхождения(СтрокаТЧ, Ложь, Организация, Дата);
		
	ИначеЕсли ТипЗнч(СтрокаТЧ.Номенклатура) = Тип("СправочникСсылка.ОбъектыСтроительства") Тогда
		
		// Заполняем реквизиты табличной части.
		ЗаполнитьСчетаУчетаВСтрокеТабЧастиОбъектовСтроительства(СтрокаТЧ, Истина, Ложь)
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" поля ввода количества
// в строке табличной части "Товары".
//
Процедура ТоварыИУслугиКоличествоПриИзменении(Элемент)

	// Рассчитать реквизиты табличной части.
	ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные, ЭтотОбъект);
	ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные, ЭтотОбъект);
	
КонецПроцедуры

Процедура ТоварыИУслугиПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ЭлементыФормы.ТоварыИУслуги.Колонки.Всего.Видимость Тогда
		ОформлениеСтроки.Ячейки.Всего.УстановитьТекст(ОбщегоНазначения.ФорматСумм(ДанныеСтроки.Сумма
		                                    + ?(СуммаВключаетНДС, 0, ДанныеСтроки.СуммаНДС)));
	КонецЕсли;

	ОформлениеСтроки.Ячейки.СчетУчетаНДС.ОтметкаНезаполненного = (ФормироватьПроводки И Не НДСВключенВСтоимость И ДанныеСтроки.СуммаНДС <> 0 И Не ЗначениеЗаполнено(ДанныеСтроки.СчетУчетаНДС));
	
	РаботаСДиалогами.ПоказатьКодАртикул(ЭлементыФормы.ТоварыИУслуги.Колонки, ОформлениеСтроки.Ячейки, ДанныеСтроки.Номенклатура);
	
КонецПроцедуры

Процедура ТоварыИУслугиНоменклатураНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	РаботаСДиалогами.НачалоВыбораЗначенияСправочникаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, 
					   СтандартнаяОбработка, Неопределено, "ТоварыИУслуги");

КонецПроцедуры

Процедура ТоварыИУслугиЦенаПриИзменении(Элемент)
	
	// Рассчитать реквизиты табличной части.
	ОбработкаТабличныхЧастей.РассчитатьСуммуТабЧасти(ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные, ЭтотОбъект);
	ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(ЭлементыФормы.ТоварыИУслуги.ТекущиеДанные, ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ "ДокументыОплаты"

Процедура ДокументыОплатыДокументОплатыПриИзменении(Элемент)
	
	ТекущиеДанные = ЭлементыФормы.ДокументыОплаты.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументОплаты) Тогда
		ТекущиеДанные.ДатаОплаты = ТекущиеДанные.ДокументОплаты.Дата;
		Если ТекущиеДанные.ДокументОплаты.Метаданные().Реквизиты.Найти("СуммаДокумента") <> Неопределено Тогда
			
			ТекущиеДанные.СуммаОплаты = 0;
			
			СуммаДокументаОплаты = ТекущиеДанные.ДокументОплаты.СуммаДокумента;
			Если ТекущиеДанные.ДокументОплаты.Метаданные().Реквизиты.Найти("ВалютаДокумента") <> Неопределено 
				И ТекущиеДанные.ДокументОплаты.ВалютаДокумента <> мВалютаРегламентированногоУчета Тогда
				
				КурсВалюты = МодульВалютногоУчета.ПолучитьКурсВалюты(ТекущиеДанные.ДокументОплаты.ВалютаДокумента, ТекущиеДанные.ДокументОплаты.Дата);
				СуммаДокументаОплаты = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(СуммаДокументаОплаты, ТекущиеДанные.ДокументОплаты.ВалютаДокумента, мВалютаРегламентированногоУчета,
																						КурсВалюты.Курс, 1, КурсВалюты.Кратность, 1);
			КонецЕсли;
			
			СуммаДокументаТекущая = ТоварыИУслуги.Итог("Сумма") + ?(Не СуммаВключаетНДС, ТоварыИУслуги.Итог("СуммаНДС"), 0);
			Если ДокументыОплаты.Итог("СуммаОплаты") + СуммаДокументаОплаты > СуммаДокументаТекущая Тогда
				СуммаДокументаОплаты = СуммаДокументаТекущая - ДокументыОплаты.Итог("СуммаОплаты");
				СуммаДокументаОплаты = ?(СуммаДокументаОплаты < 0, 0, СуммаДокументаОплаты);
			КонецЕсли;
			
			ТекущиеДанные.СуммаОплаты = СуммаДокументаОплаты;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДокументыОплатыДокументОплатыНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = ЭлементыФормы.ДокументыОплаты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура("Организация", Организация);
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Отбор.Вставить("Контрагент", Контрагент)
	КонецЕсли;
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Отбор.Вставить("ДокументыПоДоговоруКонтрагента", ДоговорКонтрагента)
	КонецЕсли;
	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "ДокументыОплаты");

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ


