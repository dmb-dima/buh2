////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит текущую дату документа - для проверки перехода документа в другой период установки номера
Перем мТекущаяДатаДокумента;

// Хранит дерево макетов печатных форм
Перем мДеревоМакетов;

// Хранит элемент управления подменю печати
Перем мПодменюПечати;

// Хранит элемент управления кнопку печать по умолчанию
Перем мПечатьПоУмолчанию;

// Хранит дерево кнопок подменю заполнение ТЧ
Перем мКнопкиЗаполненияТЧ;

// Хранит значение параметра учетной политики НДСПриНеподтвержденииСверху на дату документа
Перем мНДСПриНеподтвержденииСверху;
  
//Используется для определения необходимости открытия настроек движения после сохранения
Перем ОткрыватьНастройкуДвижений;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ПолучитьПараметрНДСПриНеподтвержденииСверху(ПересчитатьСуммыНДС = Истина)
	
	Отказ = Ложь;
	ТекНДСПриНеподтвержденииСверху = мНДСПриНеподтвержденииСверху;
	Если ЗначениеЗаполнено(Организация) Тогда
		УчетнаяПолитикаНУ = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, Отказ, Организация);
		мНДСПриНеподтвержденииСверху = ?(Отказ, Ложь, УчетнаяПолитикаНУ.НДСПриНеподтвержденииСверху);
	Иначе
		мНДСПриНеподтвержденииСверху = Ложь;
	КонецЕсли;
	
	Если ПересчитатьСуммыНДС И мНДСПриНеподтвержденииСверху <> ТекНДСПриНеподтвержденииСверху Тогда
		СтрокиНеПодтверждено = Состав.НайтиСтроки(Новый Структура("Событие", Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0));
		Если СтрокиНеПодтверждено.Количество() <> 0 Тогда
			Ответ = Вопрос("Изменился метод начисления НДС при невозможности подтверждения ставки НДС 0%. Пересчитать суммы НДС табличной части?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				Для Каждого СтрокаНеПодтверждено Из СтрокиНеПодтверждено Цикл
					Если мНДСПриНеподтвержденииСверху Тогда
						СтрокаНеПодтверждено.СтавкаНДС = Перечисления.СтавкиНДС.НДС18;
					Иначе
						СтрокаНеПодтверждено.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118;
					КонецЕсли;
					СтрокаНеПодтверждено.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(СтрокаНеПодтверждено.ПродажиСНДС0, Истина, Не мНДСПриНеподтвержденииСверху, УчетНДС.ПолучитьСтавкуНДС(СтрокаНеПодтверждено.СтавкаНДС));
				КонецЦикла;				
			КонецЕсли;			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

Процедура УстановитьВидимость()
	
	Для Каждого КнопкаЗаполнить Из ЭлементыФормы.КоманднаяПанельСостав.Кнопки.ПодменюЗаполнить.Кнопки Цикл
		КнопкаЗаполнить.Доступность = Не ТолькоПросмотр;
	КонецЦикла;
	
КонецПроцедуры

// Процедура устанавливает подменю "Заполнить" в командных панелях ТЧ документа при необходимости
//
Процедура УстановитьКнопкиПодменюЗаполненияТЧ();
	
	СоответствиеТЧ = Новый Соответствие;
	СоответствиеТЧ.Вставить(ЭлементыФормы.Состав,ЭлементыФормы.КоманднаяПанельСостав.Кнопки.ПодменюЗаполнить);
	
	мКнопкиЗаполненияТЧ = УниверсальныеМеханизмы.СформироватьПодменюЗаполненияТЧ(Ссылка, СоответствиеТЧ, Новый Действие("НажатиеНаДополнительнуюКнопкуЗаполненияТЧ"));
	
КонецПроцедуры

// Процедура устанавливает подменю "Печать" и кнопку "Печать по умолчанию" при необходимости
//
Процедура УстановитьКнопкиПечати()
	
	мДеревоМакетов = УниверсальныеМеханизмы.ПолучитьДеревоМакетовПечати(Ссылка, Неопределено, Новый Действие("ОсновныеДействияФормыПечать"), Новый Действие("ОсновныеДействияФормыУстановитьПечатьПоУмолчанию"));

	УниверсальныеМеханизмы.УстановитьПодменюПечати    (мПодменюПечати, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов.Строки.Количество() > 0);
	УниверсальныеМеханизмы.УстановитьПечатьПоУмолчанию(мПечатьПоУмолчанию, ЭлементыФормы.ОсновныеДействияФормы, мДеревоМакетов, Метаданные().Имя,Новый Действие("ОсновныеДействияФормыПечатьПоУмолчанию"));

	Если Не мПодменюПечати = Неопределено Тогда
		УниверсальныеМеханизмы.СформироватьПодменю(мДеревоМакетов, мПодменюПечати,Истина,Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьПодвал()
	
	ЭлементыФормы.СуммаБезНДС1.Значение = ОбщегоНазначения.ФорматСумм(Состав.Итог("ПродажиСНДС0"));
	ЭлементыФормы.НДС1.Значение = ОбщегоНазначения.ФорматСумм(Состав.Итог("СуммаНДС"));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

// Процедура выполняет открытие формы работы со свойствами документа
//
Процедура ДействияФормыДействиеОткрытьСвойства(Кнопка)

	РаботаСДиалогами.ОткрытьСвойстваДокумента(ЭтотОбъект, ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка кнопок печати
	УстановитьКнопкиПечати();
	
	// Установка кнопок заполнение ТЧ
	УстановитьКнопкиПодменюЗаполненияТЧ();
	      
КонецПроцедуры // ПередОткрытием()

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()

	Если ЭтоНовый() Тогда // проверить объект на то, что он еще не внесен в ИБ

		// Заполнить реквизиты значениями по умолчанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"), , "Продажа",ПараметрОбъектКопирования);

		УстановитьВремя(РежимАвтоВремя.Последним);
		
	КонецЕсли;
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияНомера(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Номер);
	    
	//Устанавливает необходимость открытия настроек движения после сохранения
	ОткрыватьНастройкуДвижений = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ПоказыватьДвиженияПриПроведении");

	// Запомнить текущие значения реквизитов формы.
	мТекущаяДатаДокумента        = Дата;
	мЗаконодательство2006 = Дата >= '20060101000000';
    ПолучитьПараметрНДСПриНеподтвержденииСверху(Ложь);
	
	// Вывести в заголовке формы вид операции.
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	
	// Установить доступность формы с учетом даты запрета редактирования	
	РаботаСДиалогами.УстановитьДоступностьФормыДляРедактирования(ЭтотОбъект, ЭтаФорма);
	
	УстановитьВидимость();

	// Установить активный реквизит.
	РаботаСДиалогами.АктивизироватьРеквизитВФорме(ЭтотОбъект, ЭтаФорма,,ЭлементыФормы.Состав);
	
	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(Ссылка));

КонецПроцедуры

// Процедура - обработчик события "ПриЗакрытии" формы.
//
Процедура ПриЗакрытии()
			
	ОбщегоНазначения.ОткрытьЖурналДокумента(ЭтаФорма);

КонецПроцедуры

// Процедура - обработчик события "ОбработкаОповещения" формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеЗначенийСвойств" И Источник = Ссылка Тогда
		РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, Параметр);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события "ПослеЗаписи" формы.
//
Процедура ПослеЗаписи()

	// Установка кнопок печати
	УстановитьКнопкиПечати();

	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	
	//Открыть настройку движений
	ОбщегоНазначения.РучнаяКорректировкаОсновнаяФормаПослеЗаписи(ЭтотОбъект.Проведен,ОткрыватьНастройкуДвижений,Ссылка,ЭтотОбъект);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если НЕ ЗначениеЗаполнено(Организация) Тогда

		Предупреждение("Не указана организация. Операция не записана.");
		Отказ = Истина;
		ТекущийЭлемент = ЭлементыФормы.Организация;
		Возврат;

	КонецЕсли;

КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	ОбновитьПодвал();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ
 
Процедура КоманднаяПанельЗаполнить(Кнопка)

	Если Ссылка.Проведен Тогда
		Ответ = Вопрос("Проведенный документ не может быть заполнен автоматически. Отменить проведение документа для заполнения?", РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьДокумент(, , , Ссылка.Проведен И Ответ = КодВозвратаДиалога.Да);
	
	РаботаСДиалогами.УстановитьЗаголовокФормыДокумента(, ЭтотОбъект, ЭтаФорма);

КонецПроцедуры
 
// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(Ссылка);
	
КонецПроцедуры
    

// Процедура вызывается при выборе пункта подменю "Движения документа по регистрам" меню "Перейти".
// командной панели формы. Процедура отрабатывает печать движений документа по регистрам.
//
Процедура ОсновныеДействияФормыНастройка(Кнопка)

	//Открывает форму движений документа для ручного редактирования
	ОбщегоНазначения.РучнаяКорректировкаОсновнаяФорма(ЭтоНовый(),Ссылка,ЭтотОбъект);

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

// Процедура - обработчик нажатия на любую из дополнительных кнопок по заполнению ТЧ
//
Процедура НажатиеНаДополнительнуюКнопкуЗаполненияТЧ(Кнопка)
	
	УниверсальныеМеханизмы.ОбработатьНажатиеНаДополнительнуюКнопкуЗаполненияТЧ(мКнопкиЗаполненияТЧ.Строки.Найти(Кнопка.Имя,"Имя",Истина),ЭтотОбъект);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать по умолчанию"
//
Процедура ОсновныеДействияФормыПечатьПоУмолчанию(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Печать"
//
Процедура ОсновныеДействияФормыПечать(Кнопка)
	
	УниверсальныеМеханизмы.ПечатьПоДополнительнойКнопке(мДеревоМакетов, ЭтотОбъект, ЭтаФорма, Кнопка.Текст);
	
КонецПроцедуры

// Процедура - обработчик нажатия на кнопку "Установить печать по умолчанию"
//
Процедура ОсновныеДействияФормыУстановитьПечатьПоУмолчанию(Кнопка)
	
	Если УниверсальныеМеханизмы.НазначитьКнопкуПечатиПоУмолчанию(мДеревоМакетов, Метаданные().Имя) Тогда
		
		УстановитьКнопкиПечати();
		
	КонецЕсли; 
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события "ПриИзменении" поля дата 
// в диалоге документа.
//
Процедура ДатаПриИзменении(Элемент)
	
	УстановитьВремя(РежимАвтоВремя.Последним);
	РаботаСДиалогами.ПроверитьНомерДокумента(ЭтотОбъект, мТекущаяДатаДокумента);
	МеханизмНумерацииОбъектов.ОбновитьПодсказкуКодНомерОбъекта(ЭтотОбъект.Метаданные(), ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	
	мТекущаяДатаДокумента = Дата; // запомним текущую дату документа для контроля номера документа
	мЗаконодательство2006 = Дата >= '20060101000000';
	ПолучитьПараметрНДСПриНеподтвержденииСверху();
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	
	Если Не ПустаяСтрока(Номер) Тогда
		МеханизмНумерацииОбъектов.СброситьУстановленныйКодНомерОбъекта(ЭтотОбъект, "Номер", ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
	КонецЕсли;
	ПолучитьПараметрНДСПриНеподтвержденииСверху();
	
	УстановитьВидимость();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТЧ

Процедура СоставСобытиеНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	Элемент.СписокВыбора.Очистить();
	Элемент.СписокВыбора.Добавить(Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0);
	Элемент.СписокВыбора.Добавить(Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0);

КонецПроцедуры

Процедура СоставСчетФактураНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ТекущаяСтрока = ЭлементыФормы.Состав.ТекущиеДанные;
	Отбор = Новый Структура("Организация, Контрагент", Организация, ТекущаяСтрока.Покупатель);

	РаботаСДиалогами.НачалоВыбораЗначенияДокументаСоставногоТипа(ЭтотОбъект, ЭтаФорма, Элемент, СтандартнаяОбработка, Отбор, "Состав");

КонецПроцедуры

Процедура СоставПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
		
	Если ДанныеСтроки.Событие = Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0 Тогда
		ОформлениеСтроки.Ячейки.СтавкаНДС.Видимость = Ложь;
		ОформлениеСтроки.Ячейки.СтавкаНДС.ТолькоПросмотр = Истина;
		ОформлениеСтроки.Ячейки.НДС.Видимость = Ложь;
		ОформлениеСтроки.Ячейки.НДС.ТолькоПросмотр = Истина;
	Иначе
		ОформлениеСтроки.Ячейки.СтавкаНДС.Видимость = Истина;
		ОформлениеСтроки.Ячейки.СтавкаНДС.ТолькоПросмотр = Ложь;
		ОформлениеСтроки.Ячейки.НДС.Видимость = Истина;
		ОформлениеСтроки.Ячейки.НДС.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура СоставСобытиеПриИзменении(Элемент)
	
	ТекущаяСтрока = ЭлементыФормы.Состав.ТекущиеДанные;
	Если ТекущаяСтрока.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0 Тогда
		Если мНДСПриНеподтвержденииСверху Тогда
			ТекущаяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.НДС18;
		Иначе
			ТекущаяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118;
		КонецЕсли;
		ТекущаяСтрока.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(ТекущаяСтрока.ПродажиСНДС0, Истина, Не мНДСПриНеподтвержденииСверху, УчетНДС.ПолучитьСтавкуНДС(ТекущаяСтрока.СтавкаНДС));
	КонецЕсли;
	
КонецПроцедуры

Процедура СоставСтавкаНДСНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	Если мНДСПриНеподтвержденииСверху Тогда
		Элемент.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС10);
		Элемент.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС18);
		Элемент.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС20);
	Иначе
		Элемент.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС10_110);
		Элемент.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС18_118);
		Элемент.СписокВыбора.Добавить(Перечисления.СтавкиНДС.НДС20_120);
	КонецЕсли;	
	
КонецПроцедуры

Процедура СоставПродажиСНДС0ПриИзменении(Элемент)
	
	ТекущаяСтрока = ЭлементыФормы.Состав.ТекущиеДанные;
	ТекущаяСтрока.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(ТекущаяСтрока.ПродажиСНДС0, Истина, Не мНДСПриНеподтвержденииСверху, УчетНДС.ПолучитьСтавкуНДС(ТекущаяСтрока.СтавкаНДС));
	
КонецПроцедуры

Процедура СоставСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрока = ЭлементыФормы.Состав.ТекущиеДанные;
	ТекущаяСтрока.СуммаНДС = УчетНДС.РассчитатьСуммуНДС(ТекущаяСтрока.ПродажиСНДС0, Истина, Не мНДСПриНеподтвержденииСверху, УчетНДС.ПолучитьСтавкуНДС(ТекущаяСтрока.СтавкаНДС));
	
КонецПроцедуры

// Процедура разрешения/запрещения редактирования номера документа
Процедура ДействияФормыРедактироватьНомер(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Номер);
			
КонецПроцедуры
