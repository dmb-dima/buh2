Перем мВалютаРегламентированногоУчета Экспорт;

// Строки, хранят реквизиты имеющие смысл только для бух. учета и упр. соответственно
// в случае если документ не отражается в каком-то виде учета, делаются невидимыми

Перем мСтрокаРеквизиты Экспорт; // (Регл)

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для определенного вида учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета() Экспорт

	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл();

КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета()

// Процедура заполняет структуры именами реквизитов, которые имеют смысл
// только для регламентированного учета
//
Процедура ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()

	мСтрокаРеквизиты =  "СчетУчетаОбъектаСтроительства,
								|Оборудование.СчетУчета";

КонецПроцедуры // ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчетаРегл()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

#Если Клиент Тогда

// Функция формирует табличный документ с печатной формой ОС-15,
// разработанной методистами
//
// Возвращаемое значение:
//  Табличный документ - печатная форма накладной
//
Функция ПечатьОС15()

	ТабДокумент = Новый ТабличныйДокумент();
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПередачаОборудованияВМонтаж_ОС15";
	Макет       = ПолучитьМакет("ОС15");

	Шапка         = Макет.ПолучитьОбласть("Шапка");
	ШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	СтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	Оборот        = Макет.ПолучитьОбласть("ОборотнаяСторона");

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПередачаОборудованияВМонтаж.Дата КАК ДатаДокумента,
	|	ПередачаОборудованияВМонтаж.Номер КАК НомерДокумента,
	|	ПередачаОборудованияВМонтаж.Организация КАК Организация,
	|	ПередачаОборудованияВМонтаж.ОбъектСтроительства КАК ОбъектВнеоборотныхАктивов,
	|	ПередачаОборудованияВМонтаж.СтатьяЗатрат,
	|	ПередачаОборудованияВМонтаж.Склад,
	// {ОбособленныеПодразделения
	|	ВЫБОР
	|		КОГДА ПередачаОборудованияВМонтаж.ПодразделениеОрганизации.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ПередачаОборудованияВМонтаж.ПодразделениеОрганизации.Наименование
	|		ИНАЧЕ ВЫРАЗИТЬ(ПередачаОборудованияВМонтаж.ПодразделениеОрганизации.НаименованиеПолное КАК СТРОКА(200))
	|	КОНЕЦ КАК ПодразделениеНаименование,
	// }ОбособленныеПодразделения 
	|	ПередачаОборудованияВМонтаж.Склад.Представление КАК СкладПредставление
	|ИЗ
	|	Документ.ПередачаОборудованияВМонтаж КАК ПередачаОборудованияВМонтаж
	|ГДЕ
	|	ПередачаОборудованияВМонтаж.Ссылка = &Ссылка";
	ВыборкаПоШапке = Запрос.Выполнить().Выбрать();
	ВыборкаПоШапке.Следующий();

	Шапка.Параметры.Заполнить(ВыборкаПоШапке);
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(
		ВыборкаПоШапке.Организация, ВыборкаПоШапке.ДатаДокумента);
	Шапка.Параметры.ОрганизацияЗаказчикНаименование = ФормированиеПечатныхФорм.ОписаниеОрганизации(
		СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
	ТабДокумент.Вывести(Шапка);
	ТабДокумент.Вывести(ШапкаТаблицы);

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка"	, ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Вид"		, ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	    	    	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйСубконто.Значение.НаименованиеПолное КАК ОборудованиеНаименование,
		|	ХозрасчетныйСубконто.Значение КАК Оборудование,
		|	Хозрасчетный.Сумма КАК СтоимостьВсего,
		|	Хозрасчетный.КоличествоКт КАК Количество
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
		|		ПО Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор И Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки
		|
		|ГДЕ
		|	Хозрасчетный.Регистратор = &Ссылка И
		|	(Хозрасчетный.Активность) И
		|	ХозрасчетныйСубконто.Вид = &Вид";
	ВыборкаПоОборудованию = Запрос.Выполнить().Выбрать();

	Пока ВыборкаПоОборудованию.Следующий() Цикл

		Если ВыборкаПоОборудованию.Количество = 0 
		 ИЛИ ВыборкаПоОборудованию.Количество = Null Тогда
			Количество = 1;
		Иначе
			Количество = ВыборкаПоОборудованию.Количество;
		КонецЕсли;

		СтрокаТаблицы.Параметры.Заполнить(ВыборкаПоОборудованию);
		СтрокаТаблицы.Параметры.СтоимостьЕдиницы = ВыборкаПоОборудованию.СтоимостьВсего / Количество;

		ТабДокумент.Вывести(СтрокаТаблицы);

	Конеццикла;

	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	Оборот.Параметры.Заполнить(ВыборкаПоШапке);
	ТабДокумент.Вывести(Оборот);

	Возврат ТабДокумент;

КонецФункции // ПечатьОС15()

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходимое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Процедура Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь, НепосредственнаяПечать = Ложь) Экспорт
	
	Если ИмяМакета = "ОС15" тогда
		// Унифицированная форма М-4 (Акт о приеме оборудования)
		ТабДокумент = ПечатьОС15();
		
	КонецЕсли;
	
	УниверсальныеМеханизмы.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, ОбщегоНазначения.СформироватьЗаголовокДокумента(ЭтотОбъект, "Передача оборудования в монтаж"), НепосредственнаяПечать);
	
КонецПроцедуры // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Структура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("ОС15","Форма ОС-15");

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверяется также правильность заполнения реквизитов ссылочных полей документа.
// Проверка выполняется по объекту и по выборке из результата запроса по шапке.
//
// Параметры: 
//  СтруктураШапкиДокумента - выборка из результата запроса по шапке документа,
//  Отказ                   - флаг отказа в проведении,
//  Заголовок               - строка, заголовок сообщения об ошибке проведения.
//
Процедура ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок)

	// Укажем, что надо проверить:
	ОбязательныеРеквизитыШапки = "Организация,ОбъектСтроительства, СтатьяЗатрат, Склад,СчетУчетаОбъектаСтроительства";

	УправлениеПроизводством.НепроверятьРеквизитыПоТипуУчета(ЭтотОбъект, ОбязательныеРеквизитыШапки, "", мСтрокаРеквизиты, "", , СтруктураШапкиДокумента);

	СтруктураОбязательныхПолей = Новый Структура(ОбязательныеРеквизитыШапки);

	// Документ должен принадлежать хотя бы к одному виду учета (управленческий, бухгалтерский, налоговый)
	ОбщегоНазначения.ПроверитьПринадлежностьКВидамУчета(СтруктураШапкиДокумента, Отказ, Заголовок);

	// Теперь вызовем общую процедуру проверки.
	ЗаполнениеДокументов.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект,СтруктураОбязательныхПолей, Отказ, Заголовок);

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Процедура проверяет правильность заполнения реквизитов документа
//
Процедура ПроверкаРеквизитовТЧ(СтруктураШапкиДокумента, Отказ, Заголовок) Экспорт

	РеквизитыТабОС = "Номенклатура,Количество,СчетУчета"; //через запятую

	УправлениеПроизводством.НепроверятьРеквизитыПоТипуУчета(ЭтотОбъект, РеквизитыТабОС, "", мСтрокаРеквизиты, "", "Оборудование", СтруктураШапкиДокумента);
	ЗаполнениеДокументов.ПроверитьЗаполнениеТабличнойЧасти(ЭтотОбъект, "Оборудование", Новый Структура(РеквизитыТабОС), Отказ, Заголовок);

КонецПроцедуры // ПроверкаРеквизитов()

Процедура ДвиженияПоРегистрам(РежимПроведения,СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок)

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	&ПустойДоговор КАК ДоговорКонтрагента,
	|	ПередачаОборудованияВМонтажОборудование.Ссылка.СчетУчетаОбъектаСтроительства КАК КорСчетСписания,
	|	ПередачаОборудованияВМонтажОборудование.Ссылка.ОбъектСтроительства КАК КорСубконтоСписания1,
	|	ПередачаОборудованияВМонтажОборудование.Ссылка.СтатьяЗатрат КАК КорСубконтоСписания2,
	|	&СпособСтроительства КАК КорСубконтоСписания3,
	|	ПередачаОборудованияВМонтажОборудование.Ссылка.ОбъектСтроительства КАК ОбъектСтроительства,
	|	ПередачаОборудованияВМонтажОборудование.Ссылка.СтатьяЗатрат КАК СтатьяЗатрат,
	|	ПередачаОборудованияВМонтажОборудование.Номенклатура,
	|	ПередачаОборудованияВМонтажОборудование.Количество КАК Количество,
	|	ПередачаОборудованияВМонтажОборудование.НомерСтроки,
	|	ПередачаОборудованияВМонтажОборудование.Ссылка.Организация,
	|	ПередачаОборудованияВМонтажОборудование.Ссылка.Склад,
	|	ПередачаОборудованияВМонтажОборудование.СчетУчета КАК СчетУчета,
	// {ОбособленныеПодразделения
	|	ПередачаОборудованияВМонтажОборудование.Ссылка.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ПередачаОборудованияВМонтажОборудование.Ссылка.ПодразделениеОрганизации КАК КорПодразделениеОрганизации,
	// }ОбособленныеПодразделения
	|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования
	|ИЗ
	|	Документ.ПередачаОборудованияВМонтаж.Оборудование КАК ПередачаОборудованияВМонтажОборудование
	|
	|ГДЕ
	|	ПередачаОборудованияВМонтажОборудование.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка",СтруктураШапкиДокумента.Ссылка );
	Запрос.УстановитьПараметр("СпособСтроительства",Перечисления.СпособыСтроительства.Подрядный );
	Запрос.УстановитьПараметр("ПустойДоговор",Справочники.ДоговорыКонтрагентов.ПустаяСсылка());

	Результат = Запрос.Выполнить();
	ТаблицаДвижений = Результат.Выгрузить();
	ТаблицаДвижений.Колонки.Добавить("Регистратор");
	ТаблицаДвижений.ЗаполнитьЗначения(ЭтотОбъект,"Регистратор");
	ТаблицаДвижений.Колонки.Добавить("СчетДоходов");

	УправлениеЗапасамиПартионныйУчет.ДвижениеПартийТоваров(Заголовок, ТаблицаДвижений, Истина, СтруктураШапкиДокумента.ОтражатьВНалоговомУчетеУСН, Отказ);
	
	// Отразим перемещение НДС на отдельный субсчет и списание по партиям НДС
    ДвиженияРегистровПодсистемыНДС(СтруктураШапкиДокумента, ТаблицаДвижений, Отказ, Заголовок);

КонецПроцедуры // ДвиженияПоРегистрам()

// Процедура вызывается из тела процедуры ДвиженияПоРегистрам
// Формирует движения по регистрам подсистемы учета НДС "НДСПокупки" 
Процедура ДвиженияРегистровПодсистемыНДС(СтруктураШапкиДокумента, ТаблицаПоОборудованию, Отказ, Заголовок)

	Если СтруктураШапкиДокумента.УпрощенныйУчетНДС Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПроводитьДокументПоРазделуУчета(СтруктураШапкиДокумента.Организация, Перечисления.РазделыУчета.НДС, СтруктураШапкиДокумента.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураШапкиДокумента.ОрганизацияПрименяетУСН тогда
		// Движения по этому документу делать не нужно
		Возврат;
	КонецЕсли;
	
	Если ТаблицаПоОборудованию.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	/////////////////////////////////////////////////////////////////////////////////
	// Подготовка таблицы списания по партиям товаров по данным партионного учета НДС
	ТаблицаНДСПартииСписания = УчетНДС.ПодготовитьТаблицуСписанияПартийДляНДС(СтруктураШапкиДокумента, ТаблицаПоОборудованию, Отказ, Заголовок);
	// Подготовка таблицы списания по партиям товаров по данным партионного учета НДС
	/////////////////////////////////////////////////////////////////////////////////
	
	Если ТаблицаНДСПартииСписания.Количество() = 0 Тогда
		//Партии не найдены или отражение в партионном учете НДС не производится.
		//Дальнейшая обработка не требуется
		Возврат;
	КонецЕсли; 
		
	/////////////////////////////////////////////////////////////////////////////////
	// НДС по партиям - отразить непосредственное списание.
	ТаблицаДвиженийПартии = Движения.НДСПоПриобретеннымЦенностям.ВыгрузитьКолонки();
	
	ОбщегоНазначения.ЗагрузитьВТаблицуЗначений(ТаблицаНДСПартииСписания, ТаблицаДвиженийПартии);
	Движения.НДСПоПриобретеннымЦенностям.мПериод          = Дата;
	Движения.НДСПоПриобретеннымЦенностям.мТаблицаДвижений = ТаблицаДвиженийПартии;
	Движения.НДСПоПриобретеннымЦенностям.ВыполнитьРасход();
	// НДС по партиям - отразить непосредственное списание.
	/////////////////////////////////////////////////////////////////////////////////

	/////////////////////////////////////////////////////////////////////////////////
	// НДС по ОС - при перемещении ТМЦ они могут перестать принадлежать к будущим ОС,
	// в этом случае необходимо снять блокировку с вычета.
	// Если в результате перемещения ТМЦ будет отнесено на счет учета ОС, необходимо включить блокировку.
	ТаблицаДвиженийПартии.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход,"ВидДвижения");
	УчетНДСФормированиеДвижений.СформироватьДвиженияПоРегиструНДСпоОСиНМА_ПеремещениеОборудования(СтруктураШапкиДокумента, ТаблицаДвиженийПартии, Движения, Отказ);
	// НДС по ОС
	/////////////////////////////////////////////////////////////////////////////////
	
КонецПроцедуры // ДвиженияРегистровПодсистемыНДС()

Процедура ОбработкаПроведения(Отказ,РежимПроведения)

	// Дерево значений, содержащее имена необходимых полей в запросе по шапке.
	Перем ДеревоПолейЗапросаПоШапке;

	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Проверка ручной корректировки
	Если ОбщегоНазначения.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект) Тогда
		Возврат
	КонецЕсли;

	// Заполним по шапке документа дерево параметров, нужных при проведении.
	ДеревоПолейЗапросаПоШапке = ОбщегоНазначения.СформироватьДеревоПолейЗапросаПоШапке();
	ОбщегоНазначения.ДобавитьСтрокуВДеревоПолейЗапросаПоШапке(ДеревоПолейЗапросаПоШапке, "Склад", "ВидСклада", "ВидСклада");

	// Сформируем запрос на дополнительные параметры, нужные при проведении, по данным шапки документа
	СтруктураШапкиДокумента = УправлениеЗапасами.СформироватьЗапросПоДеревуПолей(ЭтотОбъект, ДеревоПолейЗапросаПоШапке, СтруктураШапкиДокумента, мВалютаРегламентированногоУчета);
	СтруктураПолейУчетнойПолитикиНУ = Новый Структура("УпрощенныйУчетНДС");
	ОбщегоНазначения.ДополнитьПоложениямиУчетнойПолитики(СтруктураШапкиДокумента, СтруктураШапкиДокумента.Дата, Отказ, СтруктураШапкиДокумента.Организация, "Нал", СтруктураПолейУчетнойПолитикиНУ);

	// Заполним список реквизитов, зависимых от типа учета
	ЗаполнитьСписокРеквизитовЗависимыхОтТиповУчета();
	
	ПроверитьЗаполнениеШапки(СтруктураШапкиДокумента, Отказ, Заголовок);
	
	ПроверкаРеквизитовТЧ(СтруктураШапкиДокумента, Отказ, Заголовок);

	СтруктураПолей = Новый Структура;

	СтруктураПолей.Вставить("Номенклатура", "Номенклатура");
	СтруктураПолей.Вставить("Количество",   "Количество");
	СтруктураПолей.Вставить("СчетУчета",  "СчетУчета");

	РезультатЗапросаПоТЧ = ОбщегоНазначения.СформироватьЗапросПоТабличнойЧасти(ЭтотОбъект, "Оборудование", СтруктураПолей);
	ТаблицаПоТоварам     = РезультатЗапросаПоТЧ.Выгрузить();

	// Движения по документу
	Если Не Отказ Тогда
		ДвиженияПоРегистрам(РежимПроведения,СтруктураШапкиДокумента, ТаблицаПоТоварам, Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
			
	ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ, РучнаяКорректировка, Ложь);

КонецПроцедуры

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

