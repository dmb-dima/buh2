Перем мПоддержкаПБУ18;
Перем ОтражениеВУчете;
Перем ПериодГод2010 Экспорт;
Перем ПериодГод2011 Экспорт;
Перем УчетнаяПолитика;
Перем мСообщать Экспорт;

Процедура ОпределитьПериод()
	
	ПериодГод2010 = Год(ПериодРегистрации) >= 2010;
	ПериодГод2011 = Год(ПериодРегистрации) >= 2011;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАСЧЕТА ЕСН

Процедура ЗаписатьДанныеОДоходах(Отказ, ГоловнаяОрганизация, НаборЗаписейОДоходах)
	
	НаборЗаписейОДоходах.Очистить();
	УчетнаяПолитикаПоПерсоналуОрганизации = глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации");
	
	мГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
	Если ЗначениеЗаполнено(Организация) Тогда
		Если ЗначениеЗаполнено(Организация.ГоловнаяОрганизация) Тогда
			мГоловнаяОрганизация = Организация.ГоловнаяОрганизация;
		Иначе
			мГоловнаяОрганизация = Организация;
		КонецЕсли;			
	КонецЕсли;
		
	Если ПериодГод2010 Тогда //Страховые взносы
		
		// Основные начисления
		
		Для Каждого СтрокаТабличнойЧасти Из ОсновныеНачисления Цикл
			
			ПроверитьЗаполнениеСтрокиДохода(СтрокаТабличнойЧасти, Отказ, УчетнаяПолитикаПоПерсоналуОрганизации, мГоловнаяОрганизация);
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			
			Строка = НаборЗаписейОДоходах.Добавить();
			
			// свойства
			Строка.Период						= ПериодРегистрации;
			
			// Измерения
			Строка.ФизЛицо						= СтрокаТабличнойЧасти.Сотрудник.ФизЛицо;
			Строка.Организация					= ГоловнаяОрганизация;
			
			// ресурсы
			Строка.Результат					= СтрокаТабличнойЧасти.Результат;
			Строка.Скидка						= СтрокаТабличнойЧасти.Скидка;
			
			// реквизиты
			Строка.ВидДохода					= СтрокаТабличнойЧасти.ВидДохода;
			Строка.ВидРасчета					= СтрокаТабличнойЧасти.ВидРасчета;
			Строка.ОблагаетсяЕНВД				= СтрокаТабличнойЧасти.ОблагаетсяЕНВД;
			Строка.ОблагаетсяПоДополнительномуТарифу = СтрокаТабличнойЧасти.ОблагаетсяПоДополнительномуТарифу;
			Строка.ОбособленноеПодразделение	= Организация;
		КонецЦикла;
		
	Иначе // ЕСН
		
		// Основные начисления
		
		Для Каждого СтрокаТабличнойЧасти Из ОсновныеНачисления Цикл
			
			ПроверитьЗаполнениеСтрокиДохода(СтрокаТабличнойЧасти, Отказ, УчетнаяПолитикаПоПерсоналуОрганизации, мГоловнаяОрганизация);
			Если Отказ Тогда
				Продолжить;
			КонецЕсли;
			
			Строка = НаборЗаписейОДоходах.Добавить();
			
			// свойства
			Строка.Период						= ПериодРегистрации;
			
			// Измерения
			Строка.ФизЛицо						= СтрокаТабличнойЧасти.Сотрудник.ФизЛицо;
			Строка.Организация					= ГоловнаяОрганизация;
			
			// ресурсы
			Строка.Результат					= СтрокаТабличнойЧасти.Результат;
			Строка.Скидка						= СтрокаТабличнойЧасти.Скидка;
			
			// реквизиты
			Строка.КодДоходаЕСН					= СтрокаТабличнойЧасти.КодДоходаЕСН;
			Строка.ОблагаетсяЕНВД				= СтрокаТабличнойЧасти.ОблагаетсяЕНВД;
			Строка.ОбособленноеПодразделение	= Организация;
		КонецЦикла;
	КонецЕсли;
	
	Если Не Отказ Тогда
		// запишем движения
		НаборЗаписейОДоходах.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Предназначена для получения выборки из запроса с данными расчета ЕСН
// По текущему регистратору получает 
//    	данные о начисленной зарплате в разрезе кодов дохода ЕСН
// 		данные об инвалидности физлиц помесячно
//		присоединяет данные о скидках к доходам
//
// Параметры:
//  нет
// Возвращаемое значение:
//  Выборка результата запроса
//
Функция ПолучитьДанныеРасчетаЕСН(ГоловнаяОрганизация)
	
	Запрос = Новый Запрос();
    Запрос.УстановитьПараметр("парамКонецМесяцаПериодаРегистрации", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамОбособленноеПодразделение", Организация);
	Запрос.УстановитьПараметр("парамВидСтавокЕСНиПФР", ГоловнаяОрганизация.ВидСтавокЕСНиПФР);
    Запрос.УстановитьПараметр("парамНачалоГода", НачалоГода(ПериодРегистрации));
    Запрос.УстановитьПараметр("парамГодРасчета", Год(ПериодРегистрации));
    Запрос.УстановитьПараметр("парамМесяцРасчета", Месяц(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамПериодРегистрации", ПериодРегистрации);
    Запрос.УстановитьПараметр("парамМуж", Перечисления.ПолФизическихЛиц.Мужской);
    Запрос.УстановитьПараметр("УСН", Перечисления.СистемыНалогообложения.Упрощенная);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	ЛьготаИнвалидов = "100000";
	
	// ---------------------------------------------------------------------------
	// тексты запросов
	//

	// Периоды
	// Таблица список периодов
	// Поля:
	//		Период
	// Описание:
	//  список периодов - первые даты месяцеы с начала года по "текущий" для запроса период
	//
	
	// конец месяца налогового периода
	КонецМесяца = КонецМесяца(НачалоГода(ПериодРегистрации));
	// первый месяц
	ПериодыТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	// прибавим остальные месяцы
	Для Сч = 2 По Месяц(ПериодРегистрации) Цикл
		КонецМесяца = КонецМесяца(КонецМесяца+1);
    	ПериодыТекст = ПериодыТекст +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	
	
	// ФизлицаДляРасчета
	//
	ФизлицаДляРасчетаТекст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСНСведенияОДоходах.ФизЛицо КАК ФизЛицо
	|ИЗ
	|	РегистрНакопления.ЕСНСведенияОДоходах КАК ЕСНСведенияОДоходах
	|ГДЕ
	|	ЕСНСведенияОДоходах.Период = &парамПериодРегистрации
	|	И ЕСНСведенияОДоходах.ОбособленноеПодразделение = &парамОбособленноеПодразделение";

	// ДанныеОПравеНаПенсию
	// Таблица Таблица Данные о праве на пенсию: - таблица это список иностранцев и периодов
	// Поля:
	//		Физлицо, 
	//		Месяц - месяц налогового периода
	// 
	// Описание:
	//	Выбираем Из Списка периодов
	//	Внутреннее соединение с ГражданствоФизЛиц.СрезПоследних
	//  по равенству периодов
	//  условие: что физлицо - не имеет права на пенсию
	//
	ДанныеОПравеНаПенсиюТекст = "
	|ВЫБРАТЬ
	|	Месяц(Периоды.Период) КАК Месяц,
	|	ГражданствоФизЛиц.ФизЛицо КАК Физлицо
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		ГражданствоФизЛиц.ФизЛицо КАК Физлицо,
	|		МАКСИМУМ(ГражданствоФизЛиц.Период) КАК ПериодРегистра
	|	ИЗ
	|		("+ПериодыТекст+") КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|			ПО Периоды.Период >= ГражданствоФизЛиц.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ГражданствоФизЛиц.ФизЛицо,
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО ГражданствоФизЛиц.Период = Периоды.ПериодРегистра И ГражданствоФизЛиц.ФизЛицо = Периоды.Физлицо И ГражданствоФизЛиц.НеИмеетПравоНаПенсию
	|";
	
	
    // ДанныеОбИнвалидности
	// Таблица Данных об Инвалидности: - таблица это список инвалидов и периодов инвалидности
	// поля:
	//		Физлицо, 
	//		Месяц - месяц налогового периода
	// Описание:	
	//	Выбираем Из Список периодов (таблица - список периодов с начала года по текущий период
	//	Внутреннее соединение с СведенияОбИнвалидностиФизлиц.СрезПоследних
	//	по равенству периодов
	//  условие: что физлицо - Инвалид
	// 
	ДанныеОбИнвалидностиТекст = "
	|ВЫБРАТЬ
	|	Месяц(Периоды.Период) КАК Месяц,
	|	СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо,
	|		МАКСИМУМ(СведенияОбИнвалидностиФизлиц.Период) КАК ПериодРегистра
	|	ИЗ
	|		("+ПериодыТекст+") КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИнвалидностиФизлиц КАК СведенияОбИнвалидностиФизлиц
	|			ПО Периоды.Период >= СведенияОбИнвалидностиФизлиц.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СведенияОбИнвалидностиФизлиц.Физлицо,
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИнвалидностиФизлиц КАК СведенияОбИнвалидностиФизлиц
	|		ПО СведенияОбИнвалидностиФизлиц.Период = Периоды.ПериодРегистра И СведенияОбИнвалидностиФизлиц.Физлицо = Периоды.Физлицо И СведенияОбИнвалидностиФизлиц.Инвалидность
	|";


    // ИсчисленныйЕСН
	// Таблица Исчисленный ЕСН:
	// Поля:
	// 		ФизЛицо
	//		ФедеральныйБюджет
	//		ФСС
	//		ФФОМС
	//		ТФОМС
	//		ПФРНакопительная
	//		ПФРСтраховая
	//		ПФРНакопительнаяЕНВД
	//		ПФРСтраховаяЕНВД
	// Описание:
	// Выбираем из ЕСНИсчисленный налоги, исчисленные в целом по организации
    // с начала года по текущий период
	// группируем по физлицу
	ИсчисленныйЕСНТекст = 
	"ВЫБРАТЬ
	|	ЕСНИсчисленныйОбороты.ФизЛицо КАК ФизЛицо,
	|	ЕСНИсчисленныйОбороты.ФедеральныйБюджетОборот КАК ФедеральныйБюджет,
	|	ЕСНИсчисленныйОбороты.ФССОборот КАК ФСС,
	|	ЕСНИсчисленныйОбороты.ФФОМСОборот КАК ФФОМС,
	|	ЕСНИсчисленныйОбороты.ТФОМСОборот КАК ТФОМС,
	|	ЕСНИсчисленныйОбороты.ПФРНакопительнаяОборот КАК ПФРНакопительная,
	|	ЕСНИсчисленныйОбороты.ПФРСтраховаяОборот КАК ПФРСтраховая,
	|	ЕСНИсчисленныйОбороты.ПримененнаяЛьготаФБОборот КАК ПримененнаяЛьготаФБ,
	|	ЕСНИсчисленныйОбороты.ПримененнаяЛьготаФССОборот КАК ПримененнаяЛьготаФСС,
	|	ЕСНИсчисленныйОбороты.ПримененнаяЛьготаФОМСОборот КАК ПримененнаяЛьготаФОМС,
	|	ЕСНИсчисленныйОбороты.ПФРНакопительнаяЕНВДОборот КАК ПФРНакопительнаяЕНВД,
	|	ЕСНИсчисленныйОбороты.ПФРСтраховаяЕНВДОборот КАК ПФРСтраховаяЕНВД
	|ИЗ
	|	РегистрНакопления.ЕСНИсчисленный.Обороты(
	|		&парамНачалоГода,
	|		&парамКонецМесяцаПериодаРегистрации,
	|		,
	|		Организация = &парамГоловнаяОрганизация
	|		    И ФизЛицо В
	|		        (" + ФизлицаДляРасчетаТекст + ")) КАК ЕСНИсчисленныйОбороты";

    // ДоходыПоФизлицамМесяцамКодамТекст
	// Таблица ЕСН-доходы по ФЛ и Периодам:
    // Поля:
	//		ФизЛицо
	//		Месяц
	//		КодДоходаЕСН
	//		ОблагаетсяЕНВД
	//		Результат
	// Описание:
	// Выбираем из ЕСНОсновныеНачисления ,ЕСНДополнительныеНачисления и ЕСНСведенияОДоходах и суммируем результат
	// с начала года по текущий период
	
	ДоходыПоФизлицамМесяцамКодамТекст = 
	"ВЫБРАТЬ
	|	ЕСНСведенияОДоходах.ФизЛицо,
	|	МЕСЯЦ(ЕСНСведенияОДоходах.Период) КАК Месяц,
	|	ЕСНСведенияОДоходах.КодДоходаЕСН,
	|	ЕСНСведенияОДоходах.ОблагаетсяЕНВД,
	|	СУММА(ЕСНСведенияОДоходах.Результат - ЕСНСведенияОДоходах.Скидка) КАК ОблагаемыйДоход
	|ИЗ
	|	РегистрНакопления.ЕСНСведенияОДоходах КАК ЕСНСведенияОДоходах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + ФизлицаДляРасчетаТекст + ") КАК ФизлицаДляРасчета
	|		ПО ЕСНСведенияОДоходах.ФизЛицо = ФизлицаДляРасчета.ФизЛицо
	|ГДЕ
	|	ЕСНСведенияОДоходах.Период >= &парамНачалоГода
	|	И ЕСНСведенияОДоходах.Период <= &парамКонецМесяцаПериодаРегистрации
	|	И ЕСНСведенияОДоходах.Организация = &парамГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСНСведенияОДоходах.ФизЛицо,
	|	МЕСЯЦ(ЕСНСведенияОДоходах.Период),
	|	ЕСНСведенияОДоходах.КодДоходаЕСН,
	|	ЕСНСведенияОДоходах.ОблагаетсяЕНВД";


     // УчетнаяПолитика
	// Таблица УчетнаяПолитикаНалоговыйУчетУСН - это список периодов, когда организация переходила на УСН
	// поля:
	//		УСН, 
	//		Месяц - месяц налогового периода
	// Описание:	
	//	Выбираем Из Периоды (таблица - список периодов с начала года по текущий период)
	//	Внутреннее соединение с "псевдосрезом" последних регистра УчетнаяПолитика
	//	по равенству периодов
	//  условие: что организация использует УСН
	
 	УчетнаяПолитикаНалоговыйУчетУСН = 
	"ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	УчетнаяПолитика.Организация КАК УСН
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		МАКСИМУМ(УчетнаяПолитика.Период) КАК ПериодРегистра
	|	ИЗ
	|		("+ ПериодыТекст +") КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитика
	|		ПО Периоды.Период >= УчетнаяПолитика.Период И УчетнаяПолитика.Организация = &парамГоловнаяОрганизация
	|
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитика
	|		ПО Периоды.ПериодРегистра = УчетнаяПолитика.Период
	|		И УчетнаяПолитика.Организация = &парамГоловнаяОрганизация
	|		И УчетнаяПолитика.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)";	

	// ОблагаемаяБазаПоМесяцам
	// Таблица Баз для исчисления ЕСН по месяцам налогового периода:
	// Поля:
	//		ФизЛицо
	//		Месяц - месяц налогового периода
	//		БазаФедеральныйБюджет         - база для налогообложения ЕСН в части налога в федеральный бюджет
	//		ЛьготаИнвалидуФБ   			  - сумма льготы инвалиду по ЕСН в части налога в федеральный бюджет
	//		БазаФСС                       - база для налогообложения ЕСН в части налога в ФСС
	//		ЛьготаИнвалидуФСС  			  - сумма льготы инвалиду по ЕСН в части налога в ФСС
	//		БазаФОМС					  - база для налогообложения ЕСН в части налогов в ФОМС
	//		ЛьготаИнвалидуФОМС 			  - сумма льготы инвалиду по ЕСН в части налога в ФОМС
	//		БазаПФР			   			  - база для взносов в ПФР (включая доходы, облагаемые ЕНВД)
	//		БазаПФР_ЕНВД			   	  - сумма облагаемых ПФР доходов, подпадающих под ЕНВД
	//
	// Описание:
	//	
	// 	1) выбираем из ДоходыПоФизлицамМесяцамКодамТекст
	// 		и суммируем ОблагаемыйДоход для всех составных частей ЕСН. При этом учитывается входимость в базу
	// 		по флажку в справочнике - это База.
	//		Доход, облагаемый ЕНВД, не включаем в базу ЕСН. 
	//		Доход иностранца не включаем в базу ПФР.
	//		Для инвалидов считается база отдельно, но из неё исключаем договора подряда.
	//  2) суммировать ОблагаемыйДоход для всех составных частей ЕСН при входимости в базу и признака инвалидности - это ЛьготаИнвалиду по всем частям ЕСН

	
	ОблагаемаяБазаПоМесяцам = "
	|ВЫБРАТЬ
	|	Основная.ФизЛицо,
	|	Основная.Месяц,
	|	ВЫБОР КОГДА Основная.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет И НЕ Основная.ОблагаетсяЕНВД И УчетнаяПолитикаУСН.УСН ЕСТЬ NULL ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ КАК БазаФедеральныйБюджет,
	|	ВЫБОР КОГДА Основная.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет И НЕ Основная.ОблагаетсяЕНВД И УчетнаяПолитикаУСН.УСН ЕСТЬ NULL И НЕ((Инвалид.Физлицо) ЕСТЬ NULL ) ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ КАК ЛьготаИнвалидуФБ,
	|	ВЫБОР КОГДА Основная.КодДоходаЕСН.ВходитВБазуФСС И НЕ Основная.ОблагаетсяЕНВД И УчетнаяПолитикаУСН.УСН ЕСТЬ NULL ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ КАК БазаФСС,
	|	ВЫБОР КОГДА Основная.КодДоходаЕСН.ВходитВБазуФСС И НЕ Основная.ОблагаетсяЕНВД И УчетнаяПолитикаУСН.УСН ЕСТЬ NULL И НЕ((Инвалид.Физлицо) ЕСТЬ NULL ) ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ КАК ЛьготаИнвалидуФСС,
	|	ВЫБОР КОГДА Основная.КодДоходаЕСН.ВходитВБазуФОМС И НЕ Основная.ОблагаетсяЕНВД И УчетнаяПолитикаУСН.УСН ЕСТЬ NULL ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ КАК БазаФОМС,
	|	ВЫБОР КОГДА Основная.КодДоходаЕСН.ВходитВБазуФОМС И НЕ Основная.ОблагаетсяЕНВД И УчетнаяПолитикаУСН.УСН ЕСТЬ NULL И НЕ((Инвалид.Физлицо) ЕСТЬ NULL ) ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ КАК ЛьготаИнвалидуФОМС,
	|	ВЫБОР КОГДА Основная.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет И (Иностр.Физлицо) ЕСТЬ NULL ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ КАК БазаПФР,
	|	ВЫБОР КОГДА Основная.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет И Основная.ОблагаетсяЕНВД И (Иностр.Физлицо) ЕСТЬ NULL ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ КАК БазаПФР_ЕНВД
	|ИЗ
	|	("+ДоходыПоФизлицамМесяцамКодамТекст+") КАК Основная
	|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ДанныеОбИнвалидностиТекст+") КАК Инвалид
	|		ПО Инвалид.Физлицо = Основная.ФизЛицо И Инвалид.Месяц = Основная.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ДанныеОПравеНаПенсиюТекст+") КАК Иностр
	|		ПО Иностр.Физлицо = Основная.ФизЛицо И Иностр.Месяц = Основная.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ("+УчетнаяПолитикаНалоговыйУчетУСН+") КАК УчетнаяПолитикаУСН
	|		ПО Основная.Месяц = УчетнаяПолитикаУСН.Месяц
	|";

	// ОблагаемаяБазаПФРЗаМесяц
	// Таблица Баз для исчисления ПФР за месяц расчета
	// Поля:
	//		ФизЛицо
	//		БазаПФР			   			  - база для взносов в ПФР (включая доходы, облагаемые ЕНВД)
	//		БазаПФР_ЕНВД			   	  - сумма облагаемых ПФР доходов, подпадающих под ЕНВД
	//
	// Описание:
	//	
	// 	Выбираем из рег-ра ЕСНСведенияОДоходах подходящие доходы, при этом учитывается входимость в базу
	// 		по флажку в справочнике
	// 		и суммируем ОблагаемыйДоход - это База.
	//		Доход иностранца не включаем в базу ПФР.
	
	ОблагаемаяБазаПФРЗаМесяц = "
	|ВЫБРАТЬ
	|	Основная.ФизЛицо,
	|	СУММА(ВЫБОР КОГДА Иностр.Физлицо ЕСТЬ NULL ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ) КАК БазаПФР,
	|	СУММА(ВЫБОР КОГДА Основная.ОблагаетсяЕНВД И Иностр.Физлицо ЕСТЬ NULL ТОГДА Основная.ОблагаемыйДоход ИНАЧЕ 0 КОНЕЦ) КАК БазаПФР_ЕНВД
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСНСведенияОДоходах.ФизЛицо,
	|		ЕСНСведенияОДоходах.ОблагаетсяЕНВД,
	|		ЕСНСведенияОДоходах.Результат - ЕСНСведенияОДоходах.Скидка КАК ОблагаемыйДоход
	|	ИЗ
	|		РегистрНакопления.ЕСНСведенияОДоходах КАК ЕСНСведенияОДоходах
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + ФизлицаДляРасчетаТекст + ") КАК ФизлицаДляРасчета
	|			ПО ЕСНСведенияОДоходах.ФизЛицо = ФизлицаДляРасчета.ФизЛицо
	|	ГДЕ
	|		ЕСНСведенияОДоходах.Период МЕЖДУ &парамПериодРегистрации И &парамКонецМесяцаПериодаРегистрации
	|		И ЕСНСведенияОДоходах.Организация = &парамГоловнаяОрганизация
	|		И ЕСНСведенияОДоходах.КодДоходаЕСН.ВходитВБазуФедеральныйБюджет) КАК Основная
	|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ДанныеОПравеНаПенсиюТекст+") КАК Иностр
	|		ПО Иностр.Физлицо = Основная.ФизЛицо И Иностр.Месяц = &парамМесяцРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	Основная.ФизЛицо
	|";
	
    // БазыДляИсчисленияЕСН
	// Таблица Баз для исчисления ЕСН за год:
	// Поля:
	//		ФизЛицо
	//		БазаФедеральныйБюджет         - база для налогообложения ЕСН в части налога в федеральный бюджет
	//		ЛьготаИнвалидуФБ   			  - сумма льготы инвалиду по ЕСН в части налога в федеральный бюджет
	//		БазаФСС                       - база для налогообложения ЕСН в части налога в ФСС
	//		ЛьготаИнвалидуФСС  			  - сумма льготы инвалиду по ЕСН в части налога в ФСС
	//		БазаФОМС					  - база для налогообложения ЕСН в части налогов в ФОМС
	//		ЛьготаИнвалидуФОМС 			  - сумма льготы инвалиду по ЕСН в части налога в ФОМС
	//		БазаПФР			   			  - база для взносов в ПФР (включая доходы, облагаемые ЕНВД)
	//		БазаПФР_ЕНВД			   	  - сумма облагаемых ПФР доходов, подпадающих под ЕНВД
	//		НомерКолонкиСтавокПФР        - номер колонки ставок для взносов в ПФР в зависимости от пола и возраста физлица
	//
	// Описание:
	//   Суммируем базы ЕНС и ПФР за год.
	//	 Льготы инвалиду по ЕСН ограничиваем размером в 100000.
	//   Определяем номер колонки взносов в ПФР в зависимости от пола и возраста физлица

    // 
	БазыДляИсчисленияЕСНТекст = "
	|ВЫБРАТЬ 
	|	ФизЛицо,
	|	БазаФедеральныйБюджет,
	|	БазаФСС,
	|	БазаФОМС,
	|	БазаПФР,
	|	БазаПФР_ЕНВД,
	|   ВЫБОР КОГДА ЛьготаИнвалидуФБ 	> " + ЛьготаИнвалидов + " ТОГДА " + ЛьготаИнвалидов + " ИНАЧЕ ЛьготаИнвалидуФБ 	 КОНЕЦ КАК ЛьготаИнвалидуФБ,
	|   ВЫБОР КОГДА ЛьготаИнвалидуФСС 	> " + ЛьготаИнвалидов + " ТОГДА " + ЛьготаИнвалидов + " ИНАЧЕ ЛьготаИнвалидуФСС  КОНЕЦ КАК ЛьготаИнвалидуФСС,
	|   ВЫБОР КОГДА ЛьготаИнвалидуФОМС 	> " + ЛьготаИнвалидов + " ТОГДА " + ЛьготаИнвалидов + " ИНАЧЕ ЛьготаИнвалидуФОМС КОНЕЦ КАК ЛьготаИнвалидуФОМС,
	|   ВЫБОР КОГДА ГОД(БазаПоФизлицам.ФизЛицо.ДатаРождения) < 1967 ТОГДА 1 ИНАЧЕ 2 КОНЕЦ НомерКолонкиСтавокПФР
	|ИЗ 
	|(	ВЫБРАТЬ
	|		ФизЛицо,
	|		СУММА(БазаФедеральныйБюджет) КАК БазаФедеральныйБюджет,
	|		СУММА(БазаФСС) КАК БазаФСС,
	|		СУММА(БазаФОМС) КАК БазаФОМС,
	|		СУММА(ЛьготаИнвалидуФБ) КАК ЛьготаИнвалидуФБ,
	|		СУММА(ЛьготаИнвалидуФСС) КАК ЛьготаИнвалидуФСС,
	|		СУММА(ЛьготаИнвалидуФОМС) КАК ЛьготаИнвалидуФОМС,
	|		СУММА(БазаПФР) КАК БазаПФР,
	|		СУММА(БазаПФР_ЕНВД) КАК БазаПФР_ЕНВД
	|	ИЗ
	|		("+ОблагаемаяБазаПоМесяцам+") КАК БазаПоМесяцам 
	|
	|	СГРУППИРОВАТЬ ПО
	|		ФизЛицо) КАК БазаПоФизлицам
	|";

	
	// ИсчисленныйЕСНРезультат
	// Таблица исчисленного ЕСН:
	// Поля:
	//		ФизЛицо
    //		РезультатФедеральныйБюджет - налог в федеральный бюджет
    //		РезультатФФОМС
    //		РезультатТФОМС
    //		РезультатФСС
    //		РезультатПФРНакопительная
    //		РезультатПФРСтраховая
	//      ПримененнаяЛьготаФБ - примененная льгота инвалида
	//		ПримененнаяЛьготаФОМС - примененная льгота инвалида
	//		ПримененнаяЛьготаФСС - примененная льгота инвалида
    //		ПФРДоляЕНВД - доля доходов, подпадающих под ЕНВД, в общей базе взносов в ПФР
	//
	// Описание:
	// 	 выбираем из БазыДляИсчисленияЕСН
	//   соединение со СведенияОСтавкахЕСНиПФР несколько раз - по числу разных баз для налогообложения
	//   по соответствующему полю БазыДляИсчисленияЕСН.База...Полная
	//   соединяем с ИсчисленныйЕСН для вычитания уже исчисленного за год ЕСН
	// 
	
	// Тексты полей для выбора размера ставок в рублях и процентах для взносов в ПФР
	СтавкаПФРНакопительнаяВПроцентахТекст 	= "ВЫБОР КОГДА ДоходыПоФЛ.НомерКолонкиСтавокПФР = 1 ТОГДА СтавкиПФР.ПФРНакопительная1вПроцентах ИНАЧЕ СтавкиПФР.ПФРНакопительная2вПроцентах КОНЕЦ"; 
	СтавкаПФРНакопительнаяВРубляхТекст 		= СтрЗаменить(СтавкаПФРНакопительнаяВПроцентахТекст, "вПроцентах", "вРублях");
	СтавкаПФРСтраховаяВПроцентахТекст 		= СтрЗаменить(СтавкаПФРНакопительнаяВПроцентахТекст, "Накопительная", "Страховая");
	СтавкаПФРСтраховаяВРубляхТекст 			= СтрЗаменить(СтавкаПФРНакопительнаяВРубляхТекст, 	 "Накопительная", "Страховая");
	
	Если ПериодРегистрации >= '20070101' Тогда
		ИсчисленныйЕСНРезультатТекст = "
		|ВЫБРАТЬ
		|	ДоходыПоФЛ.ФизЛицо,
		|	СтавкиФедеральныйБюджет.ФедеральныйБюджетВРублях + (ДоходыПоФЛ.БазаФедеральныйБюджет  - СтавкиФедеральныйБюджет.СуммаДоходаС)* СтавкиФедеральныйБюджет.ФедеральныйБюджетВПроцентах / 100 - ЕСТЬNULL(ЕСНИсчисленный.ФедеральныйБюджет, 0) КАК РезультатФедеральныйБюджет,
		|	СтавкиФСС.ФССвРублях + (ДоходыПоФЛ.БазаФСС - СтавкиФСС.СуммаДоходаС) * СтавкиФСС.ФССВПроцентах / 100 - ЕСТЬNULL(ЕСНИсчисленный.ФСС, 0) КАК РезультатФСС,
		|	СтавкиФОМС.ФФОМСвРублях + (ДоходыПоФЛ.БазаФОМС - СтавкиФОМС.СуммаДоходаС) * СтавкиФОМС.ФФОМСВПроцентах / 100 - ЕСТЬNULL(ЕСНИсчисленный.ФФОМС, 0) КАК РезультатФФОМС,
		|	СтавкиФОМС.ТФОМСвРублях + (ДоходыПоФЛ.БазаФОМС - СтавкиФОМС.СуммаДоходаС) * СтавкиФОМС.ТФОМСВПроцентах / 100 - ЕСТЬNULL(ЕСНИсчисленный.ТФОМС, 0) КАК РезультатТФОМС,
		|	ВЫБОР КОГДА ДоходыПоФЛ.БазаПФР = 0 ТОГДА 0 ИНАЧЕ " + СтавкаПФРНакопительнаяВРубляхТекст + " + (ДоходыПоФЛ.БазаПФР - СтавкиПФР.СуммаДоходаС) * " + СтавкаПФРНакопительнаяВПроцентахТекст + " / 100 - ЕСТЬNULL(ЕСНИсчисленный.ПФРНакопительная, 0) КОНЕЦ КАК РезультатПФРНакопительная,
		|	ВЫБОР КОГДА ДоходыПоФЛ.БазаПФР = 0 ТОГДА 0 ИНАЧЕ " + СтавкаПФРСтраховаяВРубляхТекст + 	 " + (ДоходыПоФЛ.БазаПФР - СтавкиПФР.СуммаДоходаС) * " + СтавкаПФРСтраховаяВПроцентахТекст 	 + " / 100 - ЕСТЬNULL(ЕСНИсчисленный.ПФРСтраховая, 0) КОНЕЦ КАК РезультатПФРСтраховая,
		|	ДоходыПоФЛ.ЛьготаИнвалидуФБ 	- ЕСТЬNULL(ЕСНИсчисленный.ПримененнаяЛьготаФБ, 0) Как ПримененнаяЛьготаФБ,
		|	ДоходыПоФЛ.ЛьготаИнвалидуФСС 	- ЕСТЬNULL(ЕСНИсчисленный.ПримененнаяЛьготаФСС, 0) Как ПримененнаяЛьготаФСС,
		|	ДоходыПоФЛ.ЛьготаИнвалидуФОМС   - ЕСТЬNULL(ЕСНИсчисленный.ПримененнаяЛьготаФОМС, 0) Как ПримененнаяЛьготаФОМС,
		|	ВЫБОР КОГДА ЕСТЬNULL(БазаПФРЗаМесяц.БазаПФР,0) <> 0 ТОГДА БазаПФРЗаМесяц.БазаПФР_ЕНВД/БазаПФРЗаМесяц.БазаПФР ИНАЧЕ 0 КОНЕЦ * (" + СтавкаПФРНакопительнаяВРубляхТекст + " + (ДоходыПоФЛ.БазаПФР - СтавкиПФР.СуммаДоходаС) * " + СтавкаПФРНакопительнаяВПроцентахТекст + " / 100 - ЕСТЬNULL(ЕСНИсчисленный.ПФРНакопительная, 0)) КАК РезультатПФРНакопительнаяЕНВД,
		|	ВЫБОР КОГДА ЕСТЬNULL(БазаПФРЗаМесяц.БазаПФР,0) <> 0 ТОГДА БазаПФРЗаМесяц.БазаПФР_ЕНВД/БазаПФРЗаМесяц.БазаПФР ИНАЧЕ 0 КОНЕЦ * (" + СтавкаПФРСтраховаяВРубляхТекст 	 + " + (ДоходыПоФЛ.БазаПФР - СтавкиПФР.СуммаДоходаС) * " + СтавкаПФРСтраховаяВПроцентахТекст	+ " / 100 - ЕСТЬNULL(ЕСНИсчисленный.ПФРСтраховая, 0)) КАК РезультатПФРСтраховаяЕНВД
		|ИЗ
		|	("+БазыДляИсчисленияЕСНТекст+") КАК ДоходыПоФЛ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ОблагаемаяБазаПФРЗаМесяц+") КАК БазаПФРЗаМесяц  	
		|		ПО БазаПФРЗаМесяц.ФизЛицо = ДоходыПоФЛ.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК СтавкиФедеральныйБюджет
		|		ПО СтавкиФедеральныйБюджет.ВидСтавокЕСНиПФР = &парамВидСтавокЕСНиПФР И СтавкиФедеральныйБюджет.Год = &парамГодРасчета И ДоходыПоФЛ.БазаФедеральныйБюджет > СтавкиФедеральныйБюджет.СуммаДоходаС И ДоходыПоФЛ.БазаФедеральныйБюджет <= СтавкиФедеральныйБюджет.СуммаДоходаПо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК СтавкиПФР
		|		ПО СтавкиПФР.ВидСтавокЕСНиПФР = &парамВидСтавокЕСНиПФР И СтавкиПФР.Год = &парамГодРасчета И ДоходыПоФЛ.БазаПФР > СтавкиПФР.СуммаДоходаС И ДоходыПоФЛ.БазаПФР <= СтавкиПФР.СуммаДоходаПо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК СтавкиФСС
		|		ПО СтавкиФСС.ВидСтавокЕСНиПФР = &парамВидСтавокЕСНиПФР И СтавкиФСС.Год = &парамГодРасчета И ДоходыПоФЛ.БазаФСС > СтавкиФСС.СуммаДоходаС И ДоходыПоФЛ.БазаФСС <= СтавкиФСС.СуммаДоходаПо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК СтавкиФОМС
		|		ПО СтавкиФОМС.ВидСтавокЕСНиПФР = &парамВидСтавокЕСНиПФР И СтавкиФОМС.Год = &парамГодРасчета И ДоходыПоФЛ.БазаФОМС > СтавкиФОМС.СуммаДоходаС И ДоходыПоФЛ.БазаФОМС <= СтавкиФОМС.СуммаДоходаПо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ИсчисленныйЕСНТекст+") КАК ЕСНИсчисленный
		|		ПО ЕСНИсчисленный.ФизЛицо = ДоходыПоФЛ.ФизЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДоходыПоФЛ.ФизЛицо.Наименование
		|";
		
	Иначе
		
		ИсчисленныйЕСНРезультатТекст = "
		|ВЫБРАТЬ
		|	ДоходыПоФЛ.ФизЛицо,
		|	СтавкиФедеральныйБюджет.ФедеральныйБюджетВРублях + (ДоходыПоФЛ.БазаФедеральныйБюджет  - СтавкиФедеральныйБюджет.СуммаДоходаС)* СтавкиФедеральныйБюджет.ФедеральныйБюджетВПроцентах / 100 - ЕСТЬNULL(ЕСНИсчисленный.ФедеральныйБюджет, 0) КАК РезультатФедеральныйБюджет,
		|	СтавкиФСС.ФССвРублях + (ДоходыПоФЛ.БазаФСС - СтавкиФСС.СуммаДоходаС) * СтавкиФСС.ФССВПроцентах / 100 - ЕСТЬNULL(ЕСНИсчисленный.ФСС, 0) КАК РезультатФСС,
		|	СтавкиФОМС.ФФОМСвРублях + (ДоходыПоФЛ.БазаФОМС - СтавкиФОМС.СуммаДоходаС) * СтавкиФОМС.ФФОМСВПроцентах / 100 - ЕСТЬNULL(ЕСНИсчисленный.ФФОМС, 0) КАК РезультатФФОМС,
		|	СтавкиФОМС.ТФОМСвРублях + (ДоходыПоФЛ.БазаФОМС - СтавкиФОМС.СуммаДоходаС) * СтавкиФОМС.ТФОМСВПроцентах / 100 - ЕСТЬNULL(ЕСНИсчисленный.ТФОМС, 0) КАК РезультатТФОМС,
		|	ВЫБОР КОГДА ДоходыПоФЛ.БазаПФР = 0 ТОГДА 0 ИНАЧЕ " + СтавкаПФРНакопительнаяВРубляхТекст + " + (ДоходыПоФЛ.БазаПФР - СтавкиПФР.СуммаДоходаС) * " + СтавкаПФРНакопительнаяВПроцентахТекст + " / 100 - ЕСТЬNULL(ЕСНИсчисленный.ПФРНакопительная, 0) КОНЕЦ КАК РезультатПФРНакопительная,
		|	ВЫБОР КОГДА ДоходыПоФЛ.БазаПФР = 0 ТОГДА 0 ИНАЧЕ " + СтавкаПФРСтраховаяВРубляхТекст + 	 " + (ДоходыПоФЛ.БазаПФР - СтавкиПФР.СуммаДоходаС) * " + СтавкаПФРСтраховаяВПроцентахТекст 	 + " / 100 - ЕСТЬNULL(ЕСНИсчисленный.ПФРСтраховая, 0) КОНЕЦ КАК РезультатПФРСтраховая,
		|	ДоходыПоФЛ.ЛьготаИнвалидуФБ 	- ЕСТЬNULL(ЕСНИсчисленный.ПримененнаяЛьготаФБ, 0) Как ПримененнаяЛьготаФБ,
		|	ДоходыПоФЛ.ЛьготаИнвалидуФСС 	- ЕСТЬNULL(ЕСНИсчисленный.ПримененнаяЛьготаФСС, 0) Как ПримененнаяЛьготаФСС,
		|	ДоходыПоФЛ.ЛьготаИнвалидуФОМС   - ЕСТЬNULL(ЕСНИсчисленный.ПримененнаяЛьготаФОМС, 0) Как ПримененнаяЛьготаФОМС,
		|	ВЫБОР КОГДА ДоходыПоФЛ.БазаПФР <> 0 ТОГДА ДоходыПоФЛ.БазаПФР_ЕНВД/ДоходыПоФЛ.БазаПФР ИНАЧЕ 0 КОНЕЦ * (" + СтавкаПФРНакопительнаяВРубляхТекст + " + (ДоходыПоФЛ.БазаПФР - СтавкиПФР.СуммаДоходаС) * " + СтавкаПФРНакопительнаяВПроцентахТекст + " / 100) - ЕСТЬNULL(ЕСНИсчисленный.ПФРНакопительнаяЕНВД, 0) КАК РезультатПФРНакопительнаяЕНВД,
		|	ВЫБОР КОГДА ДоходыПоФЛ.БазаПФР <> 0 ТОГДА ДоходыПоФЛ.БазаПФР_ЕНВД/ДоходыПоФЛ.БазаПФР ИНАЧЕ 0 КОНЕЦ * (" + СтавкаПФРСтраховаяВРубляхТекст 	 + " + (ДоходыПоФЛ.БазаПФР - СтавкиПФР.СуммаДоходаС) * " + СтавкаПФРСтраховаяВПроцентахТекст	+ " / 100) - ЕСТЬNULL(ЕСНИсчисленный.ПФРСтраховаяЕНВД, 0) КАК РезультатПФРСтраховаяЕНВД
		|ИЗ
		|	("+БазыДляИсчисленияЕСНТекст+") КАК ДоходыПоФЛ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК СтавкиФедеральныйБюджет
		|		ПО СтавкиФедеральныйБюджет.ВидСтавокЕСНиПФР = &парамВидСтавокЕСНиПФР И СтавкиФедеральныйБюджет.Год = &парамГодРасчета И ДоходыПоФЛ.БазаФедеральныйБюджет > СтавкиФедеральныйБюджет.СуммаДоходаС И ДоходыПоФЛ.БазаФедеральныйБюджет <= СтавкиФедеральныйБюджет.СуммаДоходаПо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК СтавкиПФР
		|		ПО СтавкиПФР.ВидСтавокЕСНиПФР = &парамВидСтавокЕСНиПФР И СтавкиПФР.Год = &парамГодРасчета И ДоходыПоФЛ.БазаПФР > СтавкиПФР.СуммаДоходаС И ДоходыПоФЛ.БазаПФР <= СтавкиПФР.СуммаДоходаПо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК СтавкиФСС
		|		ПО СтавкиФСС.ВидСтавокЕСНиПФР = &парамВидСтавокЕСНиПФР И СтавкиФСС.Год = &парамГодРасчета И ДоходыПоФЛ.БазаФСС > СтавкиФСС.СуммаДоходаС И ДоходыПоФЛ.БазаФСС <= СтавкиФСС.СуммаДоходаПо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСтавкахЕСНиПФР КАК СтавкиФОМС
		|		ПО СтавкиФОМС.ВидСтавокЕСНиПФР = &парамВидСтавокЕСНиПФР И СтавкиФОМС.Год = &парамГодРасчета И ДоходыПоФЛ.БазаФОМС > СтавкиФОМС.СуммаДоходаС И ДоходыПоФЛ.БазаФОМС <= СтавкиФОМС.СуммаДоходаПо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ("+ИсчисленныйЕСНТекст+") КАК ЕСНИсчисленный
		|		ПО ЕСНИсчисленный.ФизЛицо = ДоходыПоФЛ.ФизЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДоходыПоФЛ.ФизЛицо.Наименование
		|";
		
	КонецЕсли;
	
	Запрос.Текст = ИсчисленныйЕСНРезультатТекст;
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции  // ПолучитьДанныеРасчетаЕСН

// Процедура рассчитывает ЕСН по введенным доходам
Процедура РасчетЕСН(ГоловнаяОрганизация)
	
	// получаем ЕСН по периоду регистрации
	ДанныеДляРасчетаЕСН = ПолучитьДанныеРасчетаЕСН(ГоловнаяОрганизация);
	
	// очищаем табличную часть
	ИсчисленныйЕСН.Очистить();
	
	// заполняем движения по регистру исчисленного ЕСН-ПФР
	Пока ДанныеДляРасчетаЕСН.Следующий() Цикл
		
		// Пропустим нулевые строки
		Если ДанныеДляРасчетаЕСН.РезультатФедеральныйБюджет = 0 И ДанныеДляРасчетаЕСН.РезультатФСС = 0 
			И ДанныеДляРасчетаЕСН.РезультатФФОМС = 0 И ДанныеДляРасчетаЕСН.РезультатТФОМС = 0 
			И ДанныеДляРасчетаЕСН.РезультатПФРНакопительная = 0 И ДанныеДляРасчетаЕСН.РезультатПФРСтраховая = 0
			И ДанныеДляРасчетаЕСН.ПримененнаяЛьготаФБ = 0 И ДанныеДляРасчетаЕСН.ПримененнаяЛьготаФСС = 0
			И ДанныеДляРасчетаЕСН.ПримененнаяЛьготаФОМС = 0 Тогда			
			Продолжить;
		КонецЕсли; 
		
		// табличная часть
		НоваяСтрока = ИсчисленныйЕСН.Добавить();
		
		НоваяСтрока.Физлицо = ДанныеДляРасчетаЕСН.ФизЛицо;
		//НоваяСтрока.Организация = ГоловнаяОрганизация;
		//НоваяСтрока.ОбособленноеПодразделение = Организация;
		НоваяСтрока.ФедеральныйБюджет = ДанныеДляРасчетаЕСН.РезультатФедеральныйБюджет;
		НоваяСтрока.ФСС = ДанныеДляРасчетаЕСН.РезультатФСС;
		НоваяСтрока.ФФОМС = ДанныеДляРасчетаЕСН.РезультатФФОМС;
		НоваяСтрока.ТФОМС = ДанныеДляРасчетаЕСН.РезультатТФОМС;
		НоваяСтрока.ПФРНакопительная = ДанныеДляРасчетаЕСН.РезультатПФРНакопительная;
		НоваяСтрока.ПФРСтраховая = ДанныеДляРасчетаЕСН.РезультатПФРСтраховая;
		НоваяСтрока.ПримененнаяЛьготаФБ = ДанныеДляРасчетаЕСН.ПримененнаяЛьготаФБ;
		НоваяСтрока.ПримененнаяЛьготаФСС = ДанныеДляРасчетаЕСН.ПримененнаяЛьготаФСС;
		НоваяСтрока.ПримененнаяЛьготаФОМС = ДанныеДляРасчетаЕСН.ПримененнаяЛьготаФОМС;
		
		НоваяСтрока.ПФРНакопительнаяЕНВД = ДанныеДляРасчетаЕСН.РезультатПФРНакопительнаяЕНВД;
		НоваяСтрока.ПФРСтраховаяЕНВД = ДанныеДляРасчетаЕСН.РезультатПФРСтраховаяЕНВД;
		
	КонецЦикла;
	
КонецПроцедуры  // РасчетЕСН

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ РАСЧЕТА ЕСН

// Предназначена для получения выборки из запроса с данными расчета 
// По текущему регистратору получает 
//    	данные о начисленной зарплате в разрезе кодов дохода 
// 		данные об инвалидности физлиц помесячно
//		присоединяет данные о скидках к доходам
//
// Параметры:
//  нет
// Возвращаемое значение:
//  Выборка результата запроса
//
Функция ПолучитьДанныеРасчетаСтраховыхВзносовЗа2010Год(ГоловнаяОрганизация, ОрганичениеНаСотрудников=Неопределено, ДанныеОДоходах)
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("парамКонецМесяцаПериодаРегистрации", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамОбособленноеПодразделение", Организация);
	Запрос.УстановитьПараметр("парамНачалоГода", НачалоГода(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамКонецГода", КонецГода(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамГодРасчета", Год(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамМесяцРасчета", Месяц(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамПредыдущийМесяц", Месяц(ПериодРегистрации) - 1);
	Запрос.УстановитьПараметр("парамПериодРегистрации", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	ПредельнаяСумма = РегистрыСведений.ПредельнаяВеличинаБазыСтраховыхВзносов.ПолучитьПоследнее(КонецМесяца(ПериодРегистрации)).Размер;
	Запрос.УстановитьПараметр("ПредельнаяСумма", ?(ЗначениеЗаполнено(ПредельнаяСумма),ПредельнаяСумма,0));
	Ставка = РегистрыСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.ПолучитьПоследнее(КонецМесяца(ПериодРегистрации), Новый Структура("Организация", Организация)).Ставка;
	Запрос.УстановитьПараметр("СтавкаВзносаФСС_НС_ПЗ", Ставка / 100);
	
	// ---------------------------------------------------------------------------
	// тексты запросов
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОДоходах.ФизЛицо,
	|	МЕСЯЦ(СведенияОДоходах.Период) КАК Месяц,
	|	СведенияОДоходах.ОблагаетсяЕНВД,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СведенияОДоходах.ВидДохода,
	|	СведенияОДоходах.Результат - СведенияОДоходах.Скидка КАК ОблагаемыйДоход
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	&ДанныеОДоходах КАК СведенияОДоходах";
	Запрос.УстановитьПараметр("ДанныеОДоходах", ДанныеОДоходах);
	Запрос.Выполнить();
	//

	// ФизлицаДляРасчета
	//
	Если ОрганичениеНаСотрудников = Неопределено Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо
		|ПОМЕСТИТЬ ВТСписокСотрудников
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СведенияОДоходах
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) = &парамПериодРегистрации
		|	И СведенияОДоходах.ОбособленноеПодразделение = &парамОбособленноеПодразделение
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОДоходах.ФизЛицо
		|ИЗ
		|	ВТДанныеДокумента КАК СведенияОДоходах
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
		Запрос.Выполнить();
		
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
        |   ТаблицаСотрудников.ФизЛицо КАК ФизЛицо
        |ПОМЕСТИТЬ ВТСписокСотрудников
        |ИЗ
        |   &парамТаблицаСотрудников КАК ТаблицаСотрудников
        |
        |ИНДЕКСИРОВАТЬ ПО
        |   ФизЛицо";
		Запрос.УстановитьПараметр("парамТаблицаСотрудников", ОрганичениеНаСотрудников);
		Запрос.Выполнить();
	КонецЕсли;
	
	// Периоды
	// Таблица список периодов
	// Поля:
	//		Период
	// Описание:
	//  список периодов - первые даты месяцев с начала года по "текущий" для запроса период
	//
	
	// конец месяца налогового периода
	КонецМесяца = КонецМесяца(НачалоГода(ПериодРегистрации));
	// первый месяц
	ПериодыТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период ПОМЕСТИТЬ ВТПериоды";
	// прибавим остальные месяцы
	Для Сч = 2 По Месяц(ПериодРегистрации) Цикл
		КонецМесяца = КонецМесяца(КонецМесяца+1);
    	ПериодыТекст = ПериодыТекст +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	Запрос.Текст = ПериодыТекст;
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчет.ВидТарифаСтраховыхВзносов, ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим)) КАК ВидТарифаСтраховыхВзносов
	|ПОМЕСТИТЬ ВТИспользуемыеТарифы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
	|			ПО Периоды.Период >= УчетнаяПолитикаНалоговыйУчет.Период
	|				И (УчетнаяПолитикаНалоговыйУчет.Организация = &парамГоловнаяОрганизация)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.Период) КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
	|		ПО Периоды.ПериодРегистра = УчетнаяПолитикаНалоговыйУчет.Период
	|			И (УчетнаяПолитикаНалоговыйУчет.Организация = &парамГоловнаяОрганизация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТарифСтраховыхВзносовСрезПоследних.ВидТарифа КАК ВидТарифа,
	|	ТарифСтраховыхВзносовСрезПоследних.ПФР,
	|	ТарифСтраховыхВзносовСрезПоследних.ПФРНакопительная,
	|	ТарифСтраховыхВзносовСрезПоследних.ФСС,
	|	ТарифСтраховыхВзносовСрезПоследних.ФФОМС,
	|	ТарифСтраховыхВзносовСрезПоследних.ТФОМС
	|ПОМЕСТИТЬ ВТТарифы
	|ИЗ
	|	РегистрСведений.ТарифСтраховыхВзносов.СрезПоследних(&парамКонецГода, ) КАК ТарифСтраховыхВзносовСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидТарифа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ГражданствоФизЛиц.ФизЛицо КАК Физлицо,
	|	ЕСТЬNULL(ГражданствоФизЛиц.НеИмеетПравоНаПенсию, ЛОЖЬ) КАК НеИмеетПравоНаПенсию,
	|	ЕСТЬNULL(ГражданствоФизЛиц.НеИмеетПравоНаМедицинскоеСтрахование, ЛОЖЬ) КАК НеИмеетПравоНаМедицинскоеСтрахование
	|ПОМЕСТИТЬ ВТДанныеОПравеНаПенсию
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		ГражданствоФизЛиц.ФизЛицо КАК Физлицо,
	|		МАКСИМУМ(ГражданствоФизЛиц.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|			ПО Периоды.Период >= ГражданствоФизЛиц.Период
	|	ГДЕ
	|		ГражданствоФизЛиц.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТСписокСотрудников КАК СписокСотрудников)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ГражданствоФизЛиц.ФизЛицо,
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО Периоды.ПериодРегистра = ГражданствоФизЛиц.Период
	|			И Периоды.Физлицо = ГражданствоФизЛиц.ФизЛицо
	|			И (ГражданствоФизЛиц.НеИмеетПравоНаПенсию
	|				ИЛИ ГражданствоФизЛиц.НеИмеетПравоНаМедицинскоеСтрахование)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо,
	|	СведенияОбИнвалидностиФизлиц.Период КАК Период,
	|	СведенияОбИнвалидностиФизлиц.Инвалидность
	|ПОМЕСТИТЬ ВТ_СведенияОбИнвалидности
	|ИЗ
	|	РегистрСведений.СведенияОбИнвалидностиФизлиц КАК СведенияОбИнвалидностиФизлиц
	|ГДЕ
	|	СведенияОбИнвалидностиФизлиц.Физлицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокСотрудников КАК СписокСотрудников)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо
	|ПОМЕСТИТЬ ВТДанныеОбИнвалидности
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо,
	|		МАКСИМУМ(СведенияОбИнвалидностиФизлиц.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СведенияОбИнвалидности КАК СведенияОбИнвалидностиФизлиц
	|			ПО Периоды.Период >= СведенияОбИнвалидностиФизлиц.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СведенияОбИнвалидностиФизлиц.Физлицо,
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СведенияОбИнвалидности КАК СведенияОбИнвалидностиФизлиц
	|		ПО (СведенияОбИнвалидностиФизлиц.Период = Периоды.ПериодРегистра)
	|			И Периоды.Физлицо = СведенияОбИнвалидностиФизлиц.Физлицо
	|			И (СведенияОбИнвалидностиФизлиц.Инвалидность)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСНСведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|	МЕСЯЦ(ЕСНСведенияОДоходах.Период) КАК Месяц,
	|	ЕСНСведенияОДоходах.ОблагаетсяЕНВД,
	|	ЕСНСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	ЕСНСведенияОДоходах.ВидДохода.ВходитВБазуПФР КАК ВходитВБазуПФР,
	|	ЕСНСведенияОДоходах.ВидДохода.ВходитВБазуФСС КАК ВходитВБазуФСС,
	|	ЕСНСведенияОДоходах.ВидДохода.ВходитВБазуФОМС КАК ВходитВБазуФОМС,
	|	ЕСНСведенияОДоходах.РезультатОборот - ЕСНСведенияОДоходах.СкидкаОборот КАК ОблагаемыйДоход
	|ПОМЕСТИТЬ ВТДоходыПоФизлицамМесяцам
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах.Обороты(
	|			&парамНачалоГода,
	|			&парамКонецМесяцаПериодаРегистрации,
	|			Месяц,
	|			Организация = &парамГоловнаяОрганизация
	|				И Физлицо В
	|					(ВЫБРАТЬ
	|						СписокСотрудников.ФизЛицо
	|					ИЗ
	|						ВТСписокСотрудников КАК СписокСотрудников)) КАК ЕСНСведенияОДоходах
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|	МЕСЯЦ(СтраховыеВзносыСведенияОДоходах.Период),
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуПФР,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФСС,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФОМС,
	|	-СтраховыеВзносыСведенияОДоходах.Результат + СтраховыеВзносыСведенияОДоходах.Скидка
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
	|ГДЕ
	|	СтраховыеВзносыСведенияОДоходах.Регистратор = &Регистратор
	|	И СтраховыеВзносыСведенияОДоходах.Организация = &парамГоловнаяОрганизация
	|	И СтраховыеВзносыСведенияОДоходах.ФизЛицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокСотрудников КАК СписокСотрудников)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|	СтраховыеВзносыСведенияОДоходах.Месяц,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуПФР,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФСС,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФОМС,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаемыйДоход
	|ИЗ
	|	ВТДанныеДокумента КАК СтраховыеВзносыСведенияОДоходах
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РанееИсчисленныеВзносы.ФизЛицо КАК ФизЛицо,
	|	МЕСЯЦ(РанееИсчисленныеВзносы.Период) КАК Месяц,
	|	РанееИсчисленныеВзносы.ФССОборот,
	|	РанееИсчисленныеВзносы.ФФОМСОборот,
	|	РанееИсчисленныеВзносы.ТФОМСОборот,
	|	РанееИсчисленныеВзносы.ПФРНакопительнаяОборот,
	|	РанееИсчисленныеВзносы.ПФРСтраховаяОборот,
	|	РанееИсчисленныеВзносы.ПФРНакопительнаяЕНВДОборот,
	|	РанееИсчисленныеВзносы.ПФРСтраховаяЕНВДОборот,
	|	РанееИсчисленныеВзносы.ПФРПоДополнительномуТарифуОборот
	|ПОМЕСТИТЬ ВТРанееИсчисленныеВзносыПоМесяцам
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыИсчисленные.Обороты(
	|			&парамНачалоГода,
	|			&парамКонецМесяцаПериодаРегистрации,
	|			Месяц,
	|			Организация = &парамГоловнаяОрганизация
	|				И ФизЛицо В
	|					(ВЫБРАТЬ
	|						СписокСотрудников.ФизЛицо
	|					ИЗ
	|						ВТСписокСотрудников КАК СписокСотрудников)) КАК РанееИсчисленныеВзносы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыИсчисленные.ФизЛицо,
	|	МЕСЯЦ(СтраховыеВзносыИсчисленные.Период),
	|	-СтраховыеВзносыИсчисленные.ФСС,
	|	-СтраховыеВзносыИсчисленные.ФФОМС,
	|	-СтраховыеВзносыИсчисленные.ТФОМС,
	|	-СтраховыеВзносыИсчисленные.ПФРНакопительная,
	|	-СтраховыеВзносыИсчисленные.ПФРСтраховая,
	|	-СтраховыеВзносыИсчисленные.ПФРНакопительнаяЕНВД,
	|	-СтраховыеВзносыИсчисленные.ПФРСтраховаяЕНВД,
	|	-СтраховыеВзносыИсчисленные.ПФРПоДополнительномуТарифу
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыИсчисленные КАК СтраховыеВзносыИсчисленные
	|ГДЕ
	|	СтраховыеВзносыИсчисленные.Регистратор = &Регистратор
	|	И СтраховыеВзносыИсчисленные.Организация = &парамГоловнаяОрганизация
	|	И СтраховыеВзносыИсчисленные.ФизЛицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокСотрудников КАК СписокСотрудников)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РанееИсчисленныеВзносы.ФизЛицо КАК ФизЛицо,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ФССОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФССОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ФФОМСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФФОМСОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ТФОМСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ТФОМСОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ФССОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФССОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ФФОМСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ФФОМСОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ТФОМСОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ТФОМСОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРНакопительнаяОборот + РанееИсчисленныеВзносы.ПФРНакопительнаяЕНВДОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРНакопительнаяОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРСтраховаяОборот + РанееИсчисленныеВзносы.ПФРСтраховаяЕНВДОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРСтраховаяОборотИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРНакопительнаяОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРНакопительнаяОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРСтраховаяОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРСтраховаяОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРНакопительнаяЕНВДОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРНакопительнаяЕНВДОборот,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА РанееИсчисленныеВзносы.ПФРСтраховаяЕНВДОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ПФРСтраховаяЕНВДОборот,
	|	СУММА(РанееИсчисленныеВзносы.ПФРПоДополнительномуТарифуОборот) КАК ПФРПоДополнительномуТарифуОборот
	|ПОМЕСТИТЬ ВТРанееИсчисленныеВзносы
	|ИЗ
	|	ВТРанееИсчисленныеВзносыПоМесяцам КАК РанееИсчисленныеВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбИнвалидности КАК Инвалид
	|		ПО РанееИсчисленныеВзносы.ФизЛицо = Инвалид.Физлицо
	|			И РанееИсчисленныеВзносы.Месяц = Инвалид.Месяц
	|
	|СГРУППИРОВАТЬ ПО
	|	РанееИсчисленныеВзносы.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Основная.ФизЛицо КАК ФизЛицо,
	|	Основная.Месяц КАК Месяц,
	|	СУММА(ВЫБОР
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФР,
	|	СУММА(ВЫБОР
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФСС,
	|	СУММА(ВЫБОР
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаМедицинскоеСтрахование))
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМС,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|					И Основная.ОблагаетсяЕНВД
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРИнвалидаЕНВД,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|				ТОГДА 0
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРПоОсновномуТарифу,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРЕНВД,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФССИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|				ТОГДА 0
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФССПоОсновномуТарифу,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФССЕНВД,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМСИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|				ТОГДА 0
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаМедицинскоеСтрахование))
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМСПоОсновномуТарифу,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА 0
	|			КОГДА Основная.ОблагаетсяЕНВД
	|					И (Иностр.Физлицо ЕСТЬ NULL 
	|						ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМСЕНВД
	|ПОМЕСТИТЬ ВТДоходыЗастрахованныхЛиц
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК Основная
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОПравеНаПенсию КАК Иностр
	|		ПО Основная.ФизЛицо = Иностр.Физлицо
	|			И Основная.Месяц = Иностр.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбИнвалидности КАК Инвалид
	|		ПО Основная.ФизЛицо = Инвалид.Физлицо
	|			И Основная.Месяц = Инвалид.Месяц
	|
	|СГРУППИРОВАТЬ ПО
	|	Основная.ФизЛицо,
	|	Основная.Месяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ДоходыЗастрахованныхЛиц.ФизЛицо КАК ФизЛицо,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаПФР) КАК БазаПФР,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаФСС) КАК БазаФСС,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаФОМС) КАК БазаФОМС
	|ПОМЕСТИТЬ ВТНакопленнаяОблагаемаяБаза
	|ИЗ
	|	ВТПериоды КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыЗастрахованныхЛиц КАК ДоходыЗастрахованныхЛиц
	|		ПО (ДоходыЗастрахованныхЛиц.Месяц МЕЖДУ 1 И МЕСЯЦ(Периоды.Период))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыЗастрахованныхЛиц.ФизЛицо,
	|	МЕСЯЦ(Периоды.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыЗастрахованныхЛиц.ФизЛицо КАК ФизЛицо,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаПФРИнвалида) КАК БазаПФРИнвалидаВсего,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаПФРИнвалидаЕНВД) КАК БазаПФРИнвалидаЕНВД
	|ПОМЕСТИТЬ ВТБазаПФРПоИнвалидам
	|ИЗ
	|	ВТДоходыЗастрахованныхЛиц КАК ДоходыЗастрахованныхЛиц
	|ГДЕ
	|	ДоходыЗастрахованныхЛиц.Месяц = &парамМесяцРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыЗастрахованныхЛиц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	База.Месяц КАК Месяц,
	|	База.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА База.БазаПФР <= &ПредельнаяСумма
	|			ТОГДА 1
	|		КОГДА База.БазаПФР = ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0)
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0) >= &ПредельнаяСумма
	|			ТОГДА 0
	|		ИНАЧЕ (&ПредельнаяСумма - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0)) / (База.БазаПФР - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0))
	|	КОНЕЦ КАК КоэффициентОбложенияПФР,
	|	ВЫБОР
	|		КОГДА База.БазаФСС <= &ПредельнаяСумма
	|			ТОГДА 1
	|		КОГДА База.БазаФСС = ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0)
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0) >= &ПредельнаяСумма
	|			ТОГДА 0
	|		ИНАЧЕ (&ПредельнаяСумма - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0)) / (База.БазаФСС - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0))
	|	КОНЕЦ КАК КоэффициентОбложенияФСС,
	|	ВЫБОР
	|		КОГДА База.БазаФОМС <= &ПредельнаяСумма
	|			ТОГДА 1
	|		КОГДА База.БазаФОМС = ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0)
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0) >= &ПредельнаяСумма
	|			ТОГДА 0
	|		ИНАЧЕ (&ПредельнаяСумма - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0)) / (База.БазаФОМС - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0))
	|	КОНЕЦ КАК КоэффициентОбложенияФОМС
	|ПОМЕСТИТЬ ВТКоэффициентыДостиженияПредельнойСуммы
	|ИЗ
	|	ВТНакопленнаяОблагаемаяБаза КАК База
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНакопленнаяОблагаемаяБаза КАК НакопленоПоПрошлыйМесяц
	|		ПО (База.Месяц - 1 = НакопленоПоПрошлыйМесяц.Месяц)
	|			И База.ФизЛицо = НакопленоПоПрошлыйМесяц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыЗастрахованныхЛиц.ФизЛицо КАК ФизЛицо,
	|	ДоходыЗастрахованныхЛиц.Месяц КАК Месяц,
	|	ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаПФРИнвалида
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРИнвалида = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|	КОНЕЦ КАК БазаПФРИнвалида,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРЕНВД = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|		ИНАЧЕ ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)) * ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаПФРЕНВД) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК БазаПФРПоОсновномуТарифу,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаПФРЕНВД
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРЕНВД = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0) - (ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)) * ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаПФРПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаПФРЕНВД) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК БазаПФРЕНВД,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФССИнвалида
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССИнвалида = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|	КОНЕЦ КАК БазаФССИнвалида,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССЕНВД = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|		ИНАЧЕ ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)) * ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаФССЕНВД) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК БазаФССПоОсновномуТарифу,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФССЕНВД
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССЕНВД = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0) - (ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)) * ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаФССПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаФССЕНВД) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК БазаФССЕНВД,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФОМСИнвалида
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСИнвалида = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|	КОНЕЦ КАК БазаФОМСИнвалида,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|		ИНАЧЕ ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)) * ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК БазаФОМСПоОсновномуТарифу,
	|	ВЫБОР
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 1
	|			ТОГДА ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу = 0
	|			ТОГДА &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0) - (ВЫРАЗИТЬ((&ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)) * ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу / (ДоходыЗастрахованныхЛиц.БазаФОМСПоОсновномуТарифу + ДоходыЗастрахованныхЛиц.БазаФОМСЕНВД) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК БазаФОМСЕНВД
	|ПОМЕСТИТЬ ВТОграниченнаяОблагаемаяБаза
	|ИЗ
	|	ВТДоходыЗастрахованныхЛиц КАК ДоходыЗастрахованныхЛиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыДостиженияПредельнойСуммы КАК Коэффициенты
	|		ПО ДоходыЗастрахованныхЛиц.ФизЛицо = Коэффициенты.ФизЛицо
	|			И ДоходыЗастрахованныхЛиц.Месяц = Коэффициенты.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНакопленнаяОблагаемаяБаза КАК НакопленнаяОблагаемаяБазаПоПредыдущийМесяц
	|		ПО ДоходыЗастрахованныхЛиц.ФизЛицо = НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.ФизЛицо
	|			И (ДоходыЗастрахованныхЛиц.Месяц - 1 = НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.Месяц)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользуемыеТарифы КАК ИспользуемыеТарифы
	|		ПО ДоходыЗастрахованныхЛиц.Месяц = ИспользуемыеТарифы.Месяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо КАК ФизЛицо,
	|	ОграниченнаяОблагаемаяБаза.ВидТарифаСтраховыхВзносов,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРПоОсновномуТарифу) * ОсновнойТариф.ПФРНакопительная / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРНакопительная,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРПоОсновномуТарифу) * ОсновнойТариф.ПФР / 100 КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРПоОсновномуТарифу) * (ОсновнойТариф.ПФР - ОсновнойТариф.ПФРНакопительная) / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРСтраховая,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФССПоОсновномуТарифу) * ОсновнойТариф.ФСС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФСС,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСПоОсновномуТарифу) * ОсновнойТариф.ФФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФФОМС,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСПоОсновномуТарифу) * ОсновнойТариф.ТФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатТФОМС
	|ПОМЕСТИТЬ ВТИсчисленоПоОсновномуТарифу
	|ИЗ
	|	ВТОграниченнаяОблагаемаяБаза КАК ОграниченнаяОблагаемаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТарифы КАК ОсновнойТариф
	|		ПО ОграниченнаяОблагаемаяБаза.ВидТарифаСтраховыхВзносов = ОсновнойТариф.ВидТарифа
	|
	|СГРУППИРОВАТЬ ПО
	|	ОсновнойТариф.ПФРНакопительная,
	|	ОсновнойТариф.ПФР,
	|	ОсновнойТариф.ФСС,
	|	ОсновнойТариф.ФФОМС,
	|	ОсновнойТариф.ТФОМС,
	|	ОграниченнаяОблагаемаяБаза.ВидТарифаСтраховыхВзносов,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРИнвалида) * ТарифИнвалида.ПФРНакопительная / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРНакопительнаяИнвалида,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРИнвалида) * ТарифИнвалида.ПФР / 100 КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРИнвалида) * (ТарифИнвалида.ПФР - ТарифИнвалида.ПФРНакопительная) / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРСтраховаяИнвалида,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФССИнвалида) * ТарифИнвалида.ФСС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФССИнвалида,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСИнвалида) * ТарифИнвалида.ФФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФФОМСИнвалида,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСИнвалида) * ТарифИнвалида.ТФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатТФОМСИнвалида,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРЕНВД) * ТарифЕНВД.ПФРНакопительная / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРНакопительнаяЕНВД,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРЕНВД) * ТарифЕНВД.ПФР / 100 КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРЕНВД) * (ТарифЕНВД.ПФР - ТарифЕНВД.ПФРНакопительная) / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРСтраховаяЕНВД
	|ПОМЕСТИТЬ ВТИсчисленоПоЛьготномуТарифу
	|ИЗ
	|	ВТОграниченнаяОблагаемаяБаза КАК ОграниченнаяОблагаемаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТарифы КАК ТарифЕНВД
	|		ПО (ТарифЕНВД.ВидТарифа = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ЕНВД))
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТарифы КАК ТарифИнвалида
	|		ПО (ТарифИнвалида.ВидТарифа = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТарифИнвалида.ПФРНакопительная,
	|	ТарифИнвалида.ПФР,
	|	ТарифИнвалида.ФСС,
	|	ТарифИнвалида.ФФОМС,
	|	ТарифИнвалида.ТФОМС,
	|	ТарифЕНВД.ПФРНакопительная,
	|	ТарифЕНВД.ПФР,
	|	ТарифЕНВД.ФСС,
	|	ТарифЕНВД.ФФОМС,
	|	ТарифЕНВД.ТФОМС,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СУММА(ДоходыФизлиц.ОблагаемыйДоход) * 0.14 КАК ЧИСЛО(15, 2)) КАК РезультатПоДополнительномуТарифу,
	|	ДоходыФизлиц.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТВзносыПоДополнительномуТарифу
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК ДоходыФизлиц
	|ГДЕ
	|	ДоходыФизлиц.ОблагаетсяПоДополнительномуТарифу
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыФизлиц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начислено.ФизЛицо КАК ФизЛицо,
	|	СУММА(Начислено.ОблагаемаяБаза) КАК ОблагаемаяБаза,
	|	СУММА(Начислено.ОблагаемаяБазаИнвалида) КАК ОблагаемаяБазаИнвалида,
	|	ВЫРАЗИТЬ(СУММА(Начислено.ОблагаемаяБаза) * &СтавкаВзносаФСС_НС_ПЗ КАК ЧИСЛО(15, 2)) КАК Взносов,
	|	ВЫРАЗИТЬ(СУММА(Начислено.ОблагаемаяБазаИнвалида) * &СтавкаВзносаФСС_НС_ПЗ * 0.6 КАК ЧИСЛО(15, 2)) КАК ВзносовПоИнвалидам
	|ПОМЕСТИТЬ ВТБазаВзносовФСС_НС_ПЗ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОсновныеНачисления.Физлицо КАК ФизЛицо,
	|		ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ NULL 
	|				ТОГДА ОсновныеНачисления.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ОблагаемаяБаза,
	|		ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ТОГДА ОсновныеНачисления.Результат
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ОблагаемаяБазаИнвалида
	|	ИЗ
	|		Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК ОсновныеНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбИнвалидности КАК Инвалид
	|			ПО ОсновныеНачисления.Физлицо = Инвалид.Физлицо
	|				И (Инвалид.Месяц = &парамМесяцРасчета)
	|	ГДЕ
	|		ОсновныеНачисления.Ссылка.ПериодРегистрации = &парамПериодРегистрации
	|		И ОсновныеНачисления.Ссылка.Проведен
	|		И ОсновныеНачисления.Ссылка.Организация = &парамОбособленноеПодразделение
	|		И ОсновныеНачисления.Физлицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТСписокСотрудников КАК СписокСотрудников)
	|		И ОсновныеНачисления.ВидРасчета.КодДоходаФСС_НС = ЗНАЧЕНИЕ(Перечисление.ДоходыФСС_НС.Облагается)) КАК Начислено
	|
	|СГРУППИРОВАТЬ ПО
	|	Начислено.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(БазаВзносовФСС_НС_ПЗ.ОблагаемаяБаза) КАК МаксимумБазы,
	|	МАКСИМУМ(БазаВзносовФСС_НС_ПЗ.ОблагаемаяБазаИнвалида) КАК МаксимумБазыИнвалида
	|ПОМЕСТИТЬ ВТМаксимумыОблагаемойБазыПоНС_ПЗ
	|ИЗ
	|	ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(БазаВзносовФСС_НС_ПЗ.ФизЛицо) КАК ФизЛицо,
	|	МАКСИМУМ(БазаВзносовФСС_НС_ПЗИнвалида.ФизЛицо) КАК ФизЛицоИнвалид
	|ПОМЕСТИТЬ ВТФизлицаОкругления
	|ИЗ
	|	ВТМаксимумыОблагаемойБазыПоНС_ПЗ КАК Максимумы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗ
	|		ПО (Максимумы.МаксимумБазы > 0)
	|			И Максимумы.МаксимумБазы = БазаВзносовФСС_НС_ПЗ.ОблагаемаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗИнвалида
	|		ПО Максимумы.МаксимумБазыИнвалида = БазаВзносовФСС_НС_ПЗИнвалида.ОблагаемаяБазаИнвалида
	|		И Максимумы.МаксимумБазыИнвалида > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СУММА(БазаВзносовФСС_НС_ПЗ.ОблагаемаяБаза) * &СтавкаВзносаФСС_НС_ПЗ КАК ЧИСЛО(15, 2)) КАК РассчитаноВзносов,
	|	ВЫРАЗИТЬ(СУММА(БазаВзносовФСС_НС_ПЗ.ОблагаемаяБазаИнвалида) * &СтавкаВзносаФСС_НС_ПЗ * 0.6 КАК ЧИСЛО(15, 2)) КАК РассчитаноВзносовПоИнвалидам,
	|	СУММА(ВЫБОР
	|			КОГДА БазаВзносовФСС_НС_ПЗ.ФизЛицо = ФизлицоОкругления.ФизЛицо
	|				ТОГДА 0
	|			ИНАЧЕ БазаВзносовФСС_НС_ПЗ.Взносов
	|		КОНЕЦ) КАК ПросуммированоВзносов,
	|	СУММА(ВЫБОР
	|			КОГДА БазаВзносовФСС_НС_ПЗ.ФизЛицо = ФизлицоОкругления.ФизЛицоИнвалид
	|				ТОГДА 0
	|			ИНАЧЕ БазаВзносовФСС_НС_ПЗ.ВзносовПоИнвалидам
	|		КОНЕЦ) КАК ПросуммированоВзносовПоИнвалидам
	|ПОМЕСТИТЬ ВТИтогоВзносовФСС_НС_ПЗ
	|ИЗ
	|	ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФизлицаОкругления КАК ФизлицоОкругления
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БазаВзносовФСС_НС_ПЗ.ФизЛицо КАК ФизЛицо,
	|	БазаВзносовФСС_НС_ПЗ.Взносов + БазаВзносовФСС_НС_ПЗ.ВзносовПоИнвалидам КАК Взносов
	|ПОМЕСТИТЬ ВТВзносыФС_НС_ПЗ
	|ИЗ
	|	ВТБазаВзносовФСС_НС_ПЗ КАК БазаВзносовФСС_НС_ПЗ
	|ГДЕ
	|	(НЕ БазаВзносовФСС_НС_ПЗ.ФизЛицо В
	|				(ВЫБРАТЬ
	|					ФизлицоОкругления.ФизЛицо
	|				ИЗ
	|					ВТФизлицаОкругления КАК ФизлицоОкругления
	|		
	|				ОБЪЕДИНИТЬ ВСЕ
	|		
	|				ВЫБРАТЬ
	|					ФизлицоОкругления.ФизЛицоИнвалид
	|				ИЗ
	|					ВТФизлицаОкругления КАК ФизлицоОкругления
	|				ГДЕ
	|					НЕ ФизЛицоОкругления.ФизЛицоИнвалид ЕСТЬ NULL))
	|	И БазаВзносовФСС_НС_ПЗ.Взносов + БазаВзносовФСС_НС_ПЗ.ВзносовПоИнвалидам <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФизлицоОкругления.ФизЛицо,
	|	ИтогоВзносовФСС_НС_ПЗ.РассчитаноВзносов - ИтогоВзносовФСС_НС_ПЗ.ПросуммированоВзносов
	|ИЗ
	|	ВТФизлицаОкругления КАК ФизлицоОкругления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогоВзносовФСС_НС_ПЗ КАК ИтогоВзносовФСС_НС_ПЗ
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ИтогоВзносовФСС_НС_ПЗ.РассчитаноВзносов <> ИтогоВзносовФСС_НС_ПЗ.ПросуммированоВзносов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФизлицоОкругления.ФизЛицоИнвалид,
	|	ИтогоВзносовФСС_НС_ПЗ.РассчитаноВзносовПоИнвалидам - ИтогоВзносовФСС_НС_ПЗ.ПросуммированоВзносовПоИнвалидам
	|ИЗ
	|	ВТФизлицаОкругления КАК ФизлицоОкругления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИтогоВзносовФСС_НС_ПЗ КАК ИтогоВзносовФСС_НС_ПЗ
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ИтогоВзносовФСС_НС_ПЗ.РассчитаноВзносовПоИнвалидам <> ИтогоВзносовФСС_НС_ПЗ.ПросуммированоВзносовПоИнвалидам
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсегоИсчислено.ФизЛицо КАК ФизЛицо,
	|	ВсегоИсчислено.Порядок,
	|	СУММА(ВсегоИсчислено.РезультатПФРНакопительная) КАК РезультатПФРНакопительная,
	|	СУММА(ВсегоИсчислено.РезультатПФРСтраховая) КАК РезультатПФРСтраховая,
	|	СУММА(ВсегоИсчислено.РезультатПФРНакопительнаяЕНВД) КАК РезультатПФРНакопительнаяЕНВД,
	|	СУММА(ВсегоИсчислено.РезультатПФРСтраховаяЕНВД) КАК РезультатПФРСтраховаяЕНВД,
	|	СУММА(ВсегоИсчислено.РезультатФСС) КАК РезультатФСС,
	|	СУММА(ВсегоИсчислено.РезультатФФОМС) КАК РезультатФФОМС,
	|	СУММА(ВсегоИсчислено.РезультатТФОМС) КАК РезультатТФОМС,
	|	СУММА(ВсегоИсчислено.РезультатПФРНакопительнаяИнвалида) КАК РезультатПФРНакопительнаяИнвалида,
	|	СУММА(ВсегоИсчислено.РезультатПФРСтраховаяИнвалида) КАК РезультатПФРСтраховаяИнвалида,
	|	СУММА(ВсегоИсчислено.РезультатПоДополнительномуТарифу) КАК РезультатПоДополнительномуТарифу,
	|	СУММА(ВсегоИсчислено.РезультатФССНесчастныеСлучаи) КАК РезультатФССНесчастныеСлучаи
	|ПОМЕСТИТЬ ВТСуммыВзносовНарастающимИтогом
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИсчисленоПоОсновномуТарифу.ФизЛицо КАК ФизЛицо,
	|		ИсчисленоПоОсновномуТарифу.ФизЛицо.Наименование КАК Порядок,
	|		ИсчисленоПоОсновномуТарифу.РезультатПФРНакопительная КАК РезультатПФРНакопительная,
	|		ИсчисленоПоОсновномуТарифу.РезультатПФРСтраховая КАК РезультатПФРСтраховая,
	|		ИсчисленоПоОсновномуТарифу.РезультатФСС КАК РезультатФСС,
	|		ИсчисленоПоОсновномуТарифу.РезультатФФОМС КАК РезультатФФОМС,
	|		ИсчисленоПоОсновномуТарифу.РезультатТФОМС КАК РезультатТФОМС,
	|		0 КАК РезультатПФРНакопительнаяЕНВД,
	|		0 КАК РезультатПФРСтраховаяЕНВД,
	|		0 КАК РезультатПФРНакопительнаяИнвалида,
	|		0 КАК РезультатПФРСтраховаяИнвалида,
	|		0 КАК РезультатПоДополнительномуТарифу,
	|		0 КАК РезультатФССНесчастныеСлучаи
	|	ИЗ
	|		ВТИсчисленоПоОсновномуТарифу КАК ИсчисленоПоОсновномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИсчисленоПоЛьготномуТарифу.ФизЛицо,
	|		ИсчисленоПоЛьготномуТарифу.ФизЛицо.Наименование,
	|		0,
	|		0,
	|		ИсчисленоПоЛьготномуТарифу.РезультатФССИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатФФОМСИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатТФОМСИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРНакопительнаяЕНВД,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРСтраховаяЕНВД,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРНакопительнаяИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРСтраховаяИнвалида,
	|		0,
	|		0
	|	ИЗ
	|		ВТИсчисленоПоЛьготномуТарифу КАК ИсчисленоПоЛьготномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВзносыПоДополнительномуТарифу.ФизЛицо,
	|		ВзносыПоДополнительномуТарифу.ФизЛицо.Наименование,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВзносыПоДополнительномуТарифу.РезультатПоДополнительномуТарифу,
	|		0
	|	ИЗ
	|		ВТВзносыПоДополнительномуТарифу КАК ВзносыПоДополнительномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВзносыФС_НС_ПЗ.ФизЛицо,
	|		ВзносыФС_НС_ПЗ.ФизЛицо.Наименование,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВзносыФС_НС_ПЗ.Взносов
	|	ИЗ
	|		ВТВзносыФС_НС_ПЗ КАК ВзносыФС_НС_ПЗ) КАК ВсегоИсчислено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсегоИсчислено.ФизЛицо,
	|	ВсегоИсчислено.Порядок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыНарастающимИтогом.ФизЛицо КАК ФизЛицо,
	|	СуммыНарастающимИтогом.Порядок КАК Порядок,
	|	СуммыНарастающимИтогом.РезультатФСС - ЕСТЬNULL(ИсчисленныеВзносы.ФССОборот, 0) - ЕСТЬNULL(ИсчисленныеВзносы.ФССОборотИнвалида, 0) КАК ФСС,
	|	СуммыНарастающимИтогом.РезультатФФОМС - ЕСТЬNULL(ИсчисленныеВзносы.ФФОМСОборот, 0) - ЕСТЬNULL(ИсчисленныеВзносы.ФФОМСОборотИнвалида, 0) КАК ФФОМС,
	|	СуммыНарастающимИтогом.РезультатТФОМС - ЕСТЬNULL(ИсчисленныеВзносы.ТФОМСОборот, 0) - ЕСТЬNULL(ИсчисленныеВзносы.ТФОМСОборотИнвалида, 0) КАК ТФОМС,
	|	СуммыНарастающимИтогом.РезультатПФРНакопительная - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборот, 0) + ВЫБОР
	|		КОГДА МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего <> 0
	|				И МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД <> 0
	|			ТОГДА ВЫРАЗИТЬ((СуммыНарастающимИтогом.РезультатПФРНакопительнаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборотИнвалида, 0)) * (МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего - МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД) / МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыНарастающимИтогом.РезультатПФРНакопительнаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборотИнвалида, 0)
	|	КОНЕЦ КАК ПФРНакопительная,
	|	СуммыНарастающимИтогом.РезультатПФРСтраховая - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборот, 0) + ВЫБОР
	|		КОГДА МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего <> 0
	|				И МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД <> 0
	|			ТОГДА ВЫРАЗИТЬ((СуммыНарастающимИтогом.РезультатПФРСтраховаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборотИнвалида, 0)) * (МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего - МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД) / МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ СуммыНарастающимИтогом.РезультатПФРСтраховаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборотИнвалида, 0)
	|	КОНЕЦ КАК ПФРСтраховая,
	|	СуммыНарастающимИтогом.РезультатПФРНакопительнаяЕНВД - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяЕНВДОборот, 0) + ВЫБОР
	|		КОГДА МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего <> 0
	|				И МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД <> 0
	|			ТОГДА СуммыНарастающимИтогом.РезультатПФРНакопительнаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборотИнвалида, 0) - (ВЫРАЗИТЬ((СуммыНарастающимИтогом.РезультатПФРНакопительнаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборотИнвалида, 0)) * (МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего - МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД) / МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего КАК ЧИСЛО(15, 2)))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРНакопительнаяЕНВД,
	|	СуммыНарастающимИтогом.РезультатПФРСтраховаяЕНВД - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяЕНВДОборот, 0) + ВЫБОР
	|		КОГДА МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего <> 0
	|				И МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД <> 0
	|			ТОГДА СуммыНарастающимИтогом.РезультатПФРСтраховаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборотИнвалида, 0) - (ВЫРАЗИТЬ((СуммыНарастающимИтогом.РезультатПФРСтраховаяИнвалида - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборотИнвалида, 0)) * (МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего - МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаЕНВД) / МесячнаяБазаПФРПоИнвалидам.БазаПФРИнвалидаВсего КАК ЧИСЛО(15, 2)))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПФРСтраховаяЕНВД,
	|	СуммыНарастающимИтогом.РезультатПоДополнительномуТарифу - ЕСТЬNULL(ИсчисленныеВзносы.ПФРПоДополнительномуТарифуОборот, 0) КАК ПоДополнительномуТарифу,
	|	СуммыНарастающимИтогом.РезультатФССНесчастныеСлучаи КАК ФССНесчастныеСлучаи
	|ПОМЕСТИТЬ ВТВзносыЗаМесяц
	|ИЗ
	|	ВТСуммыВзносовНарастающимИтогом КАК СуммыНарастающимИтогом
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРанееИсчисленныеВзносы КАК ИсчисленныеВзносы
	|		ПО СуммыНарастающимИтогом.ФизЛицо = ИсчисленныеВзносы.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТБазаПФРПоИнвалидам КАК МесячнаяБазаПФРПоИнвалидам
	|		ПО СуммыНарастающимИтогом.ФизЛицо = МесячнаяБазаПФРПоИнвалидам.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Взносы.ФизЛицо,
	|	Взносы.ФСС,
	|	Взносы.ФФОМС,
	|	Взносы.ТФОМС,
	|	Взносы.ПФРНакопительная,
	|	Взносы.ПФРСтраховая,
	|	Взносы.ПФРНакопительнаяЕНВД,
	|	Взносы.ПФРСтраховаяЕНВД,
	|	Взносы.ПоДополнительномуТарифу КАК ПФРПоДополнительномуТарифу,
	|	Взносы.ФССНесчастныеСлучаи
	|ИЗ
	|	ВТВзносыЗаМесяц КАК Взносы
	|ГДЕ
	|	(Взносы.ФСС <> 0
	|			ИЛИ Взносы.ФФОМС <> 0
	|			ИЛИ Взносы.ТФОМС <> 0
	|			ИЛИ Взносы.ПФРНакопительная <> 0
	|			ИЛИ Взносы.ПФРСтраховая <> 0
	|			ИЛИ Взносы.ПФРНакопительнаяЕНВД <> 0
	|			ИЛИ Взносы.ПФРСтраховаяЕНВД <> 0
	|			ИЛИ Взносы.ПоДополнительномуТарифу <> 0
	|			ИЛИ Взносы.ФССНесчастныеСлучаи <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Взносы.Порядок";
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаВыгрузки = РезультатЗапроса.Выгрузить();
	Возврат РезультатЗапроса;
	
КонецФункции  // ПолучитьДанныеРасчета

// Предназначена для получения выборки из запроса с данными расчета 
// По текущему регистратору получает 
//    	данные о начисленной зарплате в разрезе кодов дохода 
// 		данные об инвалидности физлиц помесячно
//		присоединяет данные о скидках к доходам
//
// Параметры:
//  нет
// Возвращаемое значение:
//  Выборка результата запроса
//
Функция ПолучитьДанныеРасчета(ГоловнаяОрганизация, ОрганичениеНаСотрудников, ДанныеОДоходах)
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("парамКонецМесяцаПериодаРегистрации", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамОбособленноеПодразделение", Организация);
	Запрос.УстановитьПараметр("парамНачалоГода", НачалоГода(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамКонецГода", КонецГода(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамГодРасчета", Год(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамМесяцРасчета", Месяц(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамПредыдущийМесяц", Месяц(ПериодРегистрации) - 1);
	Запрос.УстановитьПараметр("парамПериодРегистрации", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамЭтоЮрЛицо", ОбщегоНазначенияПереопределяемый.ЭтоЮрЛицо(Организация));
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	ПредельнаяСумма = РегистрыСведений.ПредельнаяВеличинаБазыСтраховыхВзносов.ПолучитьПоследнее(КонецМесяца(ПериодРегистрации)).Размер;
	Запрос.УстановитьПараметр("ПредельнаяСумма", ?(ЗначениеЗаполнено(ПредельнаяСумма),ПредельнаяСумма,0));
	Ставка = РегистрыСведений.СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев.ПолучитьПоследнее(КонецМесяца(ПериодРегистрации), Новый Структура("Организация", Организация)).Ставка;
	Запрос.УстановитьПараметр("СтавкаВзносаФСС_НС_ПЗ", Ставка / 100);
	
	// ---------------------------------------------------------------------------
	// тексты запросов
	//

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОДоходах.ФизЛицо,
	|	МЕСЯЦ(СведенияОДоходах.Период) КАК Месяц,
	|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СведенияОДоходах.ВидДохода,
	|	СведенияОДоходах.Результат - СведенияОДоходах.Скидка КАК ОблагаемыйДоход,
	|	СведенияОДоходах.ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	&ДанныеОДоходах КАК СведенияОДоходах";
	Запрос.УстановитьПараметр("ДанныеОДоходах", ДанныеОДоходах);
	Запрос.Выполнить();
	
	// ФизлицаДляРасчета
	//
	Если ОрганичениеНаСотрудников = Неопределено Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОДоходах.ФизЛицо КАК ФизЛицо
		|ПОМЕСТИТЬ ВТСписокСотрудников
		|ИЗ
		|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СведенияОДоходах
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(СведенияОДоходах.Период, МЕСЯЦ) = &парамПериодРегистрации
		|	И СведенияОДоходах.ОбособленноеПодразделение = &парамОбособленноеПодразделение
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОДоходах.ФизЛицо
		|ИЗ
		|	ВТДанныеДокумента КАК СведенияОДоходах
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
		Запрос.Выполнить();
		
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
        |   ТаблицаСотрудников.ФизЛицо КАК ФизЛицо
        |ПОМЕСТИТЬ ВТСписокСотрудников
        |ИЗ
        |   &парамТаблицаСотрудников КАК ТаблицаСотрудников
        |
        |ИНДЕКСИРОВАТЬ ПО
        |   ФизЛицо";
		Запрос.УстановитьПараметр("парамТаблицаСотрудников", ОрганичениеНаСотрудников);
		Запрос.Выполнить();
	КонецЕсли;
	
	// Периоды
	// Таблица список периодов
	// Поля:
	//		Период
	// Описание:
	//  список периодов - первые даты месяцев с начала года по "текущий" для запроса период
	//
	
	// конец месяца налогового периода
	КонецМесяца = КонецМесяца(НачалоГода(ПериодРегистрации));
	// первый месяц
	ПериодыТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период ПОМЕСТИТЬ ВТПериоды";
	// прибавим остальные месяцы
	Для Сч = 2 По Месяц(ПериодРегистрации) Цикл
		КонецМесяца = КонецМесяца(КонецМесяца+1);
    	ПериодыТекст = ПериодыТекст +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	Запрос.Текст = ПериодыТекст;
	Запрос.Выполнить();

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчет.ВидТарифаСтраховыхВзносов, ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим)) КАК ВидТарифаСтраховыхВзносов
	|ПОМЕСТИТЬ ВТИспользуемыеТарифы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		МАКСИМУМ(УчетнаяПолитикаНалоговыйУчет.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
	|			ПО Периоды.Период >= УчетнаяПолитикаНалоговыйУчет.Период
	|				И (УчетнаяПолитикаНалоговыйУчет.Организация = &парамГоловнаяОрганизация)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Периоды.Период) КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаНалоговыйУчет
	|		ПО Периоды.ПериодРегистра = УчетнаяПолитикаНалоговыйУчет.Период
	|			И (УчетнаяПолитикаНалоговыйУчет.Организация = &парамГоловнаяОрганизация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТарифСтраховыхВзносовСрезПоследних.ВидТарифа КАК ВидТарифа,
	|	ТарифСтраховыхВзносовСрезПоследних.ПФР,
	|	ТарифСтраховыхВзносовСрезПоследних.ПФРНакопительная,
	|	ТарифСтраховыхВзносовСрезПоследних.ФСС,
	|	ТарифСтраховыхВзносовСрезПоследних.ФФОМС,
	|	ТарифСтраховыхВзносовСрезПоследних.ТФОМС
	|ПОМЕСТИТЬ ВТТарифы
	|ИЗ
	|	РегистрСведений.ТарифСтраховыхВзносов.СрезПоследних(&парамКонецГода, ) КАК ТарифСтраховыхВзносовСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидТарифа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ГражданствоФизЛиц.ФизЛицо КАК Физлицо,
	|	ЕСТЬNULL(ГражданствоФизЛиц.НеИмеетПравоНаПенсию, ЛОЖЬ) КАК НеИмеетПравоНаПенсию,
	|	ЕСТЬNULL(ГражданствоФизЛиц.НеИмеетПравоНаМедицинскоеСтрахование, ЛОЖЬ) КАК НеИмеетПравоНаМедицинскоеСтрахование
	|ПОМЕСТИТЬ ВТДанныеОПравеНаПенсию
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		ГражданствоФизЛиц.ФизЛицо КАК Физлицо,
	|		МАКСИМУМ(ГражданствоФизЛиц.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|			ПО Периоды.Период >= ГражданствоФизЛиц.Период
	|	ГДЕ
	|		ГражданствоФизЛиц.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТСписокСотрудников КАК СписокСотрудников)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ГражданствоФизЛиц.ФизЛицо,
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО Периоды.ПериодРегистра = ГражданствоФизЛиц.Период
	|			И Периоды.Физлицо = ГражданствоФизЛиц.ФизЛицо
	|			И (ГражданствоФизЛиц.НеИмеетПравоНаПенсию
	|				ИЛИ ГражданствоФизЛиц.НеИмеетПравоНаМедицинскоеСтрахование)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо,
	|	СведенияОбИнвалидностиФизлиц.Период КАК Период,
	|	СведенияОбИнвалидностиФизлиц.Инвалидность
	|ПОМЕСТИТЬ ВТ_СведенияОбИнвалидности
	|ИЗ
	|	РегистрСведений.СведенияОбИнвалидностиФизлиц КАК СведенияОбИнвалидностиФизлиц
	|ГДЕ
	|	СведенияОбИнвалидностиФизлиц.Физлицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокСотрудников КАК СписокСотрудников)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо
	|ПОМЕСТИТЬ ВТДанныеОбИнвалидности
	|ИЗ
	|	(ВЫБРАТЬ
	|		Периоды.Период КАК Период,
	|		СведенияОбИнвалидностиФизлиц.Физлицо КАК Физлицо,
	|		МАКСИМУМ(СведенияОбИнвалидностиФизлиц.Период) КАК ПериодРегистра
	|	ИЗ
	|		ВТПериоды КАК Периоды
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СведенияОбИнвалидности КАК СведенияОбИнвалидностиФизлиц
	|			ПО Периоды.Период >= СведенияОбИнвалидностиФизлиц.Период
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СведенияОбИнвалидностиФизлиц.Физлицо,
	|		Периоды.Период) КАК Периоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СведенияОбИнвалидности КАК СведенияОбИнвалидностиФизлиц
	|		ПО (СведенияОбИнвалидностиФизлиц.Период = Периоды.ПериодРегистра)
	|			И Периоды.Физлицо = СведенияОбИнвалидностиФизлиц.Физлицо
	|			И (СведенияОбИнвалидностиФизлиц.Инвалидность)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|	МЕСЯЦ(СведенияОДоходах.Период) КАК Месяц,
	|	СведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	СведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СведенияОДоходах.ВидДохода.ВходитВБазуПФР КАК ВходитВБазуПФР,
	|	СведенияОДоходах.ВидДохода.ВходитВБазуФСС КАК ВходитВБазуФСС,
	|	СведенияОДоходах.ВидДохода.ВходитВБазуФОМС КАК ВходитВБазуФОМС,
	|	СведенияОДоходах.РезультатОборот - СведенияОДоходах.СкидкаОборот КАК ОблагаемыйДоход,
	|	СведенияОДоходах.ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТДоходыПоФизлицамМесяцам
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах.Обороты(
	|			&парамНачалоГода,
	|			&парамКонецМесяцаПериодаРегистрации,
	|			Месяц,
	|			Организация = &парамГоловнаяОрганизация
	|				И Физлицо В
	|					(ВЫБРАТЬ
	|						СписокСотрудников.ФизЛицо
	|					ИЗ
	|						ВТСписокСотрудников КАК СписокСотрудников)) КАК СведенияОДоходах
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|	МЕСЯЦ(СтраховыеВзносыСведенияОДоходах.Период),
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуПФР,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФСС,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФОМС,
	|	-СтраховыеВзносыСведенияОДоходах.Результат + СтраховыеВзносыСведенияОДоходах.Скидка,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД
	|ИЗ
	|	РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
	|ГДЕ
	|	СтраховыеВзносыСведенияОДоходах.Регистратор = &Регистратор
	|	И СтраховыеВзносыСведенияОДоходах.Организация = &парамГоловнаяОрганизация
	|	И СтраховыеВзносыСведенияОДоходах.ФизЛицо В
	|			(ВЫБРАТЬ
	|				СписокСотрудников.ФизЛицо
	|			ИЗ
	|				ВТСписокСотрудников КАК СписокСотрудников)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|	СтраховыеВзносыСведенияОДоходах.Месяц,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяПоДополнительномуТарифу,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуПФР,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФСС,
	|	СтраховыеВзносыСведенияОДоходах.ВидДохода.ВходитВБазуФОМС,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаемыйДоход,
	|	СтраховыеВзносыСведенияОДоходах.ОблагаетсяЕНВД
	|ИЗ
	|	ВТДанныеДокумента КАК СтраховыеВзносыСведенияОДоходах
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходах.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТФизлицаБезОблагаемойБазы
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК СведенияОДоходах
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходах.ФизЛицо
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходах.ВходитВБазуПФР
	|					ИЛИ СведенияОДоходах.ВходитВБазуФОМС
	|				ТОГДА СведенияОДоходах.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РанееИсчисленныеВзносы.ФизЛицо КАК ФизЛицо,
	|	СУММА(РанееИсчисленныеВзносы.ФССОборот) КАК ФССОборот,
	|	СУММА(РанееИсчисленныеВзносы.ФФОМСОборот) КАК ФФОМСОборот,
	|	СУММА(РанееИсчисленныеВзносы.ТФОМСОборот) КАК ТФОМСОборот,
	|	СУММА(РанееИсчисленныеВзносы.ПФРНакопительнаяОборот) КАК ПФРНакопительнаяОборот,
	|	СУММА(РанееИсчисленныеВзносы.ПФРСтраховаяОборот) КАК ПФРСтраховаяОборот,
	|	СУММА(РанееИсчисленныеВзносы.ПФРПоДополнительномуТарифуОборот) КАК ПФРПоДополнительномуТарифуОборот,
	|	СУММА(РанееИсчисленныеВзносы.ПФРНаДоплатуКПенсииШахтерамОборот) КАК ПФРНаДоплатуКПенсииШахтерамОборот,
	|	СУММА(РанееИсчисленныеВзносы.ФССНесчастныеСлучаиОборот) КАК ФССНесчастныеСлучаиОборот
	|ПОМЕСТИТЬ ВТРанееИсчисленныеВзносы
	|ИЗ
	|	(ВЫБРАТЬ
	|		РанееИсчисленныеВзносы.ФизЛицо КАК ФизЛицо,
	|		МЕСЯЦ(РанееИсчисленныеВзносы.Период) КАК Месяц,
	|		РанееИсчисленныеВзносы.ФССОборот + РанееИсчисленныеВзносы.ФССЕНВДОборот КАК ФССОборот,
	|		РанееИсчисленныеВзносы.ФФОМСОборот + РанееИсчисленныеВзносы.ФФОМСЕНВДОборот КАК ФФОМСОборот,
	|		РанееИсчисленныеВзносы.ТФОМСОборот + РанееИсчисленныеВзносы.ТФОМСЕНВДОборот КАК ТФОМСОборот,
	|		РанееИсчисленныеВзносы.ПФРНакопительнаяОборот + РанееИсчисленныеВзносы.ПФРНакопительнаяЕНВДОборот КАК ПФРНакопительнаяОборот,
	|		РанееИсчисленныеВзносы.ПФРСтраховаяОборот + РанееИсчисленныеВзносы.ПФРСтраховаяЕНВДОборот КАК ПФРСтраховаяОборот,
	|		РанееИсчисленныеВзносы.ПФРНаДоплатуКПенсииШахтерамОборот КАК ПФРНаДоплатуКПенсииШахтерамОборот,
	|		РанееИсчисленныеВзносы.ПФРПоДополнительномуТарифуОборот КАК ПФРПоДополнительномуТарифуОборот,
	|		ВЫБОР
	|			КОГДА РанееИсчисленныеВзносы.Период = &парамПериодРегистрации
	|				ТОГДА РанееИсчисленныеВзносы.ФССНесчастныеСлучаиОборот
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ФССНесчастныеСлучаиОборот
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыИсчисленные.Обороты(
	|				&парамНачалоГода,
	|				&парамКонецМесяцаПериодаРегистрации,
	|				Месяц,
	|				Организация = &парамГоловнаяОрганизация
	|					И (НЕ ФизЛицо В
	|							(ВЫБРАТЬ
	|								СписокСотрудников.ФизЛицо
	|							ИЗ
	|								ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|					И ФизЛицо В
	|						(ВЫБРАТЬ
	|							СписокСотрудников.ФизЛицо
	|						ИЗ
	|							ВТСписокСотрудников КАК СписокСотрудников)) КАК РанееИсчисленныеВзносы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтраховыеВзносыИсчисленные.ФизЛицо,
	|		МЕСЯЦ(СтраховыеВзносыИсчисленные.Период),
	|		-СтраховыеВзносыИсчисленные.ФСС - СтраховыеВзносыИсчисленные.ФССЕНВД,
	|		-СтраховыеВзносыИсчисленные.ФФОМС - СтраховыеВзносыИсчисленные.ФФОМСЕНВД,
	|		-СтраховыеВзносыИсчисленные.ТФОМС - СтраховыеВзносыИсчисленные.ТФОМСЕНВД,
	|		-СтраховыеВзносыИсчисленные.ПФРНакопительная - СтраховыеВзносыИсчисленные.ПФРНакопительнаяЕНВД,
	|		-СтраховыеВзносыИсчисленные.ПФРСтраховая - СтраховыеВзносыИсчисленные.ПФРСтраховаяЕНВД,
	|		-СтраховыеВзносыИсчисленные.ПФРНаДоплатуКПенсииШахтерам,
	|		-СтраховыеВзносыИсчисленные.ПФРПоДополнительномуТарифу,
	|		-СтраховыеВзносыИсчисленные.ФССНесчастныеСлучаи
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыИсчисленные КАК СтраховыеВзносыИсчисленные
	|	ГДЕ
	|		СтраховыеВзносыИсчисленные.Регистратор = &Регистратор
	|		И СтраховыеВзносыИсчисленные.Организация = &парамГоловнаяОрганизация
	|		И (НЕ СтраховыеВзносыИсчисленные.ФизЛицо В
	|					(ВЫБРАТЬ
	|						СписокСотрудников.ФизЛицо
	|					ИЗ
	|						ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|		И СтраховыеВзносыИсчисленные.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТСписокСотрудников КАК СписокСотрудников)) КАК РанееИсчисленныеВзносы
	|
	|СГРУППИРОВАТЬ ПО
	|	РанееИсчисленныеВзносы.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Основная.ФизЛицо КАК ФизЛицо,
	|	Основная.Месяц КАК Месяц,
	|	ВЫБОР
	|		КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ИЛИ ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоИнвалидныйМесяц,
	|	СУММА(ВЫБОР
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФР,
	|	СУММА(ВЫБОР
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаПенсию))
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФСС,
	|	СУММА(ВЫБОР
	|			КОГДА (Иностр.Физлицо ЕСТЬ NULL 
	|					ИЛИ (НЕ Иностр.НеИмеетПравоНаМедицинскоеСтрахование))
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМС,
	|	СУММА(ВЫБОР
	|			КОГДА (Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					ИЛИ ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов))
	|					И Основная.ВходитВБазуФСС
	|					И &парамЭтоЮрЛицо
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФСС_НСИнвалида,
	|	СУММА(ВЫБОР
	|			КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|					И &парамЭтоЮрЛицо
	|				ТОГДА 0
	|			КОГДА ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|				ТОГДА 0
	|			КОГДА Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФСС_НСПоОсновномуТарифу,
	|	ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов
	|ПОМЕСТИТЬ ВТДоходыЗастрахованныхЛиц
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК Основная
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОПравеНаПенсию КАК Иностр
	|		ПО Основная.ФизЛицо = Иностр.Физлицо
	|			И Основная.Месяц = Иностр.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбИнвалидности КАК Инвалид
	|		ПО Основная.ФизЛицо = Инвалид.Физлицо
	|			И Основная.Месяц = Инвалид.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользуемыеТарифы КАК ИспользуемыеТарифы
	|		ПО Основная.Месяц = ИспользуемыеТарифы.Месяц
	|ГДЕ
	|	(НЕ Основная.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|
	|СГРУППИРОВАТЬ ПО
	|	Основная.ФизЛицо,
	|	Основная.Месяц,
	|	ВЫБОР
	|		КОГДА Инвалид.Физлицо ЕСТЬ НЕ NULL 
	|				ИЛИ ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ИспользуемыеТарифы.ВидТарифаСтраховыхВзносов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МЕСЯЦ(Периоды.Период) КАК Месяц,
	|	ДоходыЗастрахованныхЛиц.ФизЛицо КАК ФизЛицо,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаПФР) КАК БазаПФР,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаФСС) КАК БазаФСС,
	|	СУММА(ДоходыЗастрахованныхЛиц.БазаФОМС) КАК БазаФОМС
	|ПОМЕСТИТЬ ВТНакопленнаяОблагаемаяБаза
	|ИЗ
	|	ВТПериоды КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоходыЗастрахованныхЛиц КАК ДоходыЗастрахованныхЛиц
	|		ПО (ДоходыЗастрахованныхЛиц.Месяц МЕЖДУ 1 И МЕСЯЦ(Периоды.Период))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыЗастрахованныхЛиц.ФизЛицо,
	|	МЕСЯЦ(Периоды.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	База.Месяц КАК Месяц,
	|	База.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА База.БазаПФР <= &ПредельнаяСумма
	|			ТОГДА 1
	|		КОГДА База.БазаПФР = ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0)
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0) >= &ПредельнаяСумма
	|			ТОГДА 0
	|		ИНАЧЕ (&ПредельнаяСумма - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0)) / (База.БазаПФР - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаПФР, 0))
	|	КОНЕЦ КАК КоэффициентОбложенияПФР,
	|	ВЫБОР
	|		КОГДА База.БазаФСС <= &ПредельнаяСумма
	|			ТОГДА 1
	|		КОГДА База.БазаФСС = ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0)
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0) >= &ПредельнаяСумма
	|			ТОГДА 0
	|		ИНАЧЕ (&ПредельнаяСумма - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0)) / (База.БазаФСС - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФСС, 0))
	|	КОНЕЦ КАК КоэффициентОбложенияФСС,
	|	ВЫБОР
	|		КОГДА База.БазаФОМС <= &ПредельнаяСумма
	|			ТОГДА 1
	|		КОГДА База.БазаФОМС = ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0)
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0) >= &ПредельнаяСумма
	|			ТОГДА 0
	|		ИНАЧЕ (&ПредельнаяСумма - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0)) / (База.БазаФОМС - ЕСТЬNULL(НакопленоПоПрошлыйМесяц.БазаФОМС, 0))
	|	КОНЕЦ КАК КоэффициентОбложенияФОМС
	|ПОМЕСТИТЬ ВТКоэффициентыДостиженияПредельнойСуммы
	|ИЗ
	|	ВТНакопленнаяОблагаемаяБаза КАК База
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНакопленнаяОблагаемаяБаза КАК НакопленоПоПрошлыйМесяц
	|		ПО (База.Месяц - 1 = НакопленоПоПрошлыйМесяц.Месяц)
	|			И База.ФизЛицо = НакопленоПоПрошлыйМесяц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоходыЗастрахованныхЛиц.ФизЛицо КАК ФизЛицо,
	|	ДоходыЗастрахованныхЛиц.Месяц КАК Месяц,
	|	ДоходыЗастрахованныхЛиц.ВидТарифаСтраховыхВзносов,
	|	ВЫБОР
	|		КОГДА (НЕ ДоходыЗастрахованныхЛиц.ЭтоИнвалидныйМесяц)
	|			ТОГДА 0
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 1
	|			ТОГДА НакопленнаяОблагаемаяБазаТекущийМесяц.БазаПФР - ВЫБОР
	|					КОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0) < &ПредельнаяСумма
	|						ТОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|					ИНАЧЕ &ПредельнаяСумма
	|				КОНЕЦ
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФР = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|	КОНЕЦ КАК БазаПФРИнвалида,
	|	ВЫБОР
	|		КОГДА ДоходыЗастрахованныхЛиц.ЭтоИнвалидныйМесяц
	|			ТОГДА 0
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 1
	|			ТОГДА НакопленнаяОблагаемаяБазаТекущийМесяц.БазаПФР - ВЫБОР
	|					КОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0) < &ПредельнаяСумма
	|						ТОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|					ИНАЧЕ &ПредельнаяСумма
	|				КОНЕЦ
	|		КОГДА Коэффициенты.КоэффициентОбложенияПФР = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаПФР = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаПФР, 0)
	|	КОНЕЦ КАК БазаПФРПоОсновномуТарифу,
	|	ВЫБОР
	|		КОГДА (НЕ ДоходыЗастрахованныхЛиц.ЭтоИнвалидныйМесяц)
	|			ТОГДА 0
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 1
	|			ТОГДА НакопленнаяОблагаемаяБазаТекущийМесяц.БазаФСС - ВЫБОР
	|					КОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0) < &ПредельнаяСумма
	|						ТОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|					ИНАЧЕ &ПредельнаяСумма
	|				КОНЕЦ
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФСС = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|	КОНЕЦ КАК БазаФССИнвалида,
	|	ВЫБОР
	|		КОГДА ДоходыЗастрахованныхЛиц.ЭтоИнвалидныйМесяц
	|			ТОГДА 0
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 1
	|			ТОГДА НакопленнаяОблагаемаяБазаТекущийМесяц.БазаФСС - ВЫБОР
	|					КОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0) < &ПредельнаяСумма
	|						ТОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|					ИНАЧЕ &ПредельнаяСумма
	|				КОНЕЦ
	|		КОГДА Коэффициенты.КоэффициентОбложенияФСС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФСС = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФСС, 0)
	|	КОНЕЦ КАК БазаФССПоОсновномуТарифу,
	|	ВЫБОР
	|		КОГДА (НЕ ДоходыЗастрахованныхЛиц.ЭтоИнвалидныйМесяц)
	|			ТОГДА 0
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 1
	|			ТОГДА НакопленнаяОблагаемаяБазаТекущийМесяц.БазаФОМС - ВЫБОР
	|					КОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0) < &ПредельнаяСумма
	|						ТОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|					ИНАЧЕ &ПредельнаяСумма
	|				КОНЕЦ
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМС = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|	КОНЕЦ КАК БазаФОМСИнвалида,
	|	ВЫБОР
	|		КОГДА ДоходыЗастрахованныхЛиц.ЭтоИнвалидныйМесяц
	|			ТОГДА 0
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 1
	|			ТОГДА НакопленнаяОблагаемаяБазаТекущийМесяц.БазаФОМС - ВЫБОР
	|					КОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0) < &ПредельнаяСумма
	|						ТОГДА ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|					ИНАЧЕ &ПредельнаяСумма
	|				КОНЕЦ
	|		КОГДА Коэффициенты.КоэффициентОбложенияФОМС = 0
	|			ТОГДА 0
	|		КОГДА ДоходыЗастрахованныхЛиц.БазаФОМС = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ПредельнаяСумма - ЕСТЬNULL(НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.БазаФОМС, 0)
	|	КОНЕЦ КАК БазаФОМСПоОсновномуТарифу
	|ПОМЕСТИТЬ ВТОграниченнаяОблагаемаяБаза
	|ИЗ
	|	ВТДоходыЗастрахованныхЛиц КАК ДоходыЗастрахованныхЛиц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоэффициентыДостиженияПредельнойСуммы КАК Коэффициенты
	|		ПО ДоходыЗастрахованныхЛиц.ФизЛицо = Коэффициенты.ФизЛицо
	|			И ДоходыЗастрахованныхЛиц.Месяц = Коэффициенты.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНакопленнаяОблагаемаяБаза КАК НакопленнаяОблагаемаяБазаПоПредыдущийМесяц
	|		ПО ДоходыЗастрахованныхЛиц.ФизЛицо = НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.ФизЛицо
	|			И (ДоходыЗастрахованныхЛиц.Месяц - 1 = НакопленнаяОблагаемаяБазаПоПредыдущийМесяц.Месяц)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНакопленнаяОблагаемаяБаза КАК НакопленнаяОблагаемаяБазаТекущийМесяц
	|		ПО ДоходыЗастрахованныхЛиц.ФизЛицо = НакопленнаяОблагаемаяБазаТекущийМесяц.ФизЛицо
	|			И ДоходыЗастрахованныхЛиц.Месяц = НакопленнаяОблагаемаяБазаТекущийМесяц.Месяц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо КАК ФизЛицо,
	|	ОграниченнаяОблагаемаяБаза.ВидТарифаСтраховыхВзносов,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРПоОсновномуТарифу) * ОсновнойТариф.ПФРНакопительная / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРНакопительная,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРПоОсновномуТарифу) * ОсновнойТариф.ПФР / 100 КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРПоОсновномуТарифу) * (ОсновнойТариф.ПФР - ОсновнойТариф.ПФРНакопительная) / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРСтраховая,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФССПоОсновномуТарифу) * ОсновнойТариф.ФСС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФСС,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСПоОсновномуТарифу) * ОсновнойТариф.ФФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФФОМС,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСПоОсновномуТарифу) * ОсновнойТариф.ТФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатТФОМС
	|ПОМЕСТИТЬ ВТИсчисленоПоОсновномуТарифу
	|ИЗ
	|	ВТОграниченнаяОблагаемаяБаза КАК ОграниченнаяОблагаемаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТарифы КАК ОсновнойТариф
	|		ПО ОграниченнаяОблагаемаяБаза.ВидТарифаСтраховыхВзносов = ОсновнойТариф.ВидТарифа
	|
	|СГРУППИРОВАТЬ ПО
	|	ОсновнойТариф.ПФРНакопительная,
	|	ОсновнойТариф.ПФР,
	|	ОсновнойТариф.ФСС,
	|	ОсновнойТариф.ФФОМС,
	|	ОсновнойТариф.ТФОМС,
	|	ОграниченнаяОблагаемаяБаза.ВидТарифаСтраховыхВзносов,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРИнвалида) * ТарифИнвалида.ПФРНакопительная / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРНакопительнаяИнвалида,
	|	ВЫБОР
	|		КОГДА ГОД(ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения) < 1967
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРИнвалида) * ТарифИнвалида.ПФР / 100 КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаПФРИнвалида) * (ТарифИнвалида.ПФР - ТарифИнвалида.ПФРНакопительная) / 100 КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК РезультатПФРСтраховаяИнвалида,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФССИнвалида) * ТарифИнвалида.ФСС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФССИнвалида,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСИнвалида) * ТарифИнвалида.ФФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатФФОМСИнвалида,
	|	ВЫРАЗИТЬ(СУММА(ОграниченнаяОблагаемаяБаза.БазаФОМСИнвалида) * ТарифИнвалида.ТФОМС / 100 КАК ЧИСЛО(15, 2)) КАК РезультатТФОМСИнвалида
	|ПОМЕСТИТЬ ВТИсчисленоПоЛьготномуТарифу
	|ИЗ
	|	ВТОграниченнаяОблагаемаяБаза КАК ОграниченнаяОблагаемаяБаза
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТарифы КАК ТарифИнвалида
	|		ПО (ТарифИнвалида.ВидТарифа = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТарифИнвалида.ПФРНакопительная,
	|	ТарифИнвалида.ПФР,
	|	ТарифИнвалида.ФСС,
	|	ТарифИнвалида.ФФОМС,
	|	ТарифИнвалида.ТФОМС,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо,
	|	ОграниченнаяОблагаемаяБаза.ФизЛицо.ДатаРождения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СУММА(ДоходыФизлиц.ОблагаемыйДоход) * 0.14 КАК ЧИСЛО(15, 2)) КАК РезультатПоДополнительномуТарифу,
	|	ДоходыФизлиц.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТВзносыПоДополнительномуТарифу
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК ДоходыФизлиц
	|ГДЕ
	|	ДоходыФизлиц.ОблагаетсяПоДополнительномуТарифу
	|	И ДоходыФизлиц.ВходитВБазуПФР
	|	И (НЕ ДоходыФизлиц.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыФизлиц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СУММА(ДоходыФизлиц.ОблагаемыйДоход) * 0.067 КАК ЧИСЛО(15, 2)) КАК РезультатПоДополнительномуТарифу,
	|	ДоходыФизлиц.ФизЛицо КАК ФизЛицо
	|ПОМЕСТИТЬ ВТВзносыДляШахтеров
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК ДоходыФизлиц
	|ГДЕ
	|	ДоходыФизлиц.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам
	|	И ДоходыФизлиц.ВходитВБазуПФР
	|	И (НЕ ДоходыФизлиц.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыФизлиц.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начислено.ФизЛицо КАК ФизЛицо,
	|	(ВЫРАЗИТЬ(Начислено.БазаФСС_НСПоОсновномуТарифу * &СтавкаВзносаФСС_НС_ПЗ КАК ЧИСЛО(15, 2))) + (ВЫРАЗИТЬ(Начислено.БазаФСС_НСИнвалида * &СтавкаВзносаФСС_НС_ПЗ * 0.6 КАК ЧИСЛО(15, 2))) - ЕСТЬNULL(РанееИсчисленныеВзносы.ФССНесчастныеСлучаиОборот, 0) КАК Взносов
	|ПОМЕСТИТЬ ВТВзносыФССНесчастныеСлучаи
	|ИЗ
	|	ВТДоходыЗастрахованныхЛиц КАК Начислено
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРанееИсчисленныеВзносы КАК РанееИсчисленныеВзносы
	|		ПО Начислено.ФизЛицо = РанееИсчисленныеВзносы.ФизЛицо
	|ГДЕ
	|	Начислено.Месяц = &парамМесяцРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсегоИсчислено.ФизЛицо КАК ФизЛицо,
	|	ВсегоИсчислено.Порядок,
	|	СУММА(ВсегоИсчислено.РезультатПФРНакопительная) КАК РезультатПФРНакопительная,
	|	СУММА(ВсегоИсчислено.РезультатПФРСтраховая) КАК РезультатПФРСтраховая,
	|	СУММА(ВсегоИсчислено.РезультатФСС) КАК РезультатФСС,
	|	СУММА(ВсегоИсчислено.РезультатФФОМС) КАК РезультатФФОМС,
	|	СУММА(ВсегоИсчислено.РезультатТФОМС) КАК РезультатТФОМС,
	|	СУММА(ВсегоИсчислено.РезультатПоДополнительномуТарифу) КАК РезультатПоДополнительномуТарифу,
	|	СУММА(ВсегоИсчислено.РезультатФССНесчастныеСлучаи) КАК РезультатФССНесчастныеСлучаи,
	|	СУММА(ВсегоИсчислено.РезультатДляШахтеров) КАК РезультатДляШахтеров
	|ПОМЕСТИТЬ ВТСуммыВзносовНарастающимИтогом
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИсчисленоПоОсновномуТарифу.ФизЛицо КАК ФизЛицо,
	|		ИсчисленоПоОсновномуТарифу.ФизЛицо.Наименование КАК Порядок,
	|		ИсчисленоПоОсновномуТарифу.РезультатПФРНакопительная КАК РезультатПФРНакопительная,
	|		ИсчисленоПоОсновномуТарифу.РезультатПФРСтраховая КАК РезультатПФРСтраховая,
	|		ИсчисленоПоОсновномуТарифу.РезультатФСС КАК РезультатФСС,
	|		ИсчисленоПоОсновномуТарифу.РезультатФФОМС КАК РезультатФФОМС,
	|		ИсчисленоПоОсновномуТарифу.РезультатТФОМС КАК РезультатТФОМС,
	|		0 КАК РезультатПоДополнительномуТарифу,
	|		0 КАК РезультатФССНесчастныеСлучаи,
	|		0 КАК РезультатДляШахтеров
	|	ИЗ
	|		ВТИсчисленоПоОсновномуТарифу КАК ИсчисленоПоОсновномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ИсчисленоПоЛьготномуТарифу.ФизЛицо,
	|		ИсчисленоПоЛьготномуТарифу.ФизЛицо.Наименование,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРНакопительнаяИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатПФРСтраховаяИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатФССИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатФФОМСИнвалида,
	|		ИсчисленоПоЛьготномуТарифу.РезультатТФОМСИнвалида,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		ВТИсчисленоПоЛьготномуТарифу КАК ИсчисленоПоЛьготномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВзносыПоДополнительномуТарифу.ФизЛицо,
	|		ВзносыПоДополнительномуТарифу.ФизЛицо.Наименование,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВзносыПоДополнительномуТарифу.РезультатПоДополнительномуТарифу,
	|		0,
	|		0
	|	ИЗ
	|		ВТВзносыПоДополнительномуТарифу КАК ВзносыПоДополнительномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВзносыПоДополнительномуТарифу.ФизЛицо,
	|		ВзносыПоДополнительномуТарифу.ФизЛицо.Наименование,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВзносыПоДополнительномуТарифу.РезультатПоДополнительномуТарифу
	|	ИЗ
	|		ВТВзносыДляШахтеров КАК ВзносыПоДополнительномуТарифу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВзносыФС_НС_ПЗ.ФизЛицо,
	|		ВзносыФС_НС_ПЗ.ФизЛицо.Наименование,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ВзносыФС_НС_ПЗ.Взносов,
	|		0
	|	ИЗ
	|		ВТВзносыФССНесчастныеСлучаи КАК ВзносыФС_НС_ПЗ) КАК ВсегоИсчислено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсегоИсчислено.ФизЛицо,
	|	ВсегоИсчислено.Порядок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыНарастающимИтогом.ФизЛицо КАК ФизЛицо,
	|	СуммыНарастающимИтогом.Порядок КАК Порядок,
	|	СуммыНарастающимИтогом.РезультатФСС - ЕСТЬNULL(ИсчисленныеВзносы.ФССОборот, 0) КАК ФСС,
	|	СуммыНарастающимИтогом.РезультатФФОМС - ЕСТЬNULL(ИсчисленныеВзносы.ФФОМСОборот, 0) КАК ФФОМС,
	|	СуммыНарастающимИтогом.РезультатТФОМС - ЕСТЬNULL(ИсчисленныеВзносы.ТФОМСОборот, 0) КАК ТФОМС,
	|	СуммыНарастающимИтогом.РезультатПФРНакопительная - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНакопительнаяОборот, 0) КАК ПФРНакопительная,
	|	СуммыНарастающимИтогом.РезультатПФРСтраховая - ЕСТЬNULL(ИсчисленныеВзносы.ПФРСтраховаяОборот, 0) КАК ПФРСтраховая,
	|	СуммыНарастающимИтогом.РезультатПоДополнительномуТарифу - ЕСТЬNULL(ИсчисленныеВзносы.ПФРПоДополнительномуТарифуОборот, 0) КАК ПФРПоДополнительномуТарифу,
	|	СуммыНарастающимИтогом.РезультатФССНесчастныеСлучаи КАК ФССНесчастныеСлучаи,
	|	СуммыНарастающимИтогом.РезультатДляШахтеров - ЕСТЬNULL(ИсчисленныеВзносы.ПФРНаДоплатуКПенсииШахтерамОборот, 0) КАК ПФРНаДоплатуКПенсииШахтерам
	|ПОМЕСТИТЬ ВТВзносыЗаМесяц
	|ИЗ
	|	ВТСуммыВзносовНарастающимИтогом КАК СуммыНарастающимИтогом
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРанееИсчисленныеВзносы КАК ИсчисленныеВзносы
	|		ПО СуммыНарастающимИтогом.ФизЛицо = ИсчисленныеВзносы.ФизЛицо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Основная.ФизЛицо КАК ФизЛицо,
	|	СУММА(ВЫБОР
	|			КОГДА (НЕ Основная.ОблагаетсяЕНВД)
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФР,
	|	СУММА(ВЫБОР
	|			КОГДА (НЕ Основная.ОблагаетсяЕНВД)
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФСС,
	|	СУММА(ВЫБОР
	|			КОГДА (НЕ Основная.ОблагаетсяЕНВД)
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМС,
	|	СУММА(ВЫБОР
	|			КОГДА Основная.ОблагаетсяЕНВД
	|					И Основная.ВходитВБазуПФР
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаПФРЕНВД,
	|	СУММА(ВЫБОР
	|			КОГДА Основная.ОблагаетсяЕНВД
	|					И Основная.ВходитВБазуФСС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФССЕНВД,
	|	СУММА(ВЫБОР
	|			КОГДА Основная.ОблагаетсяЕНВД
	|					И Основная.ВходитВБазуФОМС
	|				ТОГДА Основная.ОблагаемыйДоход
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК БазаФОМСЕНВД
	|ПОМЕСТИТЬ ВТРаспределениеДоходовПоЕНВД
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК Основная
	|ГДЕ
	|	(НЕ Основная.ФизЛицо В
	|				(ВЫБРАТЬ
	|					СписокСотрудников.ФизЛицо
	|				ИЗ
	|					ВТФизлицаБезОблагаемойБазы КАК СписокСотрудников))
	|	И Основная.Месяц = &парамМесяцРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	Основная.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СУММА(ВЫБОР
	|						КОГДА СведенияОДоходах.ОблагаетсяЕНВД
	|							ТОГДА СведенияОДоходах.ОблагаемыйДоход
	|						ИНАЧЕ 0
	|					КОНЕЦ) > 0
	|				И СУММА(ВЫБОР
	|						КОГДА (НЕ СведенияОДоходах.ОблагаетсяЕНВД)
	|							ТОГДА СведенияОДоходах.ОблагаемыйДоход
	|						ИНАЧЕ 0
	|					КОНЕЦ) = 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоЕНВД,
	|	СведенияОДоходах.ФизЛицо
	|ПОМЕСТИТЬ ВТВидыДеятельности
	|ИЗ
	|	ВТДоходыПоФизлицамМесяцам КАК СведенияОДоходах
	|ГДЕ
	|	СведенияОДоходах.Месяц = &парамМесяцРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходах.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Взносы.ФизЛицо,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) = 0
	|				И (НЕ ВидыДеятельности.ТолькоЕНВД)
	|			ТОГДА Взносы.ФФОМС
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ Взносы.ФФОМС - (ВЫРАЗИТЬ(Взносы.ФФОМС * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК ФФОМС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) = 0
	|				И ВидыДеятельности.ТолькоЕНВД
	|			ТОГДА Взносы.ФФОМС
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(Взносы.ФФОМС * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК ФФОМСЕНВД,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) = 0
	|				И (НЕ ВидыДеятельности.ТолькоЕНВД)
	|			ТОГДА Взносы.ТФОМС
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ Взносы.ТФОМС - (ВЫРАЗИТЬ(Взносы.ТФОМС * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК ТФОМС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) = 0
	|				И ВидыДеятельности.ТолькоЕНВД
	|			ТОГДА Взносы.ТФОМС
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(Взносы.ТФОМС * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФОМС + РаспределениеДоходовПоЕНВД.БазаФОМСЕНВД, 0) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК ТФОМСЕНВД,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФСС + РаспределениеДоходовПоЕНВД.БазаФССЕНВД, 0) = 0
	|				И (НЕ ВидыДеятельности.ТолькоЕНВД)
	|			ТОГДА Взносы.ФСС
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФСС + РаспределениеДоходовПоЕНВД.БазаФССЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ Взносы.ФСС - (ВЫРАЗИТЬ(Взносы.ФСС * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФССЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФСС + РаспределениеДоходовПоЕНВД.БазаФССЕНВД, 0) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК ФСС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФСС + РаспределениеДоходовПоЕНВД.БазаФССЕНВД, 0) = 0
	|				И ВидыДеятельности.ТолькоЕНВД
	|			ТОГДА Взносы.ФСС
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФСС + РаспределениеДоходовПоЕНВД.БазаФССЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(Взносы.ФСС * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФССЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаФСС + РаспределениеДоходовПоЕНВД.БазаФССЕНВД, 0) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК ФССЕНВД,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) = 0
	|				И (НЕ ВидыДеятельности.ТолькоЕНВД)
	|			ТОГДА Взносы.ПФРНакопительная
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ Взносы.ПФРНакопительная - (ВЫРАЗИТЬ(Взносы.ПФРНакопительная * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК ПФРНакопительная,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) = 0
	|				И ВидыДеятельности.ТолькоЕНВД
	|			ТОГДА Взносы.ПФРНакопительная
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(Взносы.ПФРНакопительная * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК ПФРНакопительнаяЕНВД,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) = 0
	|				И (НЕ ВидыДеятельности.ТолькоЕНВД)
	|			ТОГДА Взносы.ПФРСтраховая
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ Взносы.ПФРСтраховая - (ВЫРАЗИТЬ(Взносы.ПФРСтраховая * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК ПФРСтраховая,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) = 0
	|				И ВидыДеятельности.ТолькоЕНВД
	|			ТОГДА Взносы.ПФРСтраховая
	|		КОГДА ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(Взносы.ПФРСтраховая * ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) / ЕСТЬNULL(РаспределениеДоходовПоЕНВД.БазаПФР + РаспределениеДоходовПоЕНВД.БазаПФРЕНВД, 0) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК ПФРСтраховаяЕНВД,
	|	Взносы.ПФРПоДополнительномуТарифу,
	|	Взносы.ФССНесчастныеСлучаи,
	|	Взносы.ПФРНаДоплатуКПенсииШахтерам
	|ИЗ
	|	ВТВзносыЗаМесяц КАК Взносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРаспределениеДоходовПоЕНВД КАК РаспределениеДоходовПоЕНВД
	|		ПО Взносы.ФизЛицо = РаспределениеДоходовПоЕНВД.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыДеятельности КАК ВидыДеятельности
	|		ПО Взносы.ФизЛицо = ВидыДеятельности.ФизЛицо
	|ГДЕ
	|	(Взносы.ФСС <> 0
	|			ИЛИ Взносы.ФФОМС <> 0
	|			ИЛИ Взносы.ТФОМС <> 0
	|			ИЛИ Взносы.ПФРНакопительная <> 0
	|			ИЛИ Взносы.ПФРСтраховая <> 0
	|			ИЛИ Взносы.ПФРПоДополнительномуТарифу <> 0
	|			ИЛИ Взносы.ПФРНаДоплатуКПенсииШахтерам <> 0
	|			ИЛИ Взносы.ФССНесчастныеСлучаи <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Взносы.Порядок";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрСведений.УчетнаяПолитикаНалоговыйУчет", ЗаполнениеРегламентированнойОтчетностиПереопределяемый.ИмяУчетнойПолитики());
	
	Возврат Запрос.Выполнить();
	
КонецФункции  // ПолучитьДанныеРасчета
// Процедура рассчитывает Страховые взносы по введенным доходам
Процедура РасчетСтраховыхВзносов(ГоловнаяОрганизация)
	
	ДанныеОДоходах = РегистрыНакопления.СтраховыеВзносыСведенияОДоходах.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ВыборкаПоДоходам = СформироватьЗапросПоДоходамДляСтраховыхВзносов(ГоловнаяОрганизация).Выбрать();
	Пока ВыборкаПоДоходам.Следующий() Цикл
		СтрокаНабора = ДанныеОДоходах.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНабора,ВыборкаПоДоходам);
		СтрокаНабора.Организация = ГоловнаяОрганизация;
		СтрокаНабора.ОбособленноеПодразделение = Организация;
		СтрокаНабора.Период = ПериодРегистрации;
	КонецЦикла;
	
	// получаем страховые взносы по периоду регистрации
	Если ПериодРегистрации < ПроведениеРасчетов.ДатаРасширенияПеречняЛьготныхТарифовСтраховыхВзносов() Тогда
		
		ДанныеДляРасчетаСтраховыхВзносов = ПолучитьДанныеРасчетаСтраховыхВзносовЗа2010Год(ГоловнаяОрганизация,,ДанныеОДоходах).Выбрать();
		// очищаем табличную часть
		ИсчисленныеСтраховыеВзносы.Очистить();
		
		// заполняем движения по регистру исчисленного ЕСН-ПФР
		Пока ДанныеДляРасчетаСтраховыхВзносов.Следующий() Цикл
			
			// Пропустим нулевые строки
			Если ДанныеДляРасчетаСтраховыхВзносов.ФСС = 0 
				И ДанныеДляРасчетаСтраховыхВзносов.ФФОМС = 0 И ДанныеДляРасчетаСтраховыхВзносов.ТФОМС = 0 
				И ДанныеДляРасчетаСтраховыхВзносов.ПФРНакопительная = 0 И ДанныеДляРасчетаСтраховыхВзносов.ПФРСтраховая = 0
				И ДанныеДляРасчетаСтраховыхВзносов.ПФРНакопительнаяЕНВД = 0 И ДанныеДляРасчетаСтраховыхВзносов.ПФРСтраховаяЕНВД = 0
				И ДанныеДляРасчетаСтраховыхВзносов.ФССНесчастныеСлучаи = 0 И ДанныеДляРасчетаСтраховыхВзносов.ПФРПоДополнительномуТарифу = 0 
				Тогда			
				Продолжить;
			КонецЕсли; 
			
			// табличная часть
			НоваяСтрока = ИсчисленныеСтраховыеВзносы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляРасчетаСтраховыхВзносов);
			
		КонецЦикла;	
		
	Иначе
		ДанныеДляРасчетаСтраховыхВзносов = ПолучитьДанныеРасчета(ГоловнаяОрганизация,,ДанныеОДоходах).Выбрать();
		// очищаем табличную часть
		ИсчисленныеСтраховыеВзносы.Очистить();
		
		// заполняем движения по регистру исчисленного ЕСН-ПФР
		Пока ДанныеДляРасчетаСтраховыхВзносов.Следующий() Цикл
			
			// Пропустим нулевые строки
			Если ДанныеДляРасчетаСтраховыхВзносов.ФСС = 0 
				И ДанныеДляРасчетаСтраховыхВзносов.ФФОМС = 0 И ДанныеДляРасчетаСтраховыхВзносов.ТФОМС = 0 
				И ДанныеДляРасчетаСтраховыхВзносов.ПФРНакопительная = 0 И ДанныеДляРасчетаСтраховыхВзносов.ПФРСтраховая = 0
				И ДанныеДляРасчетаСтраховыхВзносов.ПФРНаДоплатуКПенсииШахтерам = 0 
				И ДанныеДляРасчетаСтраховыхВзносов.ФССНесчастныеСлучаи = 0 И ДанныеДляРасчетаСтраховыхВзносов.ПФРПоДополнительномуТарифу = 0 
				И ДанныеДляРасчетаСтраховыхВзносов.ФФОМСЕНВД = 0 
				И ДанныеДляРасчетаСтраховыхВзносов.ТФОМСЕНВД = 0 
				И ДанныеДляРасчетаСтраховыхВзносов.ФССЕНВД = 0 
                И ДанныеДляРасчетаСтраховыхВзносов.ПФРНакопительнаяЕНВД = 0 
                И ДанныеДляРасчетаСтраховыхВзносов.ПФРСтраховаяЕНВД = 0 
				Тогда			
				Продолжить;
			КонецЕсли; 
			
			// табличная часть
			НоваяСтрока = ИсчисленныеСтраховыеВзносы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДляРасчетаСтраховыхВзносов);
			
			
		КонецЦикла;	
		
	КонецЕсли;
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Выполняет проверку правильности заполнения строки табличной части о доходах
Процедура ПроверитьЗаполнениеСтрокиДохода(ВыборкаПоДоходам, Отказ, УчетнаяПолитикаПоПерсоналуОрганизации, мГоловнаяОрганизация = Неопределено)
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоДоходам.НомерСтроки) +
	""" табл. части ""Налоговая база"": ";
	
	Если ВыборкаПоДоходам.Сотрудник = Справочники.СотрудникиОрганизаций.ПустаяСсылка() Тогда
		ОбщегоНазначения.ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "Не указан сотрудник!", Отказ);
	КонецЕсли;
	
	Если ПериодГод2010 Тогда
		Если ВыборкаПоДоходам.ВидДохода = Справочники.ДоходыПоСтраховымВзносам.ПустаяСсылка() Тогда
			ОбщегоНазначения.ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "Не указан Вид дохода страховых взносов!", Отказ);
		КонецЕсли;
	Иначе
		Если ВыборкаПоДоходам.КодДоходаЕСН = Справочники.ДоходыЕСН.ПустаяСсылка() Тогда
			ОбщегоНазначения.ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке + "Не указан код дохода ЕСН!", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если мГоловнаяОрганизация = Неопределено Тогда		
		// Организация сотрудника должна совпадать с организацией документа
		Если ВыборкаПоДоходам.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
			Сообщить(СтрокаНачалаСообщенияОбОшибке + "Указанный сотрудник оформлен на другую организацию!");
			Отказ = Истина;
		КонецЕсли;
	Иначе
		Если ВыборкаПоДоходам.Сотрудник.Организация <> мГоловнаяОрганизация Тогда
			Сообщить(СтрокаНачалаСообщенияОбОшибке + "Указанный сотрудник оформлен на другую организацию!");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
Конецпроцедуры

// расчет-проведение документа
Процедура РассчитатьЕСН(ВыборкаПоШапкеДокумента = Неопределено) Экспорт
	
	ОпределитьПериод();
	
	Отказ = Ложь;

	Если ВыборкаПоШапкеДокумента = Неопределено Тогда
		ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
		ВыборкаПоШапкеДокумента.Следующий();
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Удалим рассчитанный ранее исчисленный налог
	НаборЗаписейЕСНИсчисленный = РегистрыНакопления.ЕСНИсчисленный.СоздатьНаборЗаписей();
	НаборЗаписейЕСНИсчисленный.Отбор.Регистратор.Значение = Ссылка;
	НаборЗаписейЕСНИсчисленный.Записать();
	
	// Запишем данные о доходах из табличных частей в регистры расчетов
	НаборЗаписейОДоходах = РегистрыНакопления.ЕСНСведенияОДоходах.СоздатьНаборЗаписей();
	НаборЗаписейОДоходах.Отбор.Регистратор.Значение = Ссылка;
	
	ЗаписатьДанныеОДоходах(Отказ, ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, НаборЗаписейОДоходах);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Вызовем функцию расчета ЕСН (результат расчета помещается в табличную часть  "ИсчисленныйЕСН")
	РасчетЕСН(ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	
	// Очистим ранее записанные в регистр сведения о доходах
	НаборЗаписейОДоходах.Очистить();
	НаборЗаписейОДоходах.Записать();
	
КонецПроцедуры

Процедура РассчитатьСтраховыеВзносы (ВыборкаПоШапкеДокумента = Неопределено) Экспорт
	
	ОпределитьПериод();
	
	Отказ = Ложь;

	Если ВыборкаПоШапкеДокумента = Неопределено Тогда
		ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
		ВыборкаПоШапкеДокумента.Следующий();
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Удалим рассчитанный ранее исчисленный налог
	СтраховыеВзносыИсчисленные = РегистрыНакопления.СтраховыеВзносыИсчисленные.СоздатьНаборЗаписей();
	СтраховыеВзносыИсчисленные.Отбор.Регистратор.Значение = Ссылка;
	СтраховыеВзносыИсчисленные.Записать();
	
	// Запишем данные о доходах из табличных частей в регистры расчетов
	НаборЗаписейОДоходах = РегистрыНакопления.СтраховыеВзносыСведенияОДоходах.СоздатьНаборЗаписей();
	НаборЗаписейОДоходах.Отбор.Регистратор.Значение = Ссылка;
	
	ЗаписатьДанныеОДоходах(Отказ, ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, НаборЗаписейОДоходах);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Вызовем функцию расчета ЕСН (результат расчета помещается в табличную часть  "ИсчисленныйЕСН")
	РасчетСтраховыхВзносов(ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	
	// Очистим ранее записанные в регистр сведения о доходах
	НаборЗаписейОДоходах.Очистить();
	НаборЗаписейОДоходах.Записать();	
	
КонецПроцедуры

// Процедура рассчитывает скидки по зарегистрированным доходам и записывает их в реквизит "Скидка" набора записей регистра "ЕСНДополнительныеНачисления"
// При расчете учитываются ранее предоставленные скидки в прошлых периодах и в текущем. 
// Если скидки были не предоставлены или неверно рассчитаны, то происходит их корректировка.
Процедура РасчетСкидокКДоходамЕСН(ГоловнаяОрганизация, ОсновныеНачисления)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамАвторскиеВознаграждения", Справочники.ДоходыЕСН.ДоговораАвторские);
	
	Запрос.УстановитьПараметр("парамРегистратор",						Ссылка);
	Запрос.УстановитьПараметр("парамКонецМесяцаПериодаРегистрации",		КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамНачалоГода",						НачалоГода(ПериодРегистрации));
	
	// ---------------------------------------------------------------------------------------
	// Тексты запросов
	
	
	// ДоходыПоФизлицамКодам
	// Сведения о ранее предоставленных скидках и доходах налогового периода
	// Предполагается, что доходы, по которым могут быть предоставлены скидки, не регистрируются 
	
	// в регистре "ЕСНОсновныеНачисления".
	
	ДоходыПоФизлицамКодам = 
	"ВЫБРАТЬ
	|	ЕСНСведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|	ЕСНСведенияОДоходах.КодДоходаЕСН КАК КодДоходаЕСН,
	|	ЕСНСведенияОДоходах.Результат КАК Результат,
	|	ЕСНСведенияОДоходах.Скидка КАК Скидка
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЕСНДополнительныеНачисления.Сотрудник.ФизЛицо КАК ФизЛицо
	|	ИЗ
	|		Документ.РасчетЕСН.ОсновныеНачисления КАК ЕСНДополнительныеНачисления
	|	ГДЕ
	|		ЕСНДополнительныеНачисления.Ссылка = &парамРегистратор) КАК СписокФизЛиц
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЕСНСведенияОДоходах КАК ЕСНСведенияОДоходах
	|		ПО ЕСНСведенияОДоходах.ФизЛицо = СписокФизЛиц.ФизЛицо
	|ГДЕ
	|	ЕСНСведенияОДоходах.Период МЕЖДУ &парамНачалоГода И &парамКонецМесяцаПериодаРегистрации
	|	И ЕСНСведенияОДоходах.КодДоходаЕСН <> &парамАвторскиеВознаграждения
	|	И ЕСНСведенияОДоходах.Организация = &парамГоловнаяОрганизация";
	
	//   Добавим сведения о нормативных размерах скидок ЕСН по кодам доходов
	
	// ДанныеОДоходахИСкидках
	
	ДанныеОСкидках = "
	|Выбрать
	|	ДоходыПоМесяцам.ФизЛицо,
	|	ДоходыПоМесяцам.КодДоходаЕСН,
	|	СУММА(ДоходыПоМесяцам.Результат) КАК РезультатЗаГод,
	|	СУММА(ДоходыПоМесяцам.Скидка) КАК ПредоставленнаяСкидкаЗаГод,
	|	ЕСНСкидкиКДоходам.ГодоваяСкидка КАК ПравоНаСкидку
	|ИЗ
	|	( " + ДоходыПоФизлицамКодам +  ") КАК ДоходыПоМесяцам 
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЕСНСкидкиКДоходам.СрезПоследних(&парамНачалоГода, ) КАК ЕСНСкидкиКДоходам
	|		ПО ЕСНСкидкиКДоходам.КодДоходаЕСН = ДоходыПоМесяцам.КодДоходаЕСН
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыПоМесяцам.ФизЛицо,
	|	ДоходыПоМесяцам.КодДоходаЕСН,
	|	ЕСНСкидкиКДоходам.ГодоваяСкидка";
	
	Запрос.Текст = ДанныеОСкидках;
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураПоиска = Новый Структура("Сотрудник, КодДоходаЕСН");
	
	// Выгрузим набор для модификации скидки
	МодифицируемаяТаблица = ОсновныеНачисления.Выгрузить();
	МодифицируемаяТаблица.Индексы.Добавить("Сотрудник, КодДоходаЕСН");
	
	Пока Выборка.Следующий() Цикл
		
		// В общем случае предоставляемая скидка есть
		Скидка = Мин(Выборка.РезультатЗаГод, Выборка.ПравоНаСкидку) - Выборка.ПредоставленнаяСкидкаЗаГод;
		
		Если Скидка = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Распределим скидку между строками о доходах
		ЗаполнитьЗначенияСвойств(СтруктураПоиска,Выборка); // Физлицо, КодДоходаЕСН
		
		НайдСтроки = МодифицируемаяТаблица.НайтиСтроки(СтруктураПоиска);
		Если НайдСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОсталосьРаспределить = Скидка;
		Для Каждого НайденнаяСтрока из НайдСтроки Цикл
			НайденнаяСтрока.Скидка = Мин(ОсталосьРаспределить, НайденнаяСтрока.Результат);
			ОсталосьРаспределить = ОсталосьРаспределить - НайденнаяСтрока.Скидка;
		КонецЦикла;
		// Остаток распределения сложим в первую строку
		НайдСтроки[0].Скидка = НайдСтроки[0].Скидка + ОсталосьРаспределить;
		
	КонецЦикла;
	
	// Загрузим модифицированный набор
	ОсновныеНачисления.Загрузить(МодифицируемаяТаблица);
	
КонецПроцедуры

// Процедура рассчитывает скидки по зарегистрированным доходам и записывает их в реквизит "Скидка" набора записей регистра "ЕСНДополнительныеНачисления"
// При расчете учитываются ранее предоставленные скидки в прошлых периодах и в текущем. 
// Если скидки были не предоставлены или неверно рассчитаны, то происходит их корректировка.
Процедура РасчетСкидокКДоходамСтраховыеВзносы(ГоловнаяОрганизация, НаборЗаписейОДоходах)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("парамГоловнаяОрганизация", ГоловнаяОрганизация);
	МассивАвторских = Новый Массив;
	МассивАвторских.Добавить(Справочники.ДоходыПоСтраховымВзносам.АвторскиеАудиовизуальныеПроизведения);
	МассивАвторских.Добавить(Справочники.ДоходыПоСтраховымВзносам.АвторскиеГрафическиеПроизведения);
	МассивАвторских.Добавить(Справочники.ДоходыПоСтраховымВзносам.АвторскиеДругиеМузыкальныеПроизведения);
	МассивАвторских.Добавить(Справочники.ДоходыПоСтраховымВзносам.АвторскиеИсполнениеПроизведений);
	МассивАвторских.Добавить(Справочники.ДоходыПоСтраховымВзносам.АвторскиеЛитературныеПроизведения);
	МассивАвторских.Добавить(Справочники.ДоходыПоСтраховымВзносам.АвторскиеМузыкальноСценическиеПроизведение);
	МассивАвторских.Добавить(Справочники.ДоходыПоСтраховымВзносам.АвторскиеНаучныеТруды);
	МассивАвторских.Добавить(Справочники.ДоходыПоСтраховымВзносам.АвторскиеОткрытия);
	МассивАвторских.Добавить(Справочники.ДоходыПоСтраховымВзносам.АвторскиеСкульптуры);
	Запрос.УстановитьПараметр("парамАвторскиеВознаграждения", МассивАвторских);
	
	Запрос.УстановитьПараметр("парамРегистратор",						Ссылка);
	Запрос.УстановитьПараметр("парамКонецМесяцаПериодаРегистрации",		КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамНачалоГода",						НачалоГода(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамФизлица",							НаборЗаписейОДоходах.ВыгрузитьКолонку("ФизЛицо"));
	Запрос.УстановитьПараметр("парамДанныеОДоходах",					НаборЗаписейОДоходах);
	
	// ---------------------------------------------------------------------------------------
	// Тексты запросов
	
	
	// ДоходыПоФизлицамКодам
	// Сведения о ранее предоставленных скидках и доходах налогового периода
	// Предполагается, что доходы, по которым могут быть предоставлены скидки, не регистрируются 
	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|	СведенияОДоходах.ВидДохода КАК ВидДохода,
	|	СведенияОДоходах.Результат,
	|	СведенияОДоходах.Скидка КАК Скидка
	|ПОМЕСТИТЬ ВТДанныеДокумента
	|ИЗ
	|	&парамДанныеОДоходах КАК СведенияОДоходах
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидДохода,
	|	ФизЛицо";
	Запрос.Выполнить();
	
	// ДанныеОДоходахИСкидках
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь) КАК ВидДохода,
	|	ДоходыПоМесяцам.ФизЛицо,
	|	СУММА(ДоходыПоМесяцам.Результат) КАК РезультатЗаГод,
	|	СУММА(ДоходыПоМесяцам.Скидка) КАК ПредоставленнаяСкидкаЗаГод
	|ИЗ
	|	(ВЫБРАТЬ
	|		СведенияОДоходах.ФизЛицо КАК ФизЛицо,
	|		СведенияОДоходах.РезультатОборот КАК Результат,
	|		СведенияОДоходах.СкидкаОборот КАК Скидка
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыСведенияОДоходах.Обороты(
	|				&парамНачалоГода,
	|				&парамКонецМесяцаПериодаРегистрации,
	|				Месяц,
	|				Организация = &парамГоловнаяОрганизация
	|					И ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь)
	|					И Физлицо В (&парамФизлица)) КАК СведенияОДоходах
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|		-СтраховыеВзносыСведенияОДоходах.Результат,
	|		-СтраховыеВзносыСведенияОДоходах.Скидка
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыСведенияОДоходах КАК СтраховыеВзносыСведенияОДоходах
	|	ГДЕ
	|		СтраховыеВзносыСведенияОДоходах.Регистратор = &парамРегистратор
	|		И СтраховыеВзносыСведенияОДоходах.ФизЛицо В(&парамФизлица)
	|		И СтраховыеВзносыСведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтраховыеВзносыСведенияОДоходах.ФизЛицо,
	|		СтраховыеВзносыСведенияОДоходах.Результат,
	|		0
	|	ИЗ
	|		ВТДанныеДокумента КАК СтраховыеВзносыСведенияОДоходах
	|	ГДЕ
	|		СтраховыеВзносыСведенияОДоходах.ФизЛицо В(&парамФизлица)
	|		И СтраховыеВзносыСведенияОДоходах.ВидДохода = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.Матпомощь)) КАК ДоходыПоМесяцам
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыПоМесяцам.ФизЛицо";


	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураПоиска = Новый Структура("ФизЛицо, ВидДохода");
	ПравоНаСкидку = РегистрыСведений.СтраховыеВзносыСкидкиКДоходам.ПолучитьПоследнее(НачалоГода(ПериодРегистрации), Новый Структура("ВидДохода", Справочники.ДоходыПоСтраховымВзносам.Матпомощь)).ГодоваяСкидка;
	Если Не ЗначениеЗаполнено(ПравоНаСкидку) Тогда
		ПравоНаСкидку = 0
	КонецЕсли;
	
	// Выгрузим набор для модификации скидки
	МодифицируемаяТаблица = ОсновныеНачисления.Выгрузить();
	МодифицируемаяТаблица.Индексы.Добавить("ФизЛицо, ВидДохода");
	
	Пока Выборка.Следующий() Цикл
		
		// В общем случае предоставляемая скидка есть
		Скидка = Мин(Выборка.РезультатЗаГод, ПравоНаСкидку) - Выборка.ПредоставленнаяСкидкаЗаГод;
		
		Если Скидка = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		// Распределим скидку между строками о доходах
		ЗаполнитьЗначенияСвойств(СтруктураПоиска,Выборка); // Физлицо, КодДоходаЕСН
		
		НайдСтроки = МодифицируемаяТаблица.НайтиСтроки(СтруктураПоиска);
		Если НайдСтроки.Количество() = 0 Тогда
			Продолжить;
		Иначе
			ДоходЗаМесяц = 0;
			Для Каждого СтрокаТаблицы Из НайдСтроки Цикл
				ДоходЗаМесяц = ДоходЗаМесяц + СтрокаТаблицы.Результат;
			КонецЦикла;
		КонецЕсли;
		
		Для Каждого НайденнаяСтрока из НайдСтроки Цикл
			ДоляСкидки = НайденнаяСтрока.Результат/ДоходЗаМесяц;
			НайденнаяСтрока.Скидка = Мин(Скидка*ДоляСкидки, НайденнаяСтрока.Результат);
		КонецЦикла;
		
	КонецЦикла;
	
	// Загрузим модифицированный набор
	ОсновныеНачисления.Загрузить(МодифицируемаяТаблица);
	
КонецПроцедуры
	
Процедура ЗаполнитьОсновныеНачисления(ВыборкаПоШапкеДокумента)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация",			ВыборкаПоШапкеДокумента.Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ПериодРегистрации",		ВыборкаПоШапкеДокумента.ПериодРегистрации);
	Запрос.УстановитьПараметр("ДатаНалоговогоПериода", КонецГода(ПериодРегистрации));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Сотрудник,
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Результат,
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.ВидРасчета,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыРаботникамОрганизацийНачисления.ВидРасчета.КодДоходаСтраховыеВзносы = ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыПоСтраховымВзносам.ОблагаетсяЦеликом)
	|		ИНАЧЕ НачислениеЗарплатыРаботникамОрганизацийНачисления.ВидРасчета.КодДоходаСтраховыеВзносы
	|	КОНЕЦ КАК КодДоходаСтраховыеВзносы,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыРаботникамОрганизацийНачисления.ВидРасчета.КодДоходаЕСН = ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоходыЕСН.ОблагаетсяЦеликом)
	|		ИНАЧЕ НачислениеЗарплатыРаботникамОрганизацийНачисления.ВидРасчета.КодДоходаЕСН
	|	КОНЕЦ КАК КодДоходаЕСН,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыРаботникамОрганизацийНачисления.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.МатпомощьПриРожденииРебенка)
	|			ТОГДА ВЫБОР
	|					КОГДА НачислениеЗарплатыРаботникамОрганизацийНачисления.Результат < НачислениеЗарплатыРаботникамОрганизацийНачисления.СуммаВычета * ЕСТЬNULL(ВычетПоМатпомощиПриРождении.НормативЗатрат, 0)
	|						ТОГДА НачислениеЗарплатыРаботникамОрганизацийНачисления.Результат
	|					ИНАЧЕ НачислениеЗарплатыРаботникамОрганизацийНачисления.СуммаВычета * ЕСТЬNULL(ВычетПоМатпомощиПриРождении.НормативЗатрат, 0)
	|				КОНЕЦ
	|		КОГДА НачислениеЗарплатыРаботникамОрганизацийНачисления.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код404)
	|			ТОГДА НачислениеЗарплатыРаботникамОрганизацийНачисления.СуммаВычета
	|		КОГДА НачислениеЗарплатыРаботникамОрганизацийНачисления.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код405)
	|			ТОГДА НачислениеЗарплатыРаботникамОрганизацийНачисления.СуммаВычета
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВычета
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК НачислениеЗарплатыРаботникамОрганизацийНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛВычетыПоДоходам.СрезПоследних(&ДатаНалоговогоПериода, КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.МатпомощьПриРожденииРебенка)) КАК ВычетПоМатпомощиПриРождении
	|		ПО НачислениеЗарплатыРаботникамОрганизацийНачисления.ВидРасчета.КодДоходаНДФЛ = ВычетПоМатпомощиПриРождении.КодДохода
	|ГДЕ
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка.Организация = &Организация
	|	И НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка.ПериодРегистрации = &ПериодРегистрации
	|	И НачислениеЗарплатыРаботникамОрганизацийНачисления.Ссылка.Проведен";
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Сотрудник,
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Сотрудник.ФизЛицо КАК ФизЛицо,
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.Результат,
	|	ЕСТЬNULL(ЕНВДПроцентСрезПоследних.ПодпадаетПодЕНВД, 0) КАК ПодпадаетПодЕНВД,
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.ВидРасчета,
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.КодДоходаЕСН,
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.КодДоходаСтраховыеВзносы,
	|	НачислениеЗарплатыРаботникамОрганизацийНачисления.СуммаВычета
	|ИЗ
	|	ВТСотрудники КАК НачислениеЗарплатыРаботникамОрганизацийНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентДеятельностиЕНВД.СрезПоследних(
	|				&ПериодРегистрации,
	|				Организация = &ГоловнаяОрганизация
	|					И Сотрудник В
	|						(ВЫБРАТЬ
	|							Сотрудники.Сотрудник
	|						ИЗ
	|							ВТСотрудники КАК Сотрудники)) КАК ЕНВДПроцентСрезПоследних
	|		ПО НачислениеЗарплатыРаботникамОрганизацийНачисления.Сотрудник = ЕНВДПроцентСрезПоследних.Сотрудник";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОсновныеНачисления.Очистить();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока	= ОсновныеНачисления.Добавить();
		НоваяСтрока.Сотрудник			= Выборка.Сотрудник;
		НоваяСтрока.ФизЛицо				= Выборка.ФизЛицо;
		НоваяСтрока.ВидРасчета			= Выборка.ВидРасчета;
		НоваяСтрока.КодДоходаЕСН		= Выборка.КодДоходаЕСН;
		НоваяСтрока.ВидДохода			= Выборка.КодДоходаСтраховыеВзносы;
		НоваяСтрока.Скидка				= Выборка.СуммаВычета;
		
		Если Выборка.ПодпадаетПодЕНВД = 0 Тогда
			НоваяСтрока.Результат		= Выборка.Результат;
			НоваяСтрока.ОблагаетсяЕНВД	= Ложь;
		ИначеЕсли Выборка.ПодпадаетПодЕНВД = 100 Тогда
			НоваяСтрока.Результат		= Выборка.Результат;
			НоваяСтрока.ОблагаетсяЕНВД	= Истина;
		Иначе
			Результат   = Выборка.Результат*Выборка.ПодпадаетПодЕНВД/100;
			СуммаВычета = Выборка.СуммаВычета*Выборка.ПодпадаетПодЕНВД/100;
			НоваяСтрока.Результат		= Результат;
			НоваяСтрока.Скидка			= СуммаВычета;
			НоваяСтрока.ОблагаетсяЕНВД	= Истина;
			
			НоваяСтрока	= ОсновныеНачисления.Добавить();
			НоваяСтрока.Сотрудник		= Выборка.Сотрудник;
			НоваяСтрока.ФизЛицо			= Выборка.ФизЛицо;
			НоваяСтрока.ВидРасчета		= Выборка.ВидРасчета;
			НоваяСтрока.КодДоходаЕСН	= Выборка.КодДоходаЕСН;
			НоваяСтрока.ВидДохода		= Выборка.КодДоходаСтраховыеВзносы;
			НоваяСтрока.Результат		= Выборка.Результат - Результат;
			НоваяСтрока.Скидка			= Выборка.СуммаВычета - СуммаВычета;
			НоваяСтрока.ОблагаетсяЕНВД	= Ложь;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

// заполнение доходов и расчет-проведение документа
Процедура ЗаполнитьИРассчитать(Рассчитать = Истина) Экспорт
	
	ОпределитьПериод();
	
	Если ПериодГод2010 Тогда
		ЗаполнитьИРассчитатьДляСтраховыхВзносов(Рассчитать);
	Иначе
		ЗаполнитьИРассчитатьДляЕСН(Рассчитать)
	КонецЕсли;
	
	ОсновныеНачисления.Сортировать("ФизЛицо");
	Если ПериодГод2010 Тогда
		ИсчисленныеСтраховыеВзносы.Сортировать("ФизЛицо");
	Иначе
		ИсчисленныйЕСН.Сортировать("ФизЛицо");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьИРассчитатьДляЕСН(Рассчитать = Истина)
	
	Отказ = Ложь;
	
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ);

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Удалим рассчитанный ранее исчисленный налог
	НаборЗаписейЕСНИсчисленный = РегистрыНакопления.ЕСНИсчисленный.СоздатьНаборЗаписей();
	НаборЗаписейЕСНИсчисленный.Отбор.Регистратор.Значение = Ссылка;
	НаборЗаписейЕСНИсчисленный.Записать();
	
	// Создадим ссылки на наборы записей о доходах
	НаборЗаписейОДоходах = РегистрыНакопления.ЕСНСведенияОДоходах.СоздатьНаборЗаписей();
	НаборЗаписейОДоходах.Отбор.Регистратор.Значение = Ссылка;
	
	ЗаполнитьОсновныеНачисления(ВыборкаПоШапкеДокумента);
	//
	// Автозаполним наборы записей о доходах
	ЗаписатьДанныеОДоходах(Отказ, ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, НаборЗаписейОДоходах);
	// Расчет скидок к доходам
	РасчетСкидокКДоходамЕСН(ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, ОсновныеНачисления);
	
	// Удалим движения документа после расчета
	НаборЗаписейОДоходах.Очистить();
	НаборЗаписейОДоходах.Записать();
	
	Если Отказ Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не обнаружены данные о доходах сотрудников для целей учета ЕСН. Расчет не произведен!");		
		Возврат;
	КонецЕсли;
	
	Если Рассчитать Тогда
		РассчитатьЕСН(ВыборкаПоШапкеДокумента);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьИРассчитатьДляСтраховыхВзносов(Рассчитать = Истина)
	
	Отказ = Ложь;
	
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	мГоловнаяОрганизация = ВыборкаПоШапкеДокумента.Организация;
	
	// Удалим рассчитанный ранее исчисленный налог
	НаборЗаписейСтраховыеВзносыИсчисленные = РегистрыНакопления.СтраховыеВзносыИсчисленные.СоздатьНаборЗаписей();
	НаборЗаписейСтраховыеВзносыИсчисленные.Отбор.Регистратор.Значение = Ссылка;
	НаборЗаписейСтраховыеВзносыИсчисленные.Записать();
	
	// Создадим ссылки на наборы записей о доходах
	НаборЗаписейОДоходах = РегистрыНакопления.СтраховыеВзносыСведенияОДоходах.СоздатьНаборЗаписей();
	НаборЗаписейОДоходах.Отбор.Регистратор.Значение = Ссылка;
	
	ЗаполнитьОсновныеНачисления(ВыборкаПоШапкеДокумента);
	//
	// Автозаполним наборы записей о доходах
	ЗаписатьДанныеОДоходах(Отказ, ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, НаборЗаписейОДоходах);
	// Расчет скидок к доходам
	РасчетСкидокКДоходамСтраховыеВзносы(ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, НаборЗаписейОДоходах.Выгрузить());
	
	// Удалим движения документа после расчета
	НаборЗаписейОДоходах.Очистить();
	НаборЗаписейОДоходах.Записать();
	
	
	Если Отказ Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не обнаружены данные о доходах сотрудников для целей учета страховых взносов. Расчет не произведен!");				
		Возврат;
	КонецЕсли;
	
	Если Рассчитать Тогда
		РассчитатьСтраховыеВзносы(ВыборкаПоШапкеДокумента);
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//  нет
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапросПоШапке()
 
	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);	

	Если ПериодГод2010 Тогда
		
		мГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);	
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", мГоловнаяОрганизация);
		Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетЕСН.Дата,
		|	РасчетЕСН.Организация,
		|	РасчетЕСН.Ответственный,
		|	РасчетЕСН.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА РасчетЕСН.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА РасчетЕСН.Организация
		|		ИНАЧЕ РасчетЕСН.Организация.ГоловнаяОрганизация
		|	КОНЕЦ КАК ГоловнаяОрганизация,
		|	ВЫБОР
		|		КОГДА УчетнаяПолитикаОрганизацийСрезПоследних.ВидТарифаСтраховыхВзносов = ЗНАЧЕНИЕ(Перечисление.ТарифыСтраховыхВзносов.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеУказанВидТарифа,
		|	РасчетЕСН.Ссылка
		|ИЗ
		|	Документ.РасчетЕСН КАК РасчетЕСН
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&ПериодРегистрации, Организация = &ГоловнаяОрганизация) КАК УчетнаяПолитикаОрганизацийСрезПоследних
		|		ПО (ИСТИНА)
		|ГДЕ
		|	РасчетЕСН.Ссылка = &ДокументСсылка";
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РасчетЕСН.Дата,
		|	РасчетЕСН.Организация,
		|	РасчетЕСН.Ответственный,
		|	РасчетЕСН.ПериодРегистрации,
		|	ВЫБОР
		|		КОГДА РасчетЕСН.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА РасчетЕСН.Организация
		|		ИНАЧЕ РасчетЕСН.Организация.ГоловнаяОрганизация
		|	КОНЕЦ КАК ГоловнаяОрганизация,
		|	РасчетЕСН.Ссылка
		|ИЗ
		|	Документ.РасчетЕСН КАК РасчетЕСН
		|ГДЕ
		|	РасчетЕСН.Ссылка = &ДокументСсылка";
	КонецЕсли;
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизитов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//  Отказ 		 - флаг отказа в проведении.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок = "")

	Если мСообщать <> Истина Тогда
		Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПериодРегистрации) Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Если ПериодГод2010 Тогда
			Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.НеУказанВидТарифа) И  ВыборкаПоШапкеДокумента.НеУказанВидТарифа Тогда
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;			
		Возврат;
	Иначе
		Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
			ОбщегоНазначения.ОшибкаПриПроведении("Не указана организация!", Отказ);
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПериодРегистрации) Тогда
			ОбщегоНазначения.ОшибкаПриПроведении("Не указан период регистрации!", Отказ);
		КонецЕсли;
		
		Если ПериодГод2010 Тогда
			Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.НеУказанВидТарифа) И  ВыборкаПоШапкеДокумента.НеУказанВидТарифа Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Для организации """ + ВыборкаПоШапкеДокумента.ГоловнаяОрганизация.Наименование + """ в учетной политике не указан вид тарифа страховых взносов!",Отказ, Заголовок);			
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	

КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Формирует запрос по табличным частям документа "ОсновныеНачисление" и "ДополнительныеНачисления"
Функция СформироватьЗапросПоДоходамДляЕСН(ВыборкаПоШапкеДокумента)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("парамРегистратор",		Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	""Основные"" КАК ТипСтроки,
	|	Основные.НомерСтроки,
	|	Основные.ВидРасчета,
	|	Основные.Сотрудник КАК Сотрудник,
	|	Основные.Сотрудник.Физлицо КАК Физлицо,
	|	Основные.КодДоходаЕСН,
	|	Основные.ОблагаетсяЕНВД,
	|	Основные.Результат,
	|	ВЫБОР
	|		КОГДА Основные.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	Основные.Скидка КАК Скидка
	|ИЗ
	|	Документ.РасчетЕСН.ОсновныеНачисления КАК Основные
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РаботникиОрганизацииСрез.Сотрудник КАК Сотрудник
	|		ИЗ
	|			(ВЫБРАТЬ
	|				МАКСИМУМ(РаботникиОрганизации.Период) КАК ПериодСреза,
	|				Основные.Сотрудник КАК Сотрудник
	|			ИЗ
	|				Документ.РасчетЕСН.ОсновныеНачисления КАК Основные
	|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|					ПО Основные.Сотрудник = РаботникиОрганизации.Сотрудник
	|						И (РаботникиОрганизации.Организация = &ГоловнаяОрганизация)
	|			ГДЕ
	|				Основные.Ссылка = &парамРегистратор
	|			
	|			СГРУППИРОВАТЬ ПО
	|				Основные.Сотрудник) КАК РаботникиОрганизацииСрез
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|				ПО РаботникиОрганизацииСрез.Сотрудник = РаботникиОрганизации.Сотрудник
	|					И (РаботникиОрганизации.Организация = &ГоловнаяОрганизация)
	|					И РаботникиОрганизацииСрез.ПериодСреза = РаботникиОрганизации.Период) КАК РаботникиОрганизацииСрез
	|		ПО Основные.Сотрудник = РаботникиОрганизацииСрез.Сотрудник
	|ГДЕ
	|	Основные.Ссылка = &парамРегистратор";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Формирует запрос по табличным частям документа "ОсновныеНачисление" и "ДополнительныеНачисления"
Функция СформироватьЗапросПоДоходамДляСтраховыхВзносов(ВыборкаПоШапкеДокумента)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("парамРегистратор",		Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	?(ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ГоловнаяОрганизация),ВыборкаПоШапкеДокумента.ГоловнаяОрганизация,ВыборкаПоШапкеДокумента));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	""Основные"" КАК ТипСтроки,
	|	Основные.НомерСтроки,
	|	Основные.ВидРасчета,
	|	Основные.Сотрудник КАК Сотрудник,
	|	Основные.Сотрудник.Физлицо КАК Физлицо,
	|	Основные.ВидДохода,
	|	Основные.ОблагаетсяЕНВД,
	|	Основные.ОблагаетсяПоДополнительномуТарифу,
	|	Основные.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам,
	|	Основные.Результат,
	|	ВЫБОР
	|		КОГДА Основные.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	Основные.Скидка КАК Скидка
	|ИЗ
	|	Документ.РасчетЕСН.ОсновныеНачисления КАК Основные
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РаботникиОрганизацииСрез.Сотрудник КАК Сотрудник
	|		ИЗ
	|			(ВЫБРАТЬ
	|				МАКСИМУМ(РаботникиОрганизации.Период) КАК ПериодСреза,
	|				Основные.Сотрудник КАК Сотрудник
	|			ИЗ
	|				Документ.РасчетЕСН.ОсновныеНачисления КАК Основные
	|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|					ПО Основные.Сотрудник = РаботникиОрганизации.Сотрудник
	|						И (РаботникиОрганизации.Организация = &ГоловнаяОрганизация)
	|			ГДЕ
	|				Основные.Ссылка = &парамРегистратор
	|			
	|			СГРУППИРОВАТЬ ПО
	|				Основные.Сотрудник) КАК РаботникиОрганизацииСрез
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|				ПО РаботникиОрганизацииСрез.Сотрудник = РаботникиОрганизации.Сотрудник
	|					И (РаботникиОрганизации.Организация = &ГоловнаяОрганизация)
	|					И РаботникиОрганизацииСрез.ПериодСреза = РаботникиОрганизации.Период) КАК РаботникиОрганизацииСрез
	|		ПО Основные.Сотрудник = РаботникиОрганизацииСрез.Сотрудник
	|ГДЕ
	|	Основные.Ссылка = &парамРегистратор";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Формирует запрос по табличной части документа "ИсчисленныйЕСН" 
Функция СформироватьЗапросПоНалогамДляЕСН()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("парамРегистратор",	Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Налог.НомерСтроки,
	|	Налог.ФизЛицо,
	|	Налог.ФедеральныйБюджет,
	|	Налог.ФСС,
	|	Налог.ФФОМС,
	|	Налог.ТФОМС,
	|	Налог.ПФРНакопительная,
	|	Налог.ПФРСтраховая,
	|	Налог.ПримененнаяЛьготаФБ,
	|	Налог.ПримененнаяЛьготаФСС,
	|	Налог.ПримененнаяЛьготаФОМС,
	|	Налог.ПФРНакопительнаяЕНВД,
	|	Налог.ПФРСтраховаяЕНВД
	|ИЗ
	|	Документ.РасчетЕСН.ИсчисленныйЕСН КАК Налог
	|ГДЕ
	|	Налог.Ссылка = &парамРегистратор";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Формирует запрос по табличной части документа "ИсчисленныйЕСН" 
Функция СформироватьЗапросПоНалогамДляСтраховыхВзносов()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("парамРегистратор",	Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Налог.НомерСтроки,
	|	Налог.ФизЛицо,
	|	Налог.ФСС,
	|	Налог.ФФОМС,
	|	Налог.ТФОМС,
	|	Налог.ПФРНакопительная,
	|	Налог.ПФРСтраховая,
	|	Налог.ФССНесчастныеСлучаи,
	|	Налог.ПФРПоДополнительномуТарифу,
	|	Налог.ПФРНакопительнаяЕНВД,
	|	Налог.ПФРСтраховаяЕНВД,
	|	Налог.ПФРНаДоплатуКПенсииШахтерам,
	|	Налог.ФССЕНВД,
	|	Налог.ФФОМСЕНВД,
	|	Налог.ТФОМСЕНВД
	|ИЗ
	|	Документ.РасчетЕСН.ИсчисленныеСтраховыеВзносы КАК Налог
	|ГДЕ
	|	Налог.Ссылка = &парамРегистратор";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Выполняет проверку правильности заполнения строки табличной части налогов
Процедура ПроверитьЗаполнениеСтрокиНалога(ВыборкаПоНалогам, Отказ)
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоНалогам.НомерСтроки) +
	""" табл. части Налоги (взносы с ФОТ): ";
	
	Если ВыборкаПоНалогам.ФизЛицо = Справочники.ФизическиеЛица.ПустаяСсылка() Тогда
		ОбщегоНазначения.ОшибкаПриПроведении(СтрокаНачалаСообщенияОбОшибке  + "Не указан работник!", Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет запись в движения по регистру учета налогов
Процедура ДобавитьСтрокуВДвиженияПоНалогам(ВыборкаПоНалогам, ВыборкаПоШапкеДокумента)
	
	Если ПериодГод2011 Тогда
		Строка = Движения.СтраховыеВзносыИсчисленные.Добавить();
		
		// свойства
		
		Строка.Период						= ПериодРегистрации;
		
		// Измерения
		Строка.ФизЛицо						= ВыборкаПоНалогам.ФизЛицо;
		Строка.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		
		// ресурсы
		Строка.ФСС							= ВыборкаПоНалогам.ФСС;
		Строка.ФФОМС						= ВыборкаПоНалогам.ФФОМС;
		Строка.ТФОМС						= ВыборкаПоНалогам.ТФОМС;
		Строка.ПФРНакопительная				= ВыборкаПоНалогам.ПФРНакопительная;
		Строка.ПФРСтраховая					= ВыборкаПоНалогам.ПФРСтраховая;
		Строка.ФССНесчастныеСлучаи			= ВыборкаПоНалогам.ФССНесчастныеСлучаи;
		Строка.ПФРПоДополнительномуТарифу   = ВыборкаПоНалогам.ПФРПоДополнительномуТарифу;
		Строка.ПФРНаДоплатуКПенсииШахтерам  = ВыборкаПоНалогам.ПФРНаДоплатуКПенсииШахтерам;
		Строка.ФССЕНВД						= ВыборкаПоНалогам.ФССЕНВД;
		Строка.ФФОМСЕНВД					= ВыборкаПоНалогам.ФФОМСЕНВД;
		Строка.ТФОМСЕНВД					= ВыборкаПоНалогам.ТФОМСЕНВД;
		Строка.ПФРНакопительнаяЕНВД			= ВыборкаПоНалогам.ПФРНакопительнаяЕНВД;
		Строка.ПФРСтраховаяЕНВД				= ВыборкаПоНалогам.ПФРСтраховаяЕНВД;
				
		// реквизиты
		Строка.ОбособленноеПодразделение	= ВыборкаПоШапкеДокумента.Организация;
		
	ИначеЕсли ПериодГод2010 Тогда // Страховые взносы
		
		Строка = Движения.СтраховыеВзносыИсчисленные.Добавить();
		
		// свойства
		
		Строка.Период						= ПериодРегистрации;
		
		// Измерения
		Строка.ФизЛицо						= ВыборкаПоНалогам.ФизЛицо;
		Строка.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		
		// ресурсы
		Строка.ФСС							= ВыборкаПоНалогам.ФСС;
		Строка.ФФОМС						= ВыборкаПоНалогам.ФФОМС;
		Строка.ТФОМС						= ВыборкаПоНалогам.ТФОМС;
		Строка.ПФРНакопительная				= ВыборкаПоНалогам.ПФРНакопительная;
		Строка.ПФРСтраховая					= ВыборкаПоНалогам.ПФРСтраховая;
		Строка.ПФРНакопительнаяЕНВД			= ВыборкаПоНалогам.ПФРНакопительнаяЕНВД;
		Строка.ПФРСтраховаяЕНВД				= ВыборкаПоНалогам.ПФРСтраховаяЕНВД;
		Строка.ФССНесчастныеСлучаи			= ВыборкаПоНалогам.ФССНесчастныеСлучаи;
		Строка.ПФРПоДополнительномуТарифу   = ВыборкаПоНалогам.ПФРПоДополнительномуТарифу;
		
		// реквизиты
		Строка.ОбособленноеПодразделение	= ВыборкаПоШапкеДокумента.Организация;
		
	Иначе // ЕСН
		Строка = Движения.ЕСНИсчисленный.Добавить();
		
		// свойства
		
		Строка.Период						= ПериодРегистрации;
		
		// Измерения
		Строка.ФизЛицо						= ВыборкаПоНалогам.ФизЛицо;
		Строка.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		
		// ресурсы
		Строка.ФедеральныйБюджет			= ВыборкаПоНалогам.ФедеральныйБюджет;
		Строка.ФСС							= ВыборкаПоНалогам.ФСС;
		Строка.ФФОМС						= ВыборкаПоНалогам.ФФОМС;
		Строка.ТФОМС						= ВыборкаПоНалогам.ТФОМС;
		Строка.ПФРНакопительная				= ВыборкаПоНалогам.ПФРНакопительная;
		Строка.ПФРСтраховая					= ВыборкаПоНалогам.ПФРСтраховая;
		Строка.ПФРНакопительнаяЕНВД			= ВыборкаПоНалогам.ПФРНакопительнаяЕНВД;
		Строка.ПФРСтраховаяЕНВД				= ВыборкаПоНалогам.ПФРСтраховаяЕНВД;
		Строка.ПримененнаяЛьготаФБ			= ВыборкаПоНалогам.ПримененнаяЛьготаФБ;
		Строка.ПримененнаяЛьготаФСС			= ВыборкаПоНалогам.ПримененнаяЛьготаФСС;
		Строка.ПримененнаяЛьготаФОМС		= ВыборкаПоНалогам.ПримененнаяЛьготаФОМС;
		
		// реквизиты
		Строка.ОбособленноеПодразделение	= ВыборкаПоШапкеДокумента.Организация;
	КонецЕсли;
	
КонецПроцедуры  // ДобавитьСтрокуВДвиженияПоНалогам

// Добавляет запись в движения по регистру учета доходов
Процедура ДобавитьСтрокуВДвиженияПоДоходам(ВыборкаПоДоходам, ГоловнаяОрганизация)
	
	Если ПериодГод2010 Тогда // Страховые взносы
		
		// продублируем сведения о доходах в рег-ре налогового учета
		Строка = Движения.СтраховыеВзносыСведенияОДоходах.Добавить();
		
		// свойства
		Строка.Период						= ПериодРегистрации;
		
		// Измерения
		Строка.Физлицо						= ВыборкаПоДоходам.Физлицо;
		Строка.Организация					= ГоловнаяОрганизация;
		
		Строка.ОблагаетсяПоДополнительномуТарифу = ВыборкаПоДоходам.ОблагаетсяПоДополнительномуТарифу;
		Строка.ОблагаетсяЕНВД				= ВыборкаПоДоходам.ОблагаетсяЕНВД;	
		
		Если ПериодРегистрации >= ПроведениеРасчетов.ДатаРасширенияПеречняЛьготныхТарифовСтраховыхВзносов() Тогда		
			Строка.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам = ВыборкаПоДоходам.ОблагаетсяВзносамиНаДоплатуКПенсииШахтерам;	
		КонецЕсли;
		
		
		// ресурсы
		Строка.Результат					= ВыборкаПоДоходам.Результат;
		Строка.Скидка						= ВыборкаПоДоходам.Скидка;
		
		// реквизиты
		Строка.ВидДохода					= ВыборкаПоДоходам.ВидДохода;
		Строка.ВидРасчета					= ВыборкаПоДоходам.ВидРасчета;
		Строка.ОбособленноеПодразделение	= Организация;
		
	Иначе //ЕСН
		// продублируем сведения о доходах в рег-ре налогового учета
		Строка = Движения.ЕСНСведенияОДоходах.Добавить();
		
		// свойства
		Строка.Период						= ПериодРегистрации;
		
		// Измерения
		Строка.Физлицо						= ВыборкаПоДоходам.Физлицо;
		Строка.Организация					= ГоловнаяОрганизация;
		
		// ресурсы
		Строка.Результат					= ВыборкаПоДоходам.Результат;
		Строка.Скидка						= ВыборкаПоДоходам.Скидка;
		
		// реквизиты
		Строка.КодДоходаЕСН					= ВыборкаПоДоходам.КодДоходаЕСН;
		Строка.ОблагаетсяЕНВД				= ВыборкаПоДоходам.ОблагаетсяЕНВД;
		Строка.ОбособленноеПодразделение	= Организация;
	КонецЕсли;
	
КонецПроцедуры  // ДобавитьСтрокуВДвиженияПоДоходам

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)

	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = "Проведение документа ""Начисление налогов (взносов) с ФОТ"" за " + Формат(ПериодРегистрации, "ДФ='MMMM yyyy'") + ": ";
	
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(КонецМесяца(ПериодРегистрации), Отказ, Организация);
	Если УчетнаяПолитика.Количество() = 0 Тогда		
		Возврат;
	КонецЕсли;
	
	ОпределитьПериод();
	
	// Проверка ручной корректировки
	Если ОбщегоНазначения.РучнаяКорректировкаОбработкаПроведения(РучнаяКорректировка,Отказ,Заголовок,ЭтотОбъект) Тогда
		Возврат
	КонецЕсли;

	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	ВыборкаПоШапкеДокумента.Следующий();
	мСообщать = Истина;
	ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
	мСообщать = Ложь;
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ПериодГод2010 Тогда
		ВыборкаПоДоходам = СформироватьЗапросПоДоходамДляСтраховыхВзносов(ВыборкаПоШапкеДокумента.ГоловнаяОрганизация).Выбрать();
	Иначе
		ВыборкаПоДоходам = СформироватьЗапросПоДоходамДляЕСН(ВыборкаПоШапкеДокумента).Выбрать();
	КонецЕсли; 
	
	УчетнаяПолитикаПоПерсоналуОрганизации = глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации");
	
	Если НЕ КорректировкаРасчета Тогда
		Если ВыборкаПоДоходам.Количество() = 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Нет данных о заработной плате за " + Формат(ВыборкаПоШапкеДокумента.ПериодРегистрации, "ДФ='MMMM yyyy'") + " г. Страховые взносы не начислены!",,Заголовок, СтатусСообщения.Внимание);			
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Пока ВыборкаПоДоходам.Следующий() Цикл
		ПроверитьЗаполнениеСтрокиДохода(ВыборкаПоДоходам, Отказ, УчетнаяПолитикаПоПерсоналуОрганизации);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		ДобавитьСтрокуВДвиженияПоДоходам(ВыборкаПоДоходам,ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	КонецЦикла;
	
	Если ПериодГод2010 Тогда
		ВыборкаПоНалогам = СформироватьЗапросПоНалогамДляСтраховыхВзносов().Выбрать();
	Иначе
		ВыборкаПоНалогам = СформироватьЗапросПоНалогамДляЕСН().Выбрать();
	КонецЕсли; 	
	
	Пока ВыборкаПоНалогам.Следующий() Цикл
		ПроверитьЗаполнениеСтрокиНалога(ВыборкаПоНалогам, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		ДобавитьСтрокуВДвиженияПоНалогам(ВыборкаПоНалогам, ВыборкаПоШапкеДокумента);
	КонецЦикла;
	
	Записать();
	
	//сформируем движения в бухгалтерском и налоговом учете
	СформироватьДвиженияПоРегистрамРегл(ВыборкаПоШапкеДокумента, Режим);
	ЗакрытиеМесяца.ЗаписатьСписокНеАктуальныхРегОпераций(Организация, Дата, "ФОТ");
	
КонецПроцедуры  // ОбработкаПроведения

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РасчетЕСН.Ссылка
		|ИЗ
		|	Документ.РасчетЕСН КАК РасчетЕСН
		|ГДЕ
		|	РасчетЕСН.Ссылка <> &парамСсылка
		|	И РасчетЕСН.ПериодРегистрации = &парамПериодРегистрации
		|	И РасчетЕСН.Проведен = ИСТИНА
		|	И РасчетЕСН.Организация = &парамОрганизация";
		Запрос.УстановитьПараметр("парамСсылка", Ссылка);
		Запрос.УстановитьПараметр("парамОрганизация", Организация);
		Запрос.УстановитьПараметр("парамПериодРегистрации", ПериодРегистрации);
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			#Если Клиент Тогда
			ОбщегоНазначения.ОшибкаПриПроведении("Повторение операции Начисление налогов (взносов) с ФОТ за период " + РаботаСДиалогами.ПолучитьПредставлениеПериодаРегистрации(ПериодРегистрации) + " г.", Отказ);
			#КонецЕсли
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ КорректировкаРасчета Тогда
		//Запись документа		
		Если ЭтоНовый() И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда			
			Записать(РежимЗаписиДокумента.Запись);
			ЗаполнитьИРассчитать(Ложь);
			Записать(РежимЗаписиДокумента.Запись);
			ЗаполнитьИРассчитать();
		ИначеЕсли Проведен И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда			
			ЗаполнитьИРассчитать(Ложь);
			Записать(РежимЗаписиДокумента.ОтменаПроведения);			
			ЗаполнитьИРассчитать();
		ИначеЕсли НЕ Проведен И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			ЗаполнитьИРассчитать(Ложь);
			Записать(РежимЗаписиДокумента.Запись);
			ЗаполнитьИРассчитать();
		КонецЕсли;				
	КонецЕсли;
	
	Если Год(ПериодРегистрации) >= 2010 Тогда //Страховые взносы
		КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(ИсчисленныеСтраховыеВзносы,, "Физлицо");
	Иначе
		КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(ИсчисленныйЕСН,, "Физлицо");
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвиженияПоРегистрамРегл(ВыборкаПоШапкеДокумента, Режим)
	
	ПрименениеУСН = НалоговыйУчетУСН.ПрименениеУСН(Организация, КонецМесяца(ПериодРегистрации));
	ОСН_ФизЛицо = (Организация.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо И НЕ ПрименениеУСН);
	НеВедетсяНУ = ПрименениеУСН ИЛИ ОСН_ФизЛицо;
	мПоддержкаПБУ18 = НалоговыйУчет.ПрименениеПБУ18(Организация, Дата);
		
	ПроцедурыУчетаЗарплаты.ПодготовитьТаблицуЗначений(ОтражениеВУчете);
	
	мУСН 												= УчетнаяПолитика.УСН;
	ГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	
	ОСН_ФизЛицо = (Организация.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо И НЕ мУСН);
	
	// фактически - создаем структуру промежуточной таблицы
	ПромежуточнаяТаблица = ОтражениеВУчете.Скопировать();

	НачалоПериодаОтраженияВУчете = НачалоГода(ПериодРегистрации);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// получим список физлиц и подразделения в которых они работают на конец месяца
	// готовим списки счетов расчетов
	ПроцедурыУчетаЗарплаты.СписокФИзЛицИСчетовУчета(Запрос, Организация, ГоловнаяОрганизация, ПериодРегистрации);	
	
	//////////////////////////////////////////////////////////////////////////////////////////////////
	// создание таблицы начислений
	ПроцедурыУчетаЗарплаты.ПодборНачислений(Запрос, Ссылка, УчетнаяПолитика, ПериодРегистрации, ОтражениеВУчете, ПромежуточнаяТаблица);
	// конец создания таблицы начислений
	//////////////////////////////////////////////////////////////////////////////////////////////////

	ПромежуточнаяТаблица.Очистить();
	
	ПроцедурыУчетаЗарплаты.СоздатьВТОтражениеВУчете(Запрос, ОтражениеВУчете, Ссылка);
	
	Если ПериодГод2011 Тогда
		//////////////////////////////////////////////////////////////////////////////////////////////////
		// отражение исчисленных страховых взносов
		ПроцедурыУчетаЗарплаты.ОтражениеИсчисленныхСтраховыхВзносов2011(Запрос, Ссылка, Организация, ГоловнаяОрганизация, УчетнаяПолитика, ПериодРегистрации, НачалоПериодаОтраженияВУчете, ОтражениеВУчете, ПромежуточнаяТаблица, Истина);
		// конец отражения исчисленных страховых взносов
		//////////////////////////////////////////////////////////////////////////////////////////////////
	ИначеЕсли ПериодГод2010 Тогда
		//////////////////////////////////////////////////////////////////////////////////////////////////
		// отражение исчисленных страховых взносов
		ПроцедурыУчетаЗарплаты.ОтражениеИсчисленныхСтраховыхВзносов2010(Запрос, Ссылка, Организация, ГоловнаяОрганизация, УчетнаяПолитика, ПериодРегистрации, НачалоПериодаОтраженияВУчете, ОтражениеВУчете, ПромежуточнаяТаблица, Истина);
		// конец отражения исчисленных страховых взносов
		//////////////////////////////////////////////////////////////////////////////////////////////////
	Иначе
		//////////////////////////////////////////////////////////////////////////////////////////////////
		// отражение исчисленного ЕСН
		ПроцедурыУчетаЗарплаты.ОтражениеИсчисленногоЕСН(Запрос, Ссылка, Организация, ГоловнаяОрганизация, УчетнаяПолитика, ПериодРегистрации, НачалоПериодаОтраженияВУчете, ОтражениеВУчете, ПромежуточнаяТаблица, Истина);
		// конец отражения исчисленного ЕСН
		//////////////////////////////////////////////////////////////////////////////////////////////////
	КонецЕсли;

	
	/////////////////////////////////////////////////////////////////
	РезультатЗапросаПоОтражениюВУчете = СформироватьЗапросПоОтражениюВУчете(ОтражениеВУчете);
	ВыборкаПоОтражениюВУчете = РезультатЗапросаПоОтражениюВУчете.Выбрать();
	
	Пока ВыборкаПоОтражениюВУчете.Следующий() Цикл
		ПроцедурыУчетаЗарплаты.ДобавитьСтрокуВДвиженияПоОтражениюВУчете(Ссылка, Движения, ВыборкаПоОтражениюВУчете, ПрименениеУСН, ОСН_ФизЛицо, мПоддержкаПБУ18, Организация, ПериодРегистрации);
	КонецЦикла;
	
	Если ПрименениеУСН Тогда
		СтруктураДопПараметров = Новый Структура;
		СтруктураДопПараметров.Вставить("ОтражениеВУчете", ОтражениеВУчете);
		НалоговыйУчетУСН.ДвиженияУСН(ЭтотОбъект, Режим, СтруктураДопПараметров);
	КонецЕсли;
	
	// {УчетДоходовИРасходовИП
	Если ОСН_ФизЛицо Тогда
		УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, Ложь, Организация);
		ДоходыИРасходыПредпринимателя.ОтражениеРасходовНаОплатуТруда(ЭтотОбъект, ВыборкаПоОтражениюВУчете, УчетнаяПолитика, Режим);
	КонецЕсли;
	// }УчетДоходовИРасходовИП	
	
КонецПроцедуры

Функция СформироватьЗапросПоОтражениюВУчете(ОтражениеВУчете)Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ПроцедурыУчетаЗарплаты.СоздатьВТОтражениеВУчете(Запрос, ОтражениеВУчете, Ссылка);
	
	Запрос.Текст = ПроцедурыУчетаЗарплаты.СформироватьЗапросПоОтражениюВУчете();	
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ, РучнаяКорректировка, Ложь);
	ЗакрытиеМесяца.ЗаписатьСписокНеАктуальныхРегОпераций(Организация, Дата, "ФОТ");
	
	
КонецПроцедуры
