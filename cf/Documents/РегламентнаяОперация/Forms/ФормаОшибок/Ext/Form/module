
Процедура ПечатьОтчетаОбОшибках(Кнопка)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	Макет      = ПолучитьМакет("Отчет");
	ОбластьШапка     = Макет.ПолучитьОбласть("Заголовок");
	ОбластьГруппа    = Макет.ПолучитьОбласть("ГруппаОшибок");
	ОбластьОшибка    = Макет.ПолучитьОбласть("Ошибка");
	
	ОбластьШапка.Параметры.Период              = ПредставлениеПериода(НачалоМесяца(Дата), КонецМесяца(Дата), "ФП = Истина");
	ОбластьШапка.Параметры.Номер               = Номер;
	СведенияОбОрганизации    = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация, Дата);
	ОбластьШапка.Параметры.НазваниеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
	ОбластьШапка.Параметры.ДатаСоставления     = ТекущаяДата();
	ОбластьШапка.Параметры.НазваниеОтчета      = ВидОперации;
	ДокументРезультат.Вывести(ОбластьШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка",      ЭтотОбъект.Ссылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегламентнаяОперацияОшибки.НомерСтроки КАК НомерОшибки,
	|	РегламентнаяОперацияОшибки.Описание КАК ОписаниеОшибки
	|ИЗ
	|	Документ.РегламентнаяОперация.Ошибки КАК РегламентнаяОперацияОшибки
	|ГДЕ
	|	РегламентнаяОперацияОшибки.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбластьОшибка.Параметры.НомерОшибки    = Выборка.НомерОшибки;
		ОбластьОшибка.Параметры.ОписаниеОшибки = Выборка.ОписаниеОшибки;
		ДокументРезультат.Вывести(ОбластьОшибка);
	КонецЦикла;
	
	// Зафиксируем заголовок отчета
	ДокументРезультат.ФиксацияСверху = 7;
	
	// Первую колонку не печатаем
	ДокументРезультат.ОбластьПечати = ДокументРезультат.Область(1, 2, ДокументРезультат.ВысотаТаблицы, ДокументРезультат.ШиринаТаблицы);
	
	// Присвоим имя для сохранения параметров печати табличного документа
	ДокументРезультат.ИмяПараметровПечати = "Отчет об ошибках рег.операции " + ВидОперации;
	
	УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ДокументРезультат, 7, Строка(глЗначениеПеременной("глТекущийПользователь")));
	
	УниверсальныеМеханизмы.НапечататьДокумент(ДокументРезультат, 1, Ложь, "Отчет об ошибках рег.операции " + ВидОперации, Ложь);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ЭтаФорма.Заголовок = "Список ошибок";
	ЭтаФорма.ЭлементыФормы.НадписьВидОперации.Заголовок = ВидОперации;;
	ЭтаФорма.ЭлементыФормы.НадписьВидОперации2.Заголовок = "Период: " + РаботаСДиалогами.ПолучитьПредставлениеПериодаРегистрации(Дата) + ". Организация: """+ Организация + """.";
КонецПроцедуры

