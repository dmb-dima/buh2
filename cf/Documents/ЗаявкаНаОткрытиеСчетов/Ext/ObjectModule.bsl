////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
Перем мВалютаРегламентированногоУчета Экспорт;

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(РаботникиОрганизации, , "Физлицо");
	
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	НаборЛицевыхСчетов = Движения.ЛицевыеСчетаРаботниковОрганизации;
	
	Если ВводНачальныхСведений Тогда
		Для каждого СтрокаСРаботником Из РаботникиОрганизации Цикл
			ЗаписьЛицевогоСчета = НаборЛицевыхСчетов.Добавить();
			ЗаписьЛицевогоСчета.Организация = Организация;
			ЗаписьЛицевогоСчета.Банк = БанковскийСчет.Банк;
			ЗаписьЛицевогоСчета.ФизЛицо = СтрокаСРаботником.ФизЛицо;
			ЗаписьЛицевогоСчета.НомерЛицевогоСчета = СтрокаСРаботником.НомерЛицевогоСчета;
			
		КонецЦикла;
		
	Иначе
		
		Попытка
			ИсходноеДерево = ЗначениеИзСтрокиВнутр(ТекстПодтверждения);
		Исключение
			Возврат;
		КонецПопытки;
		
		Если ТипЗнч(ИсходноеДерево) <> Тип("ДеревоЗначений") Тогда
			Возврат;
		КонецЕсли;
		
		ПарсерПодтверждения = Обработки.ИмпортЭкспортОперацийПоЛицевымСчетамРаботников.Создать();
		
		НаборЛицевыхСчетов = Движения.ЛицевыеСчетаРаботниковОрганизации;
		МассивОпераций = ПарсерПодтверждения.ПолучитьОперации(ИсходноеДерево, "РезультатОткрытияСчетов");
		
		ЗаполненныеФизЛица = Новый Соответствие;
		
		Для каждого ОперацияПодтверждения из МассивОпераций Цикл
			
			ЗаписьЛицевогоСчета = ЗаполненныеФизЛица[ОперацияПодтверждения.Сотрудник];
			
			Если ЗаписьЛицевогоСчета = Неопределено Тогда
				ЗаписьЛицевогоСчета = НаборЛицевыхСчетов.Добавить();
				ЗаписьЛицевогоСчета.Организация = Организация;
				ЗаписьЛицевогоСчета.Банк = БанковскийСчет.Банк;
				ЗаписьЛицевогоСчета.ФизЛицо = ОперацияПодтверждения.Сотрудник;
				ЗаполненныеФизЛица.Вставить(ОперацияПодтверждения.Сотрудник, ЗаписьЛицевогоСчета);
			КонецЕсли;
			
			Если ВРЕГ(СтрЗаменить(ОперацияПодтверждения.Результат, " ", "")) = "СЧЕТОТКРЫТ" Тогда
				ЗаписьЛицевогоСчета.НомерЛицевогоСчета = ОперацияПодтверждения.ЛицевойСчет;
				
			Иначе
				ЗаполненныеФизЛица.Вставить(ОперацияПодтверждения.Сотрудник, Неопределено);
				НаборЛицевыхСчетов.Удалить(ЗаписьЛицевогоСчета);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого ЗаписьВРегистр из НаборЛицевыхСчетов Цикл
		Сообщить("Записана информация о лицевом счете " + ЗаписьВРегистр.НомерЛицевогоСчета + " (работник " + ЗаписьВРегистр.ФизЛицо + " )");
	КонецЦикла;
	
КонецПроцедуры

мВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();