Перем СтруктураНаименований;
Перем СтруктураРеквизитов;
Перем УказаниеКППОбязательно;

Функция ЕстьОшибкиЗаполненияРеквизитов()
	
	ЕстьОшибки = Ложь;
	
	Если ПустаяСтрока(ИНН) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено значение " + СтруктураНаименований.ИНН, ЕстьОшибки);
	КонецЕсли;
	Если ПустаяСтрока(КПП) И УказаниеКППОбязательно Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено значение " + СтруктураНаименований.КПП, ЕстьОшибки);
	КонецЕсли;
	Если ПустаяСтрока(ТекстНаименования) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не заполнено значение " + СтруктураНаименований.ТекстНаименования, ЕстьОшибки);
	КонецЕсли;
	
	Возврат ЕстьОшибки;

КонецФункции

Процедура ИНННачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИННвладельца = ?(ПустаяСтрока(ВладелецСчета.ИНН), "0", ВладелецСчета.ИНН);
	ТекстИННвладельца = "ИНН, указанный для " + ?(ЭтоПлательщик, "плательщика", "получателя") + " (" + ИННвладельца + ")";
	
	СписокВариантов = Новый СписокЗначений;
	СписокВариантов.Добавить(ИННвладельца, ТекстИННвладельца);
	
	ВыбранныйВариант = ВыбратьИзСписка(СписокВариантов, Элемент);
	
	Если ВыбранныйВариант = Неопределено Тогда
		Возврат;
	Иначе
		ИНН = ВыбранныйВариант.Значение;
	КонецЕсли;	
	
КонецПроцедуры

Процедура КППНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КППвладельца = ?(ПустаяСтрока(ВладелецСчета.КПП), "0", ВладелецСчета.КПП);
	ТекстКППвладельца = "КПП, указанный для " + ?(ЭтоПлательщик, "плательщика", "получателя") + " (" + КППвладельца + ")";
	
	СписокВариантов = Новый СписокЗначений;
	СписокВариантов.Добавить(КППвладельца, ТекстКППвладельца);
	
	Если ЭтоПлательщик Тогда
		СписокВариантов.Добавить(Справочники.РегистрацияВИФНС.ПустаяСсылка(), "КПП по другой регистрации в ИФНС...");
	КонецЕсли;
		
	ВыбранныйВариант = ВыбратьИзСписка(СписокВариантов, Элемент);
	
	Если ВыбранныйВариант = Неопределено Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ВыбранныйВариант.Значение) = Тип("Строка") Тогда
		КПП = ВыбранныйВариант.Значение;
	Иначе
		ФормаВыбора = Справочники.РегистрацияВИФНС.ПолучитьФормуВыбора(, Элемент);
		ФормаВыбора.СправочникСписок.Отбор.Владелец.Установить(ОбщегоНазначения.ГоловнаяОрганизация(ВладелецСчета));
		ФормаВыбора.Открыть();
	КонецЕсли;	
	
КонецПроцедуры

Процедура КППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.РегистрацияВИФНС") Тогда
		СтандартнаяОбработка = Ложь;
		КПП = ВыбранноеЗначение.КПП;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	ЕстьОшибки = ЕстьОшибкиЗаполненияРеквизитов();
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	ДокументОбъект[СтруктураРеквизитов.ИНН] = ИНН;
	ДокументОбъект[СтруктураРеквизитов.КПП] = КПП;
	ДокументОбъект[СтруктураРеквизитов.ТекстНаименования] = ТекстНаименования;
	
	ЭтаФорма.Модифицированность = Ложь;
	ЭтаФорма.Закрыть(Истина);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ЭтоПлательщик Тогда
		СтруктураНаименований = Новый Структура(
			"ИНН, КПП, ТекстНаименования", "ИНН плательщика", "КПП плательщика", "Наименование плательщика");
		СтруктураРеквизитов = Новый Структура(
			"ИНН, КПП, ТекстНаименования, ВладелецСчета, БанковскийСчет", 
			"ИННПлательщика", "КПППлательщика", "ТекстПлательщика", "Организация", "СчетОрганизации");
	Иначе
		СтруктураНаименований = Новый Структура(
			"ИНН, КПП, ТекстНаименования", "ИНН получателя", "КПП получателя", "Наименование получателя");
		СтруктураРеквизитов = Новый Структура(
			"ИНН, КПП, ТекстНаименования, ВладелецСчета, БанковскийСчет", 
			"ИННПолучателя", "КПППолучателя", "ТекстПолучателя", "Контрагент", "СчетКонтрагента");
	КонецЕсли;
	
	ЭлементыФормы.НадписьИНН.Заголовок = СтруктураНаименований.ИНН + ":";
	ЭлементыФормы.НадписьКПП.Заголовок = СтруктураНаименований.КПП + ":";
	ЭлементыФормы.НадписьНаименование.Заголовок = СтруктураНаименований.ТекстНаименования + ":";
	
	ИНН = ДокументОбъект[СтруктураРеквизитов.ИНН];
	КПП = ДокументОбъект[СтруктураРеквизитов.КПП];
	ТекстНаименования = ДокументОбъект[СтруктураРеквизитов.ТекстНаименования];
	ВладелецСчета     = ДокументОбъект[СтруктураРеквизитов.ВладелецСчета];
	БанковскийСчет    = ДокументОбъект[СтруктураРеквизитов.БанковскийСчет];
	
	УказаниеКППОбязательно = ДокументОбъект.ПеречислениеВБюджет ИЛИ БанковскийСчет.ВсегдаУказыватьКПП;
	
	ЭлементыФормы.КПП.АвтоОтметкаНезаполненного = УказаниеКППОбязательно;
	
	ЭтаФорма.Заголовок = ?(ЭтоПлательщик, "Реквизиты плательщика", "Реквизиты получателя");
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЭтаФорма.Модифицированность Тогда
	
		Ответ = Вопрос("Изменить значения реквизитов в документе?", РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЕстьОшибки = ЕстьОшибкиЗаполненияРеквизитов();
			Если ЕстьОшибки Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			ДокументОбъект[СтруктураРеквизитов.ИНН] = ИНН;
			ДокументОбъект[СтруктураРеквизитов.КПП] = КПП;
			ДокументОбъект[СтруктураРеквизитов.ТекстНаименования] = ТекстНаименования;
			ЭтаФорма.Закрыть(Истина);
		ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
			ЭтаФорма.Закрыть();
		Иначе
			Отказ = Истина;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

