////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем СтруктураСохраняемыхРеквизитов;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Управление видимостью и доступностью элементов формы.
//
Процедура УстановитьВидимость()
	
	ЭлементыФормы.НалоговаяЛьгота.Заголовок = УправлениеВнеоборотнымиАктивами.ПредставлениеНалоговойЛьготыПоНалогуНаИмущество(РегистрСведенийМенеджерЗаписи);
	
КонецПроцедуры // УстановитьВидимость()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием()
	
	ОрганизацияПоУмолчанию = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");	
	
	Если НЕ Выбран() Тогда
		
		Период = НачалоГода(ОбщегоНазначения.ПолучитьРабочуюДату());
		
		Если ПараметрОбъектКопирования = Неопределено Тогда 
			
			Если Организация.Пустая() Тогда
				Организация = ОрганизацияПоУмолчанию;
			КонецЕсли;
			
			НалоговаяСтавка = 2.2;
			
			ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Установка видимости и доступности реквизитов.
	УстановитьВидимость();
	
	Если Организация.Пустая() Тогда
		ТекущийЭлемент = ЭлементыФормы.Организация;
	Иначе
		ТекущийЭлемент = ЭлементыФормы.НалоговаяСтавка;
	КонецЕсли;
	
КонецПроцедуры // ПередОткрытием()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Обработчик события "Нажатие" гиперссылки "Налоговая льгота"
//
Процедура НалоговаяЛьготаНажатие(Элемент)
	
	Форма = ПолучитьФорму("ФормаНастройкиЛьготы", ЭтаФорма);
	Форма.РегистрСведенийМенеджерЗаписи = РегистрСведенийМенеджерЗаписи;
	Форма.Открыть();
	
КонецПроцедуры // НалоговаяЛьготаНажатие()

// Обработчик события "ПередЗаписью" формы.
//
Процедура ПередЗаписью(Отказ)
	
	Если Организация.Пустая() Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация!", Отказ);
	КонецЕсли;
	
	Если Период = Дата(1,1,1) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата установки!", Отказ);
	КонецЕсли;
	
	Если НалоговаяСтавка = 0 Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана налоговая ставка!", Отказ);
	КонецЕсли;
	
	Если НЕ Отказ И НЕ Выбран() Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Период",         Период);
		Запрос.УстановитьПараметр("Организация",    Организация);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(СтавкиНалогаНаИмущество.НалоговаяСтавка) КАК НалоговаяСтавка
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмущество КАК СтавкиНалогаНаИмущество
		|ГДЕ
		|	СтавкиНалогаНаИмущество.Период = &Период
		|	И СтавкиНалогаНаИмущество.Организация = &Организация";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Количество() > 0 Тогда
			
			Выборка.Следующий();
			
			ОбщегоНазначения.СообщитьОбОшибке(Формат(Период, "ДФ=dd.MM.yyyy") + " в организации <" + СокрЛП(Организация) + "> уже введена установка ставки налога на имущество!", Отказ);
			
		КонецЕсли;
		      		
	КонецЕсли;
	
	Если Отказ Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Информация не записана.",,, СтатусСообщения.Обычное);
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

СтруктураСохраняемыхРеквизитов = Новый Структура;