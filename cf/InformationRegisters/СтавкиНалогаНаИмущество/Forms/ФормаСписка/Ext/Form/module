Процедура ПриОткрытии()
	
	// Установка "мягкого" отбора по организации
	Если НЕ РегистрСведенийСписок.Отбор.Организация.Использование Тогда
	
		РегистрСведенийСписок.Отбор.Организация.Использование = РегистрСведенийСписок1.Отбор.Организация.Использование;
		РегистрСведенийСписок.Отбор.Организация.ВидСравнения  = РегистрСведенийСписок1.Отбор.Организация.ВидСравнения;
		РегистрСведенийСписок.Отбор.Организация.Значение      = РегистрСведенийСписок1.Отбор.Организация.Значение;
		
	ИначеЕсли НЕ РегистрСведенийСписок1.Отбор.Организация.Использование Тогда
	
		РегистрСведенийСписок1.Отбор.Организация.Использование = РегистрСведенийСписок.Отбор.Организация.Использование;
		РегистрСведенийСписок1.Отбор.Организация.ВидСравнения  = РегистрСведенийСписок.Отбор.Организация.ВидСравнения;
		РегистрСведенийСписок1.Отбор.Организация.Значение      = РегистрСведенийСписок.Отбор.Организация.Значение;
		
	КонецЕсли;
		
	Если НЕ РегистрСведенийСписок.Отбор.Организация.Использование
	   И НЕ РегистрСведенийСписок1.Отбор.Организация.Использование Тогда
		
		// Проверка ведения однофирменности
		РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "РегистрСведенийСписок");
		РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "РегистрСведенийСписок1");
		
	КонецЕсли;
	
	// Сортировка по организациям и хронологии.
	Если РегистрСведенийСписок.Порядок.Количество() = 1 Тогда
		РегистрСведенийСписок.Порядок.Установить("Организация Возр, Период Возр");
	КонецЕсли;
	
	ЭлементыФормы.РегистрСведенийСписок.НастройкаПорядка.Организация.Доступность = Истина;
	
	Если РегистрСведенийСписок1.Порядок.Количество() = 1 Тогда
		РегистрСведенийСписок1.Порядок.Установить("Организация Возр, Период Возр");
	КонецЕсли;
	
	ЭлементыФормы.РегистрСведенийСписок1.НастройкаПорядка.Организация.Доступность = Истина;
	
	РегистрСведенийСписок.Колонки.Добавить("ОсвобождениеОтНалогообложения");
	РегистрСведенийСписок.Колонки.Добавить("КодНалоговойЛьготыОсвобождениеОтНалогообложения");
	РегистрСведенийСписок.Колонки.Добавить("СнижениеНалоговойСтавки");
	РегистрСведенийСписок.Колонки.Добавить("СниженнаяНалоговаяСтавка");
	РегистрСведенийСписок.Колонки.Добавить("УменьшениеСуммыНалогаВПроцентах");
	РегистрСведенийСписок.Колонки.Добавить("ПроцентУменьшения");
	
	РегистрСведенийСписок1.Колонки.Добавить("НалоговыйОрган");
	РегистрСведенийСписок1.Колонки.Добавить("КодПоОКАТО");
	РегистрСведенийСписок1.Колонки.Добавить("ПорядокНалогообложения");
	РегистрСведенийСписок1.Колонки.Добавить("НалоговаяСтавка");
	РегистрСведенийСписок1.Колонки.Добавить("КодНалоговойЛьготыОсвобождениеОтНалогообложения");
	РегистрСведенийСписок1.Колонки.Добавить("СниженнаяНалоговаяСтавка");
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеТекущейОрганизации" Тогда
		РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "РегистрСведенийСписок", Истина);
		РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "РегистрСведенийСписок1", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура РегистрСведенийСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		ОформлениеСтроки.Ячейки.НалоговаяЛьгота.ОтображатьТекст = Истина;
		ОформлениеСтроки.Ячейки.НалоговаяЛьгота.Текст           = УправлениеВнеоборотнымиАктивами.ПредставлениеНалоговойЛьготыПоНалогуНаИмущество(ОформлениеСтроки.ДанныеСтроки);

	КонецЦикла;
	
КонецПроцедуры

Процедура РегистрСведенийСписок1ПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		
		Если ОформлениеСтроки.ДанныеСтроки.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО Тогда
			
			ОформлениеСтроки.Ячейки.ПостановкаНаУчетВНалоговомОргане.Текст = СокрЛП(Перечисления.ПостановкаНаУчетВНалоговомОргане.ПоМестуНахожденияОрганизации) + " (код по ОКАТО " + ОформлениеСтроки.ДанныеСтроки.КодПоОКАТО +")";
			
		ИначеЕсли ОформлениеСтроки.ДанныеСтроки.ПостановкаНаУчетВНалоговомОргане = Перечисления.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане Тогда
			
			ОформлениеСтроки.Ячейки.ПостановкаНаУчетВНалоговомОргане.Текст = СокрЛП(ОформлениеСтроки.ДанныеСтроки.НалоговыйОрган) + " (код по ОКАТО " + ОформлениеСтроки.ДанныеСтроки.КодПоОКАТО +")";
			
		КонецЕсли;
																		   
		Если ОформлениеСтроки.ДанныеСтроки.ПорядокНалогообложения = Перечисления.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке Тогда

			ОформлениеСтроки.Ячейки.ПорядокНалогообложения.Текст = ОформлениеСтроки.Ячейки.ПорядокНалогообложения.Текст
			                                                     + " по ставке " + ОформлениеСтроки.ДанныеСтроки.СниженнаяНалоговаяСтавка + "%";
		
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры




