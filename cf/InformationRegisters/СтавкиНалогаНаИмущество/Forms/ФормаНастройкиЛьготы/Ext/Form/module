////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мСтруктураСохраняемыхРеквизитов;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Записывает данные в строку формы документа.
//
Процедура ЗаписатьДанные(Отказ = Ложь)
	
	Если ОсвобождениеОтНалогообложения Тогда
		
		Если ПустаяСтрока(КодНалоговойЛьготыОсвобождениеОтНалогообложения) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан код налоговой льготы (освобождение от налогообложения)!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СнижениеНалоговойСтавки И СниженнаяНалоговаяСтавка = 0 Тогда
		
		ОбщегоНазначения.СообщитьОбОшибке("Не указана сниженная налоговая ставка!");
		Отказ = Истина;
		
	КонецЕсли;
	
	
	Если УменьшениеСуммыНалогаВПроцентах И ПроцентУменьшения = 0 Тогда
		
		ОбщегоНазначения.СообщитьОбОшибке("Не указан процент уменьшения суммы налога!");
		Отказ = Истина;
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(РегистрСведенийМенеджерЗаписи, ЭтаФорма);
	
	Если ТипЗнч(ВладелецФормы) = Тип("Форма") 
	   И НЕ (ВладелецФормы.ЭлементыФормы.Найти("НалоговаяЛьгота") = Неопределено) Тогда
	   
		ВладелецФормы.ЭлементыФормы.НалоговаяЛьгота.Значение = УправлениеВнеоборотнымиАктивами.ПредставлениеНалоговойЛьготыПоНалогуНаИмущество(РегистрСведенийМенеджерЗаписи);
		
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры // ЗаписатьДанные()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Управление видимостью и доступностью элементов формы.
//
Процедура УстановитьВидимость()
	
	ЭлементыФормы.КодНалоговойЛьготыОсвобождениеОтНалогообложения.Доступность               = ОсвобождениеОтНалогообложения;
	ЭлементыФормы.КодНалоговойЛьготыОсвобождениеОтНалогообложения.АвтоОтметкаНезаполненного = ОсвобождениеОтНалогообложения;
	ЭлементыФормы.КодНалоговойЛьготыОсвобождениеОтНалогообложения.ОтметкаНезаполненного     = ОсвобождениеОтНалогообложения И ПустаяСтрока(КодНалоговойЛьготыОсвобождениеОтНалогообложения);
	
	ЭлементыФормы.СнижениеНалоговойСтавки.Доступность = НЕ ОсвобождениеОтНалогообложения;
	
	ЭлементыФормы.СниженнаяНалоговаяСтавка.Доступность                                = НЕ ОсвобождениеОтНалогообложения И СнижениеНалоговойСтавки;
	ЭлементыФормы.СниженнаяНалоговаяСтавка.АвтоОтметкаНезаполненного                  = НЕ ОсвобождениеОтНалогообложения И СнижениеНалоговойСтавки;
	ЭлементыФормы.СниженнаяНалоговаяСтавка.ОтметкаНезаполненного                      = НЕ ОсвобождениеОтНалогообложения И СнижениеНалоговойСтавки И (СниженнаяНалоговаяСтавка = 0);
	ЭлементыФормы.НадписьПроцентСтавки.Доступность                                    = НЕ ОсвобождениеОтНалогообложения И СнижениеНалоговойСтавки;
	
	ЭлементыФормы.УменьшениеСуммыНалогаВПроцентах.Доступность = НЕ ОсвобождениеОтНалогообложения;
	
	ЭлементыФормы.ПроцентУменьшения.Доступность                                               = НЕ ОсвобождениеОтНалогообложения И УменьшениеСуммыНалогаВПроцентах;
	ЭлементыФормы.ПроцентУменьшения.АвтоОтметкаНезаполненного                                 = НЕ ОсвобождениеОтНалогообложения И УменьшениеСуммыНалогаВПроцентах;
	ЭлементыФормы.ПроцентУменьшения.ОтметкаНезаполненного                                     = НЕ ОсвобождениеОтНалогообложения И УменьшениеСуммыНалогаВПроцентах И (ПроцентУменьшения = 0);
	ЭлементыФормы.НадписьСимволПроцентаУменьшения.Доступность                                 = НЕ ОсвобождениеОтНалогообложения И УменьшениеСуммыНалогаВПроцентах;
	
КонецПроцедуры // УстановитьВидимость()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РегистрСведенийМенеджерЗаписи);
	
	УстановитьВидимость();
	
КонецПроцедуры // ПриОткрытии()

// Обработчик события "ПередЗакрытием" формы.
// Вызывает процедуру установки видимости элементов управления формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		ТекстВопроса =
		"Данные были изменены. Сохранить изменения?";
		
		КодВозврата = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
			
		Если КодВозврата = КодВозвратаДиалога.Отмена Тогда
			
			Отказ = Истина;
			
		ИначеЕсли КодВозврата = КодВозвратаДиалога.Да Тогда
			
			ЗаписатьДанные(Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытием()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события "ПриИзменении" флага "Освобождение от налогообложения".
//
Процедура ОсвобождениеОтНалогообложенияПриИзменении(Элемент)
	
	Если Элемент.Значение Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("СнижениеНалоговойСтавки",  ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("УменьшениеСуммыНалогаВПроцентах", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ПроцентУменьшения",               ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	Иначе
	
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("СнижениеНалоговойСтавки",  ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("УменьшениеСуммыНалогаВПроцентах", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("ПроцентУменьшения",               ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // ФлагНалоговойЛьготыПриИзменении() 

// Обработчик события "ПриИзменении" флага "Снижение налоговой ставки".
//
Процедура СнижениеНалоговойСтавкиПриИзменении(Элемент)
	
	Если Элемент.Значение Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	Иначе
	
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("СниженнаяНалоговаяСтавка", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
			
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // СнижениеНалоговойСтавкиПриИзменении()

// Обработчик события "ПриИзменении" флага "Уменьшение суммы налога в процентах".
//
Процедура УменьшениеСуммыНалогаВПроцентахПриИзменении(Элемент)
	
	Если Элемент.Значение Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("ПроцентУменьшения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	Иначе
	
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ПроцентУменьшения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
			
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // УменьшениеСуммыНалогаВПроцентахПриИзменении()

// Обработчик события "НачалоВыбора" полей ввода "Код льготы".
//
Процедура КодНалоговойЛьготыНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КодЛьготы = УправлениеВнеоборотнымиАктивами.ПолучитьФормуВыбораЛьготыПоНалогуНаИмущество(Элемент.Значение).ОткрытьМодально();
	
	Если НЕ (КодЛьготы = Неопределено) Тогда
		Элемент.Значение = КодЛьготы;
	КонецЕсли;
	
КонецПроцедуры // КодНалоговойЛьготыНачалоВыбора()

// Обработчик события "Нажатие" кнопки "ОК".
//
Процедура КнопкаОКНажатие(Элемент)
	
	Отказ = Ложь;
	
	ЗаписатьДанные(Отказ);
	
	Если НЕ Отказ Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры // КнопкаОКНажатие()

// Обработчик события "Нажатие" кнопки "Отмена".
//
Процедура КнопкаОтменаНажатие(Элемент)
	
	Модифицированность = Ложь;
	
	Закрыть();
	
КонецПроцедуры // КнопкаОтменаНажатие(()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мСтруктураСохраняемыхРеквизитов = Новый Структура;