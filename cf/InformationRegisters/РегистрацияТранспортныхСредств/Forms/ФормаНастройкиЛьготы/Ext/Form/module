////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мТекущаяНалоговаяЛьгота;
Перем мСтруктураСохраняемыхРеквизитов;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Записывает данные в строку формы документа.
//
Процедура ЗаписатьДанные(Отказ = Ложь)
	
	Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда
		
		Если ПустаяСтрока(КодНалоговойЛьготыОсвобождениеОтНалогообложения) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан код льготы (освобождение от налогообложения)!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда
		
		Если ПустаяСтрока(КодНалоговойЛьготыСнижениеНалоговойСтавки) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан код льготы (снижение налоговой ставки)!");
			Отказ = Истина;
		КонецЕсли;
		
		Если ЛьготнаяСтавка = 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не заполнено значение реквизита ""Льготная ставка""!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		
		Если ПустаяСтрока(КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан код льготы (уменьшение суммы налога на сумму)!");
			Отказ = Истина;
		КонецЕсли;
		
		Если СуммаУменьшения = 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не заполнено значение реквизита ""Сумма уменьшения""!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		
		Если ПустаяСтрока(КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан код льготы (уменьшение суммы налога в процентах)!");
			Отказ = Истина;
		КонецЕсли;
		
		Если ПроцентУменьшения = 0 Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не заполнено значение реквизита ""Процент уменьшения""!");
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда
		КодНалоговойЛьготы = КодНалоговойЛьготыОсвобождениеОтНалогообложения;
	ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда
		КодНалоговойЛьготы = КодНалоговойЛьготыСнижениеНалоговойСтавки;
	ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		КодНалоговойЛьготы = КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах;
	ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		КодНалоговойЛьготы = КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(РегистрСведенийМенеджерЗаписи, ЭтаФорма);
	
	Если ТипЗнч(ВладелецФормы) = Тип("Форма") 
	   И НЕ (ВладелецФормы.ЭлементыФормы.Найти("НалоговаяЛьгота") = Неопределено) Тогда
	   
		ВладелецФормы.ЭлементыФормы.НалоговаяЛьгота.Заголовок = УправлениеВнеоборотнымиАктивами.ПредставлениеНалоговойЛьготыПоТранспортномуНалогу(РегистрСведенийМенеджерЗаписи);
		
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры // ЗаписатьДанные()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

// Управление видимостью и доступностью элементов формы.
//
Процедура УстановитьВидимость()
	
	ЭлементыФормы.КодНалоговойЛьготыОсвобождениеОтНалогообложения.Доступность               = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения);
	ЭлементыФормы.КодНалоговойЛьготыОсвобождениеОтНалогообложения.АвтоОтметкаНезаполненного = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения);
	ЭлементыФормы.КодНалоговойЛьготыОсвобождениеОтНалогообложения.ОтметкаНезаполненного     = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения) И ПустаяСтрока(КодНалоговойЛьготыОсвобождениеОтНалогообложения);
	
	ЭлементыФормы.ЛьготнаяСтавка.Доступность                                           = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки);
	ЭлементыФормы.ЛьготнаяСтавка.АвтоОтметкаНезаполненного                             = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки);
	ЭлементыФормы.ЛьготнаяСтавка.ОтметкаНезаполненного                                 = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки) И (ЛьготнаяСтавка = 0);
	ЭлементыФормы.НадписьВалютаЛьготнойСтавки.Доступность                              = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки);
	ЭлементыФормы.КодНалоговойЛьготыСнижениеНалоговойСтавки.Доступность                = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки);
	ЭлементыФормы.КодНалоговойЛьготыСнижениеНалоговойСтавки.АвтоОтметкаНезаполненного  = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки);
	ЭлементыФормы.КодНалоговойЛьготыСнижениеНалоговойСтавки.ОтметкаНезаполненного      = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки) И ПустаяСтрока(КодНалоговойЛьготыСнижениеНалоговойСтавки);
	
	ЭлементыФормы.СуммаУменьшения.Доступность                                              = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму);
	ЭлементыФормы.СуммаУменьшения.АвтоОтметкаНезаполненного                                = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму);
	ЭлементыФормы.СуммаУменьшения.ОтметкаНезаполненного                                    = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму) И (СуммаУменьшения = 0);
	ЭлементыФОрмы.НадписьВалютаУменьшенияСуммыНалога.Доступность                           = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму);
	ЭлементыФормы.КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму.Доступность               = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму);
	ЭлементыФормы.КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму.АвтоОтметкаНезаполненного = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму);
	ЭлементыФормы.КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму.ОтметкаНезаполненного     = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму) И ПустаяСтрока(КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму);
	                                                                                   
	ЭлементыФормы.ПроцентУменьшения.Доступность                                               = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах);
	ЭлементыФормы.ПроцентУменьшения.АвтоОтметкаНезаполненного                                 = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах);
	ЭлементыФормы.ПроцентУменьшения.ОтметкаНезаполненного                                     = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах) И (ПроцентУменьшения = 0);
	ЭлементыФОрмы.НадписьПроцентУменьшенияСуммыНалога.Доступность                             = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах);
	ЭлементыФОрмы.КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах.Доступность               = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах);
	ЭлементыФормы.КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах.АвтоОтметкаНезаполненного = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах);
	ЭлементыФормы.КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах.ОтметкаНезаполненного     = (НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах) И ПустаяСтрока(КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах);
	
КонецПроцедуры // УстановитьВидимость()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РегистрСведенийМенеджерЗаписи);
	
	Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда
		КодНалоговойЛьготыОсвобождениеОтНалогообложения = КодНалоговойЛьготы;
	ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда
		КодНалоговойЛьготыСнижениеНалоговойСтавки = КодНалоговойЛьготы;
	ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах = КодНалоговойЛьготы;
	ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму = КодНалоговойЛьготы;
	КонецЕсли;
	
	мТекущаяНалоговаяЛьгота = НалоговаяЛьгота;
	
	УстановитьВидимость();
	
КонецПроцедуры // ПриОткрытии()

// Обработчик события "ПередЗакрытием" формы.
// Вызывает процедуру установки видимости элементов управления формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		ТекстВопроса =
		"Данные были изменены. Сохранить изменения?";
		
		КодВозврата = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
			
		Если КодВозврата = КодВозвратаДиалога.Отмена Тогда
			
			Отказ = Истина;
			
		ИначеЕсли КодВозврата = КодВозвратаДиалога.Да Тогда
			
			ЗаписатьДанные(Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытием()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события "ПриИзменении" поля ввода "Вид налоговой льготы".
//
Процедура НалоговаяЛьготаПриИзменении(Элемент)
	
	КодНалоговойЛьготыПоУмолчанию = "20200";
	
	Если мТекущаяНалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	ИначеЕсли мТекущаяНалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ЛьготнаяСтавка", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыСнижениеНалоговойСтавки", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	ИначеЕсли мТекущаяНалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("СуммаУменьшения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	ИначеЕсли мТекущаяНалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("ПроцентУменьшения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.СохранитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
	КонецЕсли;
	
	Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("КодНалоговойЛьготыОсвобождениеОтНалогообложения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		Если ПустаяСтрока(КодНалоговойЛьготыОсвобождениеОтНалогообложения) тогда
			КодНалоговойЛьготыОсвобождениеОтНалогообложения = КодНалоговойЛьготыПоУмолчанию;
		КонецЕсли;
		
	ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("ЛьготнаяСтавка", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("КодНалоговойЛьготыСнижениеНалоговойСтавки", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		Если ПустаяСтрока(КодНалоговойЛьготыСнижениеНалоговойСтавки) тогда
			КодНалоговойЛьготыСнижениеНалоговойСтавки = КодНалоговойЛьготыПоУмолчанию;
		КонецЕсли;
		
	ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("СуммаУменьшения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		Если ПустаяСтрока(КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму) тогда
			КодНалоговойЛьготыУменьшениеСуммыНалогаНаСумму = КодНалоговойЛьготыПоУмолчанию;
		КонецЕсли;
		
	ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда
		
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("ПроцентУменьшения", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		РаботаСДиалогами.ВосстановитьЗначениеРеквизитаФормы("КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах", ЭтаФорма, мСтруктураСохраняемыхРеквизитов);
		
		Если ПустаяСтрока(КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах) тогда
			КодНалоговойЛьготыУменьшениеСуммыНалогаВПроцентах = КодНалоговойЛьготыПоУмолчанию;
		КонецЕсли;
		
	КонецЕсли;
	
	мТекущаяНалоговаяЛьгота = НалоговаяЛьгота;
	
	УстановитьВидимость();
	
КонецПроцедуры // НалоговаяЛьготаПриИзменении()

// Обработчик события "НачалоВыбора" поля ввода "Код налоговой льготы".
//
Процедура КодНалоговойЛьготыНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КодЛьготы = УправлениеВнеоборотнымиАктивами.ПолучитьФормуВыбораЛьготыПоТранспортномуНалогу(Элемент.Значение).ОткрытьМодально();
	
	Если НЕ (КодЛьготы = Неопределено) Тогда
		Элемент.Значение = КодЛьготы;
	КонецЕсли;
	
КонецПроцедуры // КодНалоговойЛьготыНачалоВыбора()

// Обработчик события "Нажатие" кнопки "ОК".
//
Процедура КнопкаОКНажатие(Элемент)
	
	Отказ = Ложь;
	
	ЗаписатьДанные(Отказ);
	
	Если НЕ Отказ Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры // КнопкаОКНажатие()

// Обработчик события "Нажатие" кнопки "Отмена".
//
Процедура КнопкаОтменаНажатие(Элемент)
	
	Модифицированность = Ложь;
	
	Закрыть();
	
КонецПроцедуры // КнопкаОтменаНажатие(()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мСтруктураСохраняемыхРеквизитов = Новый Структура;