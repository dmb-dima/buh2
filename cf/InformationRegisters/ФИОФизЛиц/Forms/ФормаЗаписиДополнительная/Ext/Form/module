////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ФизЛицоПриОткрытии  = ФизЛицо;
	ПериодПриОткрытии   = Период;
	ФамилияПриОткрытии  = Фамилия;
	ИмяПриОткрытии      = Имя;
	ОтчествоПриОткрытии = Отчество;
	
	Заголовок = "Фамилия, имя, отчество физ. лица: " + ФизЛицо.Наименование;
	
	Прочитать();
	
	// У записи обязательно должны быть заполнены измерения и период
	Если ФизЛицо = Неопределено Тогда
	    ФизЛицо = ФизЛицоПриОткрытии;
	    Период  = ПериодПриОткрытии;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Если поменяли дату, то тогда новая  запись
	Если ПериодПриОткрытии <> Период Тогда
		
		// Если что-то поменяли тогда запишем 
		Если ФамилияПриОткрытии <> Фамилия ИЛИ
			ИмяПриОткрытии <> Имя ИЛИ
			ОтчествоПриОткрытии <> Отчество Тогда
		
			МенеджерЗаписи = РегистрыСведений.ФИОФизЛиц.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период   = ПериодПриОткрытии;
			МенеджерЗаписи.ФизЛицо  = ФизЛицо;
			МенеджерЗаписи.Фамилия  = ФамилияПриОткрытии;
			МенеджерЗаписи.Имя	    = ИмяПриОткрытии;
			МенеджерЗаписи.Отчество = ОтчествоПриОткрытии;
			
			МенеджерЗаписи.Записать();
			
		КонецЕсли; 
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","ФИО"), ФизЛицо);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.ФИОФизЛиц.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры








