Перем мНачальныеПараметры;
Перем мТребуетсяПерепроведение;
Перем мПерепроведениеПоВсемОрганизациям;

Процедура УстановитьВидимость()
	
	Если СпособУказанияДатыАктуальности = "Общая дата" Тогда
		
		ЭлементыФормы.ПанельДат.ТекущаяСтраница = ЭлементыФормы.ПанельДат.Страницы.ОбщаяДата;
		
	Иначе
		
		ЭлементыФормы.ПанельДат.ТекущаяСтраница = ЭлементыФормы.ПанельДат.Страницы.РаздельныеДаты;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиДанныеПоРазделамУчета()
	
	РегистрСведенийМенеджерЗаписи.Прочитать();
	РазделыУчета.Очистить();
	мНачальныеПараметры.Очистить();
	Для Каждого РазделУчета Из Метаданные.РегистрыСведений.ДатаАктуальностиУчета.Ресурсы Цикл
		
		СтрокаРазделаУчета = РазделыУчета.Добавить();
		СтрокаРазделаУчета.РазделУчета = Перечисления.РазделыУчета[РазделУчета.Имя];
		СтрокаРазделаУчета.ДатаАктуальности = РегистрСведенийМенеджерЗаписи[РазделУчета.Имя];
		СтрокаНачДанных = мНачальныеПараметры.Добавить();
		СтрокаНачДанных.РазделУчета = СтрокаРазделаУчета.РазделУчета;
		СтрокаНачДанных.ДатаАктуальности = СтрокаРазделаУчета.ДатаАктуальности;
		
	КонецЦикла;
	
	ОбщаяДата = РазделыУчета[0].ДатаАктуальности;
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ВывестиДанныеПоРазделамУчета();
	
	Если РазделыУчета.Количество() > 0 Тогда	
		
		СуществуетОбщаяДата = Истина;
		Для Каждого РазделУчета Из РазделыУчета Цикл
			Если СуществуетОбщаяДата Тогда
				Если РазделУчета.ДатаАктуальности <> РазделыУчета[0].ДатаАктуальности Тогда
					СуществуетОбщаяДата = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если СуществуетОбщаяДата Тогда
			
			ОбщаяДата = РазделыУчета[0].ДатаАктуальности;
			
		КонецЕсли;
		
		СпособУказанияДатыАктуальности = ?(СуществуетОбщаяДата, "Общая дата", "По разделам учета");
		
	Иначе
		
		СпособУказанияДатыАктуальности = "По разделам учета";
		
	КонецЕсли;  
	
	УстановитьВидимость();

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	мТребуетсяПерепроведение = Ложь;
	мПерепроведениеПоВсемОрганизациям = Ложь;
	
	Если Модифицированность Тогда
		
		Для Каждого РазделУчета из мНачальныеПараметры Цикл
			
			ТекущиеДанные = РазделыУчета.Найти(РазделУчета.РазделУчета, "РазделУчета");
			Если ТекущиеДанные <> Неопределено Тогда

				Если ТекущиеДанные.ДатаАктуальности > РазделУчета.ДатаАктуальности Тогда
					мТребуетсяПерепроведение = Истина;
				КонецЕсли;

			КонецЕсли;
			
		КонецЦикла;
		
		Если мТребуетсяПерепроведение Тогда
			
			ФормаПерепроведения = РегистрСведенийМенеджерЗаписи.ПолучитьФорму("ФормаПерепроведения");
			ФормаПерепроведения.ВыбраннаяОрганизация = Организация;
			ТребуетсяПерепроведение = ФормаПерепроведения.ОткрытьМодально();
											
			Если ТребуетсяПерепроведение = 0 Тогда
				
				мПерепроведениеПоВсемОрганизациям = Истина;
				
			ИначеЕсли ТребуетсяПерепроведение = 2 Тогда
				
				мТребуетсяПерепроведение = Ложь;
				
			ИначеЕсли ТребуетсяПерепроведение = 3 Или ТребуетсяПерепроведение = Неопределено Тогда
				
				Отказ = Истина;
				мТребуетсяПерепроведение = Ложь;
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
			
		Для Каждого РазделУчета Из РазделыУчета Цикл
			
			ИмяПеречисления = "";
			Для Каждого ЗначениеПеречисления Из РазделУчета.РазделУчета.Метаданные().ЗначенияПеречисления Цикл
				Если Перечисления.РазделыУчета[ЗначениеПеречисления.Имя] = РазделУчета.РазделУчета Тогда
					ИмяПеречисления = ЗначениеПеречисления.Имя;
				КонецЕсли;
			КонецЦикла;
			
			РегистрСведенийМенеджерЗаписи[ИмяПеречисления] = РазделУчета.ДатаАктуальности;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбщаяДатаПриИзменении(Элемент)
	
	Для Каждого РазделУчета Из РазделыУчета Цикл
		
		РазделУчета.ДатаАктуальности = ОбщаяДата;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	
	Если мТребуетсяПерепроведение Тогда
		
		Если мНачальныеПараметры.Количество() > 0 Тогда
			
			ОбработкаПерепроведения = Обработки.ГрупповоеПерепроведениеДокументов.Создать();
			
			ДатаНачалаПерепроведения = '29990101';
			Для Каждого РазделУчета Из мНачальныеПараметры Цикл
				ДатаНачалаПерепроведенияПоРазделу = КонецДня(РазделУчета.ДатаАктуальности) + 1;
				Если ДатаНачалаПерепроведенияПоРазделу < ДатаНачалаПерепроведения Тогда
					ДатаНачалаПерепроведения = ДатаНачалаПерепроведенияПоРазделу;
				КонецЕсли;
			КонецЦикла;
			
			НоваяДатаАктуальности = '00010101';
			Для Каждого РазделУчета Из РазделыУчета Цикл
				НоваяДатаАктуальностиПоРазделу = РазделУчета.ДатаАктуальности;
				Если НоваяДатаАктуальностиПоРазделу > НоваяДатаАктуальности Тогда
					НоваяДатаАктуальности = НоваяДатаАктуальностиПоРазделу;
				КонецЕсли;
			КонецЦикла;
			СписокДатОкончания      = ОбработкаПерепроведения.СформироватьСписокДатОкончанияПроведения(НоваяДатаАктуальности);
			ДатаКонцаПерепроведения = ?(СписокДатОкончания.Количество() > 0, СписокДатОкончания[0].Значение, '00010101');
			
			ОбработкаПерепроведения.ПерепроведениеДокументов(ДатаНачалаПерепроведения, ДатаКонцаПерепроведения, 
				?(мПерепроведениеПоВсемОрганизациям, Неопределено, Организация), Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВывестиДанныеПоРазделамУчета();
	
КонецПроцедуры

Процедура СпособУказанияДатыАктуальностиПриИзменении(Элемент)
	
	Если СпособУказанияДатыАктуальности = "Общая дата" Тогда
		
		Если РазделыУчета.Количество() > 0 Тогда	
			
			СуществуетОбщаяДата = Истина;
			Для Каждого РазделУчета Из РазделыУчета Цикл
				Если СуществуетОбщаяДата Тогда
					Если РазделУчета.ДатаАктуальности <> РазделыУчета[0].ДатаАктуальности Тогда
						СуществуетОбщаяДата = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если СуществуетОбщаяДата Тогда
				
				ОбщаяДата = РазделыУчета[0].ДатаАктуальности;
				
			Иначе
				
				Ответ = Вопрос("Даты всех разделов учета будут установлены в соответствии с разделом учета " + Строка(РазделыУчета[0].РазделУчета), 
								РежимДиалогаВопрос.ОКОтмена, , КодВозвратаДиалога.Отмена, "Установка общей даты");
				Если Ответ = КодВозвратаДиалога.ОК Тогда
							
					ОбщаяДата = РазделыУчета[0].ДатаАктуальности;
					Для Каждого РазделУчета Из РазделыУчета Цикл
						РазделУчета.ДатаАктуальности = ОбщаяДата;
					КонецЦикла;
					Модифицированность = Истина;
					
				Иначе
					
					СпособУказанияДатыАктуальности = "По разделам учета";
					
				КонецЕсли;					
							
			КонецЕсли
				
		КонецЕсли;     	
		
	КонецЕсли;	
	
	УстановитьВидимость();
	
КонецПроцедуры

ЭлементыФормы.СпособУказанияДатыАктуальности.СписокВыбора.Добавить("Общая дата");
ЭлементыФормы.СпособУказанияДатыАктуальности.СписокВыбора.Добавить("По разделам учета");

мНачальныеПараметры = РазделыУчета.Скопировать();

мТребуетсяПерепроведение = Ложь;
мПерепроведениеПоВсемОрганизациям = Ложь;