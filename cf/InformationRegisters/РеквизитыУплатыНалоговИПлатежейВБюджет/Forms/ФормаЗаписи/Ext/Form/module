Процедура УстановитьВидимость()

	Если ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж Тогда
		ЭлементыФормы.ПанельПлатежейВБюджет.ТекущаяСтраница = ЭлементыФормы.ПанельПлатежейВБюджет.Страницы.НалоговыйПлатеж;
	ИначеЕсли ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ТаможенныйПлатеж Тогда
		ЭлементыФормы.ПанельПлатежейВБюджет.ТекущаяСтраница = ЭлементыФормы.ПанельПлатежейВБюджет.Страницы.ТаможенныйПлатеж;
	Иначе
		ЭлементыФормы.ПанельПлатежейВБюджет.ТекущаяСтраница = ЭлементыФормы.ПанельПлатежейВБюджет.Страницы.ИнойПлатеж;
	КонецЕсли;

КонецПроцедуры


Процедура ОбновитьНадписи()

	ЭлементыФормы.НадписьКБК.Заголовок = "Счет учета: " + ВидПлатежа.СчетУчета
		+ ",    " + "КБК: " + ВидПлатежа.КодБК;

КонецПроцедуры

Процедура ЗаполнитьСпискиРеквизитовПлатежейВБюджет()

	ЭлементыФормы.СтатусСоставителя.СписокВыбора          = УправлениеДенежнымиСредствами.ПолучитьСписокСтатусовОтправителя();
	ЭлементыФормы.ПоказательОснования.СписокВыбора        = УправлениеДенежнымиСредствами.ПолучитьСписокОснованийНалоговогоПлатежа();
	ЭлементыФормы.ПоказательТипа.СписокВыбора             = УправлениеДенежнымиСредствами.ПолучитьСписокПоказателейТипаНалоговогоПлатежа();
	ЭлементыФормы.ПоказательПериода.СписокВыбора          = УправлениеДенежнымиСредствами.ПолучитьСписокПериодичностиУплатыНалогов();
	ЭлементыФормы.ПоказательОснованияТаможня.СписокВыбора = УправлениеДенежнымиСредствами.ПолучитьСписокОснованийТаможенногоПлатежа();
	ЭлементыФормы.ПоказательТипаТаможня.СписокВыбора      = УправлениеДенежнымиСредствами.ПолучитьСписокПоказателейТипаТаможенногоПлатежа();
	
КонецПроцедуры // ЗаполнитьСпискиРеквизитовПлатежейВБюджет()

Процедура ОрганизацияПриИзменении(Элемент)
	
	ГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	// {ОбособленныеПодразделения
	Если РегистрацияВИФНС.Владелец <> ГоловнаяОрганизация Тогда
		РегистрацияВИФНС = Неопределено;
	КонецЕсли;
	
	// }ОбособленныеПодразделения
	
КонецПроцедуры

Процедура ПолучательПриИзменении(Элемент)
	
	Если СчетПолучателя.Владелец <> Получатель Тогда
		СчетПолучателя = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВидНалогаПриИзменении(Элемент)
	
	ОбновитьНадписи();
	
КонецПроцедуры

Процедура СтатусСоставителяПриИзменении(Элемент)
	
	Если ПустаяСтрока(СтатусСоставителя) Тогда
		СтатусСоставителя = "01";
	КонецЕсли;
	
	Если (СтатусСоставителя = "08")
		ИЛИ (ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж) Тогда
		ПоказательТипа      = "0";
		ПоказательОснования = "0";
		ПоказательПериода   = "0";
	Иначе
		Если ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж Тогда
			СписокОснований = ЭлементыФормы.ПоказательОснования.СписокВыбора;
		Иначе
			СписокОснований = ЭлементыФормы.ПоказательОснованияТаможня.СписокВыбора;
		КонецЕсли;
			
		Если ПустаяСтрока(ПоказательОснования) ИЛИ ПоказательОснования = "0" Тогда
			ПоказательОснования = СписокОснований[0].Значение;
			ПоказательОснованияПриИзменении("");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказательТипаПриИзменении(Элемент)
	
	Если ПустаяСтрока(ПоказательТипа) Тогда
		ПоказательТипа = "0";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказательОснованияПриИзменении(Элемент)
	
	Если ПустаяСтрока(ПоказательОснования) Тогда
		ПоказательОснования = "0";
	КонецЕсли;
	
	Если ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж Тогда
		
		Если ПоказательОснования = "ТР" Тогда
			ПоказательПериода = "-";
		ИначеЕсли ПоказательОснования = "РС" Тогда
			ПоказательПериода = "-";
		ИначеЕсли ПоказательОснования = "ОТ" Тогда
			ПоказательПериода = "-";
		ИначеЕсли ПоказательОснования = "РТ" Тогда
			ПоказательПериода = "-";
		ИначеЕсли ПоказательОснования = "ВУ" Тогда
			ПоказательПериода = "-";
		ИначеЕсли ПоказательОснования = "ПР" Тогда
			ПоказательПериода = "-";
		ИначеЕсли ПоказательОснования = "АП" Тогда
			ПоказательПериода = "0";
		ИначеЕсли ПоказательОснования = "АР" Тогда
			ПоказательПериода = "0";
		Иначе
			ПоказательПериода = "МС";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если НЕ Выбран() И (ПараметрОбъектКопирования = Неопределено) Тогда
		
		ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж;
		СтруктураРеквизитов = УправлениеДенежнымиСредствами.ПолучитьРеквизитыПлатежаВБюджетПоУмолчанию(
			РабочаяДата, Организация, Истина, ВидПеречисленияВБюджет);
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураРеквизитов);
		
		ПоказательПериода = "МС";
		
		ОчередностьПлатежа = 3;
		
	КонецЕсли;
		
	ГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	
	УстановитьВидимость();
	
	ОбновитьНадписи();
	
КонецПроцедуры

Процедура ВидПеречисленияВБюджетПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ВидПеречисленияВБюджет) Тогда
		ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж;
	КонецЕсли;
	
	СтруктураРеквизитов = УправлениеДенежнымиСредствами.ПолучитьРеквизитыПлатежаВБюджетПоУмолчанию(
		РабочаяДата, Организация, Истина, ВидПеречисленияВБюджет);
		
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураРеквизитов);
	
	УстановитьВидимость();
	
КонецПроцедуры

ЗаполнитьСпискиРеквизитовПлатежейВБюджет();
