Перем КэшКБК;
Перем КэшСчетовУчета;

Процедура ЗаполнитьКэш(МассивВидовПлатежа)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ 
	|	ВидыПлатежа.Ссылка КАК ВидПлатежа,
	|	ВидыПлатежа.КодБК,
	|	ВидыПлатежа.СчетУчета
	|ИЗ
	|	Справочник.ВидыНалоговИПлатежейВБюджет КАК ВидыПлатежа
	|ГДЕ 
	|	ВидыПлатежа.Ссылка В (&МассивВидовПлатежа)";
	Запрос.УстановитьПараметр("МассивВидовПлатежа", МассивВидовПлатежа);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КэшКБК.Вставить(Выборка.ВидПлатежа, Выборка.КодБК);
		КэшСчетовУчета.Вставить(Выборка.ВидПлатежа, Выборка.СчетУчета);
	КонецЦикла;

КонецПроцедуры

Процедура РегистрСведенийСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	МассивНенайденныхВидовПлатежа = Новый Массив;
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ВидПлатежа = ОформлениеСтроки.ДанныеСтроки.ВидПлатежа;
		Если КэшКБК[ВидПлатежа] = Неопределено Тогда
			МассивНенайденныхВидовПлатежа.Добавить(ВидПлатежа);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивНенайденныхВидовПлатежа.Количество() > 0 Тогда
		ЗаполнитьКэш(МассивНенайденныхВидовПлатежа);
	КонецЕсли;
	
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ВидПлатежа = ОформлениеСтроки.ДанныеСтроки.ВидПлатежа;
		ОформлениеСтроки.Ячейки.КодБК.УстановитьТекст(КэшКБК[ВидПлатежа]);
		ОформлениеСтроки.Ячейки.СчетУчета.УстановитьТекст(КэшСчетовУчета[ВидПлатежа]);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДействияФормыОбновить(Кнопка)
	
	КэшКБК.Очистить();
	КэшСчетовУчета.Очистить();
	РегистрСведенийСписок.Обновить();
	
КонецПроцедуры

Процедура РегистрСведенийСписокВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если РежимВыбора Тогда
		СтандартнаяОбработка = Ложь;
		ОповеститьОВыборе(Элемент.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

КэшКБК = Новый Соответствие;
КэшСчетовУчета = Новый Соответствие;
РегистрСведенийСписок.Колонки.Добавить("ВидПеречисленияВБюджет", Ложь);