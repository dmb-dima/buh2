Перем мМесяцНалоговогоПериодаДоИзменения;//Хранит первоначальное значение реквизита "МесяцНалоговогоПериода"

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - вызывается при открытии формы
Процедура ПриОткрытии()
	
	Если НЕ ЗначениеЗаполнено(Год) Тогда
		Год = Год(РабочаяДата);
		РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.ЗначениеС = НачалоГода(РабочаяДата);
		РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.ЗначениеПо = КонецГода(РабочаяДата);
		РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.ВидСравнения = ВидСравнения.ИнтервалВключаяГраницы;
	КонецЕсли;
	
	РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.Использование = Истина;
	РегистрСведенийСписок.Отбор.Организация.Использование            = Истина;

	// Проверка однофирменности
	РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "РегистрСведенийСписок");

КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеТекущейОрганизации" Тогда
		РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "РегистрСведенийСписок", Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

// Процедура - обработчик события изменения года
Процедура ГодПриИзменении(Элемент)

	ДатаВГоду = Дата("" + Прав("0000" + Формат(Год , "ЧГ=0"), 4) + "0101");
	РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.ЗначениеС = НачалоГода(ДатаВГоду);
	РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.ЗначениеПо = КонецГода(ДатаВГоду);
	РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.ВидСравнения = ВидСравнения.ИнтервалВключаяГраницы;

КонецПроцедуры

// Процедура - обработчик события изменения использования отбора по организации
Процедура ОрганизацияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	Запрос = Новый Запрос;

	Если (РегистрСведенийСписок.Отбор.ФизЛицо.Использование) Тогда

		// Выберем организации, в которых работал работник 
		Запрос.УстановитьПараметр("ФизЛицо", РегистрСведенийСписок.Отбор.ФизЛицо.Значение);
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаботникиОрганизации.ГоловнаяОрганизация КАК Организация
		|ИЗ
		|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
		|
		|ГДЕ
		|	РаботникиОрганизации.Физлицо = &Физлицо И
		|	(РаботникиОрганизации.ЗанимаемыхСтавок > 0)";

		Если РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.Использование = Истина Тогда

			Запрос.УстановитьПараметр("Год", Год);
			НачалоГода = Дата("" + Прав("0000" + Формат(Год , "ЧГ=0"), 4) + "0101");
			Запрос.УстановитьПараметр("НачалоГода", НачалоГода);
			Запрос.Текст = Запрос.Текст + "
			|	И (ГОД(РаботникиОрганизации.Период) = &Год)
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РаботникиОрганизацииСрезПоследних.ГоловнаяОрганизация
			|ИЗ
			|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&НачалоГода, Физлицо = &ФизЛицо) КАК РаботникиОрганизацииСрезПоследних
			|
			|ГДЕ
			|	(РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавок > 0)";

		КонецЕсли; 			   

	Иначе

		Запрос.Текст = "ВЫБРАТЬ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации";

	КонецЕсли; 
	Элемент.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация"));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОЙ ЧАСТИ РегистрСведенийСписок

// Процедура - обработчик события окончания редактирования строки списка
Процедура РегистрСведенийСписокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если РегистрСведенийСписок.Отбор.ФизЛицо.Использование  Тогда
		Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","НДФЛДоходыПредыдущегоМестаРаботы"), РегистрСведенийСписок.Отбор.ФизЛицо.Значение);
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события удаления строки списка
Процедура РегистрСведенийСписокПослеУдаления(Элемент)

	Если Отбор.ФизЛицо.Использование Тогда
		Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","НДФЛДоходыПредыдущегоМестаРаботы"), Отбор.ФизЛицо.Значение);
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события начала редактирования строки списка
Процедура РегистрСведенийСписокПриНачалеРедактирования(Элемент, НоваяСтрока)

	мМесяцНалоговогоПериодаДоИзменения = Элемент.ТекущиеДанные.МесяцНалоговогоПериода;

	Если НоваяСтрока Тогда

		СтрокаТЧ = ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные;
		СтрокаТЧ.Организация = глЗначениеПеременной("ОсновнаяОрганизация");
		
	КонецЕсли;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ РегистрСведенийСписок

// Процедура - обработчик события изменения месяца
Процедура РегистрСведенийСписокМесяцНалоговогоПериодаПриИзменении(Элемент)

	Элемент.Значение = НачалоМесяца(Элемент.Значение);

	Если РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.Использование = Истина Тогда
		Если (РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.ВидСравнения = ВидСравнения.ИнтервалВключаяГраницы) И 
			(Элемент.Значение < РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.ЗначениеС
			ИЛИ Элемент.Значение > РегистрСведенийСписок.Отбор.МесяцНалоговогоПериода.ЗначениеПо) Тогда
			Предупреждение("В элемент управления введены некорректные данные.");
			Элемент.Значение = мМесяцНалоговогоПериодаДоИзменения;	
		КонецЕсли; 
	КонецЕсли;	

КонецПроцедуры




