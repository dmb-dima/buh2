////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мОбработкаТайпинга;
Перем мТекстТайпинга;
Перем мПоследнееЗначениеЭлементаТайпинга;
Перем мСписокВыбораСтандартныхВычетов;
Перем мСписокВыбораСтандартныхВычетовНаДетей;
Перем мСписокВыбораСтандартныхВычетовНаДетейИнвалидов;
Перем мПредставленияКодовВычетов;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// настройка порядка
	ЭлементыФормы.РегистрСведенийСписок.НастройкаПорядка.Физлицо.Доступность = Истина;
	
КонецПроцедуры

// Процедура - вызывается при открытии формы
Процедура ПриОткрытии()

	// подготовим списки вычетов 
	
	мПредставленияКодовВычетов = Новый Соответствие;
	мСписокВыбораСтандартныхВычетов = Новый СписокЗначений;
	
	СпрСтандартныеВычеты = Справочники.ВычетыНДФЛ;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВычетыНДФЛ.Ссылка,
	|	ВычетыНДФЛ.Код,
	|	ВычетыНДФЛ.Наименование
	|ИЗ
	|	Справочник.ВычетыНДФЛ КАК ВычетыНДФЛ
	|ГДЕ
	|	ВычетыНДФЛ.ГруппаВычета = ЗНАЧЕНИЕ(Перечисление.ГруппыВычетовПоНДФЛ.Стандартные)");

	Выборка = Запрос.Выполнить().Выбрать();		
	
	Пока Выборка.Следующий() Цикл
		мСписокВыбораСтандартныхВычетов.Добавить(Выборка.Ссылка, "(" + Выборка.Код + ") " + Выборка.Наименование);	
		мПредставленияКодовВычетов.Вставить(Выборка.Ссылка, Выборка.Наименование);
	КонецЦикла;


	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ РегистрСведенийСписок

// Процедура - вызывается при выводе строки списка
Процедура РегистрСведенийСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

    ОформлениеСтроки.Ячейки.Колонка1.Видимость = Ложь;
	
	ОформлениеСтроки.Ячейки.КодВычетаЛичныйНаименование.УстановитьТекст(мПредставленияКодовВычетов[ДанныеСтроки.КодВычетаЛичный]);

КонецПроцедуры

// Процедура - обработчик события окончания редактирования строки списка
Процедура РегистрСведенийСписокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные <> Неопределено Тогда
		Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","СтандартныеВычетыНДФЛ"), ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные.ФизЛицо);
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события удаления строки списка
Процедура РегистрСведенийСписокПослеУдаления(Элемент)

	Если Отбор.ФизЛицо.Использование Тогда
		Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","СтандартныеВычетыНДФЛ"), Отбор.ФизЛицо.Значение);
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭУ ТАБЛИЧНОГО ПОЛЯ РегистрСведенийСписок

// Процедура - обработчик события изменения периода
Процедура РегистрСведенийСписокПериодПриИзменении(Элемент)
	Если Элемент.Значение > РабочаяДата Тогда
		Ответ = Вопрос("Вы действительно хотите ввести вычеты" + Символы.ПС + "на будущую дату?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Элемент.Значение = РабочаяДата
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события начало выбора в колонке "КодВычетаЛичный"
Процедура РегистрСведенийСписокКодВычетаЛичныйНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЭлементСписка = ВыбратьИзСписка(мСписокВыбораСтандартныхВычетов,Элемент,мСписокВыбораСтандартныхВычетов.НайтиПоЗначению(Элемент.Значение));
    Если ЭлементСписка <> Неопределено Тогда
		Элемент.Значение = ЭлементСписка.Значение;
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события "Авто подбор текста" поля "КодВычетаЛичный"
Процедура РегистрСведенийСписокКодВычетаЛичныйАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	ПроцедурыПоискаПоСтроке.АвтоПодборТекстаВЭлементеУправления(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка, Новый Структура("Ссылка",мСписокВыбораСтандартныхВычетов), Тип("СправочникСсылка.ВычетыНДФЛ"));
КонецПроцедуры

// Процедура - обработчик события окончания ввода текста в колонку "КодВычетаЛичный"
Процедура РегистрСведенийСписокКодВычетаЛичныйОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	ПроцедурыПоискаПоСтроке.ОкончаниеВводаТекстаВЭлементеУправления(Элемент, Текст, Значение, СтандартнаяОбработка, Новый Структура("Ссылка",мСписокВыбораСтандартныхВычетов), ЭтаФорма, Тип("СправочникСсылка.ВычетыНДФЛ"), Ложь, Ложь, Неопределено, Ложь);
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ МОДУЛЯ

мОбработкаТайпинга                 = Ложь;
мТекстТайпинга                     = "";
мПоследнееЗначениеЭлементаТайпинга = Неопределено;

