////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ФизЛицоПриОткрытии                  = ФизЛицо;
	ПериодПриОткрытии             		= Период;
	ДокументВидПриОткрытии        		= ДокументВид;
	ДокументДатаВыдачиПриОткрытии 		= ДокументДатаВыдачи;
	ДокументКемВыданПриОткрытии  		= ДокументКемВыдан;
	ДокументКодПодразделенияПриОткрытии = ДокументКодПодразделения;
	ДокументНомерПриОткрытии      		= ДокументНомер;
	ДокументСерияПриОткрытии      		= ДокументСерия;
	
	Заголовок = Заголовок + " " + ФизЛицо.Наименование;
	
	Прочитать();
	
	// У записи обязательно должны быть заполнены реквизиты ФизЛицо и Период
	Если ФизЛицо.Пустая() Тогда
	    ФизЛицо = ФизЛицоПриОткрытии;
		Период  = ПериодПриОткрытии;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Если поменяли дату, то тогда новая  запись
	Если ПериодПриОткрытии <> Период Тогда
		
		// Если что-то поменяли тогда запишем 
		Если ДокументВидПриОткрытии <> ДокументВид ИЛИ
			ДокументДатаВыдачиПриОткрытии <> ДокументДатаВыдачи ИЛИ
			ДокументКемВыданПриОткрытии <> ДокументКемВыдан ИЛИ
			ДокументКодПодразделенияПриОткрытии <> ДокументКодПодразделения ИЛИ
			ДокументНомерПриОткрытии <> ДокументНомер ИЛИ
			ДокументСерияПриОткрытии <> ДокументСерия Тогда
		
			МенеджерЗаписи = РегистрыСведений.ПаспортныеДанныеФизЛиц.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период   = ПериодПриОткрытии;
			МенеджерЗаписи.ФизЛицо  = ФизЛицо;
			
			МенеджерЗаписи.ДокументВид 				= ДокументВидПриОткрытии;
			МенеджерЗаписи.ДокументДатаВыдачи	    = ДокументДатаВыдачиПриОткрытии;
			МенеджерЗаписи.ДокументКемВыдан 	    = ДокументКемВыданПриОткрытии;
			МенеджерЗаписи.ДокументКодПодразделения = ДокументКодПодразделенияПриОткрытии;
			МенеджерЗаписи.ДокументНомер	    	= ДокументНомерПриОткрытии;
			МенеджерЗаписи.ДокументСерия 			= ДокументСерияПриОткрытии;
			
			МенеджерЗаписи.Записать();
			
		КонецЕсли; 
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","УдостоверениеЛичности"), ФизЛицо);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.ПаспортныеДанныеФизЛиц.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры




