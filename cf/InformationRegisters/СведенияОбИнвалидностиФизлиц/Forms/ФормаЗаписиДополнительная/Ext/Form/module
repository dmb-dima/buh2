////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ФизЛицоПриОткрытии             = ФизЛицо;
	ПериодПриОткрытии              = Период;
	ГруппаИнвалидностиПриОткрытии  = ГруппаИнвалидности;
	ДатаВыдачиПриОткрытии 		   = ДатаВыдачи;
	ИнвалидностьПриОткрытии  	   = Инвалидность;
	НомерСправкиПриОткрытии		   = НомерСправки;
	СерияСправкиПриОткрытии        = СерияСправки;
	СрокДействияСправкиПриОткрытии = СрокДействияСправки;
	
	Заголовок = Заголовок + " " + ФизЛицо.Наименование;
	
	Прочитать();
	
	// У записи обязательно должны быть заполнены реквизиты ФизЛицо и Период
	Если ФизЛицо.Пустая() Тогда
	    ФизЛицо = ФизЛицоПриОткрытии;
		Период  = ПериодПриОткрытии;
		// выставим по умолчанию
		ГруппаИнвалидности = "III";
		Инвалидность = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Если поменяли дату, то тогда новая  запись
	Если ПериодПриОткрытии <> Период Тогда
		
		// Если что-то поменяли тогда запишем 
		Если ГруппаИнвалидностиПриОткрытии <> ГруппаИнвалидности ИЛИ
			ДатаВыдачиПриОткрытии <> ДатаВыдачи ИЛИ
			ИнвалидностьПриОткрытии <> Инвалидность ИЛИ
			НомерСправкиПриОткрытии <> НомерСправки ИЛИ
			СерияСправкиПриОткрытии <> СерияСправки ИЛИ
			СрокДействияСправкиПриОткрытии <> СрокДействияСправки Тогда
		
			МенеджерЗаписи = РегистрыСведений.СведенияОбИнвалидностиФизлиц.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Период   = ПериодПриОткрытии;
			МенеджерЗаписи.ФизЛицо  = ФизЛицо;
			
			МенеджерЗаписи.ГруппаИнвалидности  = ГруппаИнвалидностиПриОткрытии;
			МенеджерЗаписи.ДатаВыдачи	       = ДатаВыдачиПриОткрытии;
			МенеджерЗаписи.Инвалидность 	   = ИнвалидностьПриОткрытии;
			МенеджерЗаписи.НомерСправки        = НомерСправкиПриОткрытии;
			МенеджерЗаписи.СерияСправки	       = СерияСправкиПриОткрытии;
			МенеджерЗаписи.СрокДействияСправки = СрокДействияСправкиПриОткрытии;
			
			МенеджерЗаписи.Записать();
			
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗаписи()
	
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","Инвалидность"), ФизЛицо);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.СведенияОбИнвалидностиФизлиц.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры




