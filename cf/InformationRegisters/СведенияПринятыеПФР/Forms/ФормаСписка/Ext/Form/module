Перем мЗаголовокФормы;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура УстановитьЗаголовкиОрганизацийВФорме()
	
	ЭлементыФормы.НадписьОрганизация.Заголовок = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация:");
	ЭлементыФормы.НадписьОрганизация.Значение = ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Организация:");
	
КонецПроцедуры // УстановитьЗаголовкиОрганизацийВФорме()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Если форма открывается из списка документов, процедура заполняет организацию 
// прописывает заголовок формы и выставляет отбор регистру
//
Процедура ПриОткрытии()
	
	УстановитьЗаголовкиОрганизацийВФорме();
	
	мЗаголовокФормы = "Сведения, принятые ПФР: ";
	
	Если Не ЗначениеЗаполнено(Отбор.Организация.Значение) Тогда
		// заполним организацию
		РаботаСДиалогамиЗК.ЗаполнениеОтбораПоОрганизацииПоУмолчанию(ЭтаФорма, Отбор.Организация, ЭлементыФормы.Организация, ПараметрОтборПоРегистратору, Ложь, глЗначениеПеременной("глТекущийПользователь"),мЗаголовокФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	// заполним организацию
	РаботаСДиалогамиЗК.ЗаполнениеОтбораПоОрганизацииПоУмолчанию(ЭтаФорма, Отбор.Организация, ЭлементыФормы.Организация, ПараметрОтборПоРегистратору, Ложь, глЗначениеПеременной("глТекущийПользователь"),мЗаголовокФормы);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

// Процедура прописывает заголовок формы 
//
// Параметры:
//  Элемент - элемент формы, который отображает организацию
//  
Процедура ОрганизацияПриИзменении(Элемент)

	Заголовок = мЗаголовокФормы + Элемент.Значение.Наименование;
	Отбор.Организация.Использование = Не Элемент.Значение.Пустая();
	
КонецПроцедуры

Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь
КонецПроцедуры

Процедура РегистрСведенийСписокПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Не ЗначениеЗаполнено(Отбор.Организация.Значение);
	Если Отказ Тогда
		РаботаСДиалогами.ВывестиПредупреждение(ОбщегоНазначения.ПреобразоватьСтрокуИнтерфейса("Не указана организация!"));
	КонецЕсли;
	
КонецПроцедуры

Процедура РегистрСведенийСписокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Если НоваяСтрока И Не Копирование Тогда
		ДанныеСтроки.ОтчетныйПериодПерсучетаПФР = ПроцедурыПерсонифицированногоУчета.ПредшествующийОтчетныйПериодПерсучета(ОбщегоНазначения.ПолучитьРабочуюДату());
		ДанныеСтроки.Организация = Отбор.Организация.Значение;
	КонецЕсли;
КонецПроцедуры

Процедура РегистрСведенийСписокПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ДанныеСтроки = Элемент.ТекущиеДанные;
	Если ДанныеСтроки <> Неопределено Тогда
		ДанныеСтроки.ОтчетныйПериодПерсучетаПФР = ПроцедурыПерсонифицированногоУчета.НачалоОтчетногоПериодаПерсучета(ДанныеСтроки.ОтчетныйПериодПерсучетаПФР)
	КонецЕсли;
КонецПроцедуры

Процедура РегистрСведенийСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	МассивДокументов = Новый Массив;
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		МассивДокументов.Добавить(ОформлениеСтроки.ДанныеСтроки.КомплектДокументов)
	КонецЦикла;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПередачаСЗВ4вПФР.НомерПачки,
	|	ПередачаСЗВ4вПФР.Ссылка
	|ИЗ
	|	Документ.ПередачаСЗВ4вПФР КАК ПередачаСЗВ4вПФР
	|ГДЕ
	|	ПередачаСЗВ4вПФР.Ссылка В(&Ссылки)";
	Запрос.УстановитьПараметр("Ссылки", МассивДокументов);
	Выборка = Запрос.Выполнить().Выбрать();
	СоответствиеДокументов = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		СоответствиеДокументов[Выборка.Ссылка] = Выборка.НомерПачки;
	КонецЦикла;
	
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ДатаПериода = ОформлениеСтроки.ДанныеСтроки.ОтчетныйПериодПерсучетаПФР;
		ОформлениеСтроки.Ячейки.ОтчетныйПериодПерсучетаПФР.УстановитьТекст(ПредставлениеПериода(НачалоГода(ДатаПериода), ПроцедурыПерсонифицированногоУчета.ОкончаниеОтчетногоПериодаПерсучета(ДатаПериода), "ФП = Истина" ));
		Если ЗначениеЗаполнено(ОформлениеСтроки.ДанныеСтроки.КомплектДокументов) Тогда
			ОформлениеСтроки.Ячейки.КомплектДокументов.УстановитьТекст("Пачка АДВ-6-2 № " + СоответствиеДокументов[ОформлениеСтроки.ДанныеСтроки.КомплектДокументов]);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура РегистрСведенийСписокКомплектДокументовНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки = ЭлементыФормы.РегистрСведенийСписок.ТекущиеДанные;
	ФормаВыбора = Документы.ПередачаСЗВ4вПФР.ПолучитьФормуВыбора(, Элемент, ДанныеСтроки);
	ФормаВыбора.Отбор.Организация.Установить(Отбор.Организация.Значение);
	ФормаВыбора.Отбор.Проведен.Установить(Истина);
	ФормаВыбора.Отбор.ОтчетныйПериод.Установить(ПроцедурыПерсонифицированногоУчета.НачалоОтчетногоПериодаПерсучета(ДанныеСтроки.ОтчетныйПериодПерсучетаПФР));
	ФормаВыбора.НачальноеЗначениеВыбора = ДанныеСтроки.КомплектДокументов;
	ФормаВыбора.Открыть();
	
КонецПроцедуры


