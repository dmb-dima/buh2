////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБЩЕГО НАЗНАЧЕНИЯ

// Сохраняет модифицированный набор записей о стаже
Процедура СохранитьНаборЗаписей()
	
	Если РегистрСведенийНаборЗаписей.Модифицированность() Тогда
		Попытка
			РегистрСведенийНаборЗаписей.Записать(Истина);
		Исключение
		КонецПопытки;
	КонецЕсли;  

КонецПроцедуры
 

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события "ПриЗаписи" набора записей регистра сведений
Процедура ПриЗаписи(Отказ)
	//Выполним проверку правильности ввода данных

	СтруктураПараметровПроверки = Новый Структура("МассивФизЛиц, МассивОбособленныхПодразделений, МассивЛет");
	
    Таблица = Выгрузить();
	
	ВременныйМассив = Таблица.ВыгрузитьКолонку("Год");
	МассивПредыдущихЛет = Новый Массив;
	МассивНовыхЛет = Новый Массив;
	ГраницаЛет = Год(ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами());
	Для каждого Значение Из ВременныйМассив Цикл
	    Если Значение < ГраницаЛет Тогда
			МассивПредыдущихЛет.Добавить(Значение)
		Иначе
			МассивНовыхЛет.Добавить(Значение)
		КонецЕсли;
	КонецЦикла;
	
	Если МассивПредыдущихЛет.Количество() > 0 Тогда
		СтруктураПараметровПроверки.МассивФизЛиц = Таблица.ВыгрузитьКолонку("ФизЛицо");
		СтруктураПараметровПроверки.МассивОбособленныхПодразделений = Таблица.ВыгрузитьКолонку("ОбособленноеПодразделение");
		СтруктураПараметровПроверки.МассивЛет = МассивПредыдущихЛет;
		Если НЕ ПроцедурыПерсонифицированногоУчета.СЗВ4_ПроверитьНаборЗаписейОСтаже("НаборЗаписей", СтруктураПараметровПроверки) тогда
			Текст = "При проверке введенных данных в записи о стаже обнаружены ошибки!"+Символы.ПС+"Записать с ошибками?";
			Ответ = Вопрос(Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, );
			
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если МассивНовыхЛет.Количество() > 0 Тогда
		
		ТаблицаФизлиц = Таблица.Скопировать(,"ФизЛицо,ОбособленноеПодразделение,КатегорияЗастрахованныхЛиц,ОтчетныйПериод");
		ТаблицаФизлиц.Свернуть("ФизЛицо,ОбособленноеПодразделение,КатегорияЗастрахованныхЛиц,ОтчетныйПериод");
		ТаблицаФизлиц.Колонки.ОбособленноеПодразделение.Имя = "Организация";
   		ТаблицаФизлиц.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
		НомерСтроки = 0;
		Для каждого СтрокаФизлица Из ТаблицаФизлиц Цикл
			НомерСтроки = НомерСтроки + 1;
			СтрокаФизлица.НомерСтроки = НомерСтроки;
		КонецЦикла;

		Если НЕ ПроцедурыПерсонифицированногоУчета.СЗВ6_ПроверитьНаборЗаписейОСтаже("НаборЗаписей", ТаблицаФизлиц) тогда
			Текст = "При проверке введенных данных в записи о стаже обнаружены ошибки!"+Символы.ПС+"Записать с ошибками?";
			Ответ = Вопрос(Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, );
			
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()


// Обработчик события открытия формы
Процедура ПриОткрытии()

	Если ЗначениеЗаполнено(глЗначениеПеременной("глТекущийПользователь")) Тогда

		Отбор.ОбособленноеПодразделение.Значение = глЗначениеПеременной("ОсновнаяОрганизация");

		Отбор.Год.Значение	= Год(ТекущаяДата());
		Отбор.ОбособленноеПодразделение.Использование = Истина;

	Иначе

		Отбор.Год.Значение = Макс(2002,Год(РабочаяДата));
		Отбор.ОбособленноеПодразделение.Использование = Ложь;

	КонецЕсли;

	Заголовок = "Записи о стаже для СЗВ-6 (до 2010 года - СЗВ-4) в организации " + Отбор.ОбособленноеПодразделение.Значение.Наименование;	

	Отбор.Год.Использование     = Истина;
	Отбор.ФизЛицо.Использование = Ложь;

	ЭлементыФормы.Год.Значение =  Отбор.Год.Значение;
	ЭлементыФормы.Организация.Значение =  Отбор.ОбособленноеПодразделение.Значение;
	ЭлементыФормы.ИспользованиеОтбораПоГоду.Значение =  Истина;
	ЭлементыФормы.ИспользованиеОтбораПоОрганизации.Значение =  Истина;
	ЭлементыФормы.ИспользованиеОтбораПоФизЛицу.Значение =  Ложь;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

// Обработчик события изменения признака отбора по организации
Процедура ИспользованиеОтбораПоОрганизацииПриИзменении(Элемент)
	СохранитьНаборЗаписей();
	Отбор.ОбособленноеПодразделение.Использование = Элемент.Значение
КонецПроцедуры

// Обработчик события изменения признака отбора по году
Процедура ИспользованиеОтбораПоГодуПриИзменении(Элемент)
	СохранитьНаборЗаписей();
	Отбор.Год.Использование = Элемент.Значение
КонецПроцедуры

// Обработчик события изменения признака отбора по физлицу
Процедура ИспользованиеОтбораПоФизЛицуПриИзменении(Элемент)
	СохранитьНаборЗаписей();
	Отбор.ФизЛицо.Использование = Элемент.Значение
КонецПроцедуры

// Обработчик события изменения организации
Процедура ОрганизацияПриИзменении(Элемент)

	Если Отбор.ОбособленноеПодразделение.Использование тогда
		Заголовок = "Записи о стаже для СЗВ-6 (до 2010 года - СЗВ-4) в организации " + Элемент.Значение.Наименование;
		СохранитьНаборЗаписей();
	КонецЕсли;	
	Отбор.ОбособленноеПодразделение.Значение = Элемент.Значение;
	
КонецПроцедуры

// Обработчик события изменения года
Процедура ГодПриИзменении(Элемент)
	
	Если Отбор.Год.Использование тогда
		СохранитьНаборЗаписей();
	КонецЕсли;	
	Отбор.Год.Значение = Элемент.Значение;
	
КонецПроцедуры

// Обработчик события изменения физлица
Процедура ФизЛицоПриИзменении(Элемент)
	
	Если Отбор.ФизЛицо.Использование тогда
		СохранитьНаборЗаписей();
	КонецЕсли;	
	Отбор.ФизЛицо.Значение = Элемент.Значение;
	
КонецПроцедуры

Процедура РегистрСведенийНаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока)

	Если НоваяСтрока Тогда
		СтрокаТЧ = ЭлементыФормы.РегистрСведенийНаборЗаписей.ТекущиеДанные;
		СтрокаТЧ.ОбособленноеПодразделение = глЗначениеПеременной("ОсновнаяОрганизация");
		
		Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ОтчетныйПериод) Тогда
			СтрокаТЧ.ОтчетныйПериод = ПроцедурыПерсонифицированногоУчета.ПредшествующийОтчетныйПериодПерсучета(ОбщегоНазначения.ПолучитьРабочуюДату());;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Обработчик события повтроного открытия формы
Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(глЗначениеПеременной("глТекущийПользователь")) Тогда
		
		Отбор.ОбособленноеПодразделение.Значение = глЗначениеПеременной("ОсновнаяОрганизация");
		Отбор.Год.Значение	= Год(ТекущаяДата());
		Отбор.ОбособленноеПодразделение.Использование = Истина;
		
	Иначе
		
		Отбор.Год.Значение	= Год(РабочаяДата);
		Отбор.ОбособленноеПодразделение.Использование = Ложь;
		
	КонецЕсли;
	
	Заголовок = "Записи о стаже для СЗВ-6 (до 2010 года - СЗВ-4) в организации " + Отбор.ОбособленноеПодразделение.Значение.Наименование;	
		
	Отбор.ФизЛицо.Использование 	= Истина;
	Отбор.Год.Использование 		= Истина;
	
КонецПроцедуры

Процедура РегистрСведенийНаборЗаписейОтчетныйПериодРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ОтчетныйПериод = ЭлементыФормы.РегистрСведенийНаборЗаписей.ТекущиеДанные.ОтчетныйПериод;
	Если ОтчетныйПериод < '20100101' Тогда
		ОтчетныйПериод = ДобавитьМесяц(ОтчетныйПериод, Направление * 12);
	ИначеЕсли ОтчетныйПериод = '20100101' Тогда
		Если Направление = 1 Тогда
			ОтчетныйПериод = '20100701'
		Иначе 	
			ОтчетныйПериод = '20090101'
		КонецЕсли;
	ИначеЕсли ОтчетныйПериод = '20100701' Тогда
		ОтчетныйПериод = ДобавитьМесяц(ОтчетныйПериод, Направление * 6);
	ИначеЕсли ОтчетныйПериод = '20110101' И Направление = -1 Тогда
		ОтчетныйПериод = '20100701'
	Иначе
		ОтчетныйПериод = ДобавитьМесяц(ОтчетныйПериод, Направление * 3);
	КонецЕсли;
	ЭлементыФормы.РегистрСведенийНаборЗаписей.ТекущиеДанные.ОтчетныйПериод = ОтчетныйПериод;
	Элемент.Значение = ПредставлениеПериода(НачалоГода(ОтчетныйПериод), ПроцедурыПерсонифицированногоУчета.ОкончаниеОтчетногоПериодаПерсучета(ОтчетныйПериод), "ФП = Истина" );
КонецПроцедуры

Процедура РегистрСведенийНаборЗаписейПриПолученииДанных(Элемент, ОформленияСтрок)
	// уберем ненужные ячейки колонок заголовка
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформлениеСтроки.Ячейки.КолонкаТУ.Видимость = Ложь;
		ОформлениеСтроки.Ячейки.КолонкаИТС.Видимость = Ложь;
		ОформлениеСтроки.Ячейки.КолонкаКалендарнойВыработки.Видимость = Ложь;
		ОформлениеСтроки.Ячейки.КолонкаВыслугаЛет.Видимость = Ложь;
		ОтчетныйПериод = ОформлениеСтроки.ДанныеСтроки.ОтчетныйПериод;
		ОформлениеСтроки.Ячейки.ОтчетныйПериод.Значение = ПредставлениеПериода(НачалоГода(ОтчетныйПериод), ПроцедурыПерсонифицированногоУчета.ОкончаниеОтчетногоПериодаПерсучета(ОтчетныйПериод), "ФП = Истина" );
		ОформлениеСтроки.Ячейки.КатегорияЗастрахованныхЛиц.ОтметкаНезаполненного = Не ЗначениеЗаполнено(ОформлениеСтроки.ДанныеСтроки.КатегорияЗастрахованныхЛиц) И ОтчетныйПериод >= ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами();
		ОформлениеСтроки.Ячейки.КатегорияЗастрахованныхЛиц.Видимость = ОтчетныйПериод >= ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами();
	КонецЦикла;
КонецПроцедуры

Процедура РегистрСведенийНаборЗаписейТретийПараметрИсчисляемогоСтажаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ОтчетныйПериод = ЭлементыФормы.РегистрСведенийНаборЗаписей.ТекущиеДанные.ОтчетныйПериод;
	Если ОтчетныйПериод < ПроведениеРасчетов.ДатаЗаменыЕСНСтраховымиВзносами() Тогда
		ДопустимыеЗначенияТретьегоПараметра = Новый СписокЗначений;
		ДопустимыеЗначенияТретьегоПараметра.Добавить("ДЕКРЕТ");
		ДопустимыеЗначенияТретьегоПараметра.Добавить("ДЕТИ");
	Иначе
		ДопустимыеЗначенияТретьегоПараметра = Новый СписокЗначений;
		ДопустимыеЗначенияТретьегоПараметра.Добавить("ДЕКРЕТ");
		ДопустимыеЗначенияТретьегоПараметра.Добавить("ДЕТИ");
		ДопустимыеЗначенияТретьегоПараметра.Добавить("АДМИНИСТР");
		ДопустимыеЗначенияТретьегоПараметра.Добавить("ВРНЕТРУД");
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ЭлементСписка = ВыбратьИзСписка(ДопустимыеЗначенияТретьегоПараметра,Элемент,ДопустимыеЗначенияТретьегоПараметра.НайтиПоЗначению(Элемент.Значение));
	Если ЭлементСписка <> Неопределено Тогда
		Элемент.Значение = ЭлементСписка.Значение;
	КонецЕсли;
	
КонецПроцедуры








