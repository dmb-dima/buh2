Функция ФормаЗаполнена()
	Статус = Истина;
	
	Если НЕ ЗначениеЗаполнено(КодИФНС) Тогда
		Сообщить("Не заполнен код налогового органа!", СтатусСообщения.Важное);
		Статус = Ложь;
	КонецЕсли;
	
	Если (ОтправительЯвляетсяЮЛ И НЕ ЗначениеЗаполнено(ИННЮЛОтпр)) ИЛИ (НЕ ОтправительЯвляетсяЮЛ И НЕ ЗначениеЗаполнено(ИННФЛОтпр)) Тогда
		Сообщить("Не заполнен ИНН отправителя!", СтатусСообщения.Важное);
		Статус = Ложь;
	КонецЕсли; 
	
	Если ПрПодп1 = 3 Тогда
		Если НЕ ЗначениеЗаполнено(НаимДовПодп1) ИЛИ НЕ ЗначениеЗаполнено(НомерДовПодп1) ИЛИ НЕ ЗначениеЗаполнено(ДатаДовПодп1) Тогда
			Сообщить("Не заполнены сведения о доверенности подписанта 1!", СтатусСообщения.Важное);
			Статус = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ОтправленПочтой И ПрКурьер = 3 Тогда
		Если НЕ ЗначениеЗаполнено(НаимДовКурьер) ИЛИ НЕ ЗначениеЗаполнено(НомерДовКурьер) ИЛИ НЕ ЗначениеЗаполнено(ДатаДовКурьер) Тогда
			Сообщить("Не заполнены сведения о доверенности курьера!", СтатусСообщения.Важное);
			Статус = Ложь;
		КонецЕсли;
	КонецЕсли;
	Возврат Статус;
КонецФункции 

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если НЕ ФормаЗаполнена() Тогда
		Возврат;
	КонецЕсли; 
	
	Модифицированность = Ложь;
	
	//СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("КодИФНС", КодИФНС);
	//Отправитель
	СтруктураРеквизитов.Вставить("ПрОтпр", ПрОтпр);
	СтруктураРеквизитов.Вставить("ОтправительЯвляетсяЮЛ", ОтправительЯвляетсяЮЛ);
	
	СтруктураРеквизитов.Вставить("НаимОтпр", НаимОтпр);
	СтруктураРеквизитов.Вставить("ИННЮЛОтпр", ИННЮЛОтпр);
	СтруктураРеквизитов.Вставить("КППОтпр", КППОтпр);
	
	СтруктураРеквизитов.Вставить("ИННФЛОтпр", ИННФЛОтпр);
	СтруктураРеквизитов.Вставить("ФамилияФЛОтпр", ФамилияФЛОтпр);
	СтруктураРеквизитов.Вставить("ИмяФЛОтпр", ИмяФЛОтпр);
	СтруктураРеквизитов.Вставить("ОтчествоФЛОтпр", ОтчествоФЛОтпр);
	
	//Подписант1
	СтруктураРеквизитов.Вставить("ПрПодп1", ПрПодп1);
	СтруктураРеквизитов.Вставить("ИННФЛПодп1", ИННФЛПодп1);
	СтруктураРеквизитов.Вставить("ДолжностьПодп1", ДолжностьПодп1);
	СтруктураРеквизитов.Вставить("ФамилияПодп1", ФамилияПодп1);
	СтруктураРеквизитов.Вставить("ИмяПодп1", ИмяПодп1);
	СтруктураРеквизитов.Вставить("ОтчествоПодп1", ОтчествоПодп1);
	СтруктураРеквизитов.Вставить("НаимДовПодп1", НаимДовПодп1);
	СтруктураРеквизитов.Вставить("НомерДовПодп1", НомерДовПодп1);
	СтруктураРеквизитов.Вставить("ДатаДовПодп1", ДатаДовПодп1);
	
	//Подписант2
	СтруктураРеквизитов.Вставить("ИННФЛПодп2", ИННФЛПодп2);
	СтруктураРеквизитов.Вставить("ДолжностьПодп2", ДолжностьПодп2);
	СтруктураРеквизитов.Вставить("ФамилияПодп2", ФамилияПодп2);
	СтруктураРеквизитов.Вставить("ИмяПодп2", ИмяПодп2);
	СтруктураРеквизитов.Вставить("ОтчествоПодп2", ОтчествоПодп2);
	
	//Курьер
	СтруктураРеквизитов.Вставить("ОтправленПочтой", ОтправленПочтой);
	СтруктураРеквизитов.Вставить("ПрКурьер", ПрКурьер);
	СтруктураРеквизитов.Вставить("ИННФЛКурьер", ИННФЛКурьер);
	СтруктураРеквизитов.Вставить("ФамилияКурьер", ФамилияКурьер);
	СтруктураРеквизитов.Вставить("ИмяКурьер", ИмяКурьер);
	СтруктураРеквизитов.Вставить("ОтчествоКурьер", ОтчествоКурьер);
	СтруктураРеквизитов.Вставить("НаимДовКурьер", НаимДовКурьер);
	СтруктураРеквизитов.Вставить("НомерДовКурьер", НомерДовКурьер);
	СтруктураРеквизитов.Вставить("ДатаДовКурьер", ДатаДовКурьер);
	
	Закрыть(СтруктураРеквизитов); 	
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если СтруктураРеквизитов <> Неопределено Тогда
		КодИФНС					= СтруктураРеквизитов.КодИФНС;	
		
		//Отправитель
		ПрОтпр 					= СтруктураРеквизитов.ПрОтпр;	
		ОтправительЯвляетсяЮЛ 	= СтруктураРеквизитов.ОтправительЯвляетсяЮЛ;
		
		НаимОтпр 				= СтруктураРеквизитов.НаимОтпр;	
		ИННЮЛОтпр 				= СтруктураРеквизитов.ИННЮЛОтпр;	
		КППОтпр 				= СтруктураРеквизитов.КППОтпр;	
		
		ИННФЛОтпр 				= СтруктураРеквизитов.ИННФЛОтпр;	
		ФамилияФЛОтпр           = СтруктураРеквизитов.ФамилияФЛОтпр;	
		ИмяФЛОтпр           	= СтруктураРеквизитов.ИмяФЛОтпр;	
		ОтчествоФЛОтпр          = СтруктураРеквизитов.ОтчествоФЛОтпр;	
		
		//Подписант1
		ПрПодп1 				= СтруктураРеквизитов.ПрПодп1;	
		ИННФЛПодп1 				= СтруктураРеквизитов.ИННФЛПодп1;	
		ДолжностьПодп1			= СтруктураРеквизитов.ДолжностьПодп1;	
		ФамилияПодп1         	= СтруктураРеквизитов.ФамилияПодп1;	
		ИмяПодп1           		= СтруктураРеквизитов.ИмяПодп1;	
		ОтчествоПодп1         	= СтруктураРеквизитов.ОтчествоПодп1;
		НаимДовПодп1          	= СтруктураРеквизитов.НаимДовПодп1;	
		НомерДовПодп1           = СтруктураРеквизитов.НомерДовПодп1;	
		ДатаДовПодп1         	= СтруктураРеквизитов.ДатаДовПодп1;
		
		//Подписант2
		ДолжностьПодп2			= СтруктураРеквизитов.ДолжностьПодп2;	
		ИННФЛПодп2 				= СтруктураРеквизитов.ИННФЛПодп2;	
		ФамилияПодп2         	= СтруктураРеквизитов.ФамилияПодп2;	
		ИмяПодп2           		= СтруктураРеквизитов.ИмяПодп2;	
		ОтчествоПодп2         	= СтруктураРеквизитов.ОтчествоПодп2;
		
		//Курьер
		ОтправленПочтой 		= СтруктураРеквизитов.ОтправленПочтой;	
		ПрКурьер 				= СтруктураРеквизитов.ПрКурьер;	
		ИННФЛКурьер 			= СтруктураРеквизитов.ИННФЛКурьер;	
		ФамилияКурьер          	= СтруктураРеквизитов.ФамилияКурьер;	
		ИмяКурьер           	= СтруктураРеквизитов.ИмяКурьер;	
		ОтчествоКурьер         	= СтруктураРеквизитов.ОтчествоКурьер;
		НаимДовКурьер          	= СтруктураРеквизитов.НаимДовКурьер;	
		НомерДовКурьер          = СтруктураРеквизитов.НомерДовКурьер;	
		ДатаДовКурьер         	= СтруктураРеквизитов.ДатаДовКурьер;
	КонецЕсли; 
	
	УправлениеЭУ();
	
КонецПроцедуры

Процедура УправлениеЭУ()
	
	// панель представитель
	ПанельОтпрЮФ = ЭлементыФормы.ПанельОтпрЮФ;
	СтраницаОтпрЮЛ = ПанельОтпрЮФ.Страницы.Получить(0);
	СтраницаОтпрФЛ = ПанельОтпрЮФ.Страницы.Получить(1);
	
	Если ОтправительЯвляетсяЮЛ Тогда
		ПанельОтпрЮФ.ТекущаяСтраница = СтраницаОтпрЮЛ;
	Иначе
		ПанельОтпрЮФ.ТекущаяСтраница = СтраницаОтпрФЛ;
	КонецЕсли;
	
	Если ПрПодп1 = 3 Тогда
		ЭлементыФормы.НаимДовПодп1.Доступность 	= Истина;
		ЭлементыФормы.НомерДовПодп1.Доступность = Истина;
		ЭлементыФормы.ДатаДовПодп1.Доступность 	= Истина;
	Иначе
		ЭлементыФормы.НаимДовПодп1.Доступность 	= Ложь;
		ЭлементыФормы.НомерДовПодп1.Доступность = Ложь;
		ЭлементыФормы.ДатаДовПодп1.Доступность 	= Ложь;
	КонецЕсли;
	
	Если ПрКурьер = 3 Тогда
		Если НЕ ОтправленПочтой Тогда
			ЭлементыФормы.НаимДовКурьер.Доступность		= Истина;
			ЭлементыФормы.НомерДовКурьер.Доступность 	= Истина;
			ЭлементыФормы.ДатаДовКурьер.Доступность 	= Истина;
		КонецЕсли;
	Иначе
		ЭлементыФормы.НаимДовКурьер.Доступность 	= Ложь;
		ЭлементыФормы.НомерДовКурьер.Доступность 	= Ложь;
		ЭлементыФормы.ДатаДовКурьер.Доступность 	= Ложь;
	КонецЕсли;
	
	Если ОтправленПочтой Тогда
		ЭлементыФормы.ПрКурьер1.Доступность 			= Ложь;
		ЭлементыФормы.ПрКурьер2.Доступность 			= Ложь;
		ЭлементыФормы.ПрКурьер3.Доступность 			= Ложь;
		ЭлементыФормы.ФамилияКурьер.Доступность 	= Ложь;
		ЭлементыФормы.ИмяКурьер.Доступность 		= Ложь;
		ЭлементыФормы.ОтчествоКурьер.Доступность 	= Ложь;
		ЭлементыФормы.НаимДовКурьер.Доступность 	= Ложь;
		ЭлементыФормы.НомерДовКурьер.Доступность 	= Ложь;
		ЭлементыФормы.ДатаДовКурьер.Доступность 	= Ложь;
		ЭлементыФормы.ИННФЛКурьер.Доступность 		= Ложь;
	Иначе
		Если ПрКурьер = 3 Тогда
			ЭлементыФормы.НаимДовКурьер.Доступность 	= Истина;
			ЭлементыФормы.НомерДовКурьер.Доступность 	= Истина;
			ЭлементыФормы.ДатаДовКурьер.Доступность 	= Истина;
		КонецЕсли;
		ЭлементыФормы.ПрКурьер1.Доступность 			= Истина;
		ЭлементыФормы.ПрКурьер2.Доступность 			= Истина;
		ЭлементыФормы.ПрКурьер3.Доступность 			= Истина;
		ЭлементыФормы.ФамилияКурьер.Доступность 	= Истина;
		ЭлементыФормы.ИмяКурьер.Доступность 		= Истина;
		ЭлементыФормы.ОтчествоКурьер.Доступность 	= Истина;
		ЭлементыФормы.ИННФЛКурьер.Доступность 		= Истина;
	КонецЕсли;
	
	флОтпрПредст = (ПрОтпр = 2);
	ЭлементыФормы.ОтправительЯвляетсяЮЛ.Доступность = флОтпрПредст;	
	ЭлементыФормы.ПанельОтпрЮФ.Доступность = флОтпрПредст;	
	
	Если СтруктураРеквизитов.ЭтоПБОЮЛ Тогда
		ЭлементыФормы.ПрПодп2.Доступность 	= Ложь;
		ЭлементыФормы.ПрКурьер2.Доступность = Ложь;
	Иначе
		ЭлементыФормы.ПрПодп1.Доступность 	= Ложь;
		ЭлементыФормы.ПрКурьер1.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправительЯвляетсяЮЛПриИзменении(Элемент)
	УправлениеЭУ();
КонецПроцедуры

Функция РазложитьФИО(Знач ФИОСтр)
	
	ФИОСтр = СокрЛП(ФИОСтр);
	ФИО = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	ПервыйПробел = Найти(ФИОСтр, " ");
	Если ПервыйПробел = 0 Тогда
		ФИО.Фамилия = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Фамилия = СокрЛП(Лев(ФИОСтр, ПервыйПробел - 1));
	ФИОСтр = СокрЛП(Сред(ФИОСтр, ПервыйПробел + 1));
	
	ВторойПробел = Найти(ФИОСтр, " ");
	Если ВторойПробел = 0 Тогда
		ФИО.Имя = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Имя = СокрЛП(Лев(ФИОСтр, ВторойПробел - 1));
	
	ФИО.Отчество = СокрЛП(Сред(ФиоСтр, ВторойПробел + 1));
	
	Возврат ФИО;
	
КонецФункции

Процедура ПрОтпр1ПриИзменении(Элемент)
	Если Элемент.Значение = 1 Тогда
		//заполним данные отправителя данными налогоплательщика
		ОтправительЯвляетсяЮЛ = НЕ СтруктураРеквизитов.ЭтоПБОЮЛ;
		НаимОтпр = СтруктураРеквизитов.НалогоплательщикНаимЮЛ;
		ИННЮЛОтпр 				= СтруктураРеквизитов.НалогоплательщикИННЮЛ;	
		КППОтпр 				= СтруктураРеквизитов.НалогоплательщикКПП;	
		
		ИННФЛОтпр 				= СтруктураРеквизитов.НалогоплательщикИННФЛ;	
		
		ФИОФЛОтпр = РазложитьФИО(СтруктураРеквизитов.НалогоплательщикФИО);
		ФамилияФЛОтпр           = ФИОФЛОтпр.Фамилия;	
		ИмяФЛОтпр           	= ФИОФЛОтпр.Имя;	
		ОтчествоФЛОтпр          = ФИОФЛОтпр.Отчество;	
	КонецЕсли;
	
	УправлениеЭУ();
КонецПроцедуры

Процедура ПрПодп1ПриИзменении(Элемент)
	Если Элемент.Значение <> 3 Тогда
		НаимДовПодп1 = "";
		НомерДовПодп1 = "";
		ДатаДовПодп1 = '00010101'
	КонецЕсли;
	
	УправлениеЭУ();
КонецПроцедуры

Процедура ПрКурьер1ПриИзменении(Элемент)
	Если Элемент.Значение <> 3 Тогда
		НаимДовКурьер = "";
		НомерДовКурьер = "";
		ДатаДовКурьер = '00010101'
	КонецЕсли;
	
	УправлениеЭУ();
КонецПроцедуры

Процедура ОтправленПочтойПриИзменении(Элемент)
	УправлениеЭУ();
КонецПроцедуры



СписокЮФ = Новый СписокЗначений;
СписокЮФ.Добавить(Истина, "Юр. лицо");
СписокЮФ.Добавить(Ложь, "Физ. лицо");

ЭлементыФормы.ОтправительЯвляетсяЮЛ.СписокВыбора = СписокЮФ;