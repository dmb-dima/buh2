#Если Клиент Тогда
	
	Перем НП Экспорт;
	Перем КоэфННП, КоэфНеННП;
	Перем ИтоговаяСуммаННП, ИтоговаяСуммаНеННП;
	Перем ПрименяетсяЕНВД;
	
	Функция СформироватьТекстЗапроса()
		
		Текст	= "ВЫБРАТЬ
		     	  |	РасчетРаспределенияКосвенныхРасходов.ПериодРасчета КАК Период,
		     	  |	РасчетРаспределенияКосвенныхРасходов.СтатьяЗатрат.ВидДеятельностиДляНалоговогоУчетаЗатрат КАК НалоговыйРежим,
		     	  |	РасчетРаспределенияКосвенныхРасходов.ЕНВД КАК ЕНВД,
		     	  |	РасчетРаспределенияКосвенныхРасходов.БазаРаспределения КАК База,
		     	  |	РасчетРаспределенияКосвенныхРасходов.Подразделение КАК ПодразделениеЗатрат,
		     	  |	РасчетРаспределенияКосвенныхРасходов.СтатьяЗатрат КАК СтатьяЗатрат,
		     	  |	ПОДСТРОКА(РасчетРаспределенияКосвенныхРасходов.СчетРаспределения.Код, 1, 2) КАК СчетЗатрат,
		     	  |	РасчетРаспределенияКосвенныхРасходов.ПодразделениеРаспределения КАК Подразделение,
		     	  |	РасчетРаспределенияКосвенныхРасходов.НоменклатурнаяГруппаРаспределения КАК НоменклатурнаяГруппа,
		     	  |	РасчетРаспределенияКосвенныхРасходов.СуммаБазы КАК СуммаБаза,
		     	  |	РасчетРаспределенияКосвенныхРасходов.Сумма КАК СУММА,
				  |	РасчетРаспределенияКосвенныхРасходов.СуммаПР,
				  |	РасчетРаспределенияКосвенныхРасходов.СуммаВР
		     	  |ИЗ
		     	  |	РегистрСведений.РасчетРаспределенияКосвенныхРасходов КАК РасчетРаспределенияКосвенныхРасходов
		     	  |ГДЕ
		     	  |	РасчетРаспределенияКосвенныхРасходов.Активность
		     	  |	И РасчетРаспределенияКосвенныхРасходов.Организация = &Организация
		     	  |	И РасчетРаспределенияКосвенныхРасходов.ВидОперации = &ВидОперации
		     	  |	И РасчетРаспределенияКосвенныхРасходов.ПериодРасчета МЕЖДУ &ДатаНач И &ДатаКон
		     	  |	И (НЕ РасчетРаспределенияКосвенныхРасходов.БазаРаспределения = &БазаРаспределенияПустая)
		     	  |
		     	  |УПОРЯДОЧИТЬ ПО
		     	  |	База УБЫВ
		     	  |ИТОГИ
		     	  |	МАКСИМУМ(СуммаБаза),
		     	  |	СУММА(Сумма),
		     	  |	СУММА(СуммаПР),
		     	  |	СУММА(СуммаВР)
		     	  |ПО
		     	  |	Период,
		     	  |	НалоговыйРежим,
		     	  |	База,
		     	  |	Подразделение,
		     	  |	СтатьяЗатрат,
		     	  |	СчетЗатрат,
		     	  |	ЕНВД,
		     	  |	НоменклатурнаяГруппа,
		     	  |	ПодразделениеЗатрат";
		
		Возврат Текст;
		
	КонецФункции
	
	// Выполняет запрос и формирует табличный документ-результат отчета
	// в соответствии с настройками, заданными значениями реквизитов отчета.
	//
	// Параметры:
	//	ДокументРезультат - табличный документ, формируемый отчетом
	//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
	//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
	//
	Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь, НачалоПодписи, ПоказыватьПодписи) Экспорт
		
		ДатаНач = НачалоМесяца(ПериодРегистрации);
		ДатаКон  = КонецМесяца(ПериодРегистрации);
		Если ДатаНач > ДатаКон И ДатаКон <> '00010101000000' Тогда
			Предупреждение("Дата начала периода не может быть больше даты окончания периода");
			Возврат;
		КонецЕсли;
		НачалоПериода = НачалоДня(ДатаНач);
		КонецПериода  = КонецДня(ДатаКон);
		
		ДокументРезультат.Очистить();
		
		ПрименяетсяЕНВД = НалоговыйУчет.ПрименениеЕНВД(Организация, ДатаКон);
		
		Если ВариантОперации = 1 Тогда
			Макет      = ПолучитьМакет("ОбщепроизводственныеРасходы");
		ИначеЕсли  ВариантОперации = 2 Тогда
			Макет      = ПолучитьМакет("ОбщехозяйственныеРасходы");
		КонецЕсли;
		
		МакетОбщий = ПолучитьОбщийМакет("СправкаРасчет");
		
		ЗаголовокОтчета = МакетОбщий.ПолучитьОбласть("Заголовок");
		
		
		// Когда нужен только заголовок:
		Если ТолькоЗаголовок Тогда
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
			ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДатаНач",     НачалоДня(ДатаНач));
		Запрос.УстановитьПараметр("ДатаКон",     КонецДня(ДатаКон));
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("БазаРаспределенияПустая",  Перечисления.БазыРаспределенияКосвенныхРасходов.ПустаяСсылка());
		
		ОграниченияПоПостроителюОтчета = СтандартныеОтчеты.ПолучитьТекстОграниченийПоПостроителюОтчета(ПостроительОтчета, Запрос);
		
		Если Не ПустаяСтрока(ОграниченияПоПостроителюОтчета) Тогда
			
			ОграниченияПоПостроителюОтчета = " И " + ОграниченияПоПостроителюОтчета;
			ОграниченияПоПостроителюОтчета = СтрЗаменить(ОграниченияПоПостроителюОтчета, "Субконто1", "РасчетРаспределенияКосвенныхРасходов.НоменклатурнаяГруппаРаспределения");
			
		КонецЕсли;
		
		Запрос.Текст = СформироватьТекстЗапроса();
		
		Запрос.Текст  = СтрЗаменить(Запрос.Текст, "Активность","Активность " + ОграниченияПоПостроителюОтчета);
		
		Если ВариантОтчета = 2 Тогда
			Запрос.Текст  = СтрЗаменить(Запрос.Текст, ".Сумма,",".СуммаНУ, ");
			Запрос.Текст  = СтрЗаменить(Запрос.Текст, "Сумма КАК СУММА","СуммаНУ КАК СУММА");
			Запрос.Текст  = СтрЗаменить(Запрос.Текст, "СуммаБазы КАК СуммаБаза","СуммаБазыНУ КАК СуммаБаза");
			Запрос.Текст  = СтрЗаменить(Запрос.Текст, "&БазаРаспределенияПустая)", "&БазаРаспределенияПустая) И НЕ РасчетРаспределенияКосвенныхРасходов.СуммаНУ = 0");
		ИначеЕсли ВариантОтчета = 1 Тогда	
			Запрос.Текст  = СтрЗаменить(Запрос.Текст, "&БазаРаспределенияПустая)", "&БазаРаспределенияПустая) И НЕ РасчетРаспределенияКосвенныхРасходов.Сумма = 0");
		ИначеЕсли ВариантОтчета = 3 Тогда	
			Запрос.Текст  = СтрЗаменить(Запрос.Текст, "&БазаРаспределенияПустая)", "&БазаРаспределенияПустая) И НЕ (РасчетРаспределенияКосвенныхРасходов.Сумма = 0 И РасчетРаспределенияКосвенныхРасходов.СуммаПР = 0 И РасчетРаспределенияКосвенныхРасходов.СуммаВР = 0)");
		КонецЕсли;	
		
		Если ВариантОперации = 1 Тогда
			ТекстЗаголовка = "общепроизводственных расходов";
			Запрос.Текст = Запрос.Текст + ",База,ПодразделениеЗатрат,СтатьяЗатрат,СчетЗатрат,НоменклатурнаяГруппа";
			Запрос.УстановитьПараметр("ВидОперации",    Перечисления.ВидыОперацийРасчетаКосвенныхРасходов.РаспределениеОбщепроизводственныхРасходов);
		Иначе	
			ТекстЗаголовка = "общехозяйственных расходов";
			Запрос.Текст = Запрос.Текст + ",База,СтатьяЗатрат,СчетЗатрат,Подразделение,НоменклатурнаяГруппа";
			Запрос.УстановитьПараметр("ВидОперации",    Перечисления.ВидыОперацийРасчетаКосвенныхРасходов.РаспределениеОбщехозяйственныхРасходов);
		КонецЕсли;	
		
		Результат = Запрос.Выполнить();
		
		СтрОтбор = УправлениеОтчетами.СформироватьСтрокуОтборов(ПостроительОтчета.Отбор);
		СтрОтбор = СтрЗаменить(СтрОтбор, "Субконто1", "Подразделение");
		
		Если Не ПустаяСтрока(СтрОтбор) Тогда
			ОбластьОтбор = МакетОбщий.ПолучитьОбласть("СтрокаОтбор");
			ОбластьОтбор.Параметры.ТекстПроОтбор = "Отбор: " + СтрОтбор;
			ДокументРезультат.Вывести(ОбластьОтбор);
		КонецЕсли;
		ОбластьНазваниеОтчета    = Макет.ПолучитьОбласть("НазваниеОтчета");
		
		ОбластьШапка1            = Макет.ПолучитьОбласть("Шапка");
		ОбластьШапкаПрочее1      = Макет.ПолучитьОбласть("ШапкаПрочее");
		ОбластьСтрока1           = Макет.ПолучитьОбласть("Строка");
		ОбластьСтрокаПрочее1     = Макет.ПолучитьОбласть("СтрокаПрочее");
		ОбластьПодСтрока1        = Макет.ПолучитьОбласть("ПодСтрока");
		ОбластьСтрокаРазниц1     = Макет.ПолучитьОбласть("СтрокаРазниц");
		ОбластьИтого1            = Макет.ПолучитьОбласть("Итого");
		ОбластьИтогоРазниц1      = Макет.ПолучитьОбласть("ИтогоРазниц");
		ОбластьВсего1            = Макет.ПолучитьОбласть("Всего");
		ОбластьВсегоРазниц1      = Макет.ПолучитьОбласть("ВсегоРазниц");
		ОбластьОкончаниеТаблицы1 = Макет.ПолучитьОбласть("ОкончаниеТаблицы");
		
		
		ОбластьШапка2            = Макет.ПолучитьОбласть("Шапка2");
		ОбластьСтрока2           = Макет.ПолучитьОбласть("Строка2");
		ОбластьПодСтрока2        = Макет.ПолучитьОбласть("ПодСтрока2");
		ОбластьСтрокаРазниц2     = Макет.ПолучитьОбласть("СтрокаРазниц2");
		ОбластьИтого2            = Макет.ПолучитьОбласть("Итого2");
		ОбластьИтогоРазниц2      = Макет.ПолучитьОбласть("ИтогоРазниц2");
		ОбластьВсего2            = Макет.ПолучитьОбласть("Всего2");
		ОбластьВсегоРазниц2      = Макет.ПолучитьОбласть("ВсегоРазниц2");
		ОбластьОкончаниеТаблицы2 = Макет.ПолучитьОбласть("ОкончаниеТаблицы2");
		
		ОбластьВсего3            = Макет.ПолучитьОбласть("Всего3");
		
		ОбластьПодвал             = МакетОбщий.ПолучитьОбласть("Подвал");
		
		ОбластьВыручка  = МакетОбщий.ПолучитьОбласть("Выручка");
		
		ВыборкаПериод = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Период");
		ЕстьРаспределяемыеРасходы = Ложь;
		
		Пока ВыборкаПериод.Следующий() Цикл
			
			
		ИтоговаяСуммаННП   = 0;
		ИтоговаяСуммаНеННП = 0;
			
			ЗаголовокОтчета.Параметры.Период              = "" + ПредставлениеПериода(НачалоМесяца(ВыборкаПериод.Период), КонецМесяца(ВыборкаПериод.Период), "ФП = Истина");
			СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация);
			НазваниеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
			ЗаголовокОтчета.Параметры.НазваниеОрганизации = НазваниеОрганизации;
			ЗаголовокОтчета.Параметры.ДатаСоставления     = КонецМесяца(ВыборкаПериод.Период);
			ЗаголовокОтчета.Параметры.НазваниеОтчета      = "Распределение " + ТекстЗаголовка + " на расходы производства (" +?(ВариантОтчета = 2, "налоговый учет", "бухгалтерский учет") + ")";
			ДокументРезультат.Вывести(ЗаголовокОтчета);
			
			// Параметр для показа заголовка
			ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;
	    	Всего = Новый Структура("База,Сумма,ННП,НеННП,СуммаПР,ННППР,НеННППР,СуммаВР,ННПВР,НеННПВР",0,0,0,0,0,0,0,0,0,0);
			
			ВыборкаНалоговыйРежим = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаНалоговыйРежим.Следующий() Цикл
			    ОбластьНазваниеОтчета.Параметры.НазваниеОтчета      =  ПостроитьНазвание(ВыборкаНалоговыйРежим.НалоговыйРежим);
				ДокументРезультат.Вывести(ОбластьНазваниеОтчета);
				
				Если ВыборкаНалоговыйРежим.НалоговыйРежим = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ВидДеятельностиОпределяетсяПоДоходам Тогда
					ОбластьШапка              = ОбластьШапка2;
					ОбластьСтрока             = ОбластьСтрока2;
					ОбластьПодСтрока          = ОбластьПодСтрока2;
					ОбластьСтрокаРазниц       = ОбластьСтрокаРазниц2;
					ОбластьИтого              = ОбластьИтого2;
					ОбластьИтогоРазниц        = ОбластьИтогоРазниц2;
					ОбластьВсего              = ОбластьВсего2;
					ОбластьВсегоРазниц        = ОбластьВсегоРазниц2;
					ОбластьОкончаниеТаблицы   = ОбластьОкончаниеТаблицы2;
					ЭтоРаспределяемыеРасходы  = Истина;
					ЕстьРаспределяемыеРасходы = Истина;
				Иначе
					ОбластьШапка            = ОбластьШапка1;
					ОбластьСтрока           = ОбластьСтрока1;
					ОбластьПодСтрока        = ОбластьПодСтрока1;
					ОбластьСтрокаРазниц     = ОбластьСтрокаРазниц1;
					ОбластьИтого            = ОбластьИтого1;
					ОбластьИтогоРазниц      = ОбластьИтогоРазниц1;
					ОбластьВсего            = ОбластьВсего1;
					ОбластьВсегоРазниц      = ОбластьВсегоРазниц1;
					ОбластьОкончаниеТаблицы = ОбластьОкончаниеТаблицы1;
					ЭтоРаспределяемыеРасходы= Ложь;
				КонецЕсли;
				ОбластиПрочее = Новый Структура("ОбластьШапкаПрочее,ОбластьСтрокаПрочее,ОбластьИтого,ОбластьВсего",ОбластьШапкаПрочее1,ОбластьСтрокаПрочее1,ОбластьИтого1,ОбластьВсего1);
				Если ЭтоРаспределяемыеРасходы Тогда
					ТаблицаДоходов = Неопределено;
					КоэфННП      = 0;
					КоэфНеННП    = 0;
					ПолучитьТаблицуДоходов(НачалоПериода, КонецПериода, ТаблицаДоходов, КоэфНеННП, КоэфННП);
					
				КонецЕсли;
				ВыборкаБаза = ВыборкаНалоговыйРежим.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаБаза.Следующий() Цикл
					
					Если ВыборкаБаза.База = Перечисления.БазыРаспределенияКосвенныхРасходов.ПустаяСсылка() Тогда
						Продолжить;
					КонецЕсли;
					
					ОбластьШапка.Параметры.База = "Распределение пропорционально " + БазаПадеж(ВыборкаБаза.База, "Дательный");
					ОбластьШапка.Параметры.ТекстБазы = ВыборкаБаза.База;
					Если ЭтоРаспределяемыеРасходы Тогда
						ОбластьШапка.Параметры.РасчетННП   = "(" + ?(ВариантОперации = 1, "гр.4", "гр.3") + ") * " +   КоэфННП + "(**)";
						ОбластьШапка.Параметры.РасчетНеННП = "(" + ?(ВариантОперации = 1, "гр.4", "гр.3") + ") * " + КоэфНеННП + "(**)";
					КонецЕсли;
					ДокументРезультат.Вывести(ОбластьШапка);
					
					Если ВариантОперации = 1 Тогда
	    	                Всего2 = Новый Структура("База,Сумма,ННП,НеННП,СуммаПР,ННППР,НеННППР,СуммаВР,ННПВР,НеННПВР",0,0,0,0,0,0,0,0,0,0);
						
						ВыборкаПодразделение = ВыборкаБаза.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Подразделение");
						Пока ВыборкаПодразделение.Следующий() Цикл
	    	                Всего3 = Новый Структура("База,Сумма,ННП,НеННП,СуммаПР,ННППР,НеННППР,СуммаВР,ННПВР,НеННПВР",0,0,0,0,0,0,0,0,0,0);
							
							ВыборкаСтатьяЗатрат = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяЗатрат");
							Пока ВыборкаСтатьяЗатрат.Следующий() Цикл
								ПерваяСтрокаСтатьиЗатрат = Истина;
								
								ВыборкаСчет = ВыборкаСтатьяЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СчетЗатрат");
								Пока ВыборкаСчет.Следующий() Цикл
									ВыборкаРежимРаспределения = ВыборкаСчет.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ЕНВД");
										Итого = Новый Структура("База,БазаННП,БазаНеННП,Сумма,ННП,НеННП,СуммаПР,ННППР,НеННППР,СуммаВР,ННПВР,НеННПВР",0,0,0,0,0,0,0,0,0,0,0,0);
									
									Пока ВыборкаРежимРаспределения.Следующий() Цикл
										Выборка = ВыборкаРежимРаспределения.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатурнаяГруппа");
										Пока Выборка.Следующий() Цикл
											Итого.База = Итого.База  + ?(Выборка.СуммаБаза = NULL, 0, Выборка.СуммаБаза);
											Если Выборка.ЕНВД Тогда
												Итого.БазаНеННП = Итого.БазаНеННП  + ?(Выборка.СуммаБаза = NULL, 0, Выборка.СуммаБаза);
											Иначе
												Итого.БазаННП = Итого.БазаННП  + ?(Выборка.СуммаБаза = NULL, 0, Выборка.СуммаБаза);
											КонецЕсли;	
										КонецЦикла;
										СчетчикСтрок = 0;
										
										ВыборкаНомГруппа = ВыборкаРежимРаспределения.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатурнаяГруппа");
										Пока ВыборкаНомГруппа.Следующий() Цикл
											
											Выборка = ВыборкаНомГруппа;
											
											
											Если ПерваяСтрокаСтатьиЗатрат Тогда
												Область = ОбластьСтрока;
												Область.Параметры.Подразделение    = Выборка.Подразделение;
												Область.Параметры.СтатьяЗатрат     = Выборка.СтатьяЗатрат;
												ПоказатьСумму(Область, ЭтоРаспределяемыеРасходы, "СуммаЗатрат", ВыборкаСтатьяЗатрат.Сумма, Выборка.ЕНВД); 
											Иначе
												Область = ОбластьПодСтрока;
											КонецЕсли;
											Область.Параметры.СчетЗатрат           = Выборка.СчетЗатрат;
											Область.Параметры.НоменклатурнаяГруппа = Выборка.НоменклатурнаяГруппа;
											Область.Параметры.СуммаБазы            = Выборка.СуммаБаза;
											Область.Параметры.Коэффициент          = ПолучитьКоэффициент(ЭтоРаспределяемыеРасходы, Выборка.ЕНВД, Итого, Выборка.СуммаБаза);
											ПоказатьСумму(Область, ЭтоРаспределяемыеРасходы, "Сумма", Выборка.Сумма, Выборка.ЕНВД);
											ДокументРезультат.Вывести(Область);
											
											Если ВариантОтчета = 3 Тогда
												ПоказатьСумму(ОбластьСтрокаРазниц, ЭтоРаспределяемыеРасходы, "СуммаЗатрат", ?(ПерваяСтрокаСтатьиЗатрат, ВыборкаСтатьяЗатрат.СуммаПР, 0), Выборка.ЕНВД, "ПР");
												ПоказатьСумму(ОбластьСтрокаРазниц, ЭтоРаспределяемыеРасходы, "Сумма", Выборка.СуммаПР, Выборка.ЕНВД, "ПР");
												
												Если Не ВыборкаРежимРаспределения.СуммаПР = 0 Или НЕ Выборка.СуммаПР = 0 Тогда
													ДокументРезультат.Вывести(ОбластьСтрокаРазниц);
												КонецЕсли;
												ПоказатьСумму(ОбластьСтрокаРазниц, ЭтоРаспределяемыеРасходы, "СуммаЗатрат", ?(ПерваяСтрокаСтатьиЗатрат, ВыборкаСтатьяЗатрат.СуммаВР, 0), Выборка.ЕНВД, "ВР");
												ПоказатьСумму(ОбластьСтрокаРазниц, ЭтоРаспределяемыеРасходы, "Сумма", Выборка.СуммаВР, Выборка.ЕНВД, "ВР");
												Если Не ВыборкаРежимРаспределения.СуммаВР = 0 Или НЕ Выборка.СуммаВР = 0 Тогда
													ДокументРезультат.Вывести(ОбластьСтрокаРазниц);
												КонецЕсли;
												
											КонецЕсли;
											ПерваяСтрокаСтатьиЗатрат = Ложь;
											СчетчикСтрок = СчетчикСтрок + 1;
											
											Итого.Сумма = Итого.Сумма + Выборка.Сумма;
											Если ВыборкаРежимРаспределения.ЕНВД Тогда
												Итого.НеННП = Итого.НеННП + Выборка.Сумма;
												Если ВариантОтчета = 3 Тогда
													Итого.НеННППР = Итого.НеННППР + Выборка.СуммаПр;
													Итого.НеННПВР = Итого.НеННПВР + Выборка.СуммаВр;
												КонецЕсли;
												
											Иначе
												Итого.ННП = Итого.ННП + Выборка.Сумма;
												Если ВариантОтчета = 3 Тогда
													Итого.ННППР = Итого.ННППР + Выборка.СуммаПр;
													Итого.ННПВР = Итого.ННПВР + Выборка.СуммаВр;
												КонецЕсли;
											КонецЕсли;
											
										КонецЦикла;  // НомГруппа
										
										Если СчетчикСтрок > 1 Тогда
											ПоказатьИтоги(ДокументРезультат,ОбластьИтого,ОбластьИтогоРазниц,ЭтоРаспределяемыеРасходы,Итого);	
										КонецЕсли;
										
										
									КонецЦикла; // Режим налогообложения
									
								КонецЦикла;  // СчетУчета
							РасчетВсего(Итого, Всего3)
								
						КонецЦикла;  // СтатьяЗатрат
						
							РасчетВсего(Всего3, Всего2);

							ПоказатьИтоги(ДокументРезультат,
							?(ЭтоРаспределяемыеРасходы,ОбластьВсего2,ОбластьВсего1),
							?(ЭтоРаспределяемыеРасходы,ОбластьВсегоРазниц2,ОбластьВсегоРазниц1),
							ЭтоРаспределяемыеРасходы,Всего2);
						КонецЦикла;  // Подразделение
						РасчетВсего(Всего2, Всего)
						
					Иначе
						ВыборкаСтатьяЗатрат = ВыборкаБаза.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяЗатрат");
						СчетчикСтрок = 0;
						
						Пока ВыборкаСтатьяЗатрат.Следующий() Цикл
							ПерваяСтрокаСтатьиЗатрат = Истина;
							Итого = Новый Структура("База,БазаННП,БазаНеННП,Сумма,ННП,НеННП,СуммаПР,ННППР,НеННППР,СуммаВР,ННПВР,НеННПВР",0,0,0,0,0,0,0,0,0,0,0,0);
							ВыборкаПодразделение = ВыборкаСтатьяЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Подразделение");
							Пока ВыборкаПодразделение.Следующий() Цикл
								Выборка = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатурнаяГруппа");
								Пока Выборка.Следующий() Цикл
									Итого.База = Итого.База  + ?(Выборка.СуммаБаза = NULL, 0, Выборка.СуммаБаза);
									ВыборкаРежимРаспределения = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ЕНВД");
									Пока ВыборкаРежимРаспределения.Следующий() Цикл
										
										Если ВыборкаРежимРаспределения.ЕНВД Тогда
											Итого.БазаНеННП = Итого.БазаНеННП  + ?(ВыборкаРежимРаспределения.СуммаБаза = NULL, 0, ВыборкаРежимРаспределения.СуммаБаза);
										Иначе
											Итого.БазаННП = Итого.БазаННП  + ?(ВыборкаРежимРаспределения.СуммаБаза = NULL, 0, ВыборкаРежимРаспределения.СуммаБаза);
										КонецЕсли;	
									КонецЦикла;
								КонецЦикла;
							КонецЦикла;
							
							ВыборкаСчет = ВыборкаСтатьяЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СчетЗатрат");
							Пока ВыборкаСчет.Следующий() Цикл
								
								ВыборкаРежимРаспределения = ВыборкаСчет.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ЕНВД");
								Пока ВыборкаРежимРаспределения.Следующий() Цикл
									
									ВыборкаПодразделение = ВыборкаРежимРаспределения.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Подразделение");
									Пока ВыборкаПодразделение.Следующий() Цикл
										
										
										ВыборкаНомГруппа = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатурнаяГруппа");
										Пока ВыборкаНомГруппа.Следующий() Цикл
											Выборка = ВыборкаНомГруппа;
											
											Если ПерваяСтрокаСтатьиЗатрат Тогда
												Область = ОбластьСтрока;
												Область.Параметры.СтатьяЗатрат     = Выборка.СтатьяЗатрат;
												ПоказатьСумму(Область, ЭтоРаспределяемыеРасходы, "СуммаЗатрат", ВыборкаСтатьяЗатрат.Сумма,ВыборкаСтатьяЗатрат.ЕНВД);
											Иначе
												Область = ОбластьПодСтрока;
											КонецЕсли;
											Область.Параметры.СчетЗатрат       = Выборка.СчетЗатрат;
											Область.Параметры.Подразделение    = Выборка.Подразделение;
											Область.Параметры.НоменклатурнаяГруппа = Выборка.НоменклатурнаяГруппа;
											Область.Параметры.СуммаБазы            = Выборка.СуммаБаза;
											Область.Параметры.Коэффициент          = ПолучитьКоэффициент(ЭтоРаспределяемыеРасходы, Выборка.ЕНВД, Итого, Выборка.СуммаБаза);
											ПоказатьСумму(Область, ЭтоРаспределяемыеРасходы, "Сумма", Выборка.Сумма,Выборка.ЕНВД);
											ДокументРезультат.Вывести(Область);
											
											Если ВариантОтчета = 3 Тогда
												ПоказатьСумму(ОбластьСтрокаРазниц, ЭтоРаспределяемыеРасходы, "СуммаЗатрат", ?(ПерваяСтрокаСтатьиЗатрат, ВыборкаПодразделение.СуммаПР, 0),Выборка.ЕНВД, "ПР");
												ПоказатьСумму(ОбластьСтрокаРазниц, ЭтоРаспределяемыеРасходы, "Сумма", Выборка.СуммаПР,Выборка.ЕНВД, "ПР");
												Если Не ВыборкаПодразделение.СуммаПР = 0 Или НЕ Выборка.СуммаПР = 0 Тогда
													ДокументРезультат.Вывести(ОбластьСтрокаРазниц);
												КонецЕсли;
												ПоказатьСумму(ОбластьСтрокаРазниц, ЭтоРаспределяемыеРасходы, "СуммаЗатрат", ?(ПерваяСтрокаСтатьиЗатрат, ВыборкаПодразделение.СуммаВР, 0),Выборка.ЕНВД, "ВР");
												ПоказатьСумму(ОбластьСтрокаРазниц, ЭтоРаспределяемыеРасходы, "Сумма", Выборка.СуммаВР,Выборка.ЕНВД, "ВР");
												Если Не ВыборкаПодразделение.СуммаВР = 0 Или НЕ Выборка.СуммаВР = 0 Тогда
													ДокументРезультат.Вывести(ОбластьСтрокаРазниц);
												КонецЕсли;
												
											КонецЕсли;
											
											Итого.Сумма = Итого.Сумма + Выборка.Сумма;
											Если ВыборкаРежимРаспределения.ЕНВД Тогда
												Итого.НеННП = Итого.НеННП + Выборка.Сумма;
												Если ВариантОтчета = 3 Тогда
													Итого.НеННППР = Итого.НеННППР + Выборка.СуммаПр;
													Итого.НеННПВР = Итого.НеННПВР + Выборка.СуммаВр;
												КонецЕсли;
												
											Иначе
												Итого.ННП = Итого.ННП + Выборка.Сумма;
												Если ВариантОтчета = 3 Тогда
													Итого.ННППР = Итого.ННППР + Выборка.СуммаПр;
													Итого.ННПВР = Итого.ННПВР + Выборка.СуммаВр;
												КонецЕсли;
											КонецЕсли;
											ПерваяСтрокаСтатьиЗатрат = Ложь;
											
										КонецЦикла;  // НомГруппа
									КонецЦикла;  // Подразделение
								КонецЦикла;  // Режим распределения
							КонецЦикла;  //  СчетУчета
							ПоказатьИтоги(ДокументРезультат,ОбластьИтого,ОбластьИтогоРазниц,ЭтоРаспределяемыеРасходы,Итого);									
							РасчетВсего(Итого, Всего);
							СчетчикСтрок = СчетчикСтрок + 1;
							
						КонецЦикла;  //  СтатьяЗатрат
						
						
					КонецЕсли;
					ДокументРезультат.Вывести(ОбластьОкончаниеТаблицы);
					
				КонецЦикла;  // База
				
			КонецЦикла;  // НалоговыйРежим
			
			
			ВыборкаБаза = ВыборкаПериод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "База");
			Пока ВыборкаБаза.Следующий() Цикл
				
				Если ВыборкаБаза.База = Перечисления.БазыРаспределенияКосвенныхРасходов.ПустаяСсылка() Тогда
					ПоказатьПрочиеВременныеРазницы(ВыборкаБаза, ОбластиПрочее, ДокументРезультат);
					ОбластьВсего1.Параметры.Сумма = ВыборкаБаза.Сумма;
					ДокументРезультат.Вывести(ОбластьВсего1);
					ДокументРезультат.Вывести(ОбластьОкончаниеТаблицы1);
				КонецЕсли;
			КонецЦикла;
			
			
			ОбластьВсего3.Параметры.Текст     = "Итого сумма затрат, включенных в состав расходов " + НалоговыйУчет.ПолучитьТекстВидаДеятельности(ПрименяетсяЕНВД, (Не Всего.ННП = 0), Ложь) + ":";
			ОбластьВсего3.Параметры.ТекстЕНВД = ?(Всего.НеННП = 0, "", "Итого сумма затрат, включенных в состав расходов " + НалоговыйУчет.ПолучитьТекстВидаДеятельности(ПрименяетсяЕНВД, Истина, Истина) + ":");
			ОбластьВсего3.Параметры.СуммаННП = Всего.ННП;
			ОбластьВсего3.Параметры.СуммаНЕННП = Всего.НЕННП;
			ДокументРезультат.Вывести(ОбластьВсего3);
			
			
			Если ЕстьРаспределяемыеРасходы Тогда
				ОбластьВыручка.Параметры.НазваниеОтчетаВыручка = "** - Расчет долей доходов по каждому виду деятельности в общем объеме доходов за текущий месяц";						
				ОбластьВыручка.Параметры.ТекстВыручки = "За текущий месяц";
				ОбластьВыручка.Параметры.ВыручкаННП   = ТаблицаДоходов.Итог("СуммаННП");
				ОбластьВыручка.Параметры.ВыручкаНеННП = ТаблицаДоходов.Итог("СуммаНеННП");
				ОбластьВыручка.Параметры.КоэффициентННП   = КоэфННП;
				ОбластьВыручка.Параметры.КоэффициентНеННП = КоэфНеННП;
				ДокументРезультат.Вывести(ОбластьВыручка);
			КонецЕсли;
			
			ОбластьПодвал.Параметры.ТекстПримечания = ?(ВариантОтчета = 3, "ПР: Постоянные разницы в оценке стоимости активов и обязательств. ВР: Временные разницы в оценке стоимости активов и обязательств.", "");
			ДокументРезультат.Вывести(ОбластьПодвал);
			
			ВысотаПодписи = ДокументРезультат.Области.Подвал.Низ - ДокументРезультат.Области.Подвал.Верх;
			
			ДокументРезультат.Области.Подвал.Видимость = ПоказыватьПодписи;
			
		КонецЦикла;    // Период
		
		
		// Первую колонку не печатаем
		ДокументРезультат.ОбластьПечати = ДокументРезультат.Область(1,2,ДокументРезультат.ВысотаТаблицы,ДокументРезультат.ШиринаТаблицы);
		
		// Присвоим имя для сохранения параметров печати табличного документа
		ДокументРезультат.ИмяПараметровПечати = "Распределение косвенных расходов";
		
		УправлениеОтчетами.УстановитьКолонтитулыПоУмолчанию(ДокументРезультат, ЗаголовокОтчета, Строка(глЗначениеПеременной("глТекущийПользователь")));
		
	КонецПроцедуры // СформироватьОтчет
	
	
	Процедура ПоказатьПрочиеВременныеРазницы(ВыборкаБаза, ОбластиПрочее, ДокументРезультат)
		
		ОбластьШапка = ОбластиПрочее.ОбластьШапкаПрочее;
		ОбластьСтрока = ОбластиПрочее.ОбластьСтрокаПрочее;
		ОбластьИтого = ОбластиПрочее.ОбластьИтого;
		ОбластьВсего = ОбластиПрочее.ОбластьВсего;
		
		ДокументРезультат.Вывести(ОбластьШапка);
		Если ВариантОперации = 1 Тогда
			
			ВыборкаПодразделение = ВыборкаБаза.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Подразделение");
			Пока ВыборкаПодразделение.Следующий() Цикл
				
				ВыборкаСтатьяЗатрат = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяЗатрат");
				Пока ВыборкаСтатьяЗатрат.Следующий() Цикл
					
					ВыборкаСчет = ВыборкаСтатьяЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СчетЗатрат");
					Пока ВыборкаСчет.Следующий() Цикл
						
						ВыборкаНомГруппа = ВыборкаСчет.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатурнаяГруппа");
						Пока ВыборкаНомГруппа.Следующий() Цикл
							
							Выборка = ВыборкаНомГруппа;
							Область = ОбластьСтрока;
							Область.Параметры.Подразделение    = ВыборкаПодразделение.Подразделение;
							Область.Параметры.СтатьяЗатрат     = Выборка.СтатьяЗатрат;
							Область.Параметры.СчетЗатрат       = Выборка.СчетЗатрат;
							Область.Параметры.НоменклатурнаяГруппа = Выборка.НоменклатурнаяГруппа;
							Область.Параметры.Сумма            = Выборка.СуммаВР;
							ДокументРезультат.Вывести(Область);
							
						КонецЦикла;  // НомГруппа
					КонецЦикла;  // СчетУчета
				КонецЦикла;  // СтатьяЗатрат
				
			КонецЦикла;  // Подразделение
			
		Иначе
			ВыборкаСтатьяЗатрат = ВыборкаБаза.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СтатьяЗатрат");
			Пока ВыборкаСтатьяЗатрат.Следующий() Цикл
				
				ВыборкаСчет = ВыборкаСтатьяЗатрат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "СчетЗатрат");
				Пока ВыборкаСчет.Следующий() Цикл
					
					ВыборкаПодразделение = ВыборкаСчет.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Подразделение");
					Пока ВыборкаПодразделение.Следующий() Цикл
						
						ВыборкаНомГруппа = ВыборкаПодразделение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НоменклатурнаяГруппа");
						Пока ВыборкаНомГруппа.Следующий() Цикл
							
							Выборка = ВыборкаНомГруппа;
							Область = ОбластьСтрока;
							Область.Параметры.СтатьяЗатрат     = Выборка.СтатьяЗатрат;
							Область.Параметры.СчетЗатрат       = Выборка.СчетЗатрат;
							Область.Параметры.Подразделение    = Выборка.Подразделение;
							Область.Параметры.НоменклатурнаяГруппа = Выборка.НоменклатурнаяГруппа;
							Область.Параметры.Сумма            = Выборка.СуммаВР;
							ДокументРезультат.Вывести(Область);
							
						КонецЦикла;  // НомГруппа
					КонецЦикла;  // Подразделение
				КонецЦикла;  //  СчетУчета
			КонецЦикла;  //  СтатьяЗатрат
			
		КонецЕсли;
		
	КонецПроцедуры
	
	
	Функция БазаПадеж(База, Падеж)
		
		Если База = Перечисления.БазыРаспределенияКосвенныхРасходов.МатериальныеЗатраты Тогда
			Возврат ?(Падеж = "Родительный", " материальных расходов", " материальным расходам");
		ИначеЕсли База = Перечисления.БазыРаспределенияКосвенныхРасходов.ОплатаТруда Тогда
			Возврат ?(Падеж = "Родительный", " расходов по оплате труда", " расходам по оплате труда");
		ИначеЕсли База = Перечисления.БазыРаспределенияКосвенныхРасходов.ПлановаяСебестоимость Тогда
			Возврат " плановой себестоимости";
		ИначеЕсли База = Перечисления.БазыРаспределенияКосвенныхРасходов.ОбъемВыпуска Тогда
			Возврат ?(Падеж = "Родительный", " выпуска", " выпуску");
		ИначеЕсли База = Перечисления.БазыРаспределенияКосвенныхРасходов.Выручка Тогда
			Возврат ?(Падеж = "Родительный", " выручки", " выручке");
		ИначеЕсли База = Перечисления.БазыРаспределенияКосвенныхРасходов.ОтдельныеСтатьиПрямыхЗатрат Тогда
			Возврат ?(Падеж = "Родительный", " отдельных статей прямых затрат", " отдельным статьям прямых затрат");
		ИначеЕсли База = Перечисления.БазыРаспределенияКосвенныхРасходов.ПрямыеЗатраты Тогда
			Возврат ?(Падеж = "Родительный", " прямых затрат", " прямых затрат");
			
		ИначеЕсли База = Перечисления.БазыРаспределенияКосвенныхРасходов.НеРаспределяется Тогда	
			Возврат ?(Падеж = "Родительный", " расходов, не распределяемых автоматически", " расходам, не распределяемым автоматически");
		КонецЕсли;	 
		
	КонецФункции
	
	Процедура ПолучитьТаблицуДоходов(НачалоПериода, КонецПериода, ТаблицаДоходов, КоэфНеННП, КоэфННП)
		
		СуммаННП   = 0;
		СуммаНеННП = 0;
		КоэфННП   = 0;
		КоэфНеННП = 0;
		
		НалоговыйУчет.КоэффициентРаспределенияРасходовПоВидамДеятельности(Организация, КонецПериода, НачалоПериода, Истина, СуммаННП, СуммаНеННП);
        Выручка       = СуммаННП + СуммаНеННП;
		
		ТаблицаДоходов = Новый ТаблицаЗначений;
		ТаблицаДоходов.Колонки.Добавить("СуммаННП");
		ТаблицаДоходов.Колонки.Добавить("СуммаНеННП");
		СтрокаТаблицаДоходов = ТаблицаДоходов.Добавить();
		СтрокаТаблицаДоходов.СуммаННП = СуммаННП;
		СтрокаТаблицаДоходов.СуммаНеННП = СуммаНеННП;
		Если Не Выручка  = 0 Тогда

		КоэфННП =  Окр(СуммаННП   /  Выручка, 6);
		КоэфНеННП = Окр(СуммаНеННП   /  Выручка, 6);
	КонецЕсли;

		
	КонецПроцедуры
	
	Процедура ПоказатьСумму(Область, ЭтоРаспределяемыеРасходы, Название, Сумма, ЭтоЕНВД, ВидУчета = "")
		
		Если ЭтоРаспределяемыеРасходы Тогда
			
			Если Название = "Сумма" Тогда
				Если ЭтоЕНВД Тогда
					СуммаНеННП = Сумма;
					СуммаННП   = 0;
				Иначе
					СуммаННП   = Сумма;
					СуммаНеННП = 0;
				КонецЕсли;
				
			Иначе
				СуммаНеННП = ОКР(Сумма * КоэфНеННП, 2);
				СуммаННП   = ОКР(Сумма * КоэфННП, 2);
			КонецЕсли;
			
			Если ВидУчета = "" Тогда
				Область.Параметры[Название +   "ННП"] = СуммаННП;
				Область.Параметры[Название + "НеННП"] = СуммаНеННП;
			Иначе
				Область.Параметры[Название +   "ННП"] = ?(СуммаННП = 0,   "", ВидУчета+ ": " + Формат(СуммаННП, "ЧДЦ=2"));
				Область.Параметры[Название + "НеННП"] = ?(СуммаНеННП = 0, "", ВидУчета+ ": " + Формат(СуммаНеННП, "ЧДЦ=2"));
			КонецЕсли;
			
			
		Иначе
			Если ВидУчета = "" Тогда
				Область.Параметры[Название]  = Сумма;
			Иначе
				Область.Параметры[Название]  = ?(Сумма = 0,   "", ВидУчета+ ": " + Формат(Сумма, "ЧДЦ=2"));
			КонецЕсли;
			
		КонецЕсли;
		
		Если Название = "СуммаЗатрат" Тогда
			Если ВидУчета = "" Тогда
				Область.Параметры[Название]  = Сумма;
			Иначе
				Область.Параметры[Название]  = ?(Сумма = 0,   "", ВидУчета+ ": " + Формат(Сумма, "ЧДЦ=2"));
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	Функция ПолучитьКоэффициент(ЭтоРаспределяемыеРасходы, ЭтоЕНВД, Итого, Сумма)
		
		Если ЭтоРаспределяемыеРасходы Тогда
			
			Если ЭтоЕНВД Тогда
				Возврат ?(Итого.БазаНеННП = 0, 0, Сумма / Итого.БазаНеННП)
				
			Иначе
				Возврат ?(Итого.БазаННП = 0, 0, Сумма / Итого.БазаННП)
				
			КонецЕсли;
			
		Иначе
			Возврат ?(Итого.База = 0, 0, Сумма / Итого.База)
		КонецЕсли;
	
КонецФункции
	
	Процедура ПоказатьИтоги(ДокументРезультат,ОбластьИтого,ОбластьИтогоРазниц,ЭтоРаспределяемыеРасходы, Всего)
		
		Если Не Всего.База = 0 Тогда
			ОбластьИтого.Параметры.СуммаБазы   = Всего.База;
		КонецЕсли;
		Если ЭтоРаспределяемыеРасходы Тогда
			ОбластьИтого.Параметры.СуммаННП     = Всего.ННП;
			ОбластьИтого.Параметры.СуммаНеННП   = Всего.НеННП;
		Иначе
			ОбластьИтого.Параметры.Сумма   = Всего.Сумма;
		КонецЕсли;
		ДокументРезультат.Вывести(ОбластьИтого);
		
		Если ВариантОтчета = 3 И Не ОбластьИтогоРазниц = Неопределено Тогда
			Если ЭтоРаспределяемыеРасходы Тогда
				ОбластьИтогоРазниц.Параметры.СуммаННП     = "ПР: " + Всего.ННППР;
				ОбластьИтогоРазниц.Параметры.СуммаНеННП   = "ПР: " + Всего.НеННППР;
				Если Не Всего.ННППР = 0 Или Не Всего.НеННППР = 0 Тогда
					ДокументРезультат.Вывести(ОбластьИтогоРазниц);
				КонецЕсли;
				ОбластьИтогоРазниц.Параметры.СуммаННП     = "ВР: " + Всего.ННПВР;
				ОбластьИтогоРазниц.Параметры.СуммаНеННП   = "ВР: " + Всего.НеННПВР;
				Если Не Всего.ННПВР = 0 Или Не Всего.НеННПВР = 0 Тогда
					ДокументРезультат.Вывести(ОбластьИтогоРазниц);
				КонецЕсли;
			Иначе
				ОбластьИтогоРазниц.Параметры.Сумма   = "ПР: " + Всего.СуммаПР;
				Если Не Всего.СуммаПР = 0 Тогда
					ДокументРезультат.Вывести(ОбластьИтогоРазниц);
				КонецЕсли;
				ОбластьИтогоРазниц.Параметры.Сумма   = "ВР: " + Всего.СуммаВР;
				Если Не Всего.СуммаВР = 0 Тогда
					ДокументРезультат.Вывести(ОбластьИтогоРазниц);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецПроцедуры
	
	Процедура РасчетВсего(Итого, Всего)
		Всего.Сумма   = Всего.Сумма   + Итого.Сумма;
		Всего.ННП     = Всего.ННП     + Итого.ННП;
		Всего.НеННП   = Всего.НеННП   + Итого.НеННП;
		Всего.СуммаПР = Всего.СуммаПР + Итого.СуммаПР;
		Всего.ННППР   = Всего.ННППР   + Итого.ННППР;
		Всего.НеННППР = Всего.НеННППр + Итого.НеННППР;
		Всего.СуммаВР = Всего.СуммаВР + Итого.СуммаВР;
		Всего.ННПВР   = Всего.ННПВР   + Итого.ННПВР;
		Всего.НеННПВР = Всего.НеННПВР + Итого.НеННПВР;
		
		ИтоговаяСуммаННП   = ИтоговаяСуммаННП + Итого.ННП;
		ИтоговаяСуммаНеННП = ИтоговаяСуммаНеННП + Итого.НеННП;
	КонецПроцедуры
	
	Функция ПостроитьНазвание(НалоговыйРежим)
		
		Если НалоговыйРежим = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьНеОблагаемаяЕНВД Тогда
			Возврат "Расходы " + НалоговыйУчет.ПолучитьТекстВидаДеятельности(ПрименяетсяЕНВД, Истина, Ложь);
		ИначеЕсли НалоговыйРежим = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПредпринимательскаяДеятельностьОблагаемаяЕНВД Тогда
			Возврат "Расходы " + НалоговыйУчет.ПолучитьТекстВидаДеятельности(ПрименяетсяЕНВД, Истина, Истина);
		Иначе
			Возврат "Расходы, распределяемые по всем видам деятельности";
		КонецЕсли	
		
	КонецФункции
	
    Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	СтарыеНастройки = УправлениеОтчетами.ПолучитьКопиюОтбораВТЗ(ПостроительОтчета.Отбор);
	
	СоотвСубконто = Новый Соответствие;
	
	Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	        |	ХозрасчетныйОбороты.СуммаОборотКт КАК СуммаОборотКт
	        |{ВЫБРАТЬ
	        |	ХозрасчетныйОбороты.Субконто1 КАК Субконто1}
	        |ИЗ
	        |	РегистрБухгалтерии.Хозрасчетный.Обороты(, , Месяц, Счет В ИЕРАРХИИ (&Счет), , {Субконто1.*}, , ) КАК ХозрасчетныйОбороты
	        |ИТОГИ
	        |	СУММА(СуммаОборотКт)
	        |ПО
	        |	ОБЩИЕ
	        |{ИТОГИ ПО
	        |	ХозрасчетныйОбороты.Субконто1.*}";

Массив = Новый Массив;
Массив.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство);
Массив.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства);

ПостроительОтчета.Параметры.Вставить("Счет", Массив);
ПостроительОтчета.Параметры.Вставить("ПустаяОрганизация", Справочники.Организации.ПустаяСсылка());
	
ПостроительОтчета.Текст = Текст;

СоотвСубконто = Новый Соответствие;
СоотвСубконто.Вставить("Субконто1",    ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);

		
	Сч = 0;
	Для каждого Элемент Из СоотвСубконто Цикл
		Сч = Сч+1;
		Поле = ПостроительОтчета.ДоступныеПоля.Найти(Элемент.Ключ);
		Поле.ТипЗначения = Элемент.Значение.ТипЗначения;
		Поле.Представление = Элемент.Значение.Наименование;
		
		ПостроительОтчета.Отбор.Добавить(Элемент.Ключ);
	КонецЦикла;

УправлениеОтчетами.УстановитьОтборИзТаблицы(ПостроительОтчета.Отбор, СтарыеНастройки);

КонецПроцедуры
	////////////////////////////////////////////////////////////////////////////////
	// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
	// 
	
	НП = Новый НастройкаПериода;
	
#КонецЕсли
