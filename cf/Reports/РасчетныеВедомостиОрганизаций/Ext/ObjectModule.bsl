#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ 
// 


Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок = Ложь, ТолькоЗаголовок = Ложь) Экспорт
	
	ДокументРезультат.Очистить();
	
	Макет = ПолучитьМакет("Макет");
	ЗаголовокОтчета = Макет.ПолучитьОбласть("Заголовок");
	ЗаголовокОтчета.Параметры.Период = "Период: " + ПредставлениеПериода(НачалоМесяца(Период), КонецМесяца(Период), "ФП = Истина");
	Если ПостроительОтчета.Отбор.Организация.Использование Тогда
		СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(ПостроительОтчета.Отбор.Организация.Значение);
		ЗаголовокОтчета.Параметры.Организация = "Организация: " + ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
	КонецЕсли;
	
	Если ТолькоЗаголовок Тогда
		ДокументРезультат.Вывести(ЗаголовокОтчета);
		Возврат;
	КонецЕсли;
	
	ПостроительОтчета.МакетЗаголовкаОтчета = ЗаголовокОтчета;
                                                                
	МассивКодыМатвыгоды = Новый Массив;
	МассивКодыМатвыгоды.Добавить(Справочники.ДоходыНДФЛ.Код2610ПоСтавке13);
	МассивКодыМатвыгоды.Добавить(Справочники.ДоходыНДФЛ.Код2610ПоСтавке35);
	
	ПостроительОтчета.Параметры.Вставить("КодыМатвыгоды",				МассивКодыМатвыгоды);
	ПостроительОтчета.Параметры.Вставить("ВидСтроки",					Перечисления.НДФЛРасчетыСБюджетомВидСтроки.Начисление);
	ПостроительОтчета.Параметры.Вставить("Приход",						ВидДвиженияНакопления.Приход);
	ПостроительОтчета.Параметры.Вставить("Расход",						ВидДвиженияНакопления.Расход);
	ПостроительОтчета.Параметры.Вставить("ЧерезКассу",					Перечисления.СпособыВыплатыЗарплаты.ЧерезКассу);
	ПостроительОтчета.Параметры.Вставить("ЧерезБанк",					Перечисления.СпособыВыплатыЗарплаты.ЧерезБанк);
	ПостроительОтчета.Параметры.Вставить("Прием",						Перечисления.ПричиныИзмененияСостояния.ПриемНаРаботу);
	ПостроительОтчета.Параметры.Вставить("Период",						НачалоМесяца(Период));
	ПостроительОтчета.Параметры.Вставить("СледующийПериод",				КонецМесяца(Период) + 1);
	ПостроительОтчета.Параметры.Вставить("ПустаяОрганизация",			Справочники.Организации.ПустаяСсылка());
	ПостроительОтчета.Параметры.Вставить("ВнутреннееСовместительство",	Перечисления.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство);
	ПостроительОтчета.Выполнить();
	ПостроительОтчета.ОформитьМакет();
	ПостроительОтчета.Вывести(ДокументРезультат);
	
	Ширина = ДокументРезультат.ШиринаТаблицы;
	ВысотаЗаголовка = 4;
	
	// удалим из документа итог по строкам - последняя колонка отчета
	Область = ДокументРезультат.Область(, Ширина, ВысотаЗаголовка + 1, Ширина);
	ДокументРезультат.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоГоризонтали);

	Если ДокументРезультат.Область(ВысотаЗаголовка + 1, Ширина - 2, ВысотаЗаголовка + 1, Ширина - 2).Текст = "Я" Тогда
		// удалим из документа колонку "Я", соответствующую виртуальному виду расчета "Конечное сальдо" 
		Область = ДокументРезультат.Область(, Ширина - 1, ВысотаЗаголовка + 1, Ширина - 1);
		Область.Разгруппировать();
		Область = ДокументРезультат.Область(, Ширина - 2, ВысотаЗаголовка + 1, Ширина - 2);
		ДокументРезультат.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	
	Если ДокументРезультат.Область(ВысотаЗаголовка + 1, 3, ВысотаЗаголовка + 1, 3).Текст = "А" Тогда
		// удалим из документа колонку "А", соответствующую виртуальному виду расчета "Начальное сальдо" 
		Область = ДокументРезультат.Область(, 4, ВысотаЗаголовка + 1, 4);
		Область.Разгруппировать();
		Область = ДокументРезультат.Область(, 3, ВысотаЗаголовка + 1, 3);
		Область.Разгруппировать();
		ДокументРезультат.УдалитьОбласть(Область, ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли; 
		ДокументРезультат.ФиксацияСверху = ВысотаЗаголовка + 1;
	
КонецПроцедуры

Процедура ЗаполнитьНачальныеНастройки() Экспорт
	
	ПостроительОтчета.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Данные.Организация,
	|	Данные.Организация.Наименование КАК ОрганизацияПредставление,
	|	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Данные.Физлицо,
	|	Данные.Физлицо.Наименование КАК ФизлицоПредставление,
	|	Данные.Группа,
	|	Данные.ПриоритетГруппы КАК ПриоритетГруппы,
	|	Данные.ИмяВидаРасчета,
	|	Данные.ПриоритетВидаРасчета КАК ПриоритетВидаРасчета,
	|	СУММА(Данные.Результат) КАК Результат
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОсновныеНачисленияРаботниковОрганизации.Ссылка.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА ОсновныеНачисленияРаботниковОрганизации.Ссылка.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|				ТОГДА ОсновныеНачисленияРаботниковОрганизации.Ссылка.Организация
	|			ИНАЧЕ ОсновныеНачисленияРаботниковОрганизации.Ссылка.Организация.ГоловнаяОрганизация
	|		КОНЕЦ КАК ГоловнаяОрганизация,
	|		ОсновныеНачисленияРаботниковОрганизации.Сотрудник.Физлицо КАК Физлицо,
	|		ОсновныеНачисленияРаботниковОрганизации.ВидРасчета.Наименование КАК ИмяВидаРасчета,
	|		""Начислено"" КАК ПриоритетВидаРасчета,
	|		""Всего начислено"" КАК Группа,
	|		2 КАК ПриоритетГруппы,
	|		ОсновныеНачисленияРаботниковОрганизации.Результат КАК Результат
	|	ИЗ
	|		Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК ОсновныеНачисленияРаботниковОрганизации
	|	ГДЕ
	|		ОсновныеНачисленияРаботниковОрганизации.Ссылка.ПериодРегистрации = &Период
	|		И ОсновныеНачисленияРаботниковОрганизации.Ссылка.Проведен = ИСТИНА
	|		И ОсновныеНачисленияРаботниковОрганизации.Результат <> 0
	|	{ГДЕ
	|		ОсновныеНачисленияРаботниковОрганизации.Ссылка.Организация КАК Организация}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДФЛСведенияОДоходах.ОбособленноеПодразделение,
	|		НДФЛСведенияОДоходах.Организация,
	|		НДФЛСведенияОДоходах.ФизЛицо,
	|		""Мат. выгода по займам (кредитам)"",
	|		""Матвыгода"",
	|		""Доходы в натуральной форме"",
	|		3,
	|		НДФЛСведенияОДоходах.СуммаДохода
	|	ИЗ
	|		РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|	ГДЕ
	|		НДФЛСведенияОДоходах.Период МЕЖДУ &Период И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|		И НДФЛСведенияОДоходах.КодДохода В(&КодыМатвыгоды)
	|		И НДФЛСведенияОДоходах.ИсчисленоИзЗарплаты
	|	{ГДЕ
	|		НДФЛСведенияОДоходах.ОбособленноеПодразделение КАК Организация}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДФЛРасчетыСБюджетом.ОбособленноеПодразделение,
	|		НДФЛРасчетыСБюджетом.Организация,
	|		НДФЛРасчетыСБюджетом.ФизЛицо,
	|		""НДФЛ"",
	|		""НДФЛ"",
	|		""Всего удержано"",
	|		4,
	|		НДФЛРасчетыСБюджетом.Налог
	|	ИЗ
	|		РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|	ГДЕ
	|		НДФЛРасчетыСБюджетом.Период МЕЖДУ &Период И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|		И НДФЛРасчетыСБюджетом.ВидДвижения = &Приход
	|		И НДФЛРасчетыСБюджетом.ВидСтроки = &ВидСтроки
	|		И НДФЛРасчетыСБюджетом.ИсчисленоИзЗарплаты
	|	{ГДЕ
	|		НДФЛРасчетыСБюджетом.ОбособленноеПодразделение КАК Организация}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Взаиморасчеты.Организация,
	|		ВЫБОР
	|			КОГДА Взаиморасчеты.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|				ТОГДА Взаиморасчеты.Организация
	|			ИНАЧЕ Взаиморасчеты.Организация.ГоловнаяОрганизация
	|		КОНЕЦ,
	|		Взаиморасчеты.Физлицо,
	|		""Через кассу "",
	|		"""",
	|		""Всего выплачено"",
	|		5,
	|		Взаиморасчеты.СуммаВзаиморасчетов
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыСРаботникамиОрганизаций КАК Взаиморасчеты
	|	ГДЕ
	|		Взаиморасчеты.Период МЕЖДУ &Период И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|		И Взаиморасчеты.ВидДвижения = &Расход
	|		И Взаиморасчеты.СуммаВзаиморасчетов <> 0
	|		И Взаиморасчеты.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
	|	{ГДЕ
	|		Взаиморасчеты.Организация}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Взаиморасчеты.Организация,
	|		ВЫБОР
	|			КОГДА Взаиморасчеты.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|				ТОГДА Взаиморасчеты.Организация
	|			ИНАЧЕ Взаиморасчеты.Организация.ГоловнаяОрганизация
	|		КОНЕЦ,
	|		Взаиморасчеты.Физлицо,
	|		""Задепонировано "",
	|		"""",
	|		""Всего выплачено"",
	|		5,
	|		Взаиморасчеты.СуммаВзаиморасчетов
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыСРаботникамиОрганизаций КАК Взаиморасчеты
	|	ГДЕ
	|		Взаиморасчеты.Период МЕЖДУ &Период И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|		И Взаиморасчеты.ВидДвижения = &Расход
	|		И Взаиморасчеты.СуммаВзаиморасчетов <> 0
	|		И Взаиморасчеты.Регистратор ССЫЛКА Документ.ДепонированиеОрганизаций
	|	{ГДЕ
	|		Взаиморасчеты.Организация}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Взаиморасчеты.Организация,
	|		ВЫБОР
	|			КОГДА Взаиморасчеты.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|				ТОГДА Взаиморасчеты.Организация
	|			ИНАЧЕ Взаиморасчеты.Организация.ГоловнаяОрганизация
	|		КОНЕЦ,
	|		Взаиморасчеты.Физлицо,
	|		""Перечислено в банк"",
	|		"""",
	|		""Всего выплачено"",
	|		5,
	|		Взаиморасчеты.СуммаВзаиморасчетов
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыСРаботникамиОрганизаций КАК Взаиморасчеты
	|	ГДЕ
	|		Взаиморасчеты.Период МЕЖДУ &Период И КОНЕЦПЕРИОДА(&Период, МЕСЯЦ)
	|		И Взаиморасчеты.ВидДвижения = &Расход
	|		И Взаиморасчеты.СуммаВзаиморасчетов <> 0
	|		И (Взаиморасчеты.Регистратор ССЫЛКА Документ.СписаниеСРасчетногоСчета)
	|	{ГДЕ
	|		Взаиморасчеты.Организация}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СальдоНачальное.Организация,
	|		ВЫБОР
	|			КОГДА СальдоНачальное.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|				ТОГДА СальдоНачальное.Организация
	|			ИНАЧЕ СальдоНачальное.Организация.ГоловнаяОрганизация
	|		КОНЕЦ,
	|		СальдоНачальное.Физлицо,
	|		""Начальное сальдо"",
	|		NULL,
	|		""А"",
	|		1,
	|		СальдоНачальное.СуммаВзаиморасчетовОстаток
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыСРаботникамиОрганизаций.Остатки(&Период, {(Организация)}) КАК СальдоНачальное
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СальдоНачальное.Организация,
	|		ВЫБОР
	|			КОГДА СальдоНачальное.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|				ТОГДА СальдоНачальное.Организация
	|			ИНАЧЕ СальдоНачальное.Организация.ГоловнаяОрганизация
	|		КОНЕЦ,
	|		СальдоНачальное.ФизЛицо,
	|		""Начальное сальдо"",
	|		NULL,
	|		""А"",
	|		1,
	|		СальдоНачальное.СуммаНДФЛКЗачетуОстаток
	|	ИЗ
	|		РегистрНакопления.НДФЛКЗачету.Остатки(&Период, {(Организация)}) КАК СальдоНачальное
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СальдоКонечное.Организация,
	|		ВЫБОР
	|			КОГДА СальдоКонечное.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|				ТОГДА СальдоКонечное.Организация
	|			ИНАЧЕ СальдоКонечное.Организация.ГоловнаяОрганизация
	|		КОНЕЦ,
	|		СальдоКонечное.Физлицо,
	|		""Конечное сальдо"",
	|		NULL,
	|		""Я"",
	|		6,
	|		СальдоКонечное.СуммаВзаиморасчетовОстаток
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыСРаботникамиОрганизаций.Остатки(&СледующийПериод, {(Организация)}) КАК СальдоКонечное
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СальдоКонечное.Организация,
	|		ВЫБОР
	|			КОГДА СальдоКонечное.Организация.ГоловнаяОрганизация = &ПустаяОрганизация
	|				ТОГДА СальдоКонечное.Организация
	|			ИНАЧЕ СальдоКонечное.Организация.ГоловнаяОрганизация
	|		КОНЕЦ,
	|		СальдоКонечное.ФизЛицо,
	|		""Конечное сальдо"",
	|		NULL,
	|		""Я"",
	|		6,
	|		СальдоКонечное.СуммаНДФЛКЗачетуОстаток
	|	ИЗ
	|		РегистрНакопления.НДФЛКЗачету.Остатки(&СледующийПериод, {(Организация)}) КАК СальдоКонечное) КАК Данные
	|		{ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РаботникиОрганизацийСрезПоследних.Сотрудник КАК Сотрудник,
	|			РаботникиОрганизацийСрезПоследних.Организация КАК Организация,
	|			РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизации КАК ПодразделениеОрганизации
	|		ИЗ
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				РаботникиОрганизаций.Сотрудник КАК Сотрудник
	|			ИЗ
	|				(ВЫБРАТЬ
	|					МАКСИМУМ(РаботникиОрганизаций.Период) КАК Период,
	|					РаботникиОрганизаций.Сотрудник.Физлицо КАК ФизЛицо,
	|					РаботникиОрганизаций.Организация КАК Организация
	|				ИЗ
	|					РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|				ГДЕ
	|					РаботникиОрганизаций.ПричинаИзмененияСостояния = &Прием
	|					И РаботникиОрганизаций.Период < &СледующийПериод
	|					И РаботникиОрганизаций.Сотрудник.ВидЗанятости <> &ВнутреннееСовместительство
	|				
	|				СГРУППИРОВАТЬ ПО
	|					РаботникиОрганизаций.Организация,
	|					РаботникиОрганизаций.Сотрудник.Физлицо) КАК ПоследниеДаты
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизаций
	|					ПО ПоследниеДаты.Период = РаботникиОрганизаций.Период
	|						И (РаботникиОрганизаций.ПричинаИзмененияСостояния = &Прием)
	|						И (РаботникиОрганизаций.Сотрудник.ВидЗанятости <> &ВнутреннееСовместительство)
	|						И ПоследниеДаты.Организация = РаботникиОрганизаций.Организация
	|						И ПоследниеДаты.ФизЛицо = РаботникиОрганизаций.Сотрудник.Физлицо) КАК АктуальныеПриказы
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(&СледующийПериод, Сотрудник.ВидЗанятости <> &ВнутреннееСовместительство) КАК РаботникиОрганизацийСрезПоследних
	|				ПО АктуальныеПриказы.Сотрудник = РаботникиОрганизацийСрезПоследних.Сотрудник
	|		{ГДЕ
	|			РаботникиОрганизацийСрезПоследних.ОбособленноеПодразделение КАК Организация}) КАК РаботникиОрганизацииСрезПоследних
	|		ПО Данные.Физлицо = РаботникиОрганизацииСрезПоследних.Сотрудник.Физлицо
	|			И Данные.ГоловнаяОрганизация = РаботникиОрганизацииСрезПоследних.Организация}
	|{ГДЕ
	|	Данные.Организация,
	|	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации КАК ПодразделениеОрганизации}
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Организация,
	|	Данные.Физлицо,
	|	Данные.Группа,
	|	Данные.ПриоритетГруппы,
	|	Данные.ИмяВидаРасчета,
	|	Данные.ПриоритетВидаРасчета,
	|	Данные.Организация.Наименование,
	|	Данные.Физлицо.Наименование,
	|	РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизлицоПредставление,
	|	ПриоритетГруппы,
	|	ПриоритетВидаРасчета
	|ИТОГИ
	|	МАКСИМУМ(ПриоритетГруппы),
	|	МАКСИМУМ(ПриоритетВидаРасчета),
	|	СУММА(Результат)
	|ПО
	|	ОБЩИЕ
	|{ИТОГИ ПО
	|	Организация,
	|	ПодразделениеОрганизации,
	|	Физлицо,
	|	Группа,
	|	ИмяВидаРасчета}";
	
	СтруктураПредставлениеПолей = Новый Структура("ФизЛицоНаименование,Физлицо,ИмяВидаРасчета,Организация,ПодразделениеОрганизации",
	"Работник",
	"Работник",
	"Вид расчета",
	"Организация",
	"Подразделение");
	
	УправлениеОтчетами.ЗаполнитьПредставленияПолей(СтруктураПредставлениеПолей, ПостроительОтчета);
	
	ПостроительОтчета.Отбор.Добавить("Организация");
	ПостроительОтчета.Отбор.Добавить("ПодразделениеОрганизации");
	ПостроительОтчета.ВыводитьОбщиеИтоги = Истина;
	
	ПостроительОтчета.МакетОформления = ПолучитьМакетОформления(СтандартноеОформление.Интерфейс);
	ПостроительОтчета.Макет = ПолучитьМакет("Макет");
	
	ПостроительОтчета.ИзмеренияСтроки.Добавить("Организация");
	ПостроительОтчета.ИзмеренияСтроки.Добавить("ПодразделениеОрганизации");
	ПостроительОтчета.ИзмеренияСтроки.Добавить("Физлицо");
	ПостроительОтчета.ИзмеренияКолонки.Добавить("Группа");
	ПостроительОтчета.ИзмеренияКолонки.Добавить("ИмяВидаРасчета");
	
	ПостроительОтчета.РазмещениеИзмеренийВКолонках = ТипРазмещенияИзмерений.Вместе;
	ПостроительОтчета.РазмещениеИзмеренийВСтроках = ТипРазмещенияИзмерений.Вместе;
	
	ПустаяСтруктура = Новый Структура;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

#КонецЕсли