Перем ВысотаЗаголовка;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Обновляет таблицу отчета
//
// Параметры:
//	Нет.
//
Процедура ОбновитьОтчет() Экспорт
	
	СформироватьОтчет(ЭлементыФормы.ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка);

	ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.ДокументРезультат;
	
	СтандартныеОтчеты.УправлениеПометкамиКнопокЗаголовковКоманднойПанели(ЭтаФорма, ПоказыватьЗаголовок);
	
КонецПроцедуры // ОбновитьОтчет()

//  Управляет выводом заголовка
//
// Параметры:
//	Нет.
//
Процедура ВыводЗаголовка()

	СтандартныеОтчеты.ОбработатьВыводЗаголовка(ЭтаФорма, ЭтотОбъект, ВысотаЗаголовка, ПоказыватьЗаголовок);

КонецПроцедуры // ВыводЗаголовка()

// Формирует заголовок формы
//
// Параметры:
//	Нет.
//
Процедура СформироватьЗаголовокФормы()

	ОписаниеПериода = СтандартныеОтчеты.СформироватьСтрокуОграниченийПоДатамДляФормы(ДатаНач, ДатаКон);

	Заголовок = ЗаголовокОтчета()+" (" + ОписаниеПериода + ") " + 
	?(ЗначениеЗаполнено(Организация.НаименованиеПолное), Организация.НаименованиеПолное, Организация);

КонецПроцедуры // СформироватьЗаголовокФормы()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура КнопкаНастройкаПериодаНажатие(Элемент)

	РаботаСДиалогами.ОбработчикНастройкаПериодаНажатие(ДатаНач, ДатаКон);

КонецПроцедуры // КнопкаНастройкаПериодаНажатие()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ НАЖАТИЯ КНОПОК КОМАНДНОЙ ПАНЕЛИ

Процедура КоманднаяПанельФормыСформировать(Кнопка)

	ОбновитьОтчет();

КонецПроцедуры // КоманднаяПанельФормыСформировать()

Процедура КоманднаяПанельЗаголовок(Кнопка)

	ПоказыватьЗаголовок = Не ПоказыватьЗаголовок;
	ВыводЗаголовка();

КонецПроцедуры // КоманднаяПанельЗаголовок()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриЗакрытии()

	СтруктураОбщихПараметров = Новый Структура;
	СтруктураОбщихПараметров.Вставить("Организация", Организация);
	СтруктураОбщихПараметров.Вставить("ДатаНач", ДатаНач);
	СтруктураОбщихПараметров.Вставить("ДатаКон", ДатаКон);
	СтруктураОбщихПараметров.Вставить("ПоЗабалансовымСчетам", ПоЗабалансовымСчетам);
	СтруктураОбщихПараметров.Вставить("ПоВалютам", ПоВалютам);
	СтруктураОбщихПараметров.Вставить("ПоСубсчетам", ПоСубсчетам);
	СтруктураОбщихПараметров.Вставить("ПоказыватьЗаголовок", ПоказыватьЗаголовок);

	СохранитьЗначение("ОбщиеПараметрыОтчетов"+ИмяРегистраБухгалтерии, СтруктураОбщихПараметров);

КонецПроцедуры // ПриЗакрытии()

Процедура ОбновлениеОтображения()

	СформироватьЗаголовокФормы();
	
	УправлениеДоступностьюФлажкаВключаяОбособленныеПодразделения(ЭтотОбъект, ЭтаФорма);	

КонецПроцедуры // ОбновлениеОтображения()

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	ТиповыеОтчеты.НазначитьФормеУникальныйКлючИдентификации(ЭтаФорма);

	СтруктураОбщихПараметров = ВосстановитьЗначение("ОбщиеПараметрыОтчетов"+ИмяРегистраБухгалтерии);
	Если ТипЗнч(СтруктураОбщихПараметров) = Тип("Структура") Тогда
		
		СтруктураОбщихПараметров.Свойство("Организация", Организация);
		СтруктураОбщихПараметров.Свойство("ВключаяОбособленныеПодразделения", ВключаяОбособленныеПодразделения);
		СтруктураОбщихПараметров.Свойство("ПоСубсчетам",    ПоСубсчетам);
		СтруктураОбщихПараметров.Свойство("ПоЗабалансовымСчетам", ПоЗабалансовымСчетам);
		СтруктураОбщихПараметров.Свойство("ПоВалютам",   ПоВалютам);
		СтруктураОбщихПараметров.Свойство("ДатаНач", ДатаНач);
		СтруктураОбщихПараметров.Свойство("ДатаКон", ДатаКон);
		
	КонецЕсли;

	ПоказыватьЗаголовок = Истина;
	
	ВысотаЗаголовка = 0;
	
	Организация = глЗначениеПеременной("ОсновнаяОрганизация");
	
	ЭлементыФормы.ВключаяОбособленныеПодразделения.Видимость = СтандартныеОтчеты.ДоступностьУчетаПоПодразделениям();
	
КонецПроцедуры // ПередОткрытием()

Процедура ПриОткрытии()
	
	ОбновитьОтчет();
	СформироватьЗаголовокФормы();    

КонецПроцедуры // ПриОткрытии()

Процедура ДокументРезультатОбработкаРасшифровки(ЭлементУправления, Расшифровка, СтандартнаяОбработка)
	Перем СчетДт, СчетКт, ВалютаДт, ВалютаКт;

	Если ТипЗнч(Расшифровка) =  Тип("Структура") Тогда
		
		СчетДт = Неопределено;
		ВалютаДт = Неопределено;
		
		Если Не Расшифровка.Свойство("СчетДт", СчетДт) Тогда
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
		
		Если Не Расшифровка.Свойство("ВалютаДт", ВалютаДт) Тогда
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
		
	Иначе
		Если ЭлементУправления.ТекущаяОбласть.Верх = ВысотаЗаголовка + 2 Тогда
			СчетДт = Неопределено;
		Иначе
			СчетДт = Расшифровка;
		КонецЕсли;
	КонецЕсли;
	
	// Расшифровка колонки находится в заголовке колонки
	РасшифровкаКолонки = ЭлементУправления.Область(ВысотаЗаголовка + 2, ЭлементУправления.ТекущаяОбласть.Лево).Расшифровка;
	
	Если ТипЗнч(РасшифровкаКолонки) =  Тип("Структура") Тогда
		СчетКт = РасшифровкаКолонки.СчетКт;
		ВалютаКт = РасшифровкаКолонки.ВалютаКт;
	Иначе
		СчетКт = РасшифровкаКолонки;
	КонецЕсли;
	
	ВыбраннаяРасшифровка = Новый Структура("СчетДт, СчетКт, ВалютаДт, ВалютаКт", СчетДт, СчетКт, ВалютаДт, ВалютаКт);
	
	// Добавим общую расшифровку из левого верхнего угла
	ОбщаяРасшифровка = ЭлементУправления.Область(1,1).Расшифровка;
	
	Если ТипЗнч(ОбщаяРасшифровка) = Тип("Структура") 
		ИЛИ ТипЗнч(ОбщаяРасшифровка) = Тип("Соответствие") Тогда
		
		Для Каждого Элемент Из ОбщаяРасшифровка Цикл
			ВыбраннаяРасшифровка.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;
	КонецЕсли;

	ОбработкаРасшифровкиСтандартногоОтчета(ВыбраннаяРасшифровка);
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры // ДокументРезультатОбработкаРасшифровки()

Процедура ПередСохранениемЗначений(Отказ)

	СохраняемыеРеквизиты = Новый Структура;

	СохраняемыеРеквизиты.Вставить("Организация", Организация);
	СохраняемыеРеквизиты.Вставить("ВключаяОбособленныеПодразделения", ВключаяОбособленныеПодразделения);
	СохраняемыеРеквизиты.Вставить("ПоВалютам", ПоВалютам);
	СохраняемыеРеквизиты.Вставить("ПоЗабалансовымСчетам", ПоЗабалансовымСчетам);
	СохраняемыеРеквизиты.Вставить("ПоСубсчетам", ПоСубсчетам);

КонецПроцедуры // ПередСохранениемЗначений()

Процедура ПослеВосстановленияЗначений()

	СохраняемыеРеквизиты.Свойство("Организация", Организация);
	СохраняемыеРеквизиты.Свойство("ВключаяОбособленныеПодразделения", ВключаяОбособленныеПодразделения);
	СохраняемыеРеквизиты.Свойство("ПоВалютам", ПоВалютам);
	СохраняемыеРеквизиты.Свойство("ПоЗабалансовымСчетам", ПоЗабалансовымСчетам);
	СохраняемыеРеквизиты.Свойство("ПоСубсчетам", ПоСубсчетам);

КонецПроцедуры // ПослеВосстановленияЗначений()

Процедура УправлениеДоступностьюФлажкаВключаяОбособленныеПодразделения(ОтчетОбъект, ФормаОтчета) 
	
	Организация = ОтчетОбъект.Организация;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Если ЗначениеЗаполнено(Организация.ГоловнаяОрганизация) Тогда
			ФормаОтчета.ЭлементыФормы.ВключаяОбособленныеПодразделения.Доступность = Ложь;
			ОтчетОбъект.ВключаяОбособленныеПодразделения = Ложь;
		Иначе
			ФормаОтчета.ЭлементыФормы.ВключаяОбособленныеПодразделения.Доступность = Истина;	
		КонецЕсли;
	Иначе
		ФормаОтчета.ЭлементыФормы.ВключаяОбособленныеПодразделения.Доступность = Ложь;
		ОтчетОбъект.ВключаяОбособленныеПодразделения = Ложь;
	КонецЕсли;
	
КонецПроцедуры
