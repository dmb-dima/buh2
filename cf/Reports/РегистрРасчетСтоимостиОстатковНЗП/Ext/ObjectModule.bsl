#Если Клиент Тогда
	
Перем НП Экспорт;

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	НачалоПериода = НачалоДня(ДатаНач);
	КонецПериода  = КонецДня(ДатаКон);

	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");

	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(НачалоПериода, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(КонецПериода, "ДФ=dd.MM.yyyy");
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация);
	НазваниеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = НазваниеОрганизации;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	Если НоменклатурнаяГруппа <> Справочники.НоменклатурныеГруппы.ПустаяСсылка() Тогда
		ОбластьЗаголовок.Параметры.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
	Иначе
		ОбластьЗаголовок.Параметры.НоменклатурнаяГруппа = "По всем";
	КонецЕсли;
	
	Если Подразделение <> Справочники.ПодразделенияОрганизаций.ПустаяСсылка() Тогда
		ОбластьЗаголовок.Параметры.Подразделение = Подразделение;
	Иначе
		ОбластьЗаголовок.Параметры.Подразделение = "По всем";
		
	КонецЕсли;

	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	// Проверим заполнение обязательных реквизитов
	Если НалоговыйУчет.ПроверитьЗаполнениеОбязательныхРеквизитов(ДатаНач,ДатаКон,Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;
	
	СписокСчетов = Новый Массив;
		СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.ОсновноеПроизводство);
		СписокСчетов.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства);
		
	СписокСчетов90 = Новый Массив;
		СписокСчетов90.Добавить(ПланыСчетов.Хозрасчетный.Продажи_РасходыНаПродажуНеЕНВД);
		СписокСчетов90.Добавить(ПланыСчетов.Хозрасчетный.Продажи_УправленческиеРасходыНеЕНВД);
	

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач",     Новый Граница(НачалоДня(ДатаНач), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаКон",     Новый Граница(КонецДня(ДатаКон),  ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Амортизация", Перечисления.ВидыРасходовНУ.Амортизация);
	Запрос.УстановитьПараметр("ОплатаТруда", Перечисления.ВидыРасходовНУ.ОплатаТруда);
	Запрос.УстановитьПараметр("МатериальныеРасходы", Перечисления.ВидыРасходовНУ.МатериальныеРасходы);
	Запрос.УстановитьПараметр("НалогиИСборы", Перечисления.ВидыРасходовНУ.НалогиИСборы);
	Запрос.УстановитьПараметр("ЕСН", Перечисления.ВидыРасходовНУ.ЕСН);
	Запрос.УстановитьПараметр("СтраховыеВзносы", Перечисления.ВидыРасходовНУ.СтраховыеВзносы);
    Запрос.УстановитьПараметр("Счет",  СписокСчетов);
    Запрос.УстановитьПараметр("Счет90", СписокСчетов90);
	Запрос.УстановитьПараметр("ВидСубконто",ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВыбПодразделение",Подразделение);
	Запрос.УстановитьПараметр("ВыбНоменклатурнаяГруппа",НоменклатурнаяГруппа);

	
	Запрос.Текст = 

"ВЫБРАТЬ
|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто1 КАК НоменклатурнаяГруппа,
|	СУММА(ВЫБОР
|			КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ <> &Амортизация
|					И НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ <> &НалогиИСборы
|					И НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ <> &ЕСН
|					И НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ <> &МатериальныеРасходы
|					И НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ <> &ОплатаТруда
|				ТОГДА ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНУОборотДт, 0)
|		КОНЕЦ) КАК ДругиеРасходы,
|	СУММА(ВЫБОР
|			КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ = &МатериальныеРасходы
|				ТОГДА ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНУОборотДт, 0)
|			ИНАЧЕ 0
|		КОНЕЦ) КАК МатериальныеРасходы,
|	СУММА(ВЫБОР
|			КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ = &Амортизация
|				ТОГДА ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНУОборотДт, 0)
|			ИНАЧЕ 0
|		КОНЕЦ) КАК Амортизация,
|	СУММА(ВЫБОР
|			КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ = &ОплатаТруда
|				ТОГДА ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНУОборотДт, 0)
|			ИНАЧЕ 0
|		КОНЕЦ) КАК ОплатаТруда,
|	СУММА(ВЫБОР
|			КОГДА НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ = &НалогиИСборы
|					ИЛИ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ = &ЕСН
|					ИЛИ НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ = &СтраховыеВзносы
|				ТОГДА ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНУОборотДт, 0)
|			ИНАЧЕ 0
|		КОНЕЦ) КАК ЕСН,
|	СУММА(ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНУОборотДт, 0)) КАК СуммаПрямыхРасходовЗаМесяц,
|	СУММА(ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНУНачальныйОстаток, 0)) КАК СтоимостьНачальныйОстаток,
|	СУММА(ЕСТЬNULL(НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.СуммаНУКонечныйОстаток, 0)) КАК СтоимостьКонечныйОстаток,
|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Период КАК Период
|ИЗ
|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&ДатаНач, &ДатаКон, Месяц, ДвиженияИГраницыПериода, Счет В (&Счет), , Организация = &Организация) КАК НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты
|
|СГРУППИРОВАТЬ ПО
|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто2.ВидРасходовНУ,
|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Субконто1,
|	НезавершенноеПроизводствоНалоговыйУчетОстаткиИОбороты.Период
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	КосвенныеРасходы.СубконтоКт1,
|	СУММА(ВЫБОР
|			КОГДА КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ <> &Амортизация
|					И КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ <> &НалогиИСборы
|					И КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ <> &ЕСН
|					И КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ <> &МатериальныеРасходы
|					И КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ <> &ОплатаТруда
|				ТОГДА -ЕСТЬNULL(КосвенныеРасходы.СуммаНУОборотКт, 0)
|		КОНЕЦ),
|	СУММА(ВЫБОР
|			КОГДА КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ = &МатериальныеРасходы
|				ТОГДА -ЕСТЬNULL(КосвенныеРасходы.СуммаНУОборотКт, 0)
|			ИНАЧЕ 0
|		КОНЕЦ),
|	СУММА(ВЫБОР
|			КОГДА КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ = &Амортизация
|				ТОГДА -ЕСТЬNULL(КосвенныеРасходы.СуммаНУОборотКт, 0)
|			ИНАЧЕ 0
|		КОНЕЦ),
|	СУММА(ВЫБОР
|			КОГДА КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ = &ОплатаТруда
|				ТОГДА -ЕСТЬNULL(КосвенныеРасходы.СуммаНУОборотКт, 0)
|			ИНАЧЕ 0
|		КОНЕЦ),
|	СУММА(ВЫБОР
|			КОГДА КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ = &НалогиИСборы
|					ИЛИ КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ = &ЕСН
|					ИЛИ КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ = &СтраховыеВзносы
|				ТОГДА -ЕСТЬNULL(КосвенныеРасходы.СуммаНУОборотКт, 0)
|			ИНАЧЕ 0
|		КОНЕЦ),
|	0,
|	0,
|	0,
|	КосвенныеРасходы.Период
|ИЗ
|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&ДатаНач, &ДатаКон, Месяц, СчетДт В (&Счет90), , СчетКт В (&Счет), , Организация = &Организация) КАК КосвенныеРасходы
|
|СГРУППИРОВАТЬ ПО
|	КосвенныеРасходы.СубконтоКт2.ВидРасходовНУ,
|	КосвенныеРасходы.СубконтоКт1,
|	КосвенныеРасходы.Период
|
|УПОРЯДОЧИТЬ ПО
|	Период,
|	НоменклатурнаяГруппа
|ИТОГИ
|	СУММА(ДругиеРасходы),
|	СУММА(МатериальныеРасходы),
|	СУММА(Амортизация),
|	СУММА(ОплатаТруда),
|	СУММА(ЕСН),
|	СУММА(СуммаПрямыхРасходовЗаМесяц),
|	СУММА(СтоимостьНачальныйОстаток),
|	СУММА(СтоимостьКонечныйОстаток)
|ПО
|	Период,
|	НоменклатурнаяГруппа";

	Если Подразделение <> Справочники.ПодразделенияОрганизаций.ПустаяСсылка() И НоменклатурнаяГруппа <> Справочники.НоменклатурныеГруппы.ПустаяСсылка() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация) КАК Незавершенное", "Организация = &Организация И
		|	Подразделение В(&ВыбПодразделение) И
		|	Субконто1 В (&ВыбНоменклатурнаяГруппа)) КАК Незавершенное");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация) КАК КосвенныеРасходы", "Организация = &Организация И
		|	ПодразделениеКт В(&ВыбПодразделение) И
		|	СубконтоКт1 В (&ВыбНоменклатурнаяГруппа)) КАК КосвенныеРасходы");
		
	ИначеЕсли НоменклатурнаяГруппа <> Справочники.НоменклатурныеГруппы.ПустаяСсылка() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация) КАК Незавершенное", "Организация = &Организация И
		|	Субконто1 В (&ВыбНоменклатурнаяГруппа)) КАК Незавершенное");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация) КАК КосвенныеРасходы", "Организация = &Организация И
		|	СубконтоКт1 В (&ВыбНоменклатурнаяГруппа)) КАК КосвенныеРасходы");
		
	ИначеЕсли Подразделение <> Справочники.ПодразделенияОрганизаций.ПустаяСсылка() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация) КАК Незавершенное", "Организация = &Организация И
		|	Подразделение В (&ВыбПодразделение)) КАК Незавершенное");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация) КАК КосвенныеРасходы", "Организация = &Организация И
		|	ПодразделениеКт В (&ВыбПодразделение)) КАК КосвенныеРасходы");
		
	КонецЕсли;
	
	Результат = Запрос.Выполнить();

	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого         = Макет.ПолучитьОбласть("Итого");
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");

	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);

	ВыборкаПериодов = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаПериодов.Следующий() Цикл

		МесяцГод = Формат(ВыборкаПериодов.Период,"ДФ=MMMM.yyyy");
		МесяцГод = СтрЗаменить(МесяцГод,"."," ");

		СуммаПрямыхРасходовЗаМесяц = ?(ВыборкаПериодов.СуммаПрямыхРасходовЗаМесяц = NULL, 0, ВыборкаПериодов.СуммаПрямыхРасходовЗаМесяц);

		ОбластьИтого.Параметры.МесяцГод                          = МесяцГод;
		ОбластьИтого.Параметры.СуммаОстатокНЗПНаНачалоМесяца     = ВыборкаПериодов.СтоимостьНачальныйОстаток;
		ОбластьИтого.Параметры.СуммаМатериальныхРасходовЗаМесяц  = ВыборкаПериодов.МатериальныеРасходы;
		ОбластьИтого.Параметры.СуммаДругихРасходовЗаМесяц        = ВыборкаПериодов.ДругиеРасходы;
		ОбластьИтого.Параметры.СуммаРасходовНаОплатуТрудаЗаМесяц = ВыборкаПериодов.ОплатаТруда;
		ОбластьИтого.Параметры.СуммаЕСНЗаМесяц                   = ВыборкаПериодов.ЕСН;
		ОбластьИтого.Параметры.СуммаАмортизацииЗаМесяц           = ВыборкаПериодов.Амортизация;

		ОбластьИтого.Параметры.СуммаПрямыхРасходовЗаМесяц         =  СуммаПрямыхРасходовЗаМесяц;
		ОбластьИтого.Параметры.СуммаПрямыхРасходовНаКонецМесяца  = ВыборкаПериодов.СтоимостьНачальныйОстаток + СуммаПрямыхРасходовЗаМесяц;
		ОбластьИтого.Параметры.СуммаОстатокНЗПНаКонецМесяца      = ВыборкаПериодов.СтоимостьКонечныйОстаток;
		ОбластьИтого.Параметры.СуммаОтносящаясяКВыпускуПродукции = ВыборкаПериодов.СтоимостьНачальныйОстаток + СуммаПрямыхРасходовЗаМесяц - ВыборкаПериодов.СтоимостьКонечныйОстаток;

		ДокументРезультат.Вывести(ОбластьИтого);

		ВыборкаСтрок = ВыборкаПериодов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаСтрок.Следующий() Цикл
			
			Если  ВыборкаСтрок.МатериальныеРасходы = 0 И
				  ВыборкаСтрок.ОплатаТруда = 0 И
				  ВыборкаСтрок.ЕСН = 0 И
				  ВыборкаСтрок.Амортизация = 0 И
				  ВыборкаСтрок.СтоимостьНачальныйОстаток = 0 И
				  ВыборкаСтрок.СтоимостьКонечныйОстаток = 0 Тогда
				  Продолжить;
			  КонецЕсли;

		    СуммаПрямыхРасходовЗаМесяц = ?(ВыборкаСтрок.СуммаПрямыхРасходовЗаМесяц = NULL, 0, ВыборкаСтрок.СуммаПрямыхРасходовЗаМесяц);

			ОбластьСтрока.Параметры.МесяцГод                          = МесяцГод;
			ОбластьСтрока.Параметры.ВидНоменклатуры                   = ВыборкаСтрок.НоменклатурнаяГруппа;
			ОбластьСтрока.Параметры.СуммаОстатокНЗПНаНачалоМесяца     = ВыборкаСтрок.СтоимостьНачальныйОстаток;
			ОбластьСтрока.Параметры.СуммаМатериальныхРасходовЗаМесяц  = ВыборкаСтрок.МатериальныеРасходы;
			ОбластьСтрока.Параметры.СуммаРасходовНаОплатуТрудаЗаМесяц = ВыборкаСтрок.ОплатаТруда;
			ОбластьСтрока.Параметры.СуммаЕСНЗаМесяц                   = ВыборкаСтрок.ЕСН;
			ОбластьСтрока.Параметры.СуммаАмортизацииЗаМесяц           = ВыборкаСтрок.Амортизация;
 			ОбластьСтрока.Параметры.СуммаДругихРасходовЗаМесяц        = ВыборкаСтрок.ДругиеРасходы;
			
			ОбластьСтрока.Параметры.СуммаПрямыхРасходовЗаМесяц         = СуммаПрямыхРасходовЗаМесяц;
			ОбластьСтрока.Параметры.СуммаПрямыхРасходовНаКонецМесяца  = ВыборкаСтрок.СтоимостьНачальныйОстаток + СуммаПрямыхРасходовЗаМесяц;
			ОбластьСтрока.Параметры.СуммаОстатокНЗПНаКонецМесяца      = ВыборкаСтрок.СтоимостьКонечныйОстаток ;
			ОбластьСтрока.Параметры.СуммаОтносящаясяКВыпускуПродукции = ВыборкаСтрок.СтоимостьНачальныйОстаток + СуммаПрямыхРасходовЗаМесяц - ВыборкаСтрок.СтоимостьКонечныйОстаток;

			ДокументРезультат.Вывести(ОбластьСтрока);

		КонецЦикла;

	КонецЦикла;

	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);

	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;

	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры // СформироватьОтчет

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;

#КонецЕсли