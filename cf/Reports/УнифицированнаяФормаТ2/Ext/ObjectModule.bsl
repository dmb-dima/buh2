
#Если Клиент Тогда

Перем мДлинаСуток;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом,
//	ЕстьОшибки - флаг того, что при формировании произошли ошибки
//
Процедура СформироватьОтчет(ДокументРезультат, ЕстьОшибки) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.УстановитьПараметр("Физлицо",		  Работник.Физлицо);
	Запрос.УстановитьПараметр("Сотрудник",		  Работник);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СотрудникиОрганизаций.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы)
	|			ТОГДА ""Основная""
	|		ИНАЧЕ ""По совместительству""
	|	КОНЕЦ КАК ВидРаботы,
	|	СотрудникиОрганизаций.Код КАК ТабельныйНомер,
	|	"""" КАК ТрудовойДоговорНомер,
	|	"""" КАК ТрудовойДоговорДата,
	|	СотрудникиОрганизаций.Физлицо.Наименование КАК Представление,
	|	СотрудникиОрганизаций.Физлицо.ДатаРождения КАК ДатаРождения,
	|	СотрудникиОрганизаций.Физлицо.МестоРождения КАК МестоРождения,
	|	"""" КАК МестоРожденияКодПоОКАТО,
	|	СотрудникиОрганизаций.Физлицо.Пол КАК Пол,
	|	СотрудникиОрганизаций.Физлицо.ИНН КАК ИНН,
	|	СотрудникиОрганизаций.Физлицо.СтраховойНомерПФР КАК СтраховойНомерПФР,
	|	ФИОФизЛицСрезПоследних.Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя,
	|	ФИОФизЛицСрезПоследних.Отчество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.Страна, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ""Гражданин Российской Федерации""
	|		КОГДА ГражданствоФизЛицСрезПоследних.Страна = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяССылка)
	|			ТОГДА ""Гражданин Российской Федерации""
	|		КОГДА ГражданствоФизЛицСрезПоследних.Страна = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ЛицоБезГражданства)
	|			ТОГДА ""Лицо без гражданства""
	|		ИНАЧЕ ""Иностранный гражданин""
	|	КОНЕЦ КАК Гражданство,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ГражданствоФизЛицСрезПоследних.Страна, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)) = ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.Россия)
	|			ТОГДА ""1""
	|		ИНАЧЕ ""3""
	|	КОНЕЦ КАК ГражданствоКодПоОКИН,
	|	"""" КАК СемейноеПоложение,
	|	"""" КАК СемейноеПоложениеКодПоОКИН,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид.Наименование КАК ВидДокумента,
	|	ЕСТЬNULL(ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид.КодИМНС, """") КАК КодИМНС,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан,
	|	ЕСТЬNULL(ПаспортныеДанныеФизЛицСрезПоследних.ДокументДатаВыдачи, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаВыдачи,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументКодПодразделения,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
	|	КонтактнаяИнформацияАдрес.Представление КАК АдресПоПаспорту,
	|	КонтактнаяИнформацияАдрес1.Представление КАК АдресФактический,
	|	КонтактнаяИнформацияАдрес.Поле1 КАК ИндексАдресПоПаспорту,
	|	КонтактнаяИнформацияАдрес1.Поле1 КАК ИндексАдресФактический,
	|	ПаспортныеДанныеФизЛицСрезПоследних.ДатаРегистрацииПоМестуЖительства,
	|	КонтактнаяИнформацияТелефон.Представление КАК Телефон,
	|	"""" КАК КатегорияЗапаса,
	|	"""" КАК ВоинскоеЗвание,
	|	"""" КАК Состав,
	|	"""" КАК ВУС,
	|	"""" КАК Годность,
	|	"""" КАК НаименованиеВоенкомата,
	|	"""" КАК НомерКомандыПартии,
	|	"""" КАК ЗабронированОрганизацией,
	|	"""" КАК СнятиеСУчета,
	|	РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделение КАК ОрганизацияПриема,
	|	РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделение.НаименованиеПолное КАК НаименованиеОрганизации,
	|	РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделение.КодПоОКПО КАК КодПоОКПО
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|		&ДатаАктуальности,
	|		Сотрудник = &Сотрудник
	|			И ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.ПриемНаРаботу)) КАК РаботникиОрганизацииСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ФизЛицо) КАК ПаспортныеДанныеФизЛицСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ФизЛицо) КАК ГражданствоФизЛицСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформацияАдрес
	|		ПО СотрудникиОрганизаций.Физлицо = КонтактнаяИнформацияАдрес.Объект
	|			И (КонтактнаяИнформацияАдрес.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (КонтактнаяИнформацияАдрес.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформацияАдрес1
	|		ПО СотрудникиОрганизаций.Физлицо = КонтактнаяИнформацияАдрес1.Объект
	|			И (КонтактнаяИнформацияАдрес1.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (КонтактнаяИнформацияАдрес1.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресФизЛица))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформацияТелефон
	|		ПО СотрудникиОрганизаций.Физлицо = КонтактнаяИнформацияТелефон.Объект
	|			И (КонтактнаяИнформацияТелефон.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон))
	|			И (КонтактнаяИнформацияТелефон.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонФизЛица))
	|ГДЕ
	|	СотрудникиОрганизаций.Ссылка = &Сотрудник";
	
	ВыборкаРезультата	=	Запрос.Выполнить().Выбрать();

	ДокументРезультат.Очистить();
 	Макет 				= 	ПолучитьМакет("Т2_от_5_1_2004");
	ПерваяСтраницаТ2	= 	Макет.ПолучитьОбласть("ПерваяСтраницаТ2");
	ВтораяСтраницаТ2	=	Макет.ПолучитьОбласть("ВтораяСтраницаТ2");
	ТретьяСтраницаТ2	=	Макет.ПолучитьОбласть("ТретьяСтраницаТ2");
	ЧетвертаяСтраницаТ2	=	Макет.ПолучитьОбласть("ЧетвертаяСтраницаТ2");
	
	// Дата стажа
	Если ВыборкаРезультата.Количество() > 0 И ЗначениеЗаполнено(ДатаАктуальности)Тогда
		ВтораяСтраницаТ2.Параметры["ДатаСтажа"] = "8. Стаж работы (по состоянию на  " + Формат(ДатаАктуальности, "ДЛФ=DD") + "):"
	Иначе
		ВтораяСтраницаТ2.Параметры["ДатаСтажа"] = "8. Стаж работы (по состоянию на  ""         "" _________ 20     г.):"
	КонецЕсли; 
	
	ВтораяСтраницаТ2.Параметры.НомерДокумента = "№";
	Если ВыборкаРезультата.Следующий() тогда
		
		Запрос.УстановитьПараметр("ОрганизацияПриема", ВыборкаРезультата.ОрганизацияПриема);
		
		ПерваяСтраницаТ2.Параметры.Заполнить(ВыборкаРезультата);
		ВтораяСтраницаТ2.Параметры.Заполнить(ВыборкаРезультата);
		
		Если ВыборкаРезультата.Фамилия = NULL тогда
			ФИО		=	ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(ВыборкаРезультата.Представление, " ");
			Если ФИО.Количество()	>	0	тогда
				Фамилия	=	ФИО[0];
			КонецЕсли;
			Если ФИО.Количество()	>	1	тогда
				Имя		=	ФИО[1];
			КонецЕсли;
			Если ФИО.Количество()	>	2	тогда
				Отчество=	ФИО[2];
			КонецЕсли;
			// ФИО
			ПерваяСтраницаТ2.Параметры.Фамилия					=	Фамилия;
			ПерваяСтраницаТ2.Параметры.Имя						=	Имя;
			ПерваяСтраницаТ2.Параметры.Отчество         		=	Отчество;
		КонецЕсли;
		
		ПерваяСтраницаТ2.Параметры.МестоРождения			= РегламентированнаяОтчетность.ПредставлениеМестаРождения(ВыборкаРезультата.МестоРождения);
		ПерваяСтраницаТ2.Параметры.ДатаАктуальности			= ДатаАктуальности;
		
		// ПАСПОРТ
			Если ВыборкаРезультата.ВидДокумента <> Null Тогда
				Если Найти("21,01",ВыборкаРезультата.КодИМНС) = 0 Тогда
					ВтораяСтраницаТ2.Параметры.ДатаВыдачи = "";
					ВтораяСтраницаТ2.Параметры.ДругойДокумент = ?(ЗначениеЗаполнено(ВыборкаРезультата.ВидДокумента),ВыборкаРезультата.ВидДокумента,"<вид документа не указан>") +
							" №" + ?(ЗначениеЗаполнено(ВыборкаРезультата.ДокументНомер),ВыборкаРезультата.ДокументНомер + " ","<номер документа не указан>") + ?(ЗначениеЗаполнено(ВыборкаРезультата.ДокументСерия),ВыборкаРезультата.ДокументСерия,"") + "; выдан: " + ?(ЗначениеЗаполнено(ВыборкаРезультата.ДокументКемВыдан),ВыборкаРезультата.ДокументКемВыдан,"<не указано> ") + Формат(ВыборкаРезультата.ДатаВыдачи,"ДЛФ=DD");
				Иначе	
					ВтораяСтраницаТ2.Параметры.НомерДокумента	= "Серия " + ?(ЗначениеЗаполнено(ВыборкаРезультата.ДокументСерия),ВыборкаРезультата.ДокументСерия,"") + "  № " + ?(ЗначениеЗаполнено(ВыборкаРезультата.ДокументНомер),ВыборкаРезультата.ДокументНомер + " ","");
					ВтораяСтраницаТ2.Параметры.КемВыданДокумент	= ?(ЗначениеЗаполнено(ВыборкаРезультата.ДокументКемВыдан),ВыборкаРезультата.ДокументКемВыдан,"") + ?(ЗначениеЗаполнено(ВыборкаРезультата.ДокументКодПодразделения)," № подразделения " + ВыборкаРезультата.ДокументКодПодразделения,"");
				КонецЕсли;
			КонецЕсли;
		
		// АДРЕСА
		Если ЗначениеЗаполнено(ВыборкаРезультата.ИндексАдресФактический) Тогда
			ВтораяСтраницаТ2.Параметры.АдресФактический	=	СтрЗаменить(ВыборкаРезультата.АдресФактический,ВыборкаРезультата.ИндексАдресФактический + ",","");
		КонецЕсли;
		Если ЗначениеЗаполнено(ВыборкаРезультата.ИндексАдресПоПаспорту) Тогда
			ВтораяСтраницаТ2.Параметры.АдресПоПаспорту	=	СтрЗаменить(ВыборкаРезультата.АдресПоПаспорту,ВыборкаРезультата.ИндексАдресПоПаспорту + ",","");
		КонецЕсли;
		
		// Воинский учет
		ВтораяСтраницаТ2.Параметры.КатегорияЗапаса = Лев(ВтораяСтраницаТ2.Параметры.КатегорияЗапаса,1);
		ВтораяСтраницаТ2.Параметры.Годность = Лев(ВтораяСтраницаТ2.Параметры.Годность,1);
	Иначе
		Запрос.УстановитьПараметр("ОрганизацияПриема", Справочники.Организации.ПустаяСсылка());
	КонецЕсли;

	// ПРИЕМ И ПЕРЕВОДЫ НА ДРУГУЮ РАБОТУ
	РеглВалюта = Константы.ВалютаРегламентированногоУчета.Получить();
	Запрос.УстановитьПараметр("РеглВалюта",РеглВалюта);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Работники.Период КАК Период,
	|	Работники.Сотрудник КАК Сотрудник,
	|	Работники.ПричинаИзмененияСостояния КАК ПричинаИзмененияСостояния,
	|	Работники.Должность.Наименование КАК Должность,
	|	Работники.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок,
	|	ВЫБОР
	|		КОГДА Работники.ОбособленноеПодразделение = &ОрганизацияПриема
	|			ТОГДА Работники.ПодразделениеОрганизации.Наименование
	|		ИНАЧЕ Работники.ПодразделениеОрганизации.Наименование + "" ("" + Работники.ОбособленноеПодразделение.Наименование + "")""
	|	КОНЕЦ КАК ПодразделениеОрганизации,
	|	Работники.Регистратор КАК Регистратор,
	|	Работники.Регистратор.Дата КАК ДатаПриказа,
	|	Работники.Регистратор.Номер КАК НомерПриказа
	|ПОМЕСТИТЬ ВТПеремещенияРаботника
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК Работники
	|ГДЕ
	|	Работники.Сотрудник = &Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период";
	
	Запрос.Выполнить();
	ПеремещенияРаботникаТекст = "ВТПеремещенияРаботника";
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 13
	|	Работники.Должность КАК Должность,
	|	Работники.ЗанимаемыхСтавок,
	|	Работники.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА Работники.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|			ТОГДА ДОБАВИТЬКДАТЕ(Работники.Период, ДЕНЬ, -1)
	|		ИНАЧЕ Работники.Период
	|	КОНЕЦ КАК ДатаПеревода,
	|	Работники.ПричинаИзмененияСостояния,
	|	Работники.ДатаПриказа КАК ДатаПриказа,
	|	Работники.НомерПриказа КАК НомерПриказа,
	|	УвольненияРаботники.ОснованиеУвольнения.Наименование КАК ОснованиеУвольнения,
	|	УвольненияРаботники.ДатаУвольнения,
	|	УвольненияРаботники.Ссылка.Номер КАК НомерПриказаУвольнения,
	|	УвольненияРаботники.Ссылка.Дата КАК ДатаПриказаУвольнения,
	|	ПлановыеНачисленияРаботниковОрганизаций.Размер КАК ТарифнаяСтавка
	|ИЗ
	|	" + ПеремещенияРаботникаТекст + " КАК Работники
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УвольнениеИзОрганизаций.РаботникиОрганизации КАК УвольненияРаботники
	|		ПО Работники.Сотрудник = УвольненияРаботники.Сотрудник
	|			И Работники.Регистратор = УвольненияРаботники.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботуВОрганизацию.РаботникиОрганизации КАК ПриемНаРаботуВОрганизациюРаботники
	|		ПО Работники.Сотрудник = ПриемНаРаботуВОрганизациюРаботники.Сотрудник
	|			И Работники.Регистратор = ПриемНаРаботуВОрганизациюРаботники.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ВложенныйЗапрос.ДатаПеревода КАК ДатаПеревода,
	|			ПлановыеНачисленияРаботниковОрганизаций.Размер КАК Размер
	|		ИЗ
	|			(ВЫБРАТЬ
	|				РаботникиОрганизаций.Период КАК ДатаПеревода,
	|				РаботникиОрганизаций.Сотрудник КАК Сотрудник,
	|				МАКСИМУМ(ПлановыеНачисленияРаботниковОрганизаций.Период) КАК Период
	|			ИЗ
	|				" + ПеремещенияРаботникаТекст + " КАК РаботникиОрганизаций
	|					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|					ПО РаботникиОрганизаций.Сотрудник = ПлановыеНачисленияРаботниковОрганизаций.Сотрудник
	|						И РаботникиОрганизаций.Период >= ПлановыеНачисленияРаботниковОрганизаций.Период
	|			ГДЕ
	|				ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОкладПоДням)
	|			
	|			СГРУППИРОВАТЬ ПО
	|				РаботникиОрганизаций.Период,
	|				РаботникиОрганизаций.Сотрудник) КАК ВложенныйЗапрос
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций КАК ПлановыеНачисленияРаботниковОрганизаций
	|				ПО ВложенныйЗапрос.Сотрудник = ПлановыеНачисленияРаботниковОрганизаций.Сотрудник
	|					И ВложенныйЗапрос.Период = ПлановыеНачисленияРаботниковОрганизаций.Период
	|		ГДЕ
	|			ПлановыеНачисленияРаботниковОрганизаций.ВидРасчетаИзмерение = ЗНАЧЕНИЕ(ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ОкладПоДням)) КАК ПлановыеНачисленияРаботниковОрганизаций
	|		ПО Работники.Период = ПлановыеНачисленияРаботниковОрганизаций.ДатаПеревода
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПеревода";
	
	ВыборкаРезультата	=	Запрос.Выполнить().Выбрать();
	инд	=	0;
	ЧетвертаяСтраницаТ2.Параметры.Приказ = "Приказ (распоряжение) № ____________ от   ""____"" _______________ 20___г. ";
	ЧетвертаяСтраницаТ2.Параметры.ДатаУвольнения = '00010101';
	
	Пока ВыборкаРезультата.Следующий()	Цикл

		Если ВыборкаРезультата.ПричинаИзмененияСостояния = Перечисления.ПричиныИзмененияСостояния.Увольнение тогда
            
			//Встретили увольнение документом "Прием на работу" или "Увольнение"
			ЧетвертаяСтраницаТ2.Параметры["ОснованиеУвольнения"]	=	ВыборкаРезультата.ОснованиеУвольнения;
			ЧетвертаяСтраницаТ2.Параметры["ДатаУвольнения"]			=	ВыборкаРезультата.ДатаУвольнения;
			ЧетвертаяСтраницаТ2.Параметры["Приказ"] = "Приказ (распоряжение) № """ + СокрЛП(ВыборкаРезультата.НомерПриказаУвольнения) +  """ от " + Формат(ВыборкаРезультата.ДатаПриказаУвольнения, "ДЛФ=DD");
			
		Иначе// Кадровое назначение	
			
			ОписаниеТарифнаяСтавка = Формат(ВыборкаРезультата.ТарифнаяСтавка,"ЧЦ=15; ЧДЦ=2; ЧН=' '");
			
			инд	= инд + 1;
			
			ТретьяСтраницаТ2.Параметры["ДатаПеревода"+инд]			= ВыборкаРезультата.ДатаПеревода;
			ТретьяСтраницаТ2.Параметры["ПодразделениеПеревода"+инд]	= ВыборкаРезультата.ПодразделениеОрганизации;
			ТретьяСтраницаТ2.Параметры["ДолжностьПеревода"+инд]		= ВыборкаРезультата.Должность;
			ТретьяСтраницаТ2.Параметры["ТарифнаяСтавка"+инд]		= ОписаниеТарифнаяСтавка;
			ТретьяСтраницаТ2.Параметры["ОснованиеПеревода"+инд]		= "Пр.№ "+ СокрЛП(ВыборкаРезультата.НомерПриказа) +" от " + Формат(ВыборкаРезультата.ДатаПриказа, "ДФ=dd.MM.yyyy");
			
		КонецЕсли;

	КонецЦикла;

	// Вывод страниц документа
	ДокументРезультат.Присоединить(ПерваяСтраницаТ2);
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ДокументРезультат.Присоединить(ВтораяСтраницаТ2);
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ДокументРезультат.Присоединить(ТретьяСтраницаТ2);
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ДокументРезультат.Присоединить(ЧетвертаяСтраницаТ2);
	
	//Параметры документа
	ДокументРезультат.ПолеСлева				= 0;
	ДокументРезультат.ПолеСправа			= 0;
	ДокументРезультат.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_Форма_Т2";
	ДокументРезультат.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	
КонецПроцедуры

мДлинаСуток = 86400;

#КонецЕсли

