////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

// Возвращает инициализированный массив чисел
Функция НовыйМассивЧисел(Размер)
	Массив = Новый Массив(Размер);
	Для Н =0 По Размер - 1 Цикл
		Массив[Н] = 0;
	КонецЦикла; 
	Возврат Массив;
КонецФункции 

// Преобразует массив чисел в массив чисел по нарастающей
Процедура ПреобразоватьМассивВНарастающий(Массив)
	НарастающееЧисло = 0;
	Для Н = 0 По Массив.Количество() -1 Цикл
		НарастающееЧисло = НарастающееЧисло + Массив[Н];
		Массив[Н] = НарастающееЧисло;
	КонецЦикла; 
КонецПроцедуры
 
// Преобразует значение в число
Функция ПараметрВЧисло(Параметр)
	Если Параметр = Неопределено Тогда
		Возврат 0;
	Иначе	
		Возврат Параметр;
	КонецЕсли; 
КонецФункции
 
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если Клиент Тогда

// Заполняет табличный документ
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом,
//
Процедура СформироватьОтчет(ДокументРезультат) Экспорт

	Отказ = Ложь;
	
	// Проверка параметров
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация!", Отказ);
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(НалоговыйПериод) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан налоговый период!", Отказ);
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(Физлицо) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан работник!", Отказ);
	КонецЕсли; 
	Если Отказ Тогда
		Возврат ;
	КонецЕсли; 	
	
	ДокументРезультат.Очистить();
	
	Если НалоговыйПериод < 2009 Тогда
		Макет =	ПолучитьМакет("Форма1НДФЛ");
	Иначе
		Макет =	ПолучитьМакет("Форма1НДФЛ_2009");
	КонецЕсли;
	
	// Расчет вычисляемых параметров
	ГодНП = Формат(НалоговыйПериод, "ЧГ=0");
	ДатаНачалаНП = НачалоГода(Дата(ГодНП,1,1));
	ДатаКонцаНП = КонецГода(Дата(ГодНП,1,1));
	ГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	
	СводноПоКодамОКАТО = ПустаяСтрока(КодПоОКАТО);
	
	// Массив кодов доходов, отражаемых в разделе 7: Доходы от продажи имущества
	МассивКодовДоходовРаздела7 = Новый Массив;
	МассивКодовДоходовРаздела7.Добавить(Справочники.ДоходыНДФЛ.Код1530);
	МассивКодовДоходовРаздела7.Добавить(Справочники.ДоходыНДФЛ.Код2210);
	
	ФизЛицоОтветственного = Ответственный.ФизЛицо;
	
	// Создание запроса и установка всех необходимых параметров
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ИндивидуальныйПредприниматель",Организация.ИндивидуальныйПредприниматель);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Запрос.УстановитьПараметр("Ответственный", Ответственный);
	Запрос.УстановитьПараметр("ФизлицоОтветственного", Ответственный.ФизЛицо);
	Запрос.УстановитьПараметр("ЧислоГодНП", НалоговыйПериод);
	Запрос.УстановитьПараметр("НачалоНП", ДатаНачалаНП);
	Запрос.УстановитьПараметр("КонецНП", ДатаКонцаНП);
	Запрос.УстановитьПараметр("ПоследнийМесяцНП", НачалоМесяца(ДатаКонцаНП));
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаПодписи);
	Запрос.УстановитьПараметр("МассивКодовДоходовРаздела7", МассивКодовДоходовРаздела7);
	Запрос.УстановитьПараметр("ПустойКодПоОКАТО","");
	Запрос.УстановитьПараметр("ПустойКПП","");
	Запрос.УстановитьПараметр("КодПоОКАТО",КодПоОКАТО);
	
	Запрос.УстановитьПараметр("Это2007год", НалоговыйПериод < 2008);
	Запрос.УстановитьПараметр("Это2008год", НалоговыйПериод = 2008);
	Запрос.УстановитьПараметр("Это2009год", НалоговыйПериод > 2008);
	
	// Сформируем текст запроса с датами месяцев налогового периода
	// первый месяц
	КонецМесяца = КонецМесяца(ДатаНачалаНП);
	ПериодыТекст = "ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат (КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ") КАК Период";
	ПериодыТекст = ПериодыТекст + "
	|ПОМЕСТИТЬ ВТПериоды";
	
	// прибавим остальные месяцы
	Для Сч = 2 По 12 Цикл
		КонецМесяца = КонецМесяца(КонецМесяца+1);
    	ПериодыТекст = ПериодыТекст +" ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ДАТАВРЕМЯ(" + Формат(КонецМесяца,"ДФ=гггг,М,д,Ч,м,с") + ")";
	КонецЦикла;
	
	ПериодыТекст = ПериодыТекст + "
	|ИНДЕКСИРОВАТЬ ПО Период";
	
	Запрос.Текст = ПериодыТекст;
	Запрос.Выполнить();
	
	//-----------------------------------------------------------------------------
	// ВЫБОРКА СВЕДЕНИЙ ОБ ОРГАНИЗАЦИИ И ФИЗЛИЦЕ (Данные разделов 1 и 2)
	// 	
	
	ДанныеОбОрганизацииИФизлицеТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.ИНН КАК Организация_ИНН,
	|	Организации.КПП КАК Организация_КПП,
	|	Организации.КодИМНС КАК Организация_КодИМНС,
	|	Организации.КодПоОКАТО КАК Организация_КодПоОКАТО,
	|	ВЫБОР
	|		КОГДА Организации.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА Организации.Наименование
	|		ИНАЧЕ ФИОПредпринимателя.Фамилия + "" "" + ФИОПредпринимателя.Имя + "" "" + ФИОПредпринимателя.Отчество
	|	КОНЕЦ КАК Организация_Наименование,
	|	ДанныеОФизЛице.ИНН КАК ФизЛицо_ИНН,
	|	ДанныеОФизЛице.СтраховойНомерПФР КАК ФизЛицо_СтраховойНомерПФР,
	|	ЕСТЬNULL(ФИОФизЛица.Фамилия + "" "" + ФИОФизЛица.Имя + "" "" + ФИОФизЛица.Отчество, ДанныеОФизЛице.Наименование) КАК ФизЛицо_ФИО,
	|	ЕСТЬNULL(ФИОФизлицаОтветственного.Фамилия + "" "" + ФИОФизлицаОтветственного.Имя + "" "" + ФИОФизлицаОтветственного.Отчество, ЕСТЬNULL(ДанныеОбОтветственном.ФизЛицо.Наименование, ДанныеОбОтветственном.Наименование)) КАК Ответственный_ФИО,
	|	ДолжностьОтветственного.Должность.Наименование КАК Ответственный_Должность,
	|	ПаспортныеДанныеФизЛица.ДокументВид.КодИМНС КАК ФизЛицо_КодДокументаУдЛичн,
	|	ПаспортныеДанныеФизЛица.ДокументСерия + "" "" + ПаспортныеДанныеФизЛица.ДокументНомер КАК ФизЛицо_СерияНомерДокументаУдЛичн,
	|	ДанныеОФизЛице.ДатаРождения КАК ФизЛицо_ДатаРождения,
	|	(НЕ ЕСТЬNULL(ВЫБОР
	|				КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ, ЛОЖЬ)) КАК ФизЛицо_Резидент,
	|	ЕСТЬNULL(ГражданствоФизЛица.Страна.Код, ""643"") КАК ФизЛицо_КодСтраны,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА Страны.Код
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ФизЛицо_АдресКодСтраны,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле1
	|	КОНЕЦ КАК ФизЛицо_АдресИндекс,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ РегионРегистрацииФизЛица.Код
	|	КОНЕЦ КАК ФизЛицо_АдресРегион,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле3
	|	КОНЕЦ КАК ФизЛицо_АдресРайон,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА АдресаИностранцев.Представление
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле4
	|	КОНЕЦ КАК ФизЛицо_АдресГород,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле5
	|	КОНЕЦ КАК ФизЛицо_АдресНаселенныйПункт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле6
	|	КОНЕЦ КАК ФизЛицо_АдресУлица,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле7
	|	КОНЕЦ КАК ФизЛицо_АдресДом,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле8
	|	КОНЕЦ КАК ФизЛицо_АдресКорпус,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВЫБОР
	|					КОГДА СтатусФизЛица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ, ЛОЖЬ)
	|			ТОГДА """"
	|		ИНАЧЕ АдресРегистрацииФизЛица.Поле9
	|	КОНЕЦ КАК ФизЛицо_АдресКвартира,
	|	НДФЛДоходыПредыдущегоМестаРаботы.Размер КАК ФизЛицо_ДоходыПредыдущегоМестаРаботы
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ИндивидуальныйПредприниматель) КАК ФИОПредпринимателя
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ДанныеОФизЛице
	|		ПО (ДанныеОФизЛице.Ссылка = &ФизЛицо)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК ДанныеОбОтветственном
	|		ПО (ДанныеОбОтветственном.Ссылка = &Ответственный)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ФизЛицо) КАК ФИОФизЛица
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ФизЛицоОтветственного) КАК ФИОФизлицаОтветственного
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ФизЛицо) КАК ПаспортныеДанныеФизЛица
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц.СрезПоследних(&КонецНП, ФизЛицо = &ФизЛицо) КАК ГражданствоФизЛица
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ.СрезПоследних(&КонецНП, ФизЛицо = &ФизЛицо) КАК СтатусФизЛица
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресРегистрацииФизЛица
	|		ПО (ДанныеОФизЛице.Ссылка = АдресРегистрацииФизЛица.Объект)
	|			И (АдресРегистрацииФизЛица.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АдресныйКлассификатор КАК РегионРегистрацииФизЛица
	|		ПО (РегионРегистрацииФизЛица.ТипАдресногоЭлемента = 1)
	|			И (РегионРегистрацииФизЛица.Наименование + "" "" + РегионРегистрацииФизЛица.Сокращение = АдресРегистрацииФизЛица.Поле2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаИностранцев
	|		ПО (ДанныеОФизЛице.Ссылка = АдресаИностранцев.Объект)
	|			И (АдресаИностранцев.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ИнострАдресФизЛица))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторСтранМира КАК Страны
	|		ПО (Страны.Наименование = АдресаИностранцев.Поле1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛДоходыПредыдущегоМестаРаботы КАК НДФЛДоходыПредыдущегоМестаРаботы
	|		ПО (НДФЛДоходыПредыдущегоМестаРаботы.ФизЛицо = &Физлицо)
	|			И (НДФЛДоходыПредыдущегоМестаРаботы.Организация = &ГоловнаяОрганизация)
	|			И (НДФЛДоходыПредыдущегоМестаРаботы.МесяцНалоговогоПериода = &ПоследнийМесяцНП)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ДатаАктуальности,
	|				Сотрудник.ФизЛицо = &ФизЛицоОтветственного
	|					И Организация = &Организация
	|					И Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)) КАК ДолжностьОтветственного
	|		ПО (ДолжностьОтветственного.ПричинаИзмененияСостояния <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение))
	|ГДЕ
	|	Организации.Ссылка = &Организация";
	
	
	// СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГодТекст 
	// Поля:
	//	КодВычета  - код стандартного вычета (ссылка)
	//
	// Описание: выбирает все различные коды вычетов, которые действовали в течение года по физлицу в регистре "НДФЛСтандартныеВычетыФизлиц" 
	
	СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГодТекст = 
	"ВЫБРАТЬ 
	|	ПраваНаРазличныеВычеты.КодВычета
	|ИЗ
	|	(ВЫБРАТЬ
	|		Истина КАК ЕстьВычетПоОрганизации
	|	ИЗ
	|		РегистрСведений.НДФЛПрименениеВычетов КАК НДФЛПрименениеВычетов
	|	ГДЕ
	|		НДФЛПрименениеВычетов.Организация = &ГоловнаяОрганизация
	|		И НДФЛПрименениеВычетов.Период МЕЖДУ &НачалоНП И &КонецНП
	|		И НДФЛПрименениеВычетов.Физлицо = &Физлицо
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Истина
	|	ИЗ
	|		РегистрСведений.НДФЛПрименениеВычетов.СрезПоследних(&НачалоНП, Физлицо = &Физлицо) КАК НДФЛПрименениеВычетовСрезПоследних
	|	ГДЕ
	|		НДФЛПрименениеВычетовСрезПоследних.Организация = &ГоловнаяОрганизация) КАК ВычетыВОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			НДФЛСтандартныеВычетыФизлицСрезПоследних.КодВычетаЛичный КАК КодВычета
	|		ИЗ
	|			РегистрСведений.НДФЛСтандартныеВычетыФизлиц.СрезПоследних(&НачалоНП, ФизЛицо = &ФизЛицо) КАК НДФЛСтандартныеВычетыФизлицСрезПоследних
	|		ГДЕ
	|			НДФЛСтандартныеВычетыФизлицСрезПоследних.КодВычетаЛичный <> ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	
	|			НДФЛСтандартныеВычетыФизлиц.КодВычетаЛичный
	|		ИЗ
	|			РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК НДФЛСтандартныеВычетыФизлиц
	|		ГДЕ
	|			НДФЛСтандартныеВычетыФизлиц.Период МЕЖДУ &НачалоНП И &КонецНП
	|			И НДФЛСтандартныеВычетыФизлиц.КодВычетаЛичный <> ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.ПустаяСсылка)
	|			И НДФЛСтандартныеВычетыФизлиц.Физлицо = &ФизЛицо
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ 
	|			НДФЛСтандартныеВычетыФизлицСрезПоследних.КодВычета
	|		ИЗ
	|			РегистрСведений.НДФЛСтандартныеВычетыНаДетей.СрезПоследних(&НачалоНП, ФизЛицо = &ФизЛицо) КАК НДФЛСтандартныеВычетыФизлицСрезПоследних
	|		ГДЕ
	|			ВЫБОР
	|					КОГДА НДФЛСтандартныеВычетыФизлицСрезПоследних.ПериодЗавершения <= &НачалоНП
	|							И НДФЛСтандартныеВычетыФизлицСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|						ТОГДА НДФЛСтандартныеВычетыФизлицСрезПоследних.КоличествоДетейЗавершения
	|					ИНАЧЕ НДФЛСтандартныеВычетыФизлицСрезПоследних.КоличествоДетей
	|				КОНЕЦ <> 0
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ 
	|			НДФЛСтандартныеВычетыФизлиц.КодВычета
	|		ИЗ
	|			РегистрСведений.НДФЛСтандартныеВычетыНаДетей КАК НДФЛСтандартныеВычетыФизлиц
	|		ГДЕ
	|			НДФЛСтандартныеВычетыФизлиц.Период МЕЖДУ &НачалоНП И &КонецНП
	|			И НДФЛСтандартныеВычетыФизлиц.Физлицо = &ФизЛицо
	|			И НДФЛСтандартныеВычетыФизлиц.КоличествоДетей <> 0) КАК ПраваНаРазличныеВычеты
	|		ПО (ИСТИНА)";									   
									   
	//НомераИДатыПредоставленныхСправокОДоходахТекст
	//
	// Описание: выбирает номера и даты справок о доходах для 9-го раздела 
	
	НомераИДатыПредоставленныхСправокОДоходахТекст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ДатаСправкиВИМНС), """") КАК ДатаСправкиВИМНС,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.НомерСправкиВИМНС), """") КАК НомерСправкиВИМНС,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ВнешнийНомерСправкиВИМНС), """") КАК ВнешнийНомерСправкиВИМНС,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ДатаСправкиДляФизЛица), """") КАК ДатаСправкиДляФизЛица,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.НомерСправкиДляФизЛица), """") КАК НомерСправкиДляФизЛица,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ВнешнийНомерСправкиДляФизЛица), """") КАК ВнешнийНомерСправкиДляФизЛица,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ДатаСправкиНаВзыскание), """") КАК ДатаСправкиНаВзыскание,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.НомерСправкиНаВзыскание), """") КАК НомерСправкиНаВзыскание,
	|	ЕСТЬNULL(МАКСИМУМ(НесгруппированныеДанные.ВнешнийНомерСправкиНаВзыскание), """") КАК ВнешнийНомерСправкиНаВзыскание
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		СведенияОДоходахФизлицРаботники.Ссылка.Дата КАК ДатаСправкиВИМНС,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Номер КАК НомерСправкиВИМНС,
	|		СведенияОДоходахФизлицРаботники.НомерСправки КАК ВнешнийНомерСправкиВИМНС,
	|		NULL КАК ДатаСправкиДляФизЛица,
	|		NULL КАК НомерСправкиДляФизЛица,
	|		NULL КАК ВнешнийНомерСправкиДляФизЛица,
	|		NULL КАК ДатаСправкиНаВзыскание,
	|		NULL КАК НомерСправкиНаВзыскание,
	|		NULL КАК ВнешнийНомерСправкиНаВзыскание
	|	ИЗ
	|		Документ.СведенияОДоходахФизлиц.РаботникиОрганизации КАК СведенияОДоходахФизлицРаботники
	|	ГДЕ
	|		СведенияОДоходахФизлицРаботники.Ссылка.Проведен
	|		И СведенияОДоходахФизлицРаботники.Ссылка.НалоговыйПериод = &ЧислоГодНП
	|		И СведенияОДоходахФизлицРаботники.Ссылка.НазначениеСправки = ЗНАЧЕНИЕ(Перечисление.ПорядокФормированияСправкиОДоходахФизлица.ДляПередачиВИФНС)
	|		И СведенияОДоходахФизлицРаботники.Ссылка.Организация = &ГоловнаяОрганизация
	|		И СведенияОДоходахФизлицРаботники.ФизЛицо = &ФизЛицо
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		NULL,
	|		NULL,
	|		NULL,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Дата,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Номер,
	|		СведенияОДоходахФизлицРаботники.НомерСправки,
	|		NULL,
	|		NULL,
	|		NULL
	|	ИЗ
	|		Документ.СведенияОДоходахФизлиц.РаботникиОрганизации КАК СведенияОДоходахФизлицРаботники
	|	ГДЕ
	|		СведенияОДоходахФизлицРаботники.Ссылка.Проведен
	|		И СведенияОДоходахФизлицРаботники.Ссылка.НалоговыйПериод = &ЧислоГодНП
	|		И СведенияОДоходахФизлицРаботники.Ссылка.Организация = &ГоловнаяОрганизация
	|		И (СведенияОДоходахФизлицРаботники.Ссылка.НазначениеСправки = ЗНАЧЕНИЕ(Перечисление.ПорядокФормированияСправкиОДоходахФизлица.ФизлицуВРазрезеКодовОКАТО)
	|				ИЛИ СведенияОДоходахФизлицРаботники.Ссылка.НазначениеСправки = ЗНАЧЕНИЕ(Перечисление.ПорядокФормированияСправкиОДоходахФизлица.ФизлицуСводно))
	|		И СведенияОДоходахФизлицРаботники.ФизЛицо = &ФизЛицо
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		NULL,
	|		NULL,
	|		NULL,
	|		NULL,
	|		NULL,
	|		NULL,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Дата,
	|		СведенияОДоходахФизлицРаботники.Ссылка.Номер,
	|		СведенияОДоходахФизлицРаботники.НомерСправки
	|	ИЗ
	|		Документ.СведенияОДоходахФизлиц.РаботникиОрганизации КАК СведенияОДоходахФизлицРаботники
	|	ГДЕ
	|		СведенияОДоходахФизлицРаботники.Ссылка.Проведен
	|		И СведенияОДоходахФизлицРаботники.Ссылка.НалоговыйПериод = &ЧислоГодНП
	|		И СведенияОДоходахФизлицРаботники.Ссылка.НазначениеСправки = ЗНАЧЕНИЕ(Перечисление.ПорядокФормированияСправкиОДоходахФизлица.ДляПередачиВИФНС)
	|		И СведенияОДоходахФизлицРаботники.Ссылка.Организация = &ГоловнаяОрганизация
	|		И СведенияОДоходахФизлицРаботники.ФизЛицо = &ФизЛицо
	|		И СведенияОДоходахФизлицРаботники.ПереданоНаВзыскание > 0) КАК НесгруппированныеДанные";
	
			
	//-----------------------------------------------------------------------------
	// ВЫБОРКА ДОХОДОВ ПО ВИДАМ И ПО МЕСЯЦАМ НАЛОГОВОГО ПЕРИОДА ДЛЯ ВСЕХ РАЗДЕЛОВ КАРТОЧКИ
	// Выбираются все доходы по месяцам налогового периода в разрезе кодов дохода
	// !!! В свойстве "период" регистра "НДФЛСведенияОДоходах" хранится "МесяцНалоговогоПериода"
	
	//Описание поля Раздел налоговой карточки: 
	//	7 раздел - налог с доходов седьмого раздела
	//	3 раздел - налог с доходов по ставке 13% и со всех доходов нерезидента,
	//	4 раздел - налог с доходов по ставке 9%
	//	5 раздел - налог с доходов по ставке 30%
	
	Если СводноПоКодамОКАТО Тогда
		
		ДоходыПоМесяцамПоВидамТекст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7)
		|			ТОГДА ""Раздел7""
		|		КОГДА &ЧислоГодНП > 2007
		|				И НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ &ФизЛицо_Резидент)
		|				ИЛИ НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ КАК РАЗДЕЛ,
		|	НДФЛСведенияОДоходах.КодДохода.КодДляОтчетности КАК КодДохода,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
		|	НДФЛСведенияОДоходах.СуммаДоходаОборот КАК СуммаДохода,
		|	НДФЛСведенияОДоходах.СуммаВычетаОборот КАК СуммаВычета
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах.Обороты(
		|			&НачалоНП,
		|			&КонецНП,
		|			Месяц,
		|			ФизЛицо = &ФизЛицо
		|				И Организация = &ГоловнаяОрганизация) КАК НДФЛСведенияОДоходах
		|ИТОГИ
		|	СУММА(СуммаДохода),
		|	СУММА(СуммаВычета)
		|ПО
		|	РАЗДЕЛ,
		|	КодДохода";
		
		
		//-----------------------------------------------------------------------------
		// ВЫБОРКА ВЫЧЕТОВ ПО ВИДАМ И ПО МЕСЯЦАМ НАЛОГОВОГО ПЕРИОДА
		// Выбираются вычеты по 13% доходам (кроме доходов раздела 7). Для нерезидента - все вычеты. 
		
		ВычетыПоМесяцамПоВидамТекст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НДФЛСведенияОДоходах.КодВычета.Код КАК КодВычета,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
		|	СУММА(НДФЛСведенияОДоходах.СуммаВычета) КАК СуммаВычета
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|ГДЕ
		|	НДФЛСведенияОДоходах.СуммаВычета <> 0
		|	И НДФЛСведенияОДоходах.ФизЛицо = &ФизЛицо
		|	И НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|	И НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
		|	И (НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|				И (НЕ НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7))
		|			ИЛИ (НЕ &ФизЛицо_Резидент))
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛСведенияОДоходах.КодВычета,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период),
		|	НДФЛСведенияОДоходах.КодВычета.Код
		|ИТОГИ
		|	СУММА(СуммаВычета)
		|ПО
		|	КодВычета";
		
		//-----------------------------------------------------------------------------
		// НАЛОГИ (начисленные, удержанные, возвращенные ...) ПО МЕСЯЦАМ ПО РАЗДЕЛАМ ФОРМЫ
		
		//НалогиПоМесяцамТекст
		//	Выбираются все начисленные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП")
		// 		Поле "Месяц" - месяц налогового периода.
		//	Выбираются все удержанные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП"),
		//  однако поле "месяц" вычисляется следующим образом:
		//		1) Если Период < НачалоНП то берется "Январь",
		//		2) Если Период > КонецНП то берется "Декабрь", 	
		//		3) Иначе Месяц = Месяц(Период) - период регистрации!!!. 	
		//	Выбираются все переданные на взыскание налоги по месяцам текущего НП(Условие "Период МЕЖДУ &НачалоНП И &КонецНП").
		// 	 Поле "Месяц" - период регистрации!!!
		//	Выбираются все возвращенные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП"),
		//  однако поле "месяц" вычисляется следующим образом:
		//		1) Если Период < НачалоНП то берется "Январь",
		//		2) Если Период > КонецНП то берется "Декабрь", 	
		//		3) Иначе Месяц = Месяц(Период) - период регистрации!!!. 	
		// При возврате налог записывается с минусом!
		
		// НалогиПомесячноПоСтавкамТекст
		//	Поля:
		//		Раздел - раздел налоговой карточки 
		//		Месяц - месяц налогового периода
		
		//		Начислено - сумма начисленного налога за текущий год за всю историю (без отбора по периоду регистарции, как и доходы)
		//		УдержаноВТекущемГоду - сумма удержанного налога за текущий год в текущем году
		//		УдержаноВПрошлыхГодах - сумма удержанного налога за текущий год в текущем году
		//		НачисленоВПрошлыхПериодах - сумма удержанного налога за текущий год
		//		ВозвращеноЗаТекущийГод - сумма налога, возвращенная в результате перерасчета за текущий год
		//		ВозвращеноЗаПрошлыеГоды - сумма налога, возвращенная в результате перерасчета за прошлые годы
		//		ПереданоНаВзыскание - сумма налога, переданная на взыскание
		//
		// Описание:
		//	Для каждого раздела карточки в разрезе месяцев налогового периода выбирает сведения о суммах налогов
		//
		
		//Описание поля Раздел налоговой карточки: 
		//	3 раздел - налог с доходов по ставке 13% и со всех доходов нерезидента,
		//	4 раздел - налог с доходов по ставке 9%
		//	5 раздел - налог с доходов по ставке 30%
		
		НалогиПомесячноПоСтавкамТекст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|			ТОГДА ""Начислено""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
		|			ТОГДА ""Удержано""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			ТОГДА ""ПереданоНаВзыскание""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА ""Возвращено""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ВидСтроки,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|			ТОГДА МЕСЯЦ(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода)
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.Период < &НачалоНП
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.Период > &КонецНП
		|						ТОГДА 12
		|					ИНАЧЕ МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|				КОНЕЦ
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			ТОГДА МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.Период < &НачалоНП
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.Период > &КонецНП
		|						ТОГДА 12
		|					ИНАЧЕ МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Месяц,
		|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК Ставка,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА -НДФЛРасчетыСБюджетом.Налог
		|		ИНАЧЕ НДФЛРасчетыСБюджетом.Налог
		|	КОНЕЦ КАК Налог,
		|	НДФЛРасчетыСБюджетом.КодДохода
		|ПОМЕСТИТЬ ВТНалогиПоМесяцам
		|ИЗ
		|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
		|ГДЕ
		|	НДФЛРасчетыСБюджетом.ФизЛицо = &ФизЛицо
		|	И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|	И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОНалогах.Месяц,
		|	ВЫБОР
		|		КОГДА &ЧислоГодНП > 2007
		|				И ДанныеОНалогах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ &ФизЛицо_Резидент)
		|				ИЛИ ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		КОГДА ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ КАК РАЗДЕЛ,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Начислено""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Начислено,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Удержано""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Удержано,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""ПереданоНаВзыскание""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПереданоНаВзыскание,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Возвращено""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Возвращено
		|ИЗ
		|	ВТНалогиПоМесяцам КАК ДанныеОНалогах
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОНалогах.Месяц,
		|	ВЫБОР
		|		КОГДА &ЧислоГодНП > 2007
		|				И ДанныеОНалогах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ &ФизЛицо_Резидент)
		|				ИЛИ ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		КОГДА ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ
		|ИТОГИ ПО
		|	РАЗДЕЛ";
		
		
		//СальдоРасчетовНаНачалоГода 
		// Описание: выбирается сальдо по формуле
		//	Сальдо = (приходы – расходы без учета строк ПереданоНаВзыскание по регистру НДФЛРасчетыСБюджетом по измерению МесяцНалоговогоПериода < начало налогового периода) – (оборот по виду строки ПереданоНаВзыскание по свойству Период < начало налогового периода)
		
		СальдоРасчетовНаНачалоГодаТекст = 
		"ВЫБРАТЬ
		|	Ставки.Ссылка КАК Ставка,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|				КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					ТОГДА НДФЛРасчетыСБюджетом.Налог
		|				ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
		|			КОНЕЦ), 0) КАК Сальдо
		|ИЗ
		|	Перечисление.НДФЛСтавкиНалогообложенияРезидента КАК Ставки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
		|		ПО Ставки.Ссылка = НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента
		|ГДЕ
		|	НДФЛРасчетыСБюджетом.ФизЛицо = &ФизЛицо
		|	И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
		|	И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода < &НачалоНП
		|
		|СГРУППИРОВАТЬ ПО
		|	Ставки.Ссылка";
		
				
		// МесяцыПоследнегоДоходаИлиНалогаТекст
		//	Поля:
		//		Месяц - месяц налогового периода - число от 1 до 12
		//		Физлицо
		//
		// Описание:
		//	Для каждого физлица рассчитывается последний месяц предоставления вычетов
		// 	как максимальное значение из двух следующих:
		//  1. Месяц самого позднего начисления
		//	2. Месяц самого позднего расчета налога   
		
		// ПрименениеВычетовВОрганизации
		//	Поля:
		//		Период - месяц налогового периода
		//		Физлицо
		//
		// Описание:
		//	Для каждого месяца налогового периода выбирает список физлиц, для которых применяются стандартные вычеты по 
		//	заданной организации. Учитываем тот факт, что применяемость всегда регистрируется на головную организацию,
		//  а также налоговое резидентство физлиц.
		//
			
		// Ограничим периоды предоставления вычетов
		// Ограничиваем месяцы предоставления вычетов месяцем, максимальным из двух: 1. Месяц последнего начисленного дохода ; 2. Месяц последнего расчета налога
		
		// Временная таблица ОблДоходыЗаГод 
		//	Поля:
		//		Период
		//		ОблагаемыйДоходЗаГод
		//
		//	Описание:
		//	Выбирает по всем периодам с начала года сумму дохода нарастающим итогом за год для расчета ограничения по стандартным вычетам
		//  Надо учитывать доходы, заработанные во всех структурных подразделениях организации
		//	и добавляем суммы совокупного дохода с предыдущего места работы в разрезе месяцев налогового периода,
		//  зарегистрированные на любое структурное подразделение организации
		
		// Временная таблица СтандартныеВычетыНаДетей
		//	Поля:
		//		Физлицо
		//		Период
		//		КодВычета
		//		КоличествоДетей
		//
		//	Описание:
		//	Выбирает по всем периодам года стандартные вычеты на детей на которые имеет право физлицо

		// Описание: за все месяцы налогового периода определяем текущее состояние прав на стандартные вычеты
		//           и суммы фактически предоставленных стандартных вычетов
		
		ПраваНаВычетыТекст = 
		"ВЫБРАТЬ
		|	НДФЛОбороты.Период КАК Период,
		|	СУММА(НДФЛОбороты.ОблагаемыйДоход) КАК ОблагаемыйДоходЗаГод
		|ПОМЕСТИТЬ ВТОблДоходыЗаГод
		|ИЗ
		|	(ВЫБРАТЬ
		|		Периоды.Период КАК Период,
		|		НДФЛСведенияОДоходах.СуммаДохода - НДФЛСведенияОДоходах.СуммаВычета КАК ОблагаемыйДоход
		|	ИЗ
		|		ВТПериоды КАК Периоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|			ПО Периоды.Период >= НДФЛСведенияОДоходах.Период
		|	ГДЕ
		|		НДФЛСведенияОДоходах.ФизЛицо = &Физлицо
		|		И НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
		|		И НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|		И НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|		И (НЕ НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Периоды.Период,
		|		НДФЛДоходыПредыдущегоМестаРаботы.Размер
		|	ИЗ
		|		ВТПериоды КАК Периоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛДоходыПредыдущегоМестаРаботы КАК НДФЛДоходыПредыдущегоМестаРаботы
		|			ПО (НДФЛДоходыПредыдущегоМестаРаботы.МесяцНалоговогоПериода = НАЧАЛОПЕРИОДА(Периоды.Период, МЕСЯЦ))
		|	ГДЕ
		|		НДФЛДоходыПредыдущегоМестаРаботы.ФизЛицо = &Физлицо
		|		И НДФЛДоходыПредыдущегоМестаРаботы.Организация = &ГоловнаяОрганизация) КАК НДФЛОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛОбороты.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоследниеМесяцы.ФизЛицо КАК Физлицо,
		|	МАКСИМУМ(ПоследниеМесяцы.Месяц) КАК Месяц
		|ПОМЕСТИТЬ ВТМесяцыПоследнегоДоходаИлиНалога
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		НДФЛСведенияОДоходах.ФизЛицо КАК ФизЛицо,
		|		МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц
		|	ИЗ
		|		РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|	ГДЕ
		|		НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|		И НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|		И НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
		|		И НДФЛСведенияОДоходах.ФизЛицо = &Физлицо
		|		И (НЕ НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		НДФЛРасчетыСбюджетом.ФизЛицо,
		|		МЕСЯЦ(НДФЛРасчетыСбюджетом.МесяцНалоговогоПериода)
		|	ИЗ
		|		РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСбюджетом
		|	ГДЕ
		|		НДФЛРасчетыСбюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|		И НДФЛРасчетыСбюджетом.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|		И (НДФЛРасчетыСбюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|				ИЛИ НДФЛРасчетыСбюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание))
		|		И НДФЛРасчетыСбюджетом.Организация = &ГоловнаяОрганизация
		|		И НДФЛРасчетыСбюджетом.ФизЛицо = &Физлицо) КАК ПоследниеМесяцы
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоследниеМесяцы.ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрименениеВычетовВОрганизации.Период КАК Период
		|ПОМЕСТИТЬ ВТПрименениеВычетов
		|ИЗ
		|	(ВЫБРАТЬ
		|		НДФЛПрименениеВычетовСрезПоследних.Физлицо КАК Физлицо,
		|		НДФЛПрименениеВычетовСрезПоследних.Период КАК Период
		|	ИЗ
		|		(ВЫБРАТЬ
		|			МАКСИМУМ(НДФЛПрименениеВычетов.Период) КАК ПериодСреза,
		|			НДФЛПрименениеВычетов.Физлицо КАК Физлицо,
		|			Периоды.Период КАК Период
		|		ИЗ
		|			ВТПериоды КАК Периоды
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛПрименениеВычетов КАК НДФЛПрименениеВычетов
		|				ПО (НДФЛПрименениеВычетов.Период <= Периоды.Период)
		|		ГДЕ
		|			НДФЛПрименениеВычетов.Физлицо = &Физлицо
		|		
		|		СГРУППИРОВАТЬ ПО
		|			НДФЛПрименениеВычетов.Физлицо,
		|			Периоды.Период) КАК НДФЛПрименениеВычетовСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛПрименениеВычетов КАК НДФЛПрименениеВычетов
		|			ПО (НДФЛПрименениеВычетов.Период = НДФЛПрименениеВычетовСрезПоследних.ПериодСреза)
		|				И (НДФЛПрименениеВычетов.Физлицо = НДФЛПрименениеВычетовСрезПоследних.Физлицо)
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц.СрезПоследних(&КонецНП, Физлицо = &ФизЛицо) КАК ГражданствоФизЛицСрезПоследних
		|			ПО (НДФЛПрименениеВычетов.Физлицо = ГражданствоФизЛицСрезПоследних.ФизЛицо)
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизЛицКакНалогоплательщиковНДФЛ.СрезПоследних(&КонецНП, Физлицо = &ФизЛицо) КАК Статусы
		|			ПО (НДФЛПрименениеВычетов.Физлицо = Статусы.ФизЛицо)
		|	ГДЕ
		|		НДФЛПрименениеВычетов.Организация = &ГоловнаяОрганизация
		|		И (НЕ ЕСТЬNULL(ВЫБОР
		|						КОГДА Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыНалогоплательщиковПоНДФЛ.НеРезидент)
		|							ТОГДА ИСТИНА
		|						ИНАЧЕ ЛОЖЬ
		|					КОНЕЦ, ЛОЖЬ))) КАК ПрименениеВычетовВОрганизации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыПоследнегоДоходаИлиНалога КАК МесяцыПоследнегоДоходаИлиНалога
		|		ПО (МЕСЯЦ(ПрименениеВычетовВОрганизации.Период) <= МесяцыПоследнегоДоходаИлиНалога.Месяц)
		|			И ПрименениеВычетовВОрганизации.Физлицо = МесяцыПоследнегоДоходаИлиНалога.Физлицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СрезПоследних.Период КАК Период,
		|	СрезПоследних.Физлицо КАК ФизЛицо,
		|	СрезПоследних.КодВычета КАК КодВычета,
		|	ВЫБОР
		|		КОГДА НДФЛСтандартныеВычетыНаДетей.ПериодЗавершения <= НАЧАЛОПЕРИОДА(СрезПоследних.Период, МЕСЯЦ)
		|				И НДФЛСтандартныеВычетыНаДетей.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА НДФЛСтандартныеВычетыНаДетей.КоличествоДетейЗавершения
		|		ИНАЧЕ НДФЛСтандартныеВычетыНаДетей.КоличествоДетей
		|	КОНЕЦ КАК КоличествоДетей
		|ПОМЕСТИТЬ ВТСтандартныеВычеты
		|ИЗ
		|	(ВЫБРАТЬ
		|		НДФЛСтандартныеВычетыНаДетей.Физлицо КАК Физлицо,
		|		Периоды.Период КАК Период,
		|		НДФЛСтандартныеВычетыНаДетей.КодВычета КАК КодВычета,
		|		МАКСИМУМ(НДФЛСтандартныеВычетыНаДетей.Период) КАК ПериодРегистра
		|	ИЗ
		|		ВТПериоды КАК Периоды
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыНаДетей КАК НДФЛСтандартныеВычетыНаДетей
		|			ПО Периоды.Период >= НДФЛСтандартныеВычетыНаДетей.Период
		|				И (НДФЛСтандартныеВычетыНаДетей.Физлицо = &Физлицо)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НДФЛСтандартныеВычетыНаДетей.Физлицо,
		|		НДФЛСтандартныеВычетыНаДетей.КодВычета,
		|		Периоды.Период) КАК СрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыНаДетей КАК НДФЛСтандартныеВычетыНаДетей
		|		ПО СрезПоследних.ПериодРегистра = НДФЛСтандартныеВычетыНаДетей.Период
		|			И СрезПоследних.КодВычета = НДФЛСтандартныеВычетыНаДетей.КодВычета
		|			И СрезПоследних.Физлицо = НДФЛСтандартныеВычетыНаДетей.Физлицо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НДФЛСтандартныеВычетыФизлицСрезПоследних.Период,
		|	НДФЛСтандартныеВычетыФизлицСрезПоследних.Физлицо,
		|	ВычетыФизлиц.КодВычетаЛичный,
		|	1
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(ВычетыФизлиц.Период) КАК ПериодСреза,
		|		Периоды.Период КАК Период,
		|		ВычетыФизлиц.Физлицо КАК Физлицо
		|	ИЗ
		|		ВТПериоды КАК Периоды
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК ВычетыФизлиц
		|			ПО Периоды.Период >= ВычетыФизлиц.Период
		|				И (ВычетыФизлиц.Физлицо = &Физлицо)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВычетыФизлиц.Физлицо,
		|		Периоды.Период) КАК НДФЛСтандартныеВычетыФизлицСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛСтандартныеВычетыФизлиц КАК ВычетыФизлиц
		|		ПО НДФЛСтандартныеВычетыФизлицСрезПоследних.ПериодСреза = ВычетыФизлиц.Период
		|			И НДФЛСтандартныеВычетыФизлицСрезПоследних.Физлицо = ВычетыФизлиц.Физлицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	ФизЛицо,
		|	КодВычета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Периоды.Период КАК Период,
		|	СтандартныеВычеты.ФизЛицо КАК ФизЛицо,
		|	СтандартныеВычеты.КодВычета КАК КодВычета,
		|	ВЫБОР
		|		КОГДА ПрименениеВычетов.Период ЕСТЬ NULL 
		|			ТОГДА 0
		|		КОГДА НДФЛРазмерВычетов.ОграничениеПоДоходам = 0
		|				ИЛИ ЕСТЬNULL(ДоходыЗаГод.ОблагаемыйДоходЗаГод, 0) <= НДФЛРазмерВычетов.ОграничениеПоДоходам
		|			ТОГДА ЕСТЬNULL(НДФЛРазмерВычетов.Размер, 0) * ЕСТЬNULL(СтандартныеВычеты.КоличествоДетей, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК РазмерВычета
		|ПОМЕСТИТЬ ВТПраваНаВычеты
		|ИЗ
		|	ВТПериоды КАК Периоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтандартныеВычеты КАК СтандартныеВычеты
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛРазмерВычетов.СрезПоследних(&НачалоНП, ) КАК НДФЛРазмерВычетов
		|			ПО СтандартныеВычеты.КодВычета = НДФЛРазмерВычетов.КодВычета
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПрименениеВычетов КАК ПрименениеВычетов
		|			ПО СтандартныеВычеты.Период = ПрименениеВычетов.Период
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТОблДоходыЗаГод КАК ДоходыЗаГод
		|			ПО СтандартныеВычеты.Период = ДоходыЗаГод.Период
		|		ПО Периоды.Период = СтандартныеВычеты.Период
		|ГДЕ
		|	СтандартныеВычеты.КодВычета ЕСТЬ НЕ NULL 
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	КодВычета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОНЕЦПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК Период,
		|	НДФЛРасчетыСБюджетом.КодВычета КАК КодВычета,
		|	СУММА(НДФЛРасчетыСБюджетом.ПримененныйВычет) КАК ПримененныйВычет
		|ПОМЕСТИТЬ ВТПредоставленныеВычеты
		|ИЗ
		|	РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛРасчетыСБюджетом
		|ГДЕ
		|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|	И НДФЛРасчетыСБюджетом.ФизЛицо = &Физлицо
		|	И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
		|
		|СГРУППИРОВАТЬ ПО
		|	КОНЕЦПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ),
		|	НДФЛРасчетыСБюджетом.КодВычета
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	КодВычета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МЕСЯЦ(Периоды.Период) КАК Месяц,
		|	Периоды.КодВычета КАК КодВычета,
		|	ВЫБОР
		|		КОГДА &Это2007год
		|			ТОГДА Периоды.КодВычета.КодДляОтчетности2007
		|		КОГДА &Это2008год
		|			ТОГДА Периоды.КодВычета.КодДляОтчетности2008
		|		ИНАЧЕ Периоды.КодВычета.КодДляОтчетности2009
		|	КОНЕЦ КАК КодВычетаКод,
		|	ВЫБОР
		|		КОГДА &Это2009год
		|			ТОГДА ВЫБОР
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|						ТОГДА 1
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|						ТОГДА 2
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|						ТОГДА 6
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|						ТОГДА 7
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|						ТОГДА 3
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|						ТОГДА 4
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|						ТОГДА 5
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код111)
		|						ТОГДА 8
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код113)
		|						ТОГДА 9
		|					КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код319)
		|						ТОГДА 90
		|					ИНАЧЕ -1
		|				КОНЕЦ
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|			ТОГДА 1
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|			ТОГДА 2
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|			ТОГДА 6
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|			ТОГДА 7
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|			ТОГДА 3
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|			ТОГДА 4
		|		КОГДА Периоды.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|			ТОГДА 5
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК НомерСтрокиВычета,
		|	ЕСТЬNULL(ПраваНаВычеты.РазмерВычета, 0) КАК РазмерВычета,
		|	ЕСТЬNULL(ПредоставленныеCтандартныеВычеты.ПримененныйВычет, 0) КАК ПримененныйВычет
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВТСтандартныеВычеты.Период КАК Период,
		|		ВТСтандартныеВычеты.КодВычета КАК КодВычета
		|	ИЗ
		|		ВТСтандартныеВычеты КАК ВТСтандартныеВычеты
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ВТПредоставленныеВычеты.Период,
		|		ВТПредоставленныеВычеты.КодВычета
		|	ИЗ
		|		ВТПредоставленныеВычеты КАК ВТПредоставленныеВычеты) КАК Периоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраваНаВычеты КАК ПраваНаВычеты
		|		ПО Периоды.Период = ПраваНаВычеты.Период
		|			И Периоды.КодВычета = ПраваНаВычеты.КодВычета
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПредоставленныеВычеты КАК ПредоставленныеCтандартныеВычеты
		|		ПО Периоды.Период = ПредоставленныеCтандартныеВычеты.Период
		|			И Периоды.КодВычета = ПредоставленныеCтандартныеВычеты.КодВычета
		|
		|УПОРЯДОЧИТЬ ПО
		|	КодВычетаКод,
		|	Месяц";
		
		
		// ИмущественныеВычетыТекст
		//
		// Описание: выбирает суммы предоставленных имущественных вычетов по указанной организации
		
		ИмущественныеВычетыТекст = 
		"ВЫБРАТЬ
		|	МЕСЯЦ(МесяцыНалоговогоПериода.Период) КАК МесяцНП,
		|	НДФЛИмущественныеВычетыФизлицОбороты.КодВычетаИмущественный,
		|	ЕСТЬNULL(НДФЛИмущественныеВычетыФизлицОбороты.РазмерРасход, 0) КАК РазмерВычета
		|ИЗ
		|	ВТПериоды КАК МесяцыНалоговогоПериода
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛИмущественныеВычетыФизлиц.Обороты(
		|		&НачалоНП,
		|		&КонецНП,
		|		Месяц,
		|		ФизЛицо = &ФизЛицо
		|		    И Год = &ЧислоГодНП
		|		    И Организация = &ГоловнаяОрганизация) КАК НДФЛИмущественныеВычетыФизлицОбороты
		|		ПО (НАЧАЛОПЕРИОДА(МесяцыНалоговогоПериода.Период, МЕСЯЦ) = НДФЛИмущественныеВычетыФизлицОбороты.Период)
		|ИТОГИ
		|	СУММА(РазмерВычета)
		|ПО
		|	МесяцНП";
		
	Иначе
		
		ДоходыПоМесяцамПоВидамТекст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7)
		|			ТОГДА ""Раздел7""
		|		КОГДА &ЧислоГодНП > 2007
		|				И НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ &ФизЛицо_Резидент)
		|				ИЛИ НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ КАК РАЗДЕЛ,
		|	НДФЛСведенияОДоходах.КодДохода.КодДляОтчетности КАК КодДохода,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
		|	СУММА(НДФЛСведенияОДоходах.СуммаДохода) КАК СуммаДохода,
		|	СУММА(НДФЛСведенияОДоходах.СуммаВычета) КАК СуммаВычета
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|ГДЕ
		|	ВЫБОР
		|			КОГДА НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛСведенияОДоходах.КодПоОКАТО
		|			КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО
		|			ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|		КОНЕЦ = &КодПоОКАТО
		|	И НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|	И НДФЛСведенияОДоходах.ФизЛицо = &ФизЛицо
		|	И НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛСведенияОДоходах.КодДохода.КодДляОтчетности,
		|	ВЫБОР
		|		КОГДА НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7)
		|			ТОГДА ""Раздел7""
		|		КОГДА &ЧислоГодНП > 2007
		|				И НДФЛСведенияОДоходах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ &ФизЛицо_Резидент)
		|				ИЛИ НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период)
		|ИТОГИ
		|	СУММА(СуммаДохода),
		|	СУММА(СуммаВычета)
		|ПО
		|	РАЗДЕЛ,
		|	КодДохода";
		
		//-----------------------------------------------------------------------------
		// ВЫБОРКА ВЫЧЕТОВ ПО ВИДАМ И ПО МЕСЯЦАМ НАЛОГОВОГО ПЕРИОДА
		// Выбираются вычеты по 13% доходам (кроме доходов раздела 7). Для нерезидента - все вычеты. 
		
		ВычетыПоМесяцамПоВидамТекст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НДФЛСведенияОДоходах.КодВычета.Код КАК КодВычета,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период) КАК Месяц,
		|	СУММА(НДФЛСведенияОДоходах.СуммаВычета) КАК СуммаВычета
		|ИЗ
		|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|ГДЕ
		|	ВЫБОР
		|			КОГДА НДФЛСведенияОДоходах.КодПоОКАТО <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛСведенияОДоходах.КодПоОКАТО
		|			КОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛСведенияОДоходах.ПодразделениеОрганизации.КодПоОКАТО
		|			ИНАЧЕ ЕСТЬNULL(НДФЛСведенияОДоходах.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|		КОНЕЦ = &КодПоОКАТО
		|	И НДФЛСведенияОДоходах.СуммаВычета <> 0
		|	И НДФЛСведенияОДоходах.ФизЛицо = &ФизЛицо
		|	И НДФЛСведенияОДоходах.Период МЕЖДУ &НачалоНП И &КонецНП
		|	И НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
		|	И (НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|				И (НЕ НДФЛСведенияОДоходах.КодДохода В (&МассивКодовДоходовРаздела7))
		|			ИЛИ (НЕ &ФизЛицо_Резидент))
		|
		|СГРУППИРОВАТЬ ПО
		|	НДФЛСведенияОДоходах.КодВычета,
		|	МЕСЯЦ(НДФЛСведенияОДоходах.Период),
		|	НДФЛСведенияОДоходах.КодВычета.Код
		|ИТОГИ
		|	СУММА(СуммаВычета)
		|ПО
		|	КодВычета";
		
		//-----------------------------------------------------------------------------
		// НАЛОГИ (начисленные, удержанные, возвращенные ...) ПО МЕСЯЦАМ ПО РАЗДЕЛАМ ФОРМЫ
		
		//НалогиПоМесяцамТекст
		//	Выбираются все начисленные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП")
		// 		Поле "Месяц" - месяц налогового периода.
		//	Выбираются все удержанные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП"),
		//  однако поле "месяц" вычисляется следующим образом:
		//		1) Если Период < НачалоНП то берется "Январь",
		//		2) Если Период > КонецНП то берется "Декабрь", 	
		//		3) Иначе Месяц = Месяц(Период) - период регистрации!!!. 	
		//	Выбираются все переданные на взыскание налоги по месяцам текущего НП(Условие "Период МЕЖДУ &НачалоНП И &КонецНП").
		// 	 Поле "Месяц" - период регистрации!!!
		//	Выбираются все возвращенные налоги за месяцы текущего НП (Условие "МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП"),
		//  однако поле "месяц" вычисляется следующим образом:
		//		1) Если Период < НачалоНП то берется "Январь",
		//		2) Если Период > КонецНП то берется "Декабрь", 	
		//		3) Иначе Месяц = Месяц(Период) - период регистрации!!!. 	
		// При возврате налог записывается с минусом!
		
		НалогиПоМесяцамТекст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|			ТОГДА ""Начислено""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
		|			ТОГДА ""Удержано""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			ТОГДА ""ПереданоНаВзыскание""
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА ""Возвращено""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ВидСтроки,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Начисление)
		|			ТОГДА МЕСЯЦ(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода)
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.Удержание)
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.Период < &НачалоНП
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.Период > &КонецНП
		|						ТОГДА 12
		|					ИНАЧЕ МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|				КОНЕЦ
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			ТОГДА МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.Период < &НачалоНП
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.Период > &КонецНП
		|						ТОГДА 12
		|					ИНАЧЕ МЕСЯЦ(НДФЛРасчетыСБюджетом.Период)
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Месяц,
		|	НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК Ставка,
		|	ВЫБОР
		|		КОГДА НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ВозвратНалога)
		|			ТОГДА -НДФЛРасчетыСБюджетом.Налог
		|		ИНАЧЕ НДФЛРасчетыСБюджетом.Налог
		|	КОНЕЦ КАК Налог,
		|	НДФЛРасчетыСБюджетом.КодДохода
		|ИЗ
		|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
		|ГДЕ
		|	НДФЛРасчетыСБюджетом.ФизЛицо = &ФизЛицо
		|	И ВЫБОР
		|			КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
		|			КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
		|			ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|		КОНЕЦ = &КодПоОКАТО
		|	И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|	И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация";
		
		// НалогиПомесячноПоСтавкамТекст
		//	Поля:
		//		Раздел - раздел налоговой карточки 
		//		Месяц - месяц налогового периода
		
		//		Начислено - сумма начисленного налога за текущий год за всю историю (без отбора по периоду регистарции, как и доходы)
		//		УдержаноВТекущемГоду - сумма удержанного налога за текущий год в текущем году
		//		УдержаноВПрошлыхГодах - сумма удержанного налога за текущий год в текущем году
		//		НачисленоВПрошлыхПериодах - сумма удержанного налога за текущий год
		//		ВозвращеноЗаТекущийГод - сумма налога, возвращенная в результате перерасчета за текущий год
		//		ВозвращеноЗаПрошлыеГоды - сумма налога, возвращенная в результате перерасчета за прошлые годы
		//		ПереданоНаВзыскание - сумма налога, переданная на взыскание
		//
		// Описание:
		//	Для каждого раздела карточки в разрезе месяцев налогового периода выбирает сведения о суммах налогов
		//
		
		//Описание поля Раздел налоговой карточки: 
		//	3 раздел - налог с доходов по ставке 13% и со всех доходов нерезидента,
		//	4 раздел - налог с доходов по ставке 9%
		//	5 раздел - налог с доходов по ставке 30%
		
		НалогиПомесячноПоСтавкамТекст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеОНалогах.Месяц,
		|	ВЫБОР
		|		КОГДА &ЧислоГодНП > 2007 и ДанныеОНалогах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ &ФизЛицо_Резидент)
		|				ИЛИ ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		КОГДА ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ КАК РАЗДЕЛ,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Начислено""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Начислено,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Удержано""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Удержано,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""ПереданоНаВзыскание""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПереданоНаВзыскание,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОНалогах.ВидСтроки = ""Возвращено""
		|				ТОГДА ДанныеОНалогах.Налог
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Возвращено
		|	ИЗ (" + НалогиПоМесяцамТекст + ") КАК ДанныеОНалогах
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОНалогах.Месяц,
		|	ВЫБОР
		|		КОГДА &ЧислоГодНП > 2007 и ДанныеОНалогах.КодДохода = ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.Код1010)
		|			ТОГДА ""Раздел4""
		|		КОГДА (НЕ &ФизЛицо_Резидент)
		|				ИЛИ ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
		|			ТОГДА ""Раздел3""
		|		КОГДА ДанныеОНалогах.Ставка = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка09)
		|			ТОГДА ""Раздел4""
		|		ИНАЧЕ ""Раздел5""
		|	КОНЕЦ
		|ИТОГИ ПО
		|	РАЗДЕЛ";
		
		//СальдоРасчетовНаНачалоГода 
		// Описание: выбирается сальдо по формуле
		//	Сальдо = (приходы – расходы без учета строк ПереданоНаВзыскание по регистру НДФЛРасчетыСБюджетом по измерению МесяцНалоговогоПериода < начало налогового периода) – (оборот по виду строки ПереданоНаВзыскание по свойству Период < начало налогового периода)
		
		СальдоРасчетовНаНачалоГодаТекст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Ставки.Ставка КАК Ставка,
		|	ЕСТЬNULL(ИсчисленныйУдержанныйВозвращенныйНалог.Сальдо, 0) - ЕСТЬNULL(ПереданныйНаВзысканиеНалог.Сальдо, 0) КАК Сальдо
		|ИЗ
		|	(ВЫБРАТЬ
		|		НДФЛСтавкиНалогообложенияРезидента.Ссылка КАК Ставка
		|	ИЗ
		|		Перечисление.НДФЛСтавкиНалогообложенияРезидента КАК НДФЛСтавкиНалогообложенияРезидента) КАК Ставки
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК Ставка,
		|			СУММА(ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|						ТОГДА НДФЛРасчетыСБюджетом.Налог
		|					ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
		|				КОНЕЦ) КАК Сальдо
		|		ИЗ
		|			РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
		|		ГДЕ
		|			НДФЛРасчетыСБюджетом.ФизЛицо = &ФизЛицо
		|			И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
		|			И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода < &НачалоНП
		|			И НДФЛРасчетыСБюджетом.ВидСтроки <> ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			И ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
		|						ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
		|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
		|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|				КОНЕЦ = &КодПоОКАТО
		|		
		|		СГРУППИРОВАТЬ ПО
		|			НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента) КАК ИсчисленныйУдержанныйВозвращенныйНалог
		|		ПО ИсчисленныйУдержанныйВозвращенныйНалог.Ставка = Ставки.Ставка
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК Ставка,
		|			СУММА(ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|						ТОГДА НДФЛРасчетыСБюджетом.Налог
		|					ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
		|				КОНЕЦ) КАК Сальдо
		|		ИЗ
		|			РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
		|		ГДЕ
		|			НДФЛРасчетыСБюджетом.ФизЛицо = &ФизЛицо
		|			И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
		|			И НДФЛРасчетыСБюджетом.Период < &НачалоНП
		|			И НДФЛРасчетыСБюджетом.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.НДФЛРасчетыСБюджетомВидСтроки.ПереданоНаВзыскание)
		|			И ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
		|						ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
		|					КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|						ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
		|					ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|				КОНЕЦ = &КодПоОКАТО
		|		
		|		СГРУППИРОВАТЬ ПО
		|			НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента) КАК ПереданныйНаВзысканиеНалог
		|		ПО ПереданныйНаВзысканиеНалог.Ставка = Ставки.Ставка";
		
		//-----------------------------------------------------------------------------
		// Предоставленные стандартные вычеты
		
		//ПраваНаВычетыТекст
		//	Поля:
		//		Период
		//		КодВычета
		//      ПримененныйВычет
		//
		// Описание: за все месяцы налогового периода выбираем фактически предоставленные стандартные вычеты
		
		ПраваНаВычетыТекст = 
		"ВЫБРАТЬ
		|	МЕСЯЦ(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода) КАК Месяц,
		|	НДФЛРасчетыСБюджетом.КодВычета,
		|	ВЫБОР
		|		КОГДА &Это2007год
		|			ТОГДА НДФЛРасчетыСБюджетом.КодВычета.КодДляОтчетности2007
		|		КОГДА &Это2008год
		|			ТОГДА НДФЛРасчетыСБюджетом.КодВычета.КодДляОтчетности2008
		|		ИНАЧЕ НДФЛРасчетыСБюджетом.КодВычета.КодДляОтчетности2009
		|	КОНЕЦ КАК КодВычетаКод,
		|	ВЫБОР
		|		КОГДА &Это2009год
		|			ТОГДА ВЫБОР
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|						ТОГДА 1
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|						ТОГДА 2
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|						ТОГДА 6
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|						ТОГДА 7
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|						ТОГДА 3
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|						ТОГДА 4
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|						ТОГДА 5
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код111)
		|						ТОГДА 8
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код113)
		|						ТОГДА 9
		|					КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код319)
		|						ТОГДА 90
		|					ИНАЧЕ -1
		|				КОНЕЦ
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код101)
		|			ТОГДА 1
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код102)
		|			ТОГДА 2
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код106)
		|			ТОГДА 6
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код107)
		|			ТОГДА 7
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код103)
		|			ТОГДА 3
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код104)
		|			ТОГДА 4
		|		КОГДА НДФЛРасчетыСБюджетом.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код105)
		|			ТОГДА 5
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК НомерСтрокиВычета,
		|	ЕСТЬNULL(СУММА(НДФЛРасчетыСБюджетом.ПримененныйВычет), 0) КАК ПримененныйВычет
		|ИЗ
		|	РегистрНакопления.НДФЛПредоставленныеСтандартныеВычетыФизЛиц КАК НДФЛРасчетыСБюджетом
		|ГДЕ
		|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &НачалоНП И &КонецНП
		|	И НДФЛРасчетыСБюджетом.ФизЛицо = &ФизЛицо
		|	И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
		|	И ВЫБОР
		|			КОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.КодПоОКАТО
		|			КОГДА ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|				ТОГДА НДФЛРасчетыСБюджетом.ПодразделениеОрганизации.КодПоОКАТО
		|			ИНАЧЕ ЕСТЬNULL(НДФЛРасчетыСБюджетом.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|		КОНЕЦ = &КодПоОКАТО
		|
		|СГРУППИРОВАТЬ ПО
		|	МЕСЯЦ(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода),
		|	НДФЛРасчетыСБюджетом.КодВычета
		|
		|УПОРЯДОЧИТЬ ПО
		|	КодВычетаКод,
		|	Месяц";
		
		// ИмущественныеВычетыТекст
		//
		// Описание: выбирает суммы предоставленных имущественных вычетов по указанной организации
		
		ИмущественныеВычетыТекст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МЕСЯЦ(МесяцыНалоговогоПериода.Период) КАК МесяцНП,
		|	НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
		|	ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.Размер, 0) КАК РазмерВычета
		|ИЗ
		|	ВТПериоды КАК МесяцыНалоговогоПериода
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный КАК КодВычетаИмущественный,
		|			КОНЕЦПЕРИОДА(НДФЛИмущественныеВычетыФизлиц.Период, МЕСЯЦ) КАК Период,
		|			СУММА(НДФЛИмущественныеВычетыФизлиц.Размер) КАК Размер
		|		ИЗ
		|			РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
		|		ГДЕ
		|			НДФЛИмущественныеВычетыФизлиц.Период МЕЖДУ &НачалоНП И &КонецНП
		|			И НДФЛИмущественныеВычетыФизлиц.ФизЛицо = &ФизЛицо
		|			И НДФЛИмущественныеВычетыФизлиц.Год = &ЧислоГодНП
		|			И НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация
		|			И ВЫБОР
		|					КОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО <> &ПустойКодПоОКАТО
		|						ТОГДА НДФЛИмущественныеВычетыФизлиц.КодПоОКАТО
		|					КОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО, &ПустойКодПоОКАТО) <> &ПустойКодПоОКАТО
		|						ТОГДА НДФЛИмущественныеВычетыФизлиц.ПодразделениеОрганизации.КодПоОКАТО
		|					ИНАЧЕ ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.ОбособленноеПодразделение.КодПоОКАТО, &ПустойКодПоОКАТО)
		|				КОНЕЦ = &КодПоОКАТО
		|		
		|		СГРУППИРОВАТЬ ПО
		|			НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный,
		|			КОНЕЦПЕРИОДА(НДФЛИмущественныеВычетыФизлиц.Период, МЕСЯЦ)) КАК НДФЛИмущественныеВычетыФизлиц
		|		ПО МесяцыНалоговогоПериода.Период = НДФЛИмущественныеВычетыФизлиц.Период
		|ИТОГИ
		|	СУММА(РазмерВычета)
		|ПО
		|	МесяцНП";
		
	КонецЕсли;
	
	//-----------------------------------------------------------------------------
	// ВЫПОЛНЕНИЕ ЗАПРОСОВ
	
	// Сведения о физлице и организации
	Запрос.Текст = ДанныеОбОрганизацииИФизлицеТекст;
	ДанныеОФизЛицеОрганизации  = Запрос.Выполнить().Выбрать();
	
	// параметр  ФизЛицо_Резидент нужен для выполнения последующих запросов
	ДанныеОФизЛицеОрганизации.Следующий();
	Запрос.УстановитьПараметр("ФизЛицо_Резидент", ДанныеОФизЛицеОрганизации.ФизЛицо_Резидент);
	
	// Различные коды вычетов, действующие в течение года. Нужны для хвоста третьего раздела.
	Запрос.Текст = СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГодТекст;
	СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КодВычета"); 
	
	// Доходы по кодам и по месяцам налогового периода для всех разделов карточки
	Запрос.Текст = ДоходыПоМесяцамПоВидамТекст;     
	РезультатЗапросаДоходыПоМесяцамПоВидам = Запрос.Выполнить();
	ВыборкаДоходыПоМесяцамПоВидам = РезультатЗапросаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Вычеты по кодам и по месяцам налогового периода для всех разделов карточки
	Запрос.Текст = ВычетыПоМесяцамПоВидамТекст;
	ВыборкаВычетыПоМесяцамПоВидам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Запрос.Текст = ПраваНаВычетыТекст;      
	ВыборкаСтандартныхВычетовФизЛица = Запрос.Выполнить().Выбрать();
	
	// Имущественные вычеты
	Запрос.Текст = ИмущественныеВычетыТекст;      
	ВыборкаИмущественныеВычеты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//  Начисленные, удержанные, переданные на взыскание, возвращенные налоги для всех разделов по месяцам
	Запрос.Текст = НалогиПомесячноПоСтавкамТекст;    
	ВыборкаНалогиПомесяцамПоСтавкам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	//  Сальдо по налогу на начало году по ставкам
	Запрос.Текст = СальдоРасчетовНаНачалоГодаТекст;   
	ВыборкаСальдоРасчетовНаНачалоГода = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Номера и даты справок о доходах для 9-го раздела
	Запрос.Текст = НомераИДатыПредоставленныхСправокОДоходахТекст;
	ДанныеОПредоставленныхСправках  = Запрос.Выполнить().Выбрать(); 
	ДанныеОПредоставленныхСправках.Следующий();
	
	//-----------------------------------------------------------------------------
	// ЗАПОЛНЕНИЕ ФОРМЫ
	
	// Переменные оформления
	ЦветПодсветкиНеверноИсчисленногоНалога = Новый Цвет(0,0,255);
	ЦветЧерный = Новый Цвет(0,0,0);
	
	// Области-разделы
    Раздел1 = Макет.ПолучитьОбласть("Раздел1");									    
	Раздел2 = Макет.ПолучитьОбласть("Раздел2");									    
	Раздел3 = Макет.ПолучитьОбласть("Раздел3");									    
	Раздел4 = Макет.ПолучитьОбласть("Раздел4");									    
	Раздел6 = Макет.ПолучитьОбласть("Раздел6");									    
	Раздел7 = Макет.ПолучитьОбласть("Раздел7");									    
	Раздел8 = Макет.ПолучитьОбласть("Раздел8");									    
	Раздел9 = Макет.ПолучитьОбласть("Раздел9");									    
	// Каждый вид доходов по ставке 35% должен отражаться в отдельном пятом разделе
	МассивРазделов5 = Новый Массив;
	// Добавим сразу в массив один раздел, т.к. даже если не будет доходов по ставке 35%, то все равно надо будет показать один пустой пятый раздел
	МассивРазделов5.Добавить(Макет.ПолучитьОбласть("Раздел5"));
	
	//-----------------------------------------------------------------------------
	// РАЗДЕЛ 1 и 2
	
	// Заполнение данных раздела 1
	Раздел1.Параметры.ГодНП = "" + ГодНП;
	Раздел1.Параметры.ИНН = "" + ДанныеОФизЛицеОрганизации.Организация_ИНН +"\" + ДанныеОФизЛицеОрганизации.Организация_КПП;
	Раздел1.Параметры.КодИМНС = ДанныеОФизЛицеОрганизации.Организация_КодИМНС;
	Раздел1.Параметры.КодОкато = ?(СводноПоКодамОКАТО,ДанныеОФизЛицеОрганизации.Организация_КодПоОКАТО,КодПоОКАТО);
	Раздел1.Параметры.НазваниеОрганизации = ДанныеОФизЛицеОрганизации.Организация_Наименование;
	
	// Заполнение данных раздела 2
	Раздел2.Параметры.ИНН = ДанныеОФизЛицеОрганизации.ФизЛицо_ИНН;
	Раздел2.Параметры.СтраховойНомерПФР = ДанныеОФизЛицеОрганизации.ФизЛицо_СтраховойНомерПФР;
	Раздел2.Параметры.ФИО = ДанныеОФизЛицеОрганизации.ФизЛицо_ФИО;
	Раздел2.Параметры.КодДокумента = ДанныеОФизЛицеОрганизации.ФизЛицо_КодДокументаУдЛичн;
	Раздел2.Параметры.СерияНомерДокумента = ДанныеОФизЛицеОрганизации.ФизЛицо_СерияНомерДокументаУдЛичн;
	Раздел2.Параметры.ДатаРождения = Формат(ДанныеОФизЛицеОрганизации.ФизЛицо_ДатаРождения, "ДФ=dd.MM.yyyy");
	Раздел2.Параметры.КодСтраны = ДанныеОФизЛицеОрганизации.ФизЛицо_КодСтраны;
	Раздел2.Параметры.СтранаПроживания = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресКодСтраны;
	Раздел2.Параметры.Индекс = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресИндекс;
	Раздел2.Параметры.Регион = ПроцедурыУправленияПерсоналом.ПолучитьКодРегионаРФИзКодаАдресногоЭлемента(ДанныеОФизЛицеОрганизации.ФизЛицо_АдресРегион);
	Раздел2.Параметры.Район = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресРайон;
	Раздел2.Параметры.Город = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресГород;
	Раздел2.Параметры.НаселенныйПункт = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресНаселенныйПункт;
	Раздел2.Параметры.Улица = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресУлица;
	Раздел2.Параметры.Дом = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресДом;
	Раздел2.Параметры.Корпус = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресКорпус;
	Раздел2.Параметры.Квартира = ДанныеОФизЛицеОрганизации.ФизЛицо_АдресКвартира;
	Раздел2.Параметры.Статус = ?(ДанныеОФизЛицеОрганизации.ФизЛицо_Резидент,1,2);
	Раздел2.Параметры.ЕстьВычеты = ?(СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод.Количество()>0, "1", "2");
	
	//-----------------------------------------------------------------------------
	// Расчет последнего отображаемого месяца для нарастающих цифр по последнему месяцу дохода или налога 
	
	ПоследнийМесяцДоходовИНалогов = 1;
	
	// обходим налоги
	ВыборкаНалогиПомесяцамПоСтавкам.Сбросить();
	Пока ВыборкаНалогиПомесяцамПоСтавкам.Следующий() Цикл
		ВыборкаМесяцев  = ВыборкаНалогиПомесяцамПоСтавкам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаМесяцев.Следующий() Цикл
			ПоследнийМесяцДоходовИНалогов = Макс(ВыборкаМесяцев.Месяц, ПоследнийМесяцДоходовИНалогов);
		КонецЦикла;
	КонецЦикла;	
	
	Если ПоследнийМесяцДоходовИНалогов <> 12 Тогда
		// обходим доходы
		ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
		Пока ВыборкаДоходыПоМесяцамПоВидам.Следующий() Цикл
			ВыборкаДоходовРаздела  = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДоходовРаздела.Следующий() Цикл
				ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаМесяцев.Следующий() Цикл
					ПоследнийМесяцДоходовИНалогов = Макс(ВыборкаМесяцев.Месяц, ПоследнийМесяцДоходовИНалогов);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;	
	КонецЕсли; 
	
	
	//-----------------------------------------------------------------------------
	// Заполнение данных раздела 3
	
	// признак резидента РФ
	ФизЛицо_Резидент = ДанныеОФизЛицеОрганизации.ФизЛицо_Резидент;

	// Переменные - итоги
	МесячныеДоходыБезВычетовРаздела3  = НовыйМассивЧисел(13);// сумма доходов по всем кодам доходов без вычетов помесячно (индекс первого месяца - 1)
	МесячныеСтандартныеВычеты  = НовыйМассивЧисел(13);// сумма стандартных вычетов по всем кодам вычетов помесячно
	
	// Сальдо по расчетам на начало года свернутое по всем ставкам
	СвернутоеСальдо  = 0;
	СоответствиеСальдоПоСтавкамНаНачалоГода = Новый Соответствие;
	Пока ВыборкаСальдоРасчетовНаНачалоГода.Следующий() Цикл
		СвернутоеСальдо = СвернутоеСальдо  + ВыборкаСальдоРасчетовНаНачалоГода.Сальдо;
		// заполним структуру сальд на начало года для расчета параметров 6 раздела
		Если ФизЛицо_Резидент Тогда
			СоответствиеСальдоПоСтавкамНаНачалоГода.Вставить(ВыборкаСальдоРасчетовНаНачалоГода.Ставка, ВыборкаСальдоРасчетовНаНачалоГода.Сальдо);
		Иначе// для нерезидента всё положим на ставку 13
			БылоСальдо = СоответствиеСальдоПоСтавкамНаНачалоГода.Получить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13); 
			СоответствиеСальдоПоСтавкамНаНачалоГода.Вставить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13, ?(БылоСальдо = Неопределено, 0, БылоСальдо) + ВыборкаСальдоРасчетовНаНачалоГода.Сальдо);
		КонецЕсли; 
		
	КонецЦикла; 
	Если СвернутоеСальдо > 0 Тогда
		Раздел3.Параметры["ДолгЗаНалогоплательщиком"] = СвернутоеСальдо;
	Иначе	
		Раздел3.Параметры["ДолгЗаНалоговымАгентом"] = - СвернутоеСальдо;
	КонецЕсли; 
	
	// Доходы с предыдущего месиа работы
	Раздел3.Параметры.ДоходыПредыдущегоМестаРаботы = ДанныеОФизЛицеОрганизации.ФизЛицо_ДоходыПредыдущегоМестаРаботы;

	
	// Доходы 13% (или все доходы нерезидента) по видам и по месяцам
	
	// спозиционируемся на доходах раздела 3
	ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
	Если ВыборкаДоходыПоМесяцамПоВидам.НайтиСледующий(Новый Структура("Раздел", "Раздел3")) тогда
		// выберем доходы раздела
		НомерДохода = 0;
		ВыборкаДоходовРаздела = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДоходовРаздела.Следующий() Цикл
			НомерДохода = НомерДохода + 1;
			
			Если НомерДохода <= 10 Тогда
				Раздел3.Параметры["КодДохода" + НомерДохода] = ВыборкаДоходовРаздела.КодДохода;// код дохода
				Раздел3.Параметры["Доход" + НомерДохода + "_Итого"] = ВыборкаДоходовРаздела.СуммаДохода;// доход за год
				
				ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаМесяцев.Следующий() Цикл
					Месяц = ВыборкаМесяцев.Месяц;
					Раздел3.Параметры["Доход" + НомерДохода + "_" + Месяц] = ВыборкаМесяцев.СуммаДохода;// доход за месяц по коду дохода
					// собираем суммы доходов без вычетов по всем видам доходов 
					МесячныеДоходыБезВычетовРаздела3[Месяц] = МесячныеДоходыБезВычетовРаздела3[Месяц] + ВыборкаМесяцев.СуммаДохода - ВыборкаМесяцев.СуммаВычета;
				КонецЦикла; 
			Иначе	
				Сообщить("Ошибка вывода данных (Раздел 3): форма не позволяет отразить более 10 кодов дохода!");				
			КонецЕсли; 
		
		КонецЦикла; 
	КонецЕсли;	
	
	// Вычеты к 13% доходам (или все вычеты нерезидента) по видам и по месяцам
	НомерВычета = 0;
	Пока ВыборкаВычетыПоМесяцамПоВидам.Следующий() Цикл
		НомерВычета = НомерВычета + 1;
		Если НомерВычета <= 5 Тогда
			Раздел3.Параметры["КодВычета" + НомерВычета] = "Код "+ ВыборкаВычетыПоМесяцамПоВидам.КодВычета;// код вычета
			Раздел3.Параметры["Вычет" + НомерВычета + "_Итого"] = ВыборкаВычетыПоМесяцамПоВидам.СуммаВычета;// вычет за год
			
			ВыборкаМесяцев  = ВыборкаВычетыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаМесяцев.Следующий() Цикл
				Месяц = ВыборкаМесяцев.Месяц;
				Раздел3.Параметры["Вычет" + НомерВычета + "_" + Месяц] = ВыборкаМесяцев.СуммаВычета;// вычет за месяц по коду вычета
			КонецЦикла; 
		Иначе	
			Сообщить("Ошибка вывода данных (раздел3): форма не позволяет отразить более 5 кодов вычетов!");
		КонецЕсли; 
	КонецЦикла; 
	
	// Переносим в документ совокупные доходы без вычетов в разрезе месяцев и совокупный нарастающий доход без вычетов 
	НарастающийДоходБезВычетов = 0;
	Для Месяц = 1 По ПоследнийМесяцДоходовИНалогов Цикл
		Раздел3.Параметры["МДБезВ_" + Месяц] = МесячныеДоходыБезВычетовРаздела3[Месяц];	
		НарастающийДоходБезВычетов = НарастающийДоходБезВычетов  + МесячныеДоходыБезВычетовРаздела3[Месяц];
		Раздел3.Параметры["ГДБезВ_" + Месяц] = НарастающийДоходБезВычетов;	
	КонецЦикла; 
	Раздел3.Параметры["МДБезВ_Итого"] = НарастающийДоходБезВычетов;	
	Раздел3.Параметры["ГДБезВ_Итого"] = НарастающийДоходБезВычетов;	
	
	// Стандартные вычеты (коды 101-113)
	
	СоответствиеВычетов = Новый Соответствие;
	СоциальныйВычет = НовыйМассивЧисел(13);
	
	// выборка стандартных вычетов не имеет итогов
	Пока ВыборкаСтандартныхВычетовФизЛица.Следующий() Цикл
		
		Месяц = ВыборкаСтандартныхВычетовФизЛица.Месяц;
		
		Если ЗначениеЗаполнено(ВыборкаСтандартныхВычетовФизЛица.КодВычета) Тогда
			НомерВычета = ВыборкаСтандартныхВычетовФизЛица.НомерСтрокиВычета;
			Если НомерВычета = 90 Тогда
				Если НалоговыйПериод >= 2009 Тогда
					Раздел3.Параметры["СоцВычет_" + Месяц] = ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет;// вычет за месяц по коду вычета
					СоциальныйВычет[Месяц] = ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет;
				КонецЕсли;
			ИначеЕсли НомерВычета > 0 Тогда
				СоответствиеВычетов.Вставить(НомерВычета, ?(СоответствиеВычетов[НомерВычета] = Неопределено, 0, СоответствиеВычетов[НомерВычета]) + ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет);
				Раздел3.Параметры["СВычет" + НомерВычета + "_" + Месяц] = ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет;// вычет за месяц по коду вычета
				// собираем суммы вычетов 
				МесячныеСтандартныеВычеты[Месяц] = МесячныеСтандартныеВычеты[Месяц] + ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет; 
				// Если право на вычет отличается от фактически предоставленного - отметим это
				Если СводноПоКодамОКАТО И ВыборкаСтандартныхВычетовФизЛица.ПримененныйВычет <> ВыборкаСтандартныхВычетовФизЛица.РазмерВычета Тогда
					Раздел3.Область("СВычет" + НомерВычета + "_" + Месяц).Примечание.Текст = "Фактическая сумма вычета не совпадает с правом на вычет:
					|	право на вычет = " + ПараметрВЧисло(ВыборкаСтандартныхВычетовФизЛица.РазмерВычета);
				КонецЕсли; 						
			Иначе	
				Сообщить("Ошибка вывода данных (раздел №3): указан непредусмотренный код стандартного налогового вычета - " + ВыборкаСтандартныхВычетовФизЛица.КодВычетаКод +"!");
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла; 
		
	Для каждого Элемент Из СоответствиеВычетов Цикл
		Если Не ЗначениеЗаполнено(Элемент.Значение) Тогда
			Продолжить;
		КонецЕсли; 
		НомерВычета = Элемент.Ключ;
		Раздел3.Параметры["СВычет" + НомерВычета + "_Итого"] = Элемент.Значение;// вычет за год
	КонецЦикла;
	
	// Итоги по стандартным вычетам (коды 101-107) нарастающим итогом
	НарастающиеСтандартныеВычеты = 0;
	НарастающиеСоциальныеВычеты = 0;
	Для Месяц = 1 По ПоследнийМесяцДоходовИНалогов Цикл
		НарастающиеСтандартныеВычеты = НарастающиеСтандартныеВычеты  + МесячныеСтандартныеВычеты[Месяц];
		Раздел3.Параметры["ГСВ_" + Месяц] = НарастающиеСтандартныеВычеты;	
		Если НалоговыйПериод >= 2009 Тогда
			НарастающиеСоциальныеВычеты = НарастающиеСоциальныеВычеты + СоциальныйВычет[Месяц];
			Раздел3.Параметры["ГСоцВ_" + Месяц] = НарастающиеСоциальныеВычеты;
			МесячныеСтандартныеВычеты[Месяц] = МесячныеСтандартныеВычеты[Месяц] + СоциальныйВычет[Месяц];
		КонецЕсли;
	КонецЦикла; 
	Раздел3.Параметры["ГСВ_Итого"] = НарастающиеСтандартныеВычеты;	
	Если НалоговыйПериод >= 2009 Тогда
		Раздел3.Параметры["СоцВычет_Итого"] = НарастающиеСоциальныеВычеты;	
	КонецЕсли;
	
	// Имущественные вычеты
	// выборка имущественных вычетов сгруппирована по Месяц
	НарастающиеИмущественныеВычеты = 0;
	НарастающийВычетКод311 = 0;
	НарастающийВычетКод312 = 0;
	Пока ВыборкаИмущественныеВычеты.Следующий() Цикл
		Месяц = ВыборкаИмущественныеВычеты.МесяцНП;
		Если Месяц > ПоследнийМесяцДоходовИНалогов Тогда
			Прервать
		КонецЕсли;
		НарастающиеИмущественныеВычеты = НарастающиеИмущественныеВычеты + ВыборкаИмущественныеВычеты.РазмерВычета;
		МесячныеСтандартныеВычеты[Месяц] = МесячныеСтандартныеВычеты[Месяц] + ВыборкаИмущественныеВычеты.РазмерВычета;
		Раздел3.Параметры["ГИВ_" + Месяц] = НарастающиеИмущественныеВычеты;
		ВыборкаКодов  = ВыборкаИмущественныеВычеты.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКодов.Следующий() Цикл
		    Если ВыборкаКодов.КодВычетаИмущественный = Справочники.ВычетыНДФЛ.Код311 Тогда
				Раздел3.Параметры["ИВычет1" + "_" + Месяц] = ВыборкаКодов.РазмерВычета;// вычет за месяц по коду вычета
				НарастающийВычетКод311 = НарастающийВычетКод311 + ВыборкаКодов.РазмерВычета;
			Иначе
				Раздел3.Параметры["ИВычет2" + "_" + Месяц] = ВыборкаКодов.РазмерВычета;// вычет за месяц по коду вычета
				НарастающийВычетКод312 = НарастающийВычетКод312 + ВыборкаКодов.РазмерВычета;
			КонецЕсли;
		КонецЦикла; 
	КонецЦикла; 
	Раздел3.Параметры["ГИВ_Итого"] = НарастающиеИмущественныеВычеты;	
	Раздел3.Параметры["ИВычет1_Итого"] = НарастающийВычетКод311;	
	Раздел3.Параметры["ИВычет2_Итого"] = НарастающийВычетКод312;	
	
	// Налоговая база с начала года
	НарастающаяНалоговаяБаза = 0;
	Для Месяц = 1 По ПоследнийМесяцДоходовИНалогов Цикл
		Если ФизЛицо_Резидент Тогда
			НарастающаяНалоговаяБаза = НарастающаяНалоговаяБаза + МесячныеДоходыБезВычетовРаздела3[Месяц] - МесячныеСтандартныеВычеты[Месяц];
			Раздел3.Параметры["НБаза_" + Месяц] = Макс(НарастающаяНалоговаяБаза,0);	
		Иначе	
			// Для нерезидента налоговая база показывается не нарастающим итогом
			Раздел3.Параметры["НБаза_" + Месяц] = Макс(МесячныеДоходыБезВычетовРаздела3[Месяц],0);	
		КонецЕсли; 
	КонецЦикла; 
	Раздел3.Параметры["НБаза_Итого"] = Макс(НарастающаяНалоговаяБаза,0);	
	
	ЕстьВычетОдинарный = Ложь;
	ЕстьВычетДвойной = Ложь;
	ЕстьВычетУчетверенный = Ложь;
	Для каждого Вычет Из СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод Цикл
	   Если Не ЕстьВычетОдинарный Тогда
	   	   ЕстьВычетОдинарный = Вычет = Справочники.ВычетыНДФЛ.Код101
	   КонецЕсли;
	   Если Не ЕстьВычетДвойной Тогда
	   	   ЕстьВычетДвойной = Вычет = Справочники.ВычетыНДФЛ.Код102 
		   				Или Вычет = Справочники.ВычетыНДФЛ.Код106
		   				Или Вычет = Справочники.ВычетыНДФЛ.Код111
	   КонецЕсли;
		
	   Если Не ЕстьВычетУчетверенный Тогда
	   	   ЕстьВычетУчетверенный = Вычет = Справочники.ВычетыНДФЛ.Код107 
		   				Или Вычет = Справочники.ВычетыНДФЛ.Код113
	   КонецЕсли;
	
	КонецЦикла;
	// Права на вычеты
	Раздел3.Параметры.ПравоНаВычетОдинарный		= ?(НалоговыйПериод > 2008,  "1000 руб", "600 руб") + ?(ЕстьВычетОдинарный, " - V", "");
	Раздел3.Параметры.ПравоНаВычетДвойной		= ?(НалоговыйПериод > 2008,  "2000 руб", "1200 руб") + ?(ЕстьВычетДвойной, " - V", "");
	Раздел3.Параметры.ПравоНаВычетУчетверенный	= ?(НалоговыйПериод > 2008,  "4000 руб", "2400 руб") + ?(ЕстьВычетУчетверенный, " - V", "");
	Раздел3.Параметры.ПравоНаВычет103 = "400 руб"  + ?(СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод.Найти(Справочники.ВычетыНДФЛ.Код103) <> Неопределено, " - V", "");
	Раздел3.Параметры.ПравоНаВычет104 = "500 руб"  + ?(СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод.Найти(Справочники.ВычетыНДФЛ.Код104) <> Неопределено, " - V", "");
	Раздел3.Параметры.ПравоНаВычет105 = "3000 руб" + ?(СписокРазличныхКодовСтандартныхВычетовФизЛицаЗаГод.Найти(Справочники.ВычетыНДФЛ.Код105) <> Неопределено, " - V", "");
	
	//-----------------------------------------------------------------------------
	// Раздел 4 - Дивиденды
	
	// Переменные - итоги
	// в принципе код доходов на дивиденды один (1010), но для универсальности заведем итоговые массивы
	МесячныеДоходыРаздела4  = НовыйМассивЧисел(13);// сумма доходов по всем кодам доходов помесячно (индекс первого месяца - 1)
	МесячныеВычетыРаздела4  = НовыйМассивЧисел(13);// сумма доходов по всем кодам доходов без вычетов помесячно (индекс первого месяца - 1)
	
	// Собирем доходы и вычеты по всем видам доходов 
	
	// спозиционируемся на доходах раздела 4
	ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
	Если ВыборкаДоходыПоМесяцамПоВидам.НайтиСледующий(Новый Структура("Раздел", "Раздел4")) тогда
		ВыборкаДоходовРаздела = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДоходовРаздела.Следующий() Цикл
			ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаМесяцев.Следующий() Цикл
				Месяц = ВыборкаМесяцев.Месяц;
				МесячныеДоходыРаздела4[Месяц] = МесячныеДоходыРаздела4[Месяц] + ВыборкаМесяцев.СуммаДохода;
				МесячныеВычетыРаздела4[Месяц] = МесячныеВычетыРаздела4[Месяц] + ВыборкаМесяцев.СуммаВычета;
			КонецЦикла; 
		КонецЦикла; 
	КонецЕсли;	
	
	// Выведем доходы и облагаемую базу
	ИтогоДоходовРаздела4 = 0;
	ИтогоНалоговаяБазаРаздела4 = 0;
	Для М =1 По ПоследнийМесяцДоходовИНалогов Цикл
		Раздел4.Параметры["Доход_" + М] = МесячныеДоходыРаздела4[М];	
		Раздел4.Параметры["НБаза_" + М]  = Макс(МесячныеДоходыРаздела4[М] - МесячныеВычетыРаздела4[М],0);	
		ИтогоДоходовРаздела4 = ИтогоДоходовРаздела4 + МесячныеДоходыРаздела4[М];
		ИтогоНалоговаяБазаРаздела4 = ИтогоНалоговаяБазаРаздела4 + МесячныеДоходыРаздела4[М] - МесячныеВычетыРаздела4[М];
	КонецЦикла; 
	Раздел4.Параметры["Доход_Итого"] = ИтогоДоходовРаздела4;	
	Раздел4.Параметры["НБаза_Итого"] = ИтогоНалоговаяБазаРаздела4;	
	
	
	//-----------------------------------------------------------------------------
	// Раздел 5 - Доходы по ставке 35%
	
	// Отразим каждый код дохода в отдельном пятом разделе
	
	// спозиционируемся на доходах раздела 5
	ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
	Если ВыборкаДоходыПоМесяцамПоВидам.НайтиСледующий(Новый Структура("Раздел", "Раздел5")) тогда
		
		НомерДохода = 0;
		
		ВыборкаДоходовРаздела = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДоходовРаздела.Следующий() Цикл
			
			НомерДохода = НомерДохода + 1;
			Если НомерДохода > 1 Тогда // В массиве областей раздела 5 первый элемент всегда присутствует
				МассивРазделов5.Добавить(Макет.ПолучитьОбласть("Раздел5"));
			КонецЕсли; 
			
			ОбластьРаздела = МассивРазделов5[НомерДохода - 1]; 
			ОбластьРаздела.Параметры["КодДохода"] 		= ВыборкаДоходовРаздела.КодДохода;	
			ОбластьРаздела.Параметры["Доход_Итого"] 	= ВыборкаДоходовРаздела.СуммаДохода;	
			ОбластьРаздела.Параметры["Вычет_Итого"]  	= ВыборкаДоходовРаздела.СуммаВычета;	
			ОбластьРаздела.Параметры["НБаза_Итого"]  	= ВыборкаДоходовРаздела.СуммаДохода - ВыборкаДоходовРаздела.СуммаВычета;	
			
			ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаМесяцев.Следующий() Цикл
				Месяц = ВыборкаМесяцев.Месяц;
				ОбластьРаздела.Параметры["Доход_" + Месяц] 	= ВыборкаМесяцев.СуммаДохода;	
				ОбластьРаздела.Параметры["Вычет_" + Месяц]  = ВыборкаМесяцев.СуммаВычета;	
				ОбластьРаздела.Параметры["НБаза_" + Месяц]  = Макс(ВыборкаМесяцев.СуммаДохода - ВыборкаМесяцев.СуммаВычета,0);	
			КонецЦикла; 
			
		КонецЦикла; 
	КонецЕсли;	
	
	//-----------------------------------------------------------------------------
	// Раздел 7 - Доходы, налогообложение которых осуществляется налоговыми  органами
	
	// Отразим суммы доходов
	
	// спозиционируемся на доходах раздела 7
	ВыборкаДоходыПоМесяцамПоВидам.Сбросить();
	Если ВыборкаДоходыПоМесяцамПоВидам.НайтиСледующий(Новый Структура("Раздел", "Раздел7")) тогда
		НомерДохода = 0;
		ВыборкаДоходовРаздела = ВыборкаДоходыПоМесяцамПоВидам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДоходовРаздела.Следующий() Цикл
			НомерДохода = НомерДохода + 1;
			
			Если НомерДохода <=3 Тогда
				Раздел7.Параметры["КодДохода"+НомерДохода] = ВыборкаДоходовРаздела.КодДохода;	
				Раздел7.Параметры["Доход" + НомерДохода + "_Итого"]	= ВыборкаДоходовРаздела.СуммаДохода;	
				
				ВыборкаМесяцев  = ВыборкаДоходовРаздела.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаМесяцев.Следующий() Цикл
					Месяц = ВыборкаМесяцев.Месяц;
					Раздел7.Параметры["Доход" + НомерДохода + "_" + Месяц] 	= ВыборкаМесяцев.СуммаДохода;	
				КонецЦикла; 
			Иначе	
				Сообщить("Ошибка вывода данных (Раздел 7): форма не позволяет отразить более 3 кодов дохода!");
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;	
	
	
	//-----------------------------------------------------------------------------
	// Одновременный вывод налогов по разделам 3,4,5: Начисленный, удержанный, переданный на взыскание, возвращенный, сальдо
	
	ВыборкаНалогиПомесяцамПоСтавкам.Сбросить();
	Пока ВыборкаНалогиПомесяцамПоСтавкам.Следующий() Цикл
		
		// Начисленный и удержанный налог отражаем нарастающим итогом только в 3 разделе для резидентов
		Раздел = ВыборкаНалогиПомесяцамПоСтавкам.Раздел;
		Если Раздел = "Раздел3" Тогда
			НомерРаздела = 3;
			ОбластьРаздела = Раздел3;
			СтавкаНалога = ?(ФизЛицо_Резидент, ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛотСтавкиНалогообложенияРезидента(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13), ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛСНерезидента());
		ИначеЕсли Раздел = "Раздел4" Тогда 	
			НомерРаздела = 4;
			ОбластьРаздела = Раздел4;
			СтавкаНалога = ?(ФизЛицо_Резидент, ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛотСтавкиНалогообложенияРезидента(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09),ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛСНерезидента(Справочники.ДоходыНДФЛ.Код1010,ДатаКонцаНП));
		Иначе	
			НомерРаздела = 5;
			ОбластьРаздела = МассивРазделов5[0]; // Все налоги покажем для первого 35% дохода
			СтавкаНалога = ПроцедурыУправленияПерсоналом.ЗначениеСтавкиНДФЛотСтавкиНалогообложенияРезидента(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35);
		КонецЕсли; 
		
		// Переложим данные из выборки в массивы
		НалогИсчисленный = НовыйМассивЧисел(13);
		НалогУдержанный = НовыйМассивЧисел(13);
		НалогИсчисленныйНарастающимИтогом = НовыйМассивЧисел(13);
		НалогУдержанныйНарастающимИтогом = НовыйМассивЧисел(13);
		НалогВозвращенный = НовыйМассивЧисел(13);
		НалогВзысканный = НовыйМассивЧисел(13);
		ВыборкаМесяцев  = ВыборкаНалогиПомесяцамПоСтавкам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаМесяцев.Следующий() Цикл
			Месяц = ВыборкаМесяцев.Месяц;
			НалогИсчисленный[Месяц] = ВыборкаМесяцев.Начислено;
			НалогУдержанный[Месяц] = ВыборкаМесяцев.Удержано;
			НалогИсчисленныйНарастающимИтогом[Месяц] = ВыборкаМесяцев.Начислено;
			НалогУдержанныйНарастающимИтогом[Месяц] = ВыборкаМесяцев.Удержано;
			НалогВзысканный[Месяц] = ВыборкаМесяцев.ПереданоНаВзыскание;
			НалогВозвращенный[Месяц] = ВыборкаМесяцев.Возвращено;
		КонецЦикла;
		
		// для подсчета сальдо по расчетам необходимо всегда использовать
		// даные нарастающим итогом
		ПреобразоватьМассивВНарастающий(НалогИсчисленныйНарастающимИтогом);
		ПреобразоватьМассивВНарастающий(НалогУдержанныйНарастающимИтогом);
			
		// Если надо, преобразуем массивы в нарастающие
		Если Раздел = "Раздел3" И ФизЛицо_Резидент Тогда
			ПреобразоватьМассивВНарастающий(НалогИсчисленный);
			ПреобразоватьМассивВНарастающий(НалогУдержанный);
			ПреобразоватьМассивВНарастающий(НалогВзысканный);
			ПреобразоватьМассивВНарастающий(НалогВозвращенный);
		КонецЕсли; 
		
		// Вывод данных в карточку
		Для М = 1 По ПоследнийМесяцДоходовИНалогов Цикл
			
			ОбластьРаздела.Параметры["НИ_" + М] 	= НалогИсчисленный[М];
			
			// Рассчитаем сумму налога от базы по ставке
			НалогИсчисленныйПоБазе = Окр(ПараметрВЧисло(ОбластьРаздела.Параметры["НБаза_" + М]) * СтавкаНалога/100,0);
			
			// Если налог исчислен неверно, то выделим цветом
			Если СводноПоКодамОКАТО И НалогИсчисленный[М]  <> НалогИсчисленныйПоБазе Тогда
				ЦветТекстаЯчейки =  ЦветПодсветкиНеверноИсчисленногоНалога;
				ОбластьРаздела.Области["Р"+ НомерРаздела + "НИ_" + М].Примечание.Текст = "Неверно исчислена сумма налога:
				|	Налоговая база = " + ПараметрВЧисло(ОбластьРаздела.Параметры["НБаза_" + М]) + "
				|	Ставка налога = " + СтавкаНалога + "%
				|	Налог исчисленный = " + НалогИсчисленныйПоБазе;
			Иначе	
				ЦветТекстаЯчейки =  ЦветЧерный;
			КонецЕсли; 						
            ОбластьРаздела.Области["Р" + НомерРаздела + "НИ_" + М].ЦветТекста = ЦветТекстаЯчейки;
			
			
			ОбластьРаздела.Параметры["НУ_" + М] 	= НалогУдержанный[М];
			ОбластьРаздела.Параметры["Взыск_" + М] 	= НалогВзысканный[М];
			ОбластьРаздела.Параметры["Возврат_" + М]= НалогВозвращенный[М];
			
			Сальдо = НалогИсчисленныйНарастающимИтогом[М] -  НалогУдержанныйНарастающимИтогом[М];
			Если Сальдо > 0 Тогда
				ОбластьРаздела.Параметры["ДолгНП_" + М] = Сальдо; // долг за налогоплательщиком
			Иначе
				ОбластьРаздела.Параметры["ДолгНА_" + М] = - Сальдо; // долг за налоговым агентом
			КонецЕсли; 
		КонецЦикла; 
		
		// Итоги по строкам
		ОбластьРаздела.Параметры["НИ_Итого"] = ВыборкаНалогиПомесяцамПоСтавкам.Начислено;
		ОбластьРаздела.Параметры["НУ_Итого"] = ВыборкаНалогиПомесяцамПоСтавкам.Удержано;
		ОбластьРаздела.Параметры["Взыск_Итого"] = ВыборкаНалогиПомесяцамПоСтавкам.ПереданоНаВзыскание;
		ОбластьРаздела.Параметры["Возврат_Итого"] = ВыборкаНалогиПомесяцамПоСтавкам.Возвращено;
		Сальдо = ВыборкаНалогиПомесяцамПоСтавкам.Начислено -  ВыборкаНалогиПомесяцамПоСтавкам.Удержано;
		Если Сальдо > 0 Тогда
			ОбластьРаздела.Параметры["ДолгНП_Итого"] = Сальдо; // долг за налогоплательщиком
		Иначе
			ОбластьРаздела.Параметры["ДолгНА_Итого"] = - Сальдо; // долг за налоговым агентом
		КонецЕсли; 
	КонецЦикла; 
	
	//-----------------------------------------------------------------------------
	// Раздел 6
	
	ИтогоНачислено = 0;
	ИтогоУдержано = 0;
	ИтогоПереданоНаВзыскание = 0;
	ИтогоСальдо = 0;
	
	Для Н = 1 по 3 Цикл
		
		Если Н = 1 Тогда
			СуффиксСтроки = "06";
			ОбластьРаздела = Раздел4;
			СальдоНаНачалоГода 	= СоответствиеСальдоПоСтавкамНаНачалоГода.Получить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка09);
		ИначеЕсли Н = 2 Тогда
			СуффиксСтроки = ?(ФизЛицо_Резидент,"13","30");
			ОбластьРаздела = Раздел3;
			СальдоНаНачалоГода 	= СоответствиеСальдоПоСтавкамНаНачалоГода.Получить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13);
		Иначе	
			СуффиксСтроки = "35";
			ОбластьРаздела = МассивРазделов5[0];
			СальдоНаНачалоГода 	= СоответствиеСальдоПоСтавкамНаНачалоГода.Получить(Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка35);
		КонецЕсли; 
		
		// Расчет параметров
		НачисленоЗаГод				= ПараметрВЧисло(ОбластьРаздела.Параметры["НИ_Итого"]);
		УдержаноЗаГод 				= ПараметрВЧисло(ОбластьРаздела.Параметры["НУ_Итого"]);
		ПереданоНаВзысканиеЗаГод 	= ПараметрВЧисло(ОбластьРаздела.Параметры["Взыск_Итого"]);
		ВозвращеноЗаГод 			= ПараметрВЧисло(ОбластьРаздела.Параметры["Возврат_Итого"]);
		
		СальдоНаКонецГода 			= ПараметрВЧисло(СальдоНаНачалоГода) + (НачисленоЗаГод - УдержаноЗаГод + ВозвращеноЗаГод) - ПереданоНаВзысканиеЗаГод;
		
		// Переписываем в таблицу
		Раздел6.Параметры["НИ_Ст" + СуффиксСтроки] = НачисленоЗаГод;
		// в колонке удержано показываем за минусом возврата 
		Раздел6.Параметры["НУ_Ст" + СуффиксСтроки] = УдержаноЗаГод - ВозвращеноЗаГод;
		Раздел6.Параметры["Взыск_Ст" + СуффиксСтроки] =  ПереданоНаВзысканиеЗаГод;
		Если СальдоНаКонецГода > 0 Тогда
			Раздел6.Параметры["ДолгНП_Ст" + СуффиксСтроки] =  СальдоНаКонецГода;
		Иначе	
			Раздел6.Параметры["ДолгНА_Ст" + СуффиксСтроки] =  - СальдоНаКонецГода;
		КонецЕсли; 
		
		// Расчет итогов по строкам
		ИтогоНачислено = ИтогоНачислено + НачисленоЗаГод;
		ИтогоУдержано = ИтогоУдержано + УдержаноЗаГод - ВозвращеноЗаГод;
		ИтогоПереданоНаВзыскание = ИтогоПереданоНаВзыскание + ПереданоНаВзысканиеЗаГод;
		ИтогоСальдо = ИтогоСальдо + СальдоНаКонецГода;
	КонецЦикла; 
	
	// вывод итогов по строкам раздела
	Раздел6.Параметры["НИ_Итого"] = ИтогоНачислено;
	Раздел6.Параметры["НУ_Итого"] = ИтогоУдержано;
	Раздел6.Параметры["Взыск_Итого"] =  ИтогоПереданоНаВзыскание;
	Если ИтогоСальдо > 0 Тогда
		Раздел6.Параметры["ДолгНП_Итого"] =  ИтогоСальдо;
	Иначе	
		Раздел6.Параметры["ДолгНА_Итого"] =  - ИтогоСальдо;
	КонецЕсли; 
	
	//-----------------------------------------------------------------------------
	// Раздел 9
	Раздел9.Параметры.ДатаПодписи = Формат(ДатаПодписи, "ДФ=dd.MM.yyyy");
	Раздел9.Параметры.ФИООтветственного = ДанныеОФизЛицеОрганизации.Ответственный_ФИО;
	Раздел9.Параметры.ДолжностьОтветственного = ДанныеОФизЛицеОрганизации.Ответственный_Должность;
	Раздел9.Параметры.НомерСправкиВИМНС = Строка(ДанныеОПредоставленныхСправках.ВнешнийНомерСправкиВИМНС) + ?(ПустаяСтрока(ДанныеОПредоставленныхСправках.НомерСправкиВИМНС),"", " (" + ДанныеОПредоставленныхСправках.НомерСправкиВИМНС + ")") ;
	Раздел9.Параметры.ДатаСправкиВИМНС = Формат(ДанныеОПредоставленныхСправках.ДатаСправкиВИМНС, "ДФ=dd.MM.yyyy");
	Раздел9.Параметры.НомерСправкиДляФизЛица = Строка(ДанныеОПредоставленныхСправках.ВнешнийНомерСправкиДляФизЛица) + ?(ПустаяСтрока(ДанныеОПредоставленныхСправках.НомерСправкиДляФизЛица), "", " (" + ДанныеОПредоставленныхСправках.НомерСправкиДляФизЛица + ")");
	Раздел9.Параметры.ДатаСправкиДляФизЛица = Формат(ДанныеОПредоставленныхСправках.ДатаСправкиДляФизЛица, "ДФ=dd.MM.yyyy");
	Раздел9.Параметры.НомерСправкиНаВзыскание = Строка(ДанныеОПредоставленныхСправках.ВнешнийНомерСправкиНаВзыскание) + ?(ПустаяСтрока(ДанныеОПредоставленныхСправках.НомерСправкиНаВзыскание), "", " (" + ДанныеОПредоставленныхСправках.НомерСправкиНаВзыскание + ")");
	Раздел9.Параметры.ДатаСправкиНаВзыскание = Формат(ДанныеОПредоставленныхСправках.ДатаСправкиНаВзыскание, "ДФ=dd.MM.yyyy");
	
	//Раздел3.Показать();
	
	//-----------------------------------------------------------------------------
	// Вывод страниц документа
	ДокументРезультат.Вывести(Раздел1);
	ДокументРезультат.Вывести(Раздел2);
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ДокументРезультат.Вывести(Раздел3);
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ДокументРезультат.Вывести(Раздел4);
	Для Каждого Раздел из МассивРазделов5  Цикл
		ДокументРезультат.Вывести(Раздел);
	КонецЦикла; 
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	ДокументРезультат.Вывести(Раздел6);
	ДокументРезультат.Вывести(Раздел7);
	ДокументРезультат.Вывести(Раздел8);
	ДокументРезультат.Вывести(Раздел9);
	ДокументРезультат.Область(1,1).Примечание.Текст = " ";
	ДокументРезультат.Область(1,1).Примечание.Текст = "";

	//Параметры документа
	ДокументРезультат.Автомасштаб 			= 	Истина;
	ДокументРезультат.ОриентацияСтраницы 	= 	ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ТолькоПросмотр		= 	Истина;

КонецПроцедуры


#КонецЕсли

