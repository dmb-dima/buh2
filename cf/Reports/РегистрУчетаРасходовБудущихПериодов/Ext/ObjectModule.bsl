#Если Клиент Тогда
	
Перем НП Экспорт;

Процедура ВывестиИтоговыеСтроки(ДокументРезультат, Макет, ВыборкаИсточник, ИтогоСуммаСписания, ИтогоНеСписаннаяСумма,ВыборкаРБП)

	НачалоПериода   = НачалоДня(ДатаНач);
	КонецПериода    = КонецДня(ДатаКон);

	ОбластьНаНачало = Макет.ПолучитьОбласть("НаНачало");
	ОбластьСтрока   = Макет.ПолучитьОбласть("Строка");

	ВыборкаСтрок    = ВыборкаИсточник.Выбрать();

	Пока ВыборкаСтрок.Следующий() Цикл

		Если ВыборкаСтрок.Период = НачалоПериода И ВыборкаСтрок.Списано = 0  Тогда

			ОбластьНаНачало.Параметры.СуммаНаНачалоПериода = ВыборкаСтрок.Остаток;
			ДокументРезультат.Вывести(ОбластьНаНачало);

		ИначеЕсли ВыборкаСтрок.Период = КонецПериода И ВыборкаСтрок.Списано = 0 Тогда

			ИтогоНеСписаннаяСумма = ИтогоНеСписаннаяСумма + ВыборкаСтрок.Остаток;

		ИначеЕсли ВыборкаСтрок.Списано <= 0 Тогда

			Продолжить

		Иначе

			ОбластьСтрока.Параметры.ДатаОперации = ВыборкаСтрок.Период;
			ОбластьСтрока.Параметры.Док = ВыборкаСтрок.Регистратор;

			Начало    = ВыборкаРБП.Субконто1.ДатаНачалаСписания;
			Окончание = ВыборкаСтрок.Период;

			Если (ЗначениеЗаполнено(Начало)) 
			   И (ЗначениеЗаполнено(Окончание))
			   И (Начало <= Окончание) Тогда

				КоличествоМесяцев = (Год(Окончание) - Год(Начало)) * 12;
				КоличествоМесяцев = КоличествоМесяцев + Месяц(Окончание) - Месяц(Начало);

				Если День(Начало) - День(Окончание) <= 3 Тогда
					КоличествоМесяцев = КоличествоМесяцев + 1;
				КонецЕсли;

			Иначе
				КоличествоМесяцев = "";

			КонецЕсли;

			ОбластьСтрока.Параметры.КолвоМесяцевФактическогоСписания = КоличествоМесяцев;
			ОбластьСтрока.Параметры.СуммаСписания                    = ВыборкаСтрок.Списано;
			ОбластьСтрока.Параметры.НеСписаннаяСумма                 = ВыборкаСтрок.Остаток;

			ДокументРезультат.Вывести(ОбластьСтрока);

			ИтогоСуммаСписания = ИтогоСуммаСписания + ВыборкаСтрок.Списано;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ВывестиИтоговыеСтроки()

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//

Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	НачалоПериода = НачалоДня(ДатаНач);
	КонецПериода = КонецДня(ДатаКон);

	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");

	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(НачалоПериода, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(КонецПериода, "ДФ=dd.MM.yyyy");
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация);
	НазваниеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = НазваниеОрганизации;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	// Проверим заполнение обязательных реквизитов
	Если НалоговыйУчет.ПроверитьЗаполнениеОбязательныхРеквизитов(ДатаНач,ДатаКон,Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;

	Массив = Новый Массив;
	Массив.Добавить(Перечисления.ВидыРБП.УбыткиПрошлыхЛет);
	Массив.Добавить(Перечисления.ВидыРБП.УбыткиПрошлыхЛетОбслуживающихПроизводствИХозяйств);
	Массив.Добавить(Перечисления.ВидыРБП.УбыткиОтРеализацииАмортизируемогоИмущества);
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач",        НачалоПериода);
	Запрос.УстановитьПараметр("ДатаКон",        КонецПериода);
	Запрос.УстановитьПараметр("Счет97",         ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов);
	Запрос.УстановитьПараметр("Счет76",         ПланыСчетов.Хозрасчетный.ПлатежиПоДобровольномуСтрахованиюРаботников);
	Запрос.УстановитьПараметр("СчетИсключение", ПланыСчетов.Хозрасчетный.РасходыНаОплатуТрудаБудущихПериодов);
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("Убытки",         Массив);
	Запрос.УстановитьПараметр("ВидыСубконто",   ВидыСубконто);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Субконто1 КАК Субконто1,
	|	ВложенныйЗапрос.Период КАК Период,
	|	СУММА(ВложенныйЗапрос.Списано) КАК Списано,
	|	МИНИМУМ(ВложенныйЗапрос.Остаток) КАК Остаток,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Сумма КАК Сумма,
	|	ВложенныйЗапрос.Субконто2 КАК Субконто2,
	|	ВложенныйЗапрос.Субконто3 КАК Субконто3
	|ИЗ
	|	(ВЫБРАТЬ
	|		ХозрасчетныйОстаткиИОбороты97.Субконто1 КАК Субконто1,
	|		ХозрасчетныйОстаткиИОбороты97.Период КАК Период,
	|		ХозрасчетныйОстаткиИОбороты97.СуммаНУОборотКт КАК Списано,
	|		ХозрасчетныйОстаткиИОбороты97.СуммаНУКонечныйОстатокДт КАК Остаток,
	|		ХозрасчетныйОстаткиИОбороты97.Регистратор КАК Регистратор,
	|		ХозрасчетныйОстаткиИОбороты97.Субконто1.Сумма КАК Сумма,
	|		ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты97.Субконто2, 0) КАК Субконто2,
	|		ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты97.Субконто3, 0) КАК Субконто3
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
	|				&ДатаНач,
	|				&ДатаКон,
	|				Регистратор,
	|				ДвиженияИГраницыПериода,
	|				Счет В ИЕРАРХИИ (&Счет97),
	|				,
	|				Организация = &Организация
	|					И (НЕ Субконто1.ВидРБП В (&Убытки))) КАК ХозрасчетныйОстаткиИОбороты97
	|	ГДЕ
	|		ХозрасчетныйОстаткиИОбороты97.Счет <> &СчетИсключение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ХозрасчетныйОстаткиИОбороты76.Субконто1,
	|		ХозрасчетныйОстаткиИОбороты76.Период,
	|		ХозрасчетныйОстаткиИОбороты76.СуммаНУОборотКт,
	|		ХозрасчетныйОстаткиИОбороты76.СуммаНУКонечныйОстатокДт,
	|		ХозрасчетныйОстаткиИОбороты76.Регистратор,
	|		ХозрасчетныйОстаткиИОбороты76.Субконто1.Сумма,
	|		ЕСТЬNULL(ХозрасчетныйОстаткиИОбороты76.Субконто2, 0),
	|		0
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
	|				&ДатаНач,
	|				&ДатаКон,
	|				Регистратор,
	|				ДвиженияИГраницыПериода,
	|				Счет В ИЕРАРХИИ (&Счет76),
	|				&ВидыСубконто,
	|				Организация = &Организация
	|					И (НЕ Субконто1.ВидРБП В (&Убытки))) КАК ХозрасчетныйОстаткиИОбороты76
	|	ГДЕ
	|		ХозрасчетныйОстаткиИОбороты76.Счет <> &СчетИсключение) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Субконто1,
	|	ВложенныйЗапрос.Сумма,
	|	ВложенныйЗапрос.Субконто2,
	|	ВложенныйЗапрос.Субконто3
	|
	|УПОРЯДОЧИТЬ ПО
	|	Субконто1,
	|	Субконто2,
	|	Субконто3,
	|	Период
	|ИТОГИ ПО
	|	Субконто1,
	|	Субконто2,
	|	Субконто3";
	Результат = Запрос.Выполнить();

	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьЗаголовокРБП = Макет.ПолучитьОбласть("ЗаголовокРБП");
	ОбластьГруппа       = Макет.ПолучитьОбласть("Группа");
	ОбластьНаНачало     = Макет.ПолучитьОбласть("НаНачало");
	ОбластьСтрока       = Макет.ПолучитьОбласть("Строка");
	ОбластьИтого        = Макет.ПолучитьОбласть("Итого");
	ОбластьПодвал       = Макет.ПолучитьОбласть("Подвал");

	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);

	ВыборкаРБП = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаРБП.Следующий() Цикл

		ИтогоСуммаСписания    = 0;
		ИтогоНеСписаннаяСумма = 0;

		ОбластьЗаголовокРБП.Параметры.Статья                = ВыборкаРБП.Субконто1.Наименование;
		ОбластьЗаголовокРБП.Параметры.РБП                   = ВыборкаРБП.Субконто1;
		ОбластьЗаголовокРБП.Параметры.ОбщаяСуммаРасходов    = Формат(ВыборкаРБП.Субконто1.Сумма, "ЧДЦ=2");
		ОбластьЗаголовокРБП.Параметры.ВидРасхода            = ВыборкаРБП.Субконто1.ВидРБП;
		ОбластьЗаголовокРБП.Параметры.СрокВключенияРасходов = Формат(ВыборкаРБП.Субконто1.ДатаОкончанияСписания, "ДФ=dd.MM.yyyy");
		ОбластьЗаголовокРБП.Параметры.ДатаНачалаУчета       = Формат(ВыборкаРБП.Субконто1.ДатаНачалаСписания, "ДФ=dd.MM.yyyy");

		ДокументРезультат.Вывести(ОбластьЗаголовокРБП);

		ВыборкаРаботников = ВыборкаРБП.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Субконто2");

		Пока ВыборкаРаботников.Следующий() Цикл

			Отступ = "    ";

			Если ЗначениеЗаполнено(ВыборкаРаботников.Субконто2) Тогда

				ОбластьГруппа.Параметры.Группировка      = Отступ + ВыборкаРаботников.Субконто2;
				ОбластьГруппа.Параметры.СуммаСписания    = ВыборкаРаботников.Списано;
				ОбластьГруппа.Параметры.НеСписаннаяСумма = ВыборкаРаботников.Остаток;
				ДокументРезультат.Вывести(ОбластьГруппа);

				Отступ = Отступ + "    ";

			КонецЕсли;

			ВыборкаНачислений = ВыборкаРаботников.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Субконто3");

			Пока ВыборкаНачислений.Следующий() Цикл

				Если ЗначениеЗаполнено(ВыборкаНачислений.Субконто3) Тогда

					ОбластьГруппа.Параметры.Группировка      = Отступ + ВыборкаНачислений.Субконто3;
					ОбластьГруппа.Параметры.СуммаСписания    = ВыборкаНачислений.Списано;
					ОбластьГруппа.Параметры.НеСписаннаяСумма = ВыборкаНачислений.Остаток;
					ДокументРезультат.Вывести(ОбластьГруппа);

				КонецЕсли;

				ВывестиИтоговыеСтроки(ДокументРезультат, Макет, ВыборкаНачислений, ИтогоСуммаСписания, ИтогоНеСписаннаяСумма,ВыборкаРБП);

			КонецЦикла;

		КонецЦикла;

		ОбластьИтого.Параметры.ИтогоСуммаСписания = ИтогоСуммаСписания;
		ОбластьИтого.Параметры.НеСписаннаяСумма   = ИтогоНеСписаннаяСумма;

		ДокументРезультат.Вывести(ОбластьИтого);

	КонецЦикла;

	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);

	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;

	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры // СформироватьОтчет

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;

#КонецЕсли