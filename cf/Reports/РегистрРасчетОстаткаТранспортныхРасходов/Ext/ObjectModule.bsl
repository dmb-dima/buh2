#Если Клиент Тогда
	
Перем НП Экспорт;

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	НачалоПериода = НачалоМесяца(ДатаНач);
	КонецПериода = КонецМесяца(ДатаКон);
	
	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");

	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(НачалоПериода, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(КонецПериода, "ДФ=dd.MM.yyyy");
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация);
	НазваниеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = НазваниеОрганизации;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	// Проверим заполнение обязательных реквизитов
	Если НалоговыйУчет.ПроверитьЗаполнениеОбязательныхРеквизитов(ДатаНач,ДатаКон,Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;

	СписокСчетовТоваров = Новый Массив;
	СписокСчетовТоваров.Добавить(ПланыСчетов.Хозрасчетный.ТоварыНаСкладах);
	СписокСчетовТоваров.Добавить(ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ);
	СписокСчетовТоваров.Добавить(ПланыСчетов.Хозрасчетный.ТараПодТоваромИПорожняя);
	СписокСчетовТоваров.Добавить(ПланыСчетов.Хозрасчетный.ПокупныеИзделия);
	СписокСчетовТоваров.Добавить(ПланыСчетов.Хозрасчетный.ПокупныеТоварыОтгруженные);
	
	// Формируем запрос по виду затрат
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Начало", НачалоПериода);
	Запрос.УстановитьПараметр("Конец",  КонецПериода);
	Запрос.УстановитьПараметр("ВидСубконто",  ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("ВидЗатрат",    Перечисления.ВидыРасходовНУ.ТранспортныеРасходы);
	Запрос.УстановитьПараметр("Счет",		  ПланыСчетов.Хозрасчетный.ИздержкиОбращения);
	Запрос.УстановитьПараметр("Организация",  Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НалоговыйОстаткиИОбороты.Период КАК Период,
	|	СУММА(НалоговыйОстаткиИОбороты.СуммаНУНачальныйОстатокДт) КАК СНД44,
	|	СУММА(НалоговыйОстаткиИОбороты.СуммаНУКонечныйОстатокДт) КАК СКД44,
	|	СУММА(НалоговыйОстаткиИОбороты.СуммаНУОборотДт) КАК ДО44
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&Начало, &Конец, Месяц, ДвиженияИГраницыПериода, Счет = (&Счет), , Организация = &Организация) КАК НалоговыйОстаткиИОбороты
    |ГДЕ
	|НалоговыйОстаткиИОбороты.Субконто1.ВидРасходовНУ = &ВидЗатрат
	|
	|СГРУППИРОВАТЬ ПО
	|	НалоговыйОстаткиИОбороты.Период
	|УПОРЯДОЧИТЬ ПО
	|	Период
	//|ИТОГИ СУММА(СНД44), СУММА(СКД44), СУММА(ДО44) ПО
	//|Период ПЕРИОДАМИ(МЕСЯЦ, &Начало, &Конец)
	|";
	
	Результат = Запрос.Выполнить().Выбрать();
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");
	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
	ВсегоСуммаСписанияТранспортныхРасходов = 0;
	
     ТекущаяДата = ДатаНач;
	
	Пока Результат.Следующий() Цикл
	// Состав показателей регистра (графа/колонка таблицы)
	//  1 / Период
	//  2 / СНД44 
	//  3 / ДО44
	//  4 / СНД41 
	//  5 / ДО41
	//  6 / Продано
	//  7 / ПроданоЕНВД
	//  8 / Списано-Продано
	//  9 / СКД41 
	// 10 / (расчетная)
	// 11 / (расчетная, контрольная по СКД44)
	// 12 / (расчетная)
	
	СКД44 = ?(Результат.СКД44 = NULL, 0,Результат.СКД44);
	СНД44 = ?(Результат.СНД44 = NULL, 0,Результат.СНД44);
	ДО44  = ?(Результат.ДО44  = NULL, 0,Результат.ДО44);
	ТранспортныеРасходы = СНД44+ДО44;
	ПараметрыТР = Новый Структура("СуммаРасходовКсписанию, СуммаРасходовКсписаниюНУ, СуммаРасходовКсписаниюПР, СуммаРасходовКсписаниюВР, НачОстатокТоваров,ПриходТовара,СуммаРеализованныхТоваров,СуммаРеализованныхТоваровЕНДВ,Списано,КонОстатокТоваров,СреднийПроцент, СреднийПроцентНУ, СреднийПроцентПР, СреднийПроцентВР,СуммаРасходов, СуммаРасходовНУ, СуммаРасходовПР, СуммаРасходовВР,ДоляРасхода",
								  0,0,0,0,0,0,0,0,0,0,0,0,0,0,ТранспортныеРасходы,0,0,0,0);
	 НалоговыйУчет.РасчетТранспортныхРасходов(НачалоМесяца(Результат.Период),КонецМесяца(Результат.Период), Организация, ПараметрыТР);  

		ОбластьСтрока.Параметры.ДатаОперации        				= Результат.Период;
		ОбластьСтрока.Параметры.СуммаПрямыхРасходовНаНачало         = СНД44;
		ОбластьСтрока.Параметры.СуммаТранспортныхРасходовЗаМесяц 	= ДО44;
		ОбластьСтрока.Параметры.ОстатокТовараНаНачало 				= ПараметрыТР.НачОстатокТоваров;
		ОбластьСтрока.Параметры.ПриобретеноТоваровЗаМесяц 			= ПараметрыТР.ПриходТовара;
		ОбластьСтрока.Параметры.РеализованноТоваровЗаМесяц 			= ПараметрыТР.СуммаРеализованныхТоваров;
		ОбластьСтрока.Параметры.ВыбылоПоЕНДВЗаМесяц 				= ПараметрыТР.СуммаРеализованныхТоваровЕНДВ;
		ОбластьСтрока.Параметры.ПрочееВыбытиеЗаМесяц 				= ПараметрыТР.Списано;
		ОбластьСтрока.Параметры.ОстатокТовараНаКонец 				= ПараметрыТР.КонОстатокТоваров;
		ОбластьСтрока.Параметры.СреднийПроцент 						= ПараметрыТР.СреднийПроцент;
		ОстатокТранспортныхРасходов                                 = СНД44 + ДО44 - ПараметрыТР.СуммаРасходовКсписанию;
		ОбластьСтрока.Параметры.СуммаПрямыхРасходовДляОстаткаТоваров = ОстатокТранспортныхРасходов;
		Если ОстатокТранспортныхРасходов <> Результат.СКД44 Тогда
			Сообщить(Формат(Результат.Период, "ДФ=ММММ.yyyy")+ " г. Результат расчета не совпадает с данными на счетах по контрольному показателю:",СтатусСообщения.Внимание); 
			Сообщить("Остаток транспортных расходов на конец месяца (расчетный): "+ОстатокТранспортныхРасходов);
			Сообщить("Остаток транспортных расходов на конец месяца (по счетам): "+Результат.СКД44);
		КонецЕсли;
		ОбластьСтрока.Параметры.СуммаРасходаТекущегоМесяца 			= ПараметрыТР.СуммаРасходовКсписанию;
		ВсегоСуммаСписанияТранспортныхРасходов						= ВсегоСуммаСписанияТранспортныхРасходов + ПараметрыТР.СуммаРасходовКсписанию;		
		ДокументРезультат.Вывести(ОбластьСтрока);
		
	КонецЦикла;

	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);
	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;
	ОбластьПодвал.Параметры.ИтогоСуммаРасходовЗаПериод = ВсегоСуммаСписанияТранспортныхРасходов;
	ДокументРезультат.Вывести(ОбластьПодвал);
	
КонецПроцедуры // СформироватьОтчет

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;

#КонецЕсли