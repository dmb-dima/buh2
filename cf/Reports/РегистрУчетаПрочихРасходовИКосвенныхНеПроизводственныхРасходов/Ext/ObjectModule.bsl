#Если Клиент Тогда
	
Перем НП Экспорт;

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт
	
	ДокументРезультат.Очистить();

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");

	ОбластьЗаголовок.Параметры.Заголовок = ?(ВидОтчета = 1, "Регистр учета прочих расходов текущего периода",
	                                                       "Регистр учета косвенных расходов текущего периода");
	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(ДатаНач, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(ДатаКон, "ДФ=dd.MM.yyyy");
	СведенияОбОрганизации = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(Организация);
	НазваниеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = НазваниеОрганизации;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	// Проверим заполнение обязательных реквизитов
	Если НалоговыйУчет.ПроверитьЗаполнениеОбязательныхРеквизитов(ДатаНач,ДатаКон,Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНач",       НачалоДня(ДатаНач));
	Запрос.УстановитьПараметр("ДатаКон",       КонецДня(ДатаКон));
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("ВидСубконто",   ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
	Запрос.УстановитьПараметр("Счет91",        ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы);
	Запрос.УстановитьПараметр("ПрочиеКосвенные",Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеКосвенныеРасходы);
	Запрос.УстановитьПараметр("НалогиИСборы",Перечисления.ВидыПрочихДоходовИРасходов.НалогиИСборы);
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.ВидыРБП.УбыткиПрошлыхЛет);
	Массив.Добавить(Перечисления.ВидыРБП.УбыткиПрошлыхЛетОбслуживающихПроизводствИХозяйств);
	Массив.Добавить(Перечисления.ВидыРБП.УбыткиОтРеализацииАмортизируемогоИмущества);
	Запрос.УстановитьПараметр("Убытки",    Массив);

	СчетаЗатрат = Новый Массив;
	
	Если ВидОтчета = 1 Тогда
		
    	Запрос.УстановитьПараметр("ЕСН",                       Перечисления.ВидыРасходовНУ.ЕСН);
    	Запрос.УстановитьПараметр("СтраховыеВзносы",           Перечисления.ВидыРасходовНУ.СтраховыеВзносы);
		Запрос.УстановитьПараметр("НалогиИСборы",              Перечисления.ВидыРасходовНУ.НалогиИСборы);
		Запрос.УстановитьПараметр("Командировочные",           Перечисления.ВидыРасходовНУ.КомандировочныеРасходы);
		Запрос.УстановитьПараметр("Представительские",         Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы);
		Запрос.УстановитьПараметр("РасходыНаРекламу",          Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые);
		Запрос.УстановитьПараметр("РемонтОсновныхСредств",     Перечисления.ВидыРасходовНУ.РемонтОсновныхСредств);
		Запрос.УстановитьПараметр("ОсвоениеПриродныхРесурсов", Перечисления.ВидыРасходовНУ.ОсвоениеПриродныхРесурсов);
		Запрос.УстановитьПараметр("НаНИОКР",                   Перечисления.ВидыРасходовНУ.НИОКР);
		Запрос.УстановитьПараметр("ОбязательноеИДобровольноеСтрахованиеИмущества", Перечисления.ВидыРасходовНУ.ОбязательноеИДобровольноеСтрахованиеИмущества);
		Запрос.УстановитьПараметр("АрендаФедеральногоИМуниципальногоИмущества",    Перечисления.ВидыРасходовНУ.АрендаФедеральногоИМуниципальногоИмущества);
		Запрос.УстановитьПараметр("Прочие",                    Перечисления.ВидыРасходовНУ.ПрочиеРасходы);
		Запрос.УстановитьПараметр("Счет97",                  ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов);
		Запрос.УстановитьПараметр("ВидРБП",                  Перечисления.ВидыРБП.УбыткиОтРеализацииАмортизируемогоИмущества);
		
		ВидыЗатрат = Новый Массив;
    	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ЕСН);
    	ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.СтраховыеВзносы);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.НалогиИСборы);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.КомандировочныеРасходы);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.РемонтОсновныхСредств);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ОсвоениеПриродныхРесурсов);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.НИОКР);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ОбязательноеИДобровольноеСтрахованиеИмущества);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.АрендаФедеральногоИМуниципальногоИмущества);
		ВидыЗатрат.Добавить(Перечисления.ВидыРасходовНУ.ПрочиеРасходы);
		Запрос.УстановитьПараметр("ВидыЗатрат", ВидыЗатрат);
		Запрос.УстановитьПараметр("СчетаЗатрат",   НАлоговыйУчет.ПолучитьМассивСчетовУчетаРасходов());
	Иначе
		СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
		СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаПродажу);
		Запрос.УстановитьПараметр("СчетаЗатрат",   СчетаЗатрат);
	КонецЕсли;
	
	
	Запрос.УстановитьПараметр("СчетаИздержекОбращения",   ПланыСчетов.Хозрасчетный.ИздержкиОбращения);
	Запрос.УстановитьПараметр("ТранспортныеРасходы", Перечисления.ВидыРасходовНУ.ТранспортныеРасходы);
	
	Если ВидОтчета = 1 Тогда
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Период КАК ДатаОперации,
	|	ХозрасчетныйОборотыДтКт.Регистратор КАК Расшифровка,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &ЕСН
	|			ТОГДА ""ЕСН""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &СтраховыеВзносы
	|			ТОГДА ""Страховые взносы""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &НалогиИСборы
	|			ТОГДА ""Налоги и сборы""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &Командировочные
	|			ТОГДА ""Командировочные""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &Представительские
	|			ТОГДА ""Представительские расходы""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &РасходыНаРекламу
	|			ТОГДА ""Расходы на рекламу""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &РемонтОсновныхСредств
	|			ТОГДА ""Расходы на ремонт основных средств""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &ОсвоениеПриродныхРесурсов
	|			ТОГДА ""Расходы на освоение природных ресурсов""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &НаНИОКР
	|			ТОГДА ""Расходы на научные исследования и (или) опытно-конструкторские разработки""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &ОбязательноеИДобровольноеСтрахованиеИмущества
	|			ТОГДА ""Расходы на обязательное и добровольное страхование имущества""
	|		КОГДА ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ = &АрендаФедеральногоИМуниципальногоИмущества
	|			ТОГДА ""Расходы  по  аренде федерального имущества""
	|		ИНАЧЕ ""Прочие""
	|	КОНЕЦ КАК ВидЗатрат,
	|	ХозрасчетныйОборотыДтКт.СуммаНУОборотДт КАК Сумма,
	|	Хозрасчетный.Содержание,
	|	Хозрасчетный.НомерСтроки КАК НомерПроводки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт В ИЕРАРХИИ (&СчетаЗатрат),
	|			&ВидСубконто,
	|			,
	|			,
	|			Организация = &Организация
	|				И СубконтоДт1.ВидРасходовНУ В (&ВидыЗатрат)) КАК ХозрасчетныйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО ХозрасчетныйОборотыДтКт.Регистратор = Хозрасчетный.Регистратор
	|			И ХозрасчетныйОборотыДтКт.НомерСтроки = Хозрасчетный.НомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Период,
	|	ХозрасчетныйОборотыДтКт.Регистратор,
	|	""Убыток от реализации амортизируемого имущества"",
	|	ХозрасчетныйОборотыДтКт.СуммаНУОборотДт,
	|	Хозрасчетный.Содержание,
	|	Хозрасчетный.НомерСтроки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт В ИЕРАРХИИ (&Счет91),
	|			,
	|			СчетКт В ИЕРАРХИИ (&Счет97),
	|			,
	|			Организация = &Организация
	|				И СубконтоКт1.ВидРБП В (&Убытки)) КАК ХозрасчетныйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО ХозрасчетныйОборотыДтКт.Регистратор = Хозрасчетный.Регистратор
	|			И ХозрасчетныйОборотыДтКт.НомерСтроки = Хозрасчетный.НомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Период,
	|	ХозрасчетныйОборотыДтКт.Регистратор,
	|	""Прочие косвенные расходы"",
	|	ХозрасчетныйОборотыДтКт.СуммаНУОборотДт,
	|	Хозрасчетный.Содержание,
	|	Хозрасчетный.НомерСтроки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт В ИЕРАРХИИ (&Счет91),
	|			,
	|			,
	|			,
	|			Организация = &Организация
	|				И (СубконтоДт1.ВидПрочихДоходовИРасходов = &ПрочиеКосвенные ИЛИ СубконтоДт1.ВидПрочихДоходовИРасходов = &НалогиИСборы)) КАК ХозрасчетныйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО ХозрасчетныйОборотыДтКт.Регистратор = Хозрасчетный.Регистратор
	|			И ХозрасчетныйОборотыДтКт.НомерСтроки = Хозрасчетный.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОперации,
	|	НомерПроводки";
	
Иначе
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Период КАК ДатаОперации,
	|	ХозрасчетныйОборотыДтКт.Регистратор КАК Расшифровка,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОборотыДтКт.СчетДт В ИЕРАРХИИ (&Сч442)
	|			ТОГДА ""Коммерческие расходы""
	|		КОГДА ХозрасчетныйОборотыДтКт.СчетДт В ИЕРАРХИИ (&Сч26)
	|			ТОГДА ""Общехозяйственные расходы""
	|		ИНАЧЕ ""Расходы на продажу""
	|	КОНЕЦ КАК ВидЗатрат,
	|	ХозрасчетныйОборотыДтКт.СуммаНУОборотДт КАК Сумма,
	|	Хозрасчетный.Содержание,
	|	Хозрасчетный.НомерСтроки КАК НомерПроводки,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидРасходовНУ КАК ВидЗатратНУ
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт В ИЕРАРХИИ (&СчетаЗатрат),
	|			&ВидСубконто,
	|			,
	|			,
	|			Организация = &Организация
	|				И (НЕ СубконтоДт1.ВидРасходовНУ = &ТранспортныеРасходы)) КАК ХозрасчетныйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО ХозрасчетныйОборотыДтКт.Регистратор = Хозрасчетный.Регистратор
	|			И ХозрасчетныйОборотыДтКт.НомерСтроки = Хозрасчетный.НомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Период,
	|	ХозрасчетныйОборотыДтКт.Регистратор,
	|	""Прочие косвенные расходы"",
	|	ХозрасчетныйОборотыДтКт.СуммаНУОборотДт,
	|	Хозрасчетный.Содержание,
	|	Хозрасчетный.НомерСтроки,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт В ИЕРАРХИИ (&Счет91),
	|			,
	|			,
	|			,
	|			Организация = &Организация
	|				И СубконтоДт1.ВидПрочихДоходовИРасходов = &ПрочиеКосвенные) КАК ХозрасчетныйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО ХозрасчетныйОборотыДтКт.Регистратор = Хозрасчетный.Регистратор
	|			И ХозрасчетныйОборотыДтКт.НомерСтроки = Хозрасчетный.НомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОборотыДтКт.Период,
	|	ХозрасчетныйОборотыДтКт.Регистратор,
	|	""Налоги и сборы"",
	|	ХозрасчетныйОборотыДтКт.СуммаНУОборотДт,
	|	Хозрасчетный.Содержание,
	|	Хозрасчетный.НомерСтроки,
	|	ХозрасчетныйОборотыДтКт.СубконтоДт1.ВидПрочихДоходовИРасходов
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
	|			&ДатаНач,
	|			&ДатаКон,
	|			Запись,
	|			СчетДт В ИЕРАРХИИ (&Счет91),
	|			,
	|			,
	|			,
	|			Организация = &Организация
	|				И СубконтоДт1.ВидПрочихДоходовИРасходов = &НалогиИСборы) КАК ХозрасчетныйОборотыДтКт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО ХозрасчетныйОборотыДтКт.Регистратор = Хозрасчетный.Регистратор
	|			И ХозрасчетныйОборотыДтКт.НомерСтроки = Хозрасчетный.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОперации,
	|	НомерПроводки";
	
	Запрос.УстановитьПараметр("Сч442",        ПланыСчетов.Хозрасчетный.КоммерческиеРасходы);
	Запрос.УстановитьПараметр("Сч26",        ПланыСчетов.Хозрасчетный.ОбщехозяйственныеРасходы);
	
КонецЕсли;

	Результат = Запрос.Выполнить();

	ТаблицаОтладки = Новый ТаблицаЗначений;
	ТаблицаОтладки = Результат.Выгрузить();

	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");

	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);

	Выборка = Результат.Выбрать();

	СписокВидов = Новый СписокЗначений();
	Для каждого ЭлементСписка из ВидыРасходов Цикл

		Если ЭлементСписка.Пометка Тогда
			СписокВидов.Добавить(ЭлементСписка.Значение);
		КонецЕсли;

	КонецЦикла;

	ИтогоСумма = 0;

	Пока Выборка.Следующий() Цикл

		Если СписокВидов.НайтиПоЗначению(Выборка.ВидЗатрат) = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ОбластьСтрока.Параметры.ДатаОперации  = Выборка.ДатаОперации;
		ОбластьСтрока.Параметры.СтатьяРасхода = Выборка.ВидЗатрат + ?(ВидОтчета = 2, ": " + Выборка.ВидЗатратНУ, "");
		ОбластьСтрока.Параметры.ОбъектУчета   = ПолучитьЗапись(Выборка.Содержание,Выборка.Расшифровка);
		ОбластьСтрока.Параметры.Сумма         = Выборка.Сумма;
		ОбластьСтрока.Параметры.Расшифровка   = Выборка.Расшифровка;

		ИтогоСумма = ИтогоСумма + Выборка.Сумма;

		ДокументРезультат.Вывести(ОбластьСтрока);

	КонецЦикла;

	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);

	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;
	ОбластьПодвал.Параметры.ИтогоСумма              = ИтогоСумма;

	ДокументРезультат.Вывести(ОбластьПодвал);

КонецПроцедуры // СформироватьОтчет

Процедура ЗаполнитьСписокВидовРасходов() Экспорт

		// Заполним список видов расходов если он пуст.
	    ВидыРасходов.Очистить(); 
		
		Если ВидОтчета = 1 Тогда
			
		ВидыРасходов.Добавить("ЕСН");
		ВидыРасходов.Добавить("Страховые взносы");
		ВидыРасходов.Добавить("Налоги и сборы");
		ВидыРасходов.Добавить("Командировочные расходы");
		ВидыРасходов.Добавить("Представительские расходы");
		ВидыРасходов.Добавить("Расходы на рекламу");
		ВидыРасходов.Добавить("Расходы на ремонт основных средств");
		ВидыРасходов.Добавить("Расходы на освоение природных ресурсов");
		ВидыРасходов.Добавить("Расходы на научные исследования и (или) опытно-конструкторские разработки", "Расходы на НИОКР");
		ВидыРасходов.Добавить("Расходы на обязательное и добровольное страхование имущества", "Расходы на страхование имущества");
		ВидыРасходов.Добавить("Расходы  по  аренде федерального имущества");
		ВидыРасходов.Добавить("Убыток от реализации амортизируемого имущества");
		ВидыРасходов.Добавить("Прочие");
		ВидыРасходов.Добавить("Прочие косвенные расходы");
		
	Иначе
		
		ВидыРасходов.Добавить("Расходы на продажу");
		ВидыРасходов.Добавить("Прочие косвенные расходы");
		ВидыРасходов.Добавить("Налоги и сборы");
		
	КонецЕсли;
	

	ВидыРасходов.ЗаполнитьПометки(Истина);

КонецПроцедуры

Функция ПолучитьЗапись(Содержание, ДокументЗаписи)
	
	ЕстьСодержание = ЗначениеЗаполнено(Содержание);
	
	Текст = ?(ЕстьСодержание, Содержание + ", ", "") + "(" + ДокументЗаписи + ")";
	
	Возврат Текст;
	
КонецФункции           


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;

#КонецЕсли