
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ 
// 

#Если Клиент Тогда

Процедура СформироватьОтчет(ДокументРезультат, Режим = "Заявление") Экспорт
	
	ДокументРезультат.Очистить();
	//Параметры документа
	ДокументРезультат.ПолеСлева				= 0;
	ДокументРезультат.ПолеСправа			= 0;
	ДокументРезультат.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_Форма_ДСВ1";
	ДокументРезультат.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	
	Макет = ПолучитьМакет("ФормаДСВ_1");
	
	Если Режим <> "Бланк" Тогда
		
		Запрос = Новый Запрос();
		
		Запрос.УстановитьПараметр("ФизЛицо",	Физлицо);
		Запрос.УстановитьПараметр("Организация",Организация);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Организации.НаименованиеТерриториальногоОрганаПФР КАК НаименованиеПФР,
		|	ФизическиеЛица.СтраховойНомерПФР,
		|	ФИОФизЛицСрезПоследних.Фамилия КАК Фамилия,
		|	ФИОФизЛицСрезПоследних.Имя КАК Имя,
		|	ФИОФизЛицСрезПоследних.Отчество КАК Отчество,
		|	ВЫБОР
		|		КОГДА АдресаДляИнформирования.Поле1 ЕСТЬ НЕ NULL 
		|			ТОГДА АдресаДляИнформирования.Представление
		|		КОГДА АдресаФактические.Поле1 ЕСТЬ НЕ NULL 
		|			ТОГДА АдресаФактические.Представление
		|		КОГДА АдресаРегистрации.Поле1 ЕСТЬ НЕ NULL 
		|			ТОГДА АдресаРегистрации.Представление
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК АдресДляИнформирования
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаФактические
		|		ПО ФизическиеЛица.Ссылка = АдресаФактические.Объект
		|			И (АдресаФактические.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресФизЛица))
		|			И (АдресаФактические.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаРегистрации
		|		ПО ФизическиеЛица.Ссылка = АдресаРегистрации.Объект
		|			И (АдресаРегистрации.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресФизЛица))
		|			И (АдресаРегистрации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК АдресаДляИнформирования
		|		ПО ФизическиеЛица.Ссылка = АдресаДляИнформирования.Объект
		|			И (АдресаДляИнформирования.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ИнформАдресФизЛица))
		|			И (АдресаДляИнформирования.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО (Организации.Ссылка = &Организация)
		|ГДЕ
		|	ФизическиеЛица.Ссылка = &ФизЛицо";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Макет.Параметры.НаименованиеПФР = СокрЛП(Выборка.НаименованиеПФР);
			Макет.Параметры.Фамилия = Выборка.Фамилия;
			Макет.Параметры.Имя = Выборка.Имя;
			Макет.Параметры.Отчество = Выборка.Отчество;
			Адрес = Выборка.АдресДляИнформирования;
			Если СтрДлина(Адрес) > 70 Тогда
				ИндексПереносаСтроки = 70;
				Пока ИндексПереносаСтроки > 0 Цикл
					Если Сред(Адрес, ИндексПереносаСтроки, 1) = "," Тогда
						Прервать;
					Иначе
						ИндексПереносаСтроки = ИндексПереносаСтроки - 1;
					КонецЕсли;
				КонецЦикла;
				Макет.Параметры.Адрес_1 = Лев(Адрес, ИндексПереносаСтроки);
				Макет.Параметры.Адрес_2 = Сред(Адрес, ИндексПереносаСтроки + 1);
			Иначе
				Макет.Параметры.Адрес_1 = Адрес;
			КонецЕсли;
			
			НомерПФР = СтрЗаменить(СтрЗаменить(Выборка.СтраховойНомерПФР, "-", ""), " ", "");
			Макет.Параметры.НомерПФР_1 = Сред(НомерПФР, 1, 1);
			Макет.Параметры.НомерПФР_2 = Сред(НомерПФР, 2, 1);
			Макет.Параметры.НомерПФР_3 = Сред(НомерПФР, 3, 1);
			Макет.Параметры.НомерПФР_4 = Сред(НомерПФР, 4, 1);
			Макет.Параметры.НомерПФР_5 = Сред(НомерПФР, 5, 1);
			Макет.Параметры.НомерПФР_6 = Сред(НомерПФР, 6, 1);
			Макет.Параметры.НомерПФР_7 = Сред(НомерПФР, 7, 1);
			Макет.Параметры.НомерПФР_8 = Сред(НомерПФР, 8, 1);
			Макет.Параметры.НомерПФР_9 = Сред(НомерПФР, 9, 1);
			Макет.Параметры.НомерПФР_10 = Сред(НомерПФР, 10, 1);
			Макет.Параметры.НомерПФР_11 = Сред(НомерПФР, 11, 1);
			
		КонецЕсли;
		
		Если Не (ЗначениеЗаполнено(Макет.Параметры.НаименованиеПФР) И ЗначениеЗаполнено(Макет.Параметры.Адрес_1)) Тогда
			ОбщегоНазначения.СообщитьИнформациюПользователю("Некоторые поля отчета не заполнены!");
			Если Не ЗначениеЗаполнено(Макет.Параметры.НаименованиеПФР) Тогда
				ОбщегоНазначения.СообщитьИнформациюПользователю("В организации не указано наименование территориального органа ПФР.");
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Макет.Параметры.Адрес_1) Тогда
				ОбщегоНазначения.СообщитьИнформациюПользователю("У физлица не указаны адреса.");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ДокументРезультат.Вывести(Макет);
	
КонецПроцедуры // СформироватьОтчет()

#КонецЕсли

