
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	// Проверим, заполнены ли все необходимые поля
	Если Тип.Пустая() Тогда
		Предупреждение("Не выбран тип!");
		Возврат;
	ИначеЕсли ПустаяСтрока(Наименование) Тогда
		Предупреждение("Не указано наименование!");
		Возврат;
	КонецЕсли;
	
	// Проверим, есть ли с таким же именем
	ТЗ = "ВЫБРАТЬ
	     |	ВидыКонтактнойИнформации.Ссылка
	     |ИЗ
	     |	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	     |ГДЕ
	     |	ВидыКонтактнойИнформации.Наименование = &Наименование
	     |	И ВидыКонтактнойИнформации.Тип = &Тип
	     |	И ВидыКонтактнойИнформации.ВидОбъектаКонтактнойИнформации = &ВидОбъекта";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТЗ;
	Запрос.УстановитьПараметр("Тип",          Тип);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("ВидОбъекта",   ВидОбъекта);
	
	Если Запрос.Выполнить().Выбрать().Следующий() Тогда
		Предупреждение("Уже есть такой вид контактной информации!");
		Возврат;
	КонецЕсли;
	
	// Получим порядок для нового вида
	ТЗ = "ВЫБРАТЬ
	     |	МАКСИМУМ(ВидыКонтактнойИнформации.Порядок) КАК Порядок
	     |ИЗ
	     |	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	     |ГДЕ
	     |	ВидыКонтактнойИнформации.ВидОбъектаКонтактнойИнформации = &ВидОбъекта
	     |
	     |СГРУППИРОВАТЬ ПО
	     |	ВидыКонтактнойИнформации.ВидОбъектаКонтактнойИнформации";
		 
	Запрос = Новый Запрос;
	Запрос.Текст = ТЗ;
	Запрос.УстановитьПараметр("ВидОбъекта", ВидОбъекта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Порядок = ?(Выборка.Порядок = Null, 1, Выборка.Порядок+1);
	Иначе
		Порядок = 1;
	КонецЕсли;
	
	// Запишем все
	Попытка
		
		Если ЗначениеЗаполнено(ЗаменяемыйВид) Тогда
			Объект = ЗаменяемыйВид.ПолучитьОбъект();
			НовыйПорядок = Объект.Порядок;
			Объект.Порядок = Порядок;
			Объект.Записать();
			Порядок = НовыйПорядок;
		КонецЕсли;
	
		Элемент = Справочники.ВидыКонтактнойИнформации.СоздатьЭлемент();
		Элемент.Наименование = Наименование;
		Элемент.Тип = Тип;
		Элемент.ВидОбъектаКонтактнойИнформации = ВидОбъекта;
		Элемент.РедактированиеВДиалоге = РедактироватьВДиалоге;
		Элемент.Порядок = Порядок;
		Элемент.Записать();
		
		Закрыть(Элемент.Ссылка);
	
	Исключение
		Предупреждение(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры
