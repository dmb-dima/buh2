////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()

	ЭтаФорма.Заголовок = "Дополнительные реквизиты: " + ОбработкаОбъектЗначенияСвойств.ОбъектОтбораЗначений;

КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Если Модифицированность Тогда
		Вопрос = "Измененные значения свойств не были записаны! Сохранить изменения?";
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ДаНетОтмена);

		Если      Ответ = КодВозвратаДиалога.Да Тогда
			ОбработкаОбъектЗначенияСвойств.ЗаписатьЗначенияСвойств();

		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаписиНовогоОбъекта(Объект, Источник)

	ОбработкаОбъектЗначенияСвойств.ПроверитьДобавитьНовоеСвойство(Объект.Ссылка, ЭлементыФормы.СвойстваИЗначения);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ СВОЙСТВ И ЗНАЧЕНИЙ

Процедура КоманднаяПанельДействиеПеречитать(Кнопка)

	Если Модифицированность Тогда
		Вопрос = "Измененные значения свойств не были записаны! Продолжить?";
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ОкОтмена);

		Если Ответ <> КодВозвратаДиалога.ОК Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ОбработкаОбъектЗначенияСвойств.ПрочитатьЗаполнитьСвойстваИЗначения();

	Модифицированность = Ложь;

КонецПроцедуры

Процедура СвойстваИЗначенияПередНачаломДобавления(Элемент, Отказ, Копирование)

	Отказ = Истина;

	ОбработкаОбъектЗначенияСвойств.ОткрытьФормуНовогоСвойства(ЭтаФорма, Элемент, Копирование);

КонецПроцедуры

Процедура СвойстваИЗначенияПередУдалением(Элемент, Отказ)

	Отказ = Истина;

	ОбработкаОбъектЗначенияСвойств.ИнвертироватьПометкуУдаленияСвойства(Элемент.ТекущиеДанные);

КонецПроцедуры

Процедура СвойстваИЗначенияПриНачалеРедактирования(Элемент, НоваяСтрока)
	
	ОбработкаОбъектЗначенияСвойств.ПривестиТипЗначенияСвойства(Элемент.ТекущиеДанные, Элемент.Колонки.Значение.ЭлементУправления);
	
КонецПроцедуры

Процедура СвойстваИЗначенияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОбработкаОбъектЗначенияСвойств.ПриВыводеСтрокиСвойствИЗначений(Элемент, ОформлениеСтроки, ДанныеСтроки);
	
КонецПроцедуры

Процедура СвойстваИЗначенияЗначениеПриИзменении(Элемент)

	Модифицированность = Истина;

КонецПроцедуры

Процедура СвойстваИЗначенияЗначениеОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ЭлементыФормы.СвойстваИЗначения.ТекущиеДанные.Значение = Неопределено;

	ОбработкаОбъектЗначенияСвойств.ПривестиТипЗначенияСвойства(ЭлементыФормы.СвойстваИЗначения.ТекущиеДанные, Элемент);

	Модифицированность = Истина;

КонецПроцедуры

Процедура КнопкаOKНажатие(Элемент)

	// Запись значений свойств и закрытие формы.
	Если ОбработкаОбъектЗначенияСвойств.ЗаписатьЗначенияСвойств() Тогда
		Модифицированность = Ложь;
		Оповестить("ОбновлениеЗначенийСвойств", РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ОбъектОтбораЗначений), ОбъектОтбораЗначений);
		Закрыть();
	КонецЕсли;

КонецПроцедуры

Процедура КоманднаяПанельДействийЗаписать(Кнопка)
	
	Если ОбработкаОбъектЗначенияСвойств.ЗаписатьЗначенияСвойств() Тогда
		Модифицированность = Ложь;
		Оповестить("ОбновлениеЗначенийСвойств", РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ОбъектОтбораЗначений), ОбъектОтбораЗначений);
	КонецЕсли;
	
КонецПроцедуры

Процедура СвойстваИЗначенияПриИзмененииФлажка(Элемент, Колонка)
	
	Элемент.ТекущиеДанные.Значение = Не Элемент.ТекущиеДанные.Свойство.ТипЗначения.ПривестиЗначение(Элемент.ТекущиеДанные.Значение);
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры




