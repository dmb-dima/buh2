////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мТаблицаСтраницРаздела;
Перем мСпрашиватьОСохранении;
Перем мИмяТекущейСтраницы;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура модифицирует таблицу значений - начальное значение выбора
// и передает владельцу формы выбранное значение.
//
Процедура ВывестиВыбраннуюСтраницу(ВыбраннаяСтрока)

	// сначала снимем признак активной страницы с текущей
	// страницы (на момент открытия формы списка страниц)
	ТекСтраницаРаздела = мТаблицаСтраницРаздела.Найти(Истина, "АктивнаяСтраница");

	Если ТекСтраницаРаздела <> Неопределено Тогда
		ТекСтраницаРаздела.АктивнаяСтраница = Ложь;
	КонецЕсли;

	ВыбраннаяСтрока.АктивнаяСтраница = Истина;

	ВозвращаемыеПараметры = Новый Структура;
	ВозвращаемыеПараметры.Вставить("ТаблицаСтраницРаздела",  мТаблицаСтраницРаздела);
	ВозвращаемыеПараметры.Вставить("ФлагМодифицированности", Модифицированность);

	Закрыть(ВозвращаемыеПараметры);

КонецПроцедуры // ВывестиВыбраннуюСтраницу()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()

	Если ТипЗнч(НачальноеЗначениеВыбора) = Тип("ТаблицаЗначений") Тогда
		мТаблицаСтраницРаздела = НачальноеЗначениеВыбора.Скопировать();
		// мИмяТекущейСтраницы - используется как переключатель между старыми декларациями и новыми.
		мИмяТекущейСтраницы = Неопределено;
	ИначеЕсли ТипЗнч(НачальноеЗначениеВыбора) = Тип("Структура") Тогда
		мТаблицаСтраницРаздела = НачальноеЗначениеВыбора.ТаблицаРаздела.Скопировать();
		мИмяТекущейСтраницы = НачальноеЗначениеВыбора.ИмяТекущейСтраницы;

		ПерестановкаСтраницЗапрещена = Неопределено;
		Если НЕ НачальноеЗначениеВыбора.Свойство("ПерестановкаСтраницЗапрещена", ПерестановкаСтраницЗапрещена) Тогда
			ПерестановкаСтраницЗапрещена = Ложь;
		КонецЕсли;

	КонецЕсли;

	Если мИмяТекущейСтраницы = Неопределено Или ПерестановкаСтраницЗапрещена Тогда
		ЭлементыФормы.ТаблицаДопСтраниц.ИзменятьПорядокСтрок = Ложь;
	КонецЕсли;

	Если мИмяТекущейСтраницы = Неопределено Тогда
		ЭлементыФормы.ТаблицаДопСтраниц.ТолькоПросмотр = Истина;
	КонецЕсли;

	ЭлементыФормы.ТаблицаДопСтраниц.Значение = мТаблицаСтраницРаздела;
	ЭлементыФормы.ТаблицаДопСтраниц.Колонки.Представление.Данные = "Представление";

	Если мИмяТекущейСтраницы <> Неопределено Тогда
		ЭлементыФормы.ТаблицаДопСтраниц.Колонки.АвтоматическоеПредставление.ДанныеФлажка = "АвтоматическоеПредставление";
		ЭлементыФормы.ТаблицаДопСтраниц.Колонки.АвтоматическоеПредставление.Видимость = Истина;
	Иначе
		ЭлементыФормы.ТаблицаДопСтраниц.Колонки.АвтоматическоеПредставление.Видимость = Ложь;
	КонецЕсли;

	// отметим строку таблицы, соответствующую текущей активной странице
	// многостраничного раздела
	ТекСтраницаРаздела = мТаблицаСтраницРаздела.Найти(Истина, "АктивнаяСтраница");

	Если ТекСтраницаРаздела <> Неопределено Тогда
		ЭлементыФормы.ТаблицаДопСтраниц.ТекущаяСтрока = ТекСтраницаРаздела;
	КонецЕсли;

КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик выбора строки табличного поля ТаблицаДопСтраниц.
// Вызывается при двойном щелчке мыши или нажатии Enter.
//
Процедура ТаблицаДопСтраницВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	//Если Элемент.ТекущаяКолонка.Имя <> "АвтоматическоеПредставление" Тогда

	// При двойном щелчке на строчке, при выходе не нужно справшивать об изменениях.
	мСпрашиватьОСохранении = Ложь;

	//// Измениться признак АктивнаяСтраница
	//Модифицированность = Истина;

	ВывестиВыбраннуюСтраницу(ВыбраннаяСтрока);
	//КонецЕсли;

КонецПроцедуры // ТаблицаДопСтраницВыбор()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки "ОК" командной панели формы.
// Отрабатывает выбранное значение - страницу многостраничного раздела.
//
Процедура ОсновныеДействияФормыКнопкаВыбратьНажатие(Кнопка)

	ВыбраннаяСтрока = ЭлементыФормы.ТаблицаДопСтраниц.ТекущаяСтрока;
	мСпрашиватьОСохранении = Ложь;
	ВывестиВыбраннуюСтраницу(ВыбраннаяСтрока);

КонецПроцедуры // ОсновныеДействияФормыКнопкаВыбратьНажатие()

// ОсновныеДействияФормыЗакрыть
//
Процедура ОсновныеДействияФормыЗакрыть(Кнопка)

	мСпрашиватьОСохранении = Истина;
	Закрыть();

КонецПроцедуры // ОсновныеДействияФормыЗакрыть

// ПередЗакрытием
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Если мСпрашиватьОСохранении <> Ложь И Модифицированность Тогда
		Ответ = Вопрос("Данные на форме были изменены. Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			мСпрашиватьОСохранении = Неопределено;
			Возврат;
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			мСпрашиватьОСохранении = Неопределено;
			Отказ = Истина;
			Возврат;
		ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
			ВывестиВыбраннуюСтраницу(ЭлементыФормы.ТаблицаДопСтраниц.ТекущаяСтрока);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры // ПередЗакрытием

// ТаблицаДопСтраницПредставлениеПриИзменении
//
Процедура ТаблицаДопСтраницПредставлениеПриИзменении(Элемент)

	АктивнаяСтрока = мТаблицаСтраницРаздела.Найти(Элемент.Значение, "Представление");
	Если АктивнаяСтрока <> Неопределено Тогда
		АктивнаяСтрока.АвтоматическоеПредставление = Ложь;
	КонецЕсли;

КонецПроцедуры // ТаблицаДопСтраницПредставлениеПриИзменении

// ТаблицаДопСтраницПриИзмененииФлажка
//
Процедура ТаблицаДопСтраницПриИзмененииФлажка(Элемент, Колонка)

	Если (мИмяТекущейСтраницы = Неопределено) Или (НЕ Элемент.ТекущаяСтрока.АвтоматическоеПредставление) Тогда
		Возврат;
	КонецЕсли;

	ИмяПредставления = РегламентированнаяОтчетность.роПолучитьСвойствоРаздела(Этаформа.ВладелецФормы, мИмяТекущейСтраницы, "ИмяПредставления");

	НомерСтраницы = 0;

	Для Каждого Стр Из мТаблицаСтраницРаздела Цикл

		НомерСтраницы = НомерСтраницы + 1;

		Если Стр <> Элемент.ВыделенныеСтроки[0] Тогда
			Продолжить;
		КонецЕсли;

		СохрПредставлениеСтраницы = Стр.Представление;

		Если НЕ ЗначениеЗаполнено(ИмяПредставления) Тогда
			ТекущееПредставлениеСтраницы = Неопределено;
		Иначе
			Стр.Данные.Свойство(ИмяПредставления, ТекущееПредставлениеСтраницы);
		КонецЕсли;

		ТекущееПредставлениеСтраницы = ?(РегламентированнаяОтчетность.ПустоеЗначение(ТекущееПредставлениеСтраницы), "Лист N " + Строка(НомерСтраницы), ТекущееПредставлениеСтраницы + ". Лист N " + Строка(НомерСтраницы));
		Если Не ПустаяСтрока(ТекущееПредставлениеСтраницы) Тогда
			Стр.Представление = ТекущееПредставлениеСтраницы;
		Иначе
			Стр.Представление = "Новая страница";
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ТаблицаДопСтраницПриИзмененииФлажка

// ТаблицаДопСтраницАвтоматическоеПредставлениеПриИзменении
//
Процедура ТаблицаДопСтраницАвтоматическоеПредставлениеПриИзменении(Элемент, Колонка)
	// Процедура "заглушка"
	Возврат;
КонецПроцедуры // ТаблицаДопСтраницАвтоматическоеПредставлениеПриИзменении