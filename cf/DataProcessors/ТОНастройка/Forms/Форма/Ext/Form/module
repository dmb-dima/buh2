///////////////////////////////////////////////////////////////////////////////
//// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мСоответствиеСтраницВидуТО;
Перем мСоответствиеСтраницТаблицамТО;
Перем мСоответствиеНазванийСтраниц;
Перем мВидыТО;

///////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "При открытии" формы. Данное событие
// возникает при открытии формы, до показа окна пользователю.
//
// Параметры:
//  Нет.
//
Процедура ПриОткрытии()

	НастроитьОтборы();

	ЭлементыФормы.ДляКомпьютера.Значение = "для компьютера """ + ПолучитьСерверТО().ПолучитьИмяКомпьютераТО() + """";

	ОбновитьТекстКнопкиВыгрузкаООВидаТО();
	ОбновитьТекстКнопкиОбновитьОбработкиОбслуживания();

КонецПроцедуры

// Процедура - обработчик события "Начало выбора" поля ввода
// "ФискальныеРегистраторыМодель".
//
// Параметры:
//  Элемент              - <ПолеВвода>
//                       - Поле ввода, с которым связано данное событие
//                         ("ФискальныеРегистраторыМодель").
//
//  СтандартнаяОбработка - <Булево>
//                       - В данный параметр передается признак выполнения
//                         стандартной (системной) обработки события. Если
//                         в теле процедуры-обработчика установить данному
//                         параметру значение Ложь, стандартная обработка
//                         события производиться не будет
//                         Значение по умолчанию: Истина.
//
Процедура ФискальныеРегистраторыМодельНачалоВыбора(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	ОткрытьФормуВыбораМодели(Элемент, мВидыТО.ФискальныйРегистратор);

КонецПроцедуры // ФискальныеРегистраторыМодельНачалоВыбора()

// Процедура - обработчик события "Перед окончанием редактирования" строки
// табличного поля "".
//
// Параметры:
//  Элемент              - <ТабличноеПоле>
//                       - Табличное поле, с которым связано данное событие
//                         ("ФискальныеРегистраторы").
//
//  НоваяСтрока          - <Булево>
//                       - Установлен в Истина, если строка была добавлена
//                         или скопирована.
//
//  ОтменаРедактирования - <Булево>
//                       - Истина, если произошла отмена редактирования.
//
//  Отказ                - <Булево>
//                       - Признак отказа от выполнения действия.
//                         Значение по умолчанию: Ложь.
//
Процедура ФискальныеРегистраторыПередОкончаниемРедактирования(Элемент, НоваяСтрока,
                                                              ОтменаРедактирования, Отказ)

	ОбработкаОкончанияРедактирования(Элемент,
	                                 НоваяСтрока,
	                                 ОтменаРедактирования,
	                                 Отказ);

КонецПроцедуры // ФискальныеРегистраторыПередОкончаниемРедактирования()

// Процедура представляет обработчик события "Нажатие" кнопки
// "Параметры" командной панели "КоманднаяПанельФР".
//
// Параметры:
//  Кнопка - <КнопкаКоманднойПанели>
//         - Кнопка, с которой связано данное событие (кнопка "Параметры").
//
Процедура КоманднаяПанельФРПараметры(Кнопка)

	ОткрытьФормуНастройки(ЭлементыФормы.ФискальныеРегистраторы);

КонецПроцедуры // КоманднаяПанельФРПараметры()

// Процедура представляет обработчик события "Нажатие" кнопки
// "Проверка" командной панели "ОсновныеДействияФормы".
//
// Параметры:
//  Кнопка - <КнопкаКоманднойПанели>
//         - Кнопка, с которой связано данное событие (кнопка "Проверка").
//
Процедура ОсновныеДействияФормыПроверка(Кнопка)

	НетОшибок = Истина;
	ВидыТО    = Новый Соответствие();

	Если Не ПолучитьСерверТО().ОтключитьОбработкиОбслуживания() Тогда
		ТекстОшибки = "Перед выполнением проверки необходимо закрыть все формы, использующие торговое оборудование.";
		Сообщить(ТекстОшибки, СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|    РегТО.Модель    КАК Модель,
	|    РегТО.Параметры КАК Параметры,
	|    РегТО.Вид       КАК Вид
	|ИЗ
	|    РегистрСведений.ТорговоеОборудование КАК РегТО
	|ГДЕ
	|    РегТО.Компьютер = &Компьютер
	|    И РегТО.Подключено
	|");
	Запрос.УстановитьПараметр("Компьютер", ПолучитьСерверТО().ПолучитьИмяКомпьютераТО());
	РезультатЗапроса = Запрос.Выполнить();
	Обработка        = Неопределено;
	Объект           = Неопределено;
	ДопИнфо          = "";
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДопИнфо          = "";
			Результат = РаботаСТорговымОборудованием.ПолучитьОбработкуОбслуживанияТО(Выборка.Модель, Обработка);
			ВидыТО[Выборка.Вид] = Истина;
			Если НЕ ЗначениеЗаполнено(Результат) Тогда
				Форма     = Обработка.ПолучитьФорму();
				Параметры = ?(ПустаяСтрока(Выборка.Параметры),
				              Новый Структура(),
				              ЗначениеИзСтрокиВнутр(Выборка.Параметры));
				Результат = Форма.СоздатьОбъектДрайвера(Объект,
				                                        Выборка.Модель.Модель,
				                                        Параметры);
				Если НЕ ЗначениеЗаполнено(Результат) Тогда
					Объект.ОписаниеОшибки = "";
					Результат = Форма.ТестУстройства(Объект);
					ДопИнфо = Объект.ОписаниеОшибки;
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(Результат) Тогда
				НетОшибок  = Ложь;
				ТекстОшибки = """" + Выборка.Модель + """:
				              |" + ПолучитьСерверТО().ПолучитьТекстОшибкиТО(Результат);
				ТекстОшибки = ТекстОшибки + "
				                            |" + ДопИнфо;
				Сообщить(ТекстОшибки, СтатусСообщения.Важное);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если НетОшибок Тогда
		Предупреждение("Торговое оборудование настроено корректно.");
	КонецЕсли;

КонецПроцедуры // ОсновныеДействияФормыПроверка()

///////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура настраивает отбор записей регистра сведений для определённого ТП.
//
// Параметры:
//  ИмяТП - <Строка>
//        - Имя табличного поля, параметры отбора которого необходимо настроить.
//
//  Вид   - <ПеречислениеСсылка.ВидыТорговогоОборудования>
//
Процедура НастроитьОтбор(ИмяТП, Вид)

	ЭлементыФормы[ИмяТП].Значение.Отбор.Компьютер.Установить(ПолучитьСерверТО().ПолучитьИмяКомпьютераТО());
	ЭлементыФормы[ИмяТП].Значение.Отбор.Вид.Установить(Вид);

КонецПроцедуры // НастроитьОтбор()

// Процедура осуществляет настройку отбора табличных полей формы
//
// Параметры:
//  Нет.
//
Процедура НастроитьОтборы()

	НастроитьОтбор("ФискальныеРегистраторы"  , мВидыТО.ФискальныйРегистратор);

КонецПроцедуры // НастроитьОтборы()

// Процедура выполняет действия, связанные с окончанием редактирования строки
// табличного поля, в котором содержатся сведения о подключаемом торговом
// оборудовании.
//
// Параметры:
//  Элемент     - <ТабличноеПоле>
//              - Табличное поле, с которым связано данное событие.
//
//  НоваяСтрока - <Булево>
//              - Признак редактирования новой строки.
//
//  Отмена      - <Булево>
//              - Отмена редактирования.
//
//  Отказ       - <Булево>
//              - Выходной параметр; отказ от редактирования строки ТП.
//
Процедура ОбработкаОкончанияРедактирования(Элемент, НоваяСтрока, Отмена, Отказ)

	Если Отмена Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные = Элемент.ТекущиеДанные;

	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.Модель) Тогда
		Предупреждение("Не указана модель торгового оборудования!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если Элемент.Колонки.Найти("КассаККМ") <> Неопределено Тогда
		Если НЕ ЗначениеЗаполнено(ТекущиеДанные.КассаККМ) Тогда
			Предупреждение("Не указана касса ККМ!");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;

	Если НоваяСтрока Тогда
		ТекущиеДанные.Идентификатор = ВРег(Новый УникальныйИдентификатор());
		ТекущиеДанные.Компьютер     = ПолучитьСерверТО().ПолучитьИмяКомпьютераТО();
	КонецЕсли;

	НастроитьОтборы();

КонецПроцедуры // ОбработкаОкончанияРедактирования()

// Процедура открывает форму выбора элемента справочника "Торговое оборудование".
//
// Параметры:
//  Элемент - <ПолеВвода>
//          - Поле ввода, в которое осуществляется подбор.
//
//  Вид     - <ПеречислениеСсылка.ВидыТорговогоОборудования>
//          - Вид торгового оборудования, подходящий для данного поля ввода.
//
Процедура ОткрытьФормуВыбораМодели(Элемент, Вид)

	СтандартнаяОбработка = Ложь;
	Форма                = Справочники.ТорговоеОборудование.ПолучитьФормуВыбора(, Элемент, );

	Если Форма.Открыта() Тогда
		Форма.Активизировать();
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|    СпрОбработки.Ссылка КАК ОбработкаОбслуживания
	|ИЗ
	|    Справочник.ОбработкиОбслуживанияТО КАК СпрОбработки
	|ГДЕ
	|    СпрОбработки.Вид = &Вид");
	Запрос.УстановитьПараметр("Вид", Вид);

	Список    = Новый СписокЗначений();
	Список.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0));

	Форма.Отбор.ОбработкаОбслуживания.ВидСравнения  = ВидСравнения.ВСписке;
	Форма.Отбор.ОбработкаОбслуживания.Значение      = Список;
	Форма.Отбор.ОбработкаОбслуживания.Использование = Истина;

	Форма.ЭлементыФормы.СправочникСписок.НастройкаОтбора.ОбработкаОбслуживания.Доступность = Ложь;

	Форма.Открыть();

КонецПроцедуры // ОткрытьФормуВыбораМодели()

// Процедура осуществляет открытие формы настройки для выбранной модели торгового
// оборудования.
//
// Параметры:
//  ТП - <ТабличноеПоле>
//     - Табличное поле, для текущего выбранного элемента которого следует открыть
//       форму настройки.
//
Процедура ОткрытьФормуНастройки(ТП)

	Если ТП.ТекущиеДанные = Неопределено Тогда
		Предупреждение("Необходимо выбрать строку табличного поля, соответствующую
		               |настраиваемой модели торгового оборудования.");
		Возврат;
	КонецЕсли;

	Обработка = Неопределено;
	Ошибка    = РаботаСТорговымОборудованием.ПолучитьОбработкуОбслуживанияТО(ТП.ТекущиеДанные.Модель, Обработка);
	Если НЕ ЗначениеЗаполнено(Ошибка) Тогда
		Набор = РегистрыСведений.ТорговоеОборудование.СоздатьНаборЗаписей();
		Набор.Отбор.Идентификатор.Установить(ТП.ТекущаяСтрока.Идентификатор);
		Набор.Прочитать();

		Параметры = ?(ПустаяСтрока(Набор[0].Параметры),
		              Новый Структура(),
		              ЗначениеИзСтрокиВнутр(Набор[0].Параметры));
		Обработка.НастроитьОборудование(ТП.ТекущиеДанные.Модель, Параметры);
		Набор[0].Параметры = ЗначениеВСтрокуВнутр(Параметры);
		Набор.Записать();
	Иначе
		Предупреждение("Ошибка при попытке открыть форму настройки параметров модели:
		               |" + ПолучитьСерверТО().ПолучитьТекстОшибкиТО(Ошибка));
	КонецЕсли;

КонецПроцедуры // ОткрытьФормуНастройки()

Процедура ОбработкиОбслуживанияЗагрузитьВсе(Кнопка)

	РаботаСТорговымОборудованием.ИмпортироватьОбработки(Ложь,);

КонецПроцедуры

Процедура ОбработкиОбслуживанияОбновитьТекущие(Кнопка)

	РаботаСТорговымОборудованием.ИмпортироватьОбработки(Истина,);

КонецПроцедуры

Процедура ОбработкиОбслуживанияВыгрузитьВсе(Кнопка)

	РаботаСТорговымОборудованием.ЭкспортироватьОбработки();

КонецПроцедуры

Процедура ОбработкиОбслуживанияВыгрузитьТекущегоВидаТО(Кнопка)

	РаботаСТорговымОборудованием.ЭкспортироватьОбработки(мСоответствиеСтраницВидуТО[ЭлементыФормы.КлассыТорговогоОборудования.ТекущаяСтраница]);

КонецПроцедуры

Процедура ОбработкиОбслуживанияСправочникОбработкиОбслуживания(Кнопка)

	ФормаСписка = Справочники.ОбработкиОбслуживанияТО.ПолучитьФормуСписка();
	Если Не ФормаСписка.Открыта() Тогда
		ФормаСписка.Открыть();
	Иначе
		ФормаСписка.Активизировать();
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкиОбслуживанияТекущаяОбработкаОбслуживания(Кнопка)

	ТекущаяТаблицаТО = мСоответствиеСтраницТаблицамТО[ЭлементыФормы.КлассыТорговогоОборудования.ТекущаяСтраница];

	Если ТекущаяТаблицаТО.ТекущаяСтрока <> Неопределено Тогда
		ЭлементаСправочника = ТекущаяТаблицаТО.ТекущиеДанные.Модель.ОбработкаОбслуживания;
		ЭлементаСправочника.ПолучитьФорму().Открыть();
	Иначе
		Предупреждение("Для обновления обработки обслуживания выберите модель устройства");
	КонецЕсли;

КонецПроцедуры

// Обработчики начала выбора из списка таблиц ТО

// Процедура открывает форму выбора элемента справочника "Торговое оборудование".
//
// Параметры:
//  Элемент - <ПолеВвода>
//          - Поле ввода, в которое осуществляется подбор.
//
//  Вид     - <ПеречислениеСсылка.ВидыТорговогоОборудования>
//          - Вид торгового оборудования, подходящий для данного поля ввода.
//
Процедура ЗаполнитьСписокВыбораСозданнымиМоделями(Элемент, Вид)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СпрТО.Ссылка КАК Модель
	|ИЗ
	|	Справочник.ТорговоеОборудование КАК СпрТО
	|ГДЕ
	|	СпрТО.ОбработкаОбслуживания.Вид = &Вид");

	Запрос.УстановитьПараметр("Вид", Вид);

	Элемент.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Модель"));

КонецПроцедуры // ЗаполнитьСписокВыбораСозданнымиМоделями()

Процедура ФискальныеРегистраторыМодельНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	ЗаполнитьСписокВыбораСозданнымиМоделями(Элемент, мВидыТО.ФискальныйРегистратор);

КонецПроцедуры

Процедура ОткрытьПомощникНастройки(Кнопка)

	РаботаСТорговымОборудованием.ОткрытьПомощникПодключенияИНастройкиТО(мСоответствиеСтраницВидуТО[ЭлементыФормы.КлассыТорговогоОборудования.ТекущаяСтраница]);

КонецПроцедуры

// Процедура
//
// Параметры:
//  
//
Процедура ВывестиКоличествоПодключенныхУстройствНаЗакладках()

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР КОГДА РегТО.Подключено ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КолвоПодключено,
	|	КОЛИЧЕСТВО(РегТО.Вид) КАК Колво,
	|	РегТО.Вид КАК ВидТО
	|ИЗ
	|	РегистрСведений.ТорговоеОборудование КАК РегТО
	|ГДЕ
	|	РегТО.Компьютер = &Компьютер
	|СГРУППИРОВАТЬ ПО
	|	РегТО.Вид
	|");

	Запрос.УстановитьПараметр("Компьютер", ПолучитьСерверТО().ПолучитьИмяКомпьютераТО());

	ТЗ = Запрос.Выполнить().Выгрузить();
	ТЗ.Индексы.Добавить("ВидТО");

	Для Каждого ТекСтраница Из ЭлементыФормы.КлассыТорговогоОборудования.Страницы Цикл
		ТекСтрокаКолво = ТЗ.Найти(мСоответствиеСтраницВидуТО[ТекСтраница], "ВидТО");

		Если ТекСтрокаКолво = Неопределено Тогда
			ТекКолво = "";
		Иначе
			ТекКолво = " (" + ТекСтрокаКолво.КолвоПодключено + "/" + ТекСтрокаКолво.Колво + ")";
		КонецЕсли;

		ТекСтраница.Заголовок = мСоответствиеНазванийСтраниц[ТекСтраница] + ТекКолво;
	КонецЦикла;

КонецПроцедуры // ВывестиКоличествоПодключенныхУстройствНаЗакладках()

Процедура ОбновлениеОтображения()

	ВывестиКоличествоПодключенныхУстройствНаЗакладках();

КонецПроцедуры

Процедура ФискальныеРегистраторыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	УстановитьПодключено(Элемент, НоваяСтрока, Копирование)

КонецПроцедуры

Процедура УстановитьПодключено(Элемент, НоваяСтрока, Копирование)

	Если НоваяСтрока И Не Копирование Тогда
		Элемент.ТекущиеДанные.Подключено = Истина;
		Элемент.ТекущиеДанные.Вид        = мСоответствиеСтраницВидуТО[ЭлементыФормы.КлассыТорговогоОборудования.ТекущаяСтраница];
	КонецЕсли;

КонецПроцедуры

Процедура КлассыТорговогоОборудованияПриСменеСтраницы(Элемент, ТекущаяСтраница)

	ОбновитьТекстКнопкиВыгрузкаООВидаТО();
	ОбновитьТекстКнопкиОбновитьОбработкиОбслуживания();

КонецПроцедуры

Процедура ОбновитьТекстКнопкиОбновитьОбработкиОбслуживания()

	ТекущаяТаблицаТО = мСоответствиеСтраницТаблицамТО[ЭлементыФормы.КлассыТорговогоОборудования.ТекущаяСтраница];

	Если ТекущаяТаблицаТО.ТекущаяСтрока <> Неопределено Тогда
		ЭлементыФормы.ОбработкиОбслуживания.Кнопки.ОбновитьОбработкуОбслуживания.Текст = "Обновить обработку обслуживания для """ + 
			ТекущаяТаблицаТО.ТекущиеДанные.Модель + """";
		ЭлементыФормы.ОбработкиОбслуживания.Кнопки.ОбновитьОбработкуОбслуживания.Доступность = Истина;
	Иначе
		ЭлементыФормы.ОбработкиОбслуживания.Кнопки.ОбновитьОбработкуОбслуживания.Текст = "Обновить обработку обслуживания (Не выбрана модель устройства)";
		ЭлементыФормы.ОбработкиОбслуживания.Кнопки.ОбновитьОбработкуОбслуживания.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры

Процедура ФискальныеРегистраторыПриАктивизацииСтроки(Элемент)

	ОбновитьТекстКнопкиОбновитьОбработкиОбслуживания();

КонецПроцедуры

Процедура ФискальныеРегистраторыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	ОбновитьТекстКнопкиОбновитьОбработкиОбслуживания();

КонецПроцедуры

Процедура ОбновитьТекстКнопкиВыгрузкаООВидаТО()

	ЭлементыФормы.ОбработкиОбслуживания.Кнопки.ЭкспортироватьТекущегоВидаТО.Текст = "Выгрузить обработки обслуживания вида """ + 
	    мСоответствиеСтраницВидуТО[ЭлементыФормы.КлассыТорговогоОборудования.ТекущаяСтраница] + """";

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
//// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

СтраницыТО = ЭлементыФормы.КлассыТорговогоОборудования.Страницы;
мВидыТО    = Перечисления.ВидыТорговогоОборудования;

мСоответствиеСтраницВидуТО = Новый Соответствие;
мСоответствиеСтраницВидуТО.Вставить(СтраницыТО.ФискальныеРегистраторы, мВидыТО.ФискальныйРегистратор);

мСоответствиеСтраницТаблицамТО = Новый Соответствие;
мСоответствиеСтраницТаблицамТО.Вставить(СтраницыТО.ФискальныеРегистраторы, ЭлементыФормы.ФискальныеРегистраторы);

мСоответствиеНазванийСтраниц = Новый Соответствие;
мСоответствиеНазванийСтраниц.Вставить(СтраницыТО.ФискальныеРегистраторы, "Фискальные регистраторы");
