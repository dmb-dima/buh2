Перем мПоказыватьПанельФункций;


Процедура КнопкаОКНажатие(Кнопка)
	
	УправлениеПользователями.УстановитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОткрыватьПриЗапускеПанельФункций", ПоказыватьПанельФункций);
	
	Для Каждого Строка Из ДеревоЗакладок.Строки Цикл
		
		ВладелецФормы.ЭлементыФормы.ОсновнаяПанель.Страницы[Строка.ИмяЗакладки].Видимость = Строка.Видимость;
		
		Если Строка.Строки.Количество() > 0 Тогда
			
			Для Каждого ВложеннаяСтрока Из Строка.Строки Цикл
				
				ВладелецФормы.ЭлементыФормы["ВложеннаяПанель" + Строка.ИмяЗакладки].Страницы[ВложеннаяСтрока.ИмяЗакладки].Видимость = ВложеннаяСтрока.Видимость;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если мПоказыватьПанельФункций И НЕ ПоказыватьПанельФункций Тогда
		
		Если Вопрос("Был снят флаг ""Показывать Панель функций"".
			        |Закрыть Панель функций?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			
			Закрыть(Истина);
			
		Иначе
			
			Закрыть();
			
		КонецЕсли;
		
	Иначе
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДеревоЗакладок()
	
	УчетЗарплатыИКадровВоВнешнейПрограмме = Константы.УчетЗарплатыИКадровВоВнешнейПрограмме.Получить();
	
	// Добавляем закладки Основной панели.
	Для Каждого Страница Из ВладелецФормы.ЭлементыФормы.ОсновнаяПанель.Страницы Цикл
		
		Если (Страница.Имя = "Зарплата" ИЛИ Страница.Имя = "Кадры") И УчетЗарплатыИКадровВоВнешнейПрограмме Тогда
			Продолжить;
		ИначеЕсли Страница.Имя = "ЗарплатаИКадры" И НЕ УчетЗарплатыИКадровВоВнешнейПрограмме Тогда			
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ДеревоЗакладок.Строки.Добавить();
		НоваяСтрока.Закладка    = Страница.Заголовок;
		НоваяСтрока.ИмяЗакладки = Страница.Имя;
		НоваяСтрока.Видимость   = Страница.Видимость;
		
	КонецЦикла;
	
	// Добавляем закладки вложенных панелей.
	Для Каждого ЭлементУправления Из ВладелецФормы.ЭлементыФормы Цикл
		
		Если Лев(ЭлементУправления.Имя, 15) = "ВложеннаяПанель" Тогда

			СтрокаДереваЗакладок = ДеревоЗакладок.Строки.Найти(Сред(ЭлементУправления.Имя, 16), "ИмяЗакладки");
			
			Если НЕ (СтрокаДереваЗакладок = Неопределено) Тогда
				
				Для Каждого Страница Из ЭлементУправления.Страницы Цикл
					
					НоваяСтрока = СтрокаДереваЗакладок.Строки.Добавить();
					НоваяСтрока.Закладка    = Страница.Заголовок;
					НоваяСтрока.ИмяЗакладки = Страница.Имя;
					НоваяСтрока.Видимость   = Страница.Видимость;
					
				КонецЦикла;
								
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьДеревоЗакладок()

Процедура ПриОткрытии()
	
	ПоказыватьПанельФункций  = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОткрыватьПриЗапускеПанельФункций");
	мПоказыватьПанельФункций = ПоказыватьПанельФункций;
	
	СформироватьДеревоЗакладок();
	
КонецПроцедуры

Процедура УстановитьФлажки(Строки, ЗначениеФлажка)
	
	Для Каждого Строка Из Строки Цикл
		
		Строка.Видимость = ЗначениеФлажка;
		
		Если Строка.Строки.Количество() > 0 Тогда
			
			УстановитьФлажки(Строка.Строки, ЗначениеФлажка);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельВидимостьЗакладокУстановитьФлажки(Кнопка)
	
	УстановитьФлажки(ДеревоЗакладок.Строки, Истина);
	
КонецПроцедуры

Процедура УстановитьФлажкиРодителей(Строка, ЗначениеФлажка)
	
	Если Строка.Родитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеФлажка Тогда
		Строка.Родитель.Видимость = ЗначениеФлажка;
	Иначе
		
		КоличествоФлажков = 0;
		
		Для Каждого ВложеннаяСтрока Из Строка.Родитель.Строки Цикл
			КоличествоФлажков = КоличествоФлажков + Число(ВложеннаяСтрока.Видимость);
		КонецЦикла;
		
		Если КоличествоФлажков = 0 Тогда // Все флажки до последнего сняты.
			Строка.Родитель.Видимость = ЗначениеФлажка;
		КонецЕсли;
		
	КонецЕсли;
		
	УстановитьФлажкиРодителей(Строка.Родитель, ЗначениеФлажка);
		
КонецПроцедуры 

Процедура УстановитьФлажкиПотомков(Строка, ЗначениеФлажка)
	
	Если Строка.Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Для Каждого ВложеннаяСтрока Из Строка.Строки Цикл
		
		ВложеннаяСтрока.Видимость = ЗначениеФлажка;
		
		УстановитьФлажкиПотомков(ВложеннаяСтрока, ЗначениеФлажка);
		
	КонецЦикла;
		
КонецПроцедуры 

Процедура ДеревоЗакладокПриИзмененииФлажка(Элемент, Колонка)
	
	УстановитьФлажкиРодителей(Элемент.ТекущиеДанные, Элемент.ТекущиеДанные.Видимость);
	УстановитьФлажкиПотомков(Элемент.ТекущиеДанные,  Элемент.ТекущиеДанные.Видимость);
	
	КоличествоФлажков = 0;
	
	Для Каждого Строка Из ДеревоЗакладок.Строки Цикл
		
		КоличествоФлажков = КоличествоФлажков + Строка.Видимость;
		
	КонецЦикла;
	
	Если КоличествоФлажков = 0 Тогда
		
		Элемент.ТекущиеДанные.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

