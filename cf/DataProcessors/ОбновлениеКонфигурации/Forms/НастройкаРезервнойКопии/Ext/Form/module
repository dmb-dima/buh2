Процедура ПриОткрытии()
	
	ОбновитьСостояниеЭлементовУправления();
	
КонецПроцедуры

Процедура ПолеКаталогРезервнойКопииНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Каталог = ПолучитьКаталогФайла(ИмяКаталогаРезервнойКопииИБ);
	Диалог.ПроверятьСуществованиеФайла = Истина;
	Диалог.Заголовок = "Выбор каталога резервной копии ИБ";
	
	Если Диалог.Выбрать() Тогда
		Элемент.Значение = Диалог.Каталог;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПереключательПриИзменении(Элемент)
	
	ОбновитьСостояниеЭлементовУправления();	
			
КонецПроцедуры

Процедура ОбновитьСостояниеЭлементовУправления()
	
	ЭлементыФормы.ПолеКаталогРезервнойКопии.АвтоОтметкаНезаполненного = (СоздаватьРезервнуюКопию = 1);
	ЭлементыФормы.ПолеКаталогРезервнойКопии.Доступность = (СоздаватьРезервнуюКопию = 1);
	ИнфоСтраницы = ЭлементыФормы.ПанельИнформация.Страницы;
	ЭлементыФормы.ПанельИнформация.ТекущаяСтраница = ?(СоздаватьРезервнуюКопию = 2, ИнфоСтраницы.БезОтката, 
		ИнфоСтраницы.АвтоматическийОткат);
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если СоздаватьРезервнуюКопию = 1 Тогда
		Файл = Новый Файл(ИмяКаталогаРезервнойКопииИБ);
		Отказ = НЕ Файл.Существует() ИЛИ НЕ Файл.ЭтоКаталог();
		Если Отказ Тогда
			Предупреждение("Укажите существующий каталог для сохранения резервной копии ИБ.");
			ТекущийЭлемент = ЭлементыФормы.ПолеКаталогРезервнойКопии;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

