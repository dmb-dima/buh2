////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события "ПередОткрытием" формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ПолучитьПараметрыБлокировки();
	
КонецПроцедуры // ПередОткрытием

// Процедура - обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии()
	
	ЭлементыФормы.ПанельКодРазрешения.Свертка = РежимСверткиЭлементаУправления.Нет;
	
	// в случае, если пользователь не имеет административных прав - 
	// предоставляем ему возможность только просмотра данных блокировки
	Если Не ПравоДоступа("Администрирование", Метаданные) Тогда
		ТолькоПросмотр = Истина;
		ЭлементыФормы.ПанельКодРазрешения.Свертка = РежимСверткиЭлементаУправления.Низ;
		ЭлементыФормы.Сообщение.УстановитьПривязку(ГраницаЭлементаУправления.Низ,
		                                             ЭлементыФормы.ПанельКодРазрешения, ГраницаЭлементаУправления.Верх);
	КонецЕсли;		
	Если ОпределитьЭтаИнформационнаяБазаФайловая() Тогда
		ЭлементыФормы.ПанельПараметрыАдминистрированияСервера.Свертка = РежимСверткиЭлементаУправления.Верх;
	КонецЕсли;
												 
КонецПроцедуры // ПриОткрытии()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура устанавливает блокировку соединений
//
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если УстановитьБлокировкуСоединений Тогда
		
		// проверки возможности установки блокировки				
		Если ЗначениеЗаполнено(ОкончаниеБлокировки) и  НачалоБлокировки > ОкончаниеБлокировки Тогда
			Сообщить("Дата окончания блокировки не можеть быть меньше даты начала блокировки. Блокировка не установлена...", СтатусСообщения.Важное);
			Возврат;
		КонецЕсли;	
				
		Если НЕ ПроверитьПараметрыАдминистрированияИБ() Тогда
			Возврат;
		КонецЕсли;	
		
		ТекстВопроса = 
			"После установки блокировки в информационной базе будет запущен 
			|режим завершения работы всех пользователей (включая Вас) 
			|на период действия блокировки.
			|Вы действительно хотите установить блокировку?";
		
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, 60, 
			КодВозвратаДиалога.Нет, "Установка блокировки соединений с информационной базой");				
		
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	УстановитьБлокировку();
	
КонецПроцедуры // КнопкаВыполнитьНажатие(Кнопка)

// Процедура выбора периода блокировки
//
Процедура ВыбПериодНажатие(Элемент)
	
	НастройкаПериода = Новый НастройкаПериода;
	
	НастройкаПериода.УстановитьПериод(НачалоБлокировки, ?(ОкончаниеБлокировки = '0001-01-01', ОкончаниеБлокировки, КонецДня(ОкончаниеБлокировки)));
	
	НастройкаПериода.РедактироватьКакИнтервал 	= Истина;
	НастройкаПериода.РедактироватьКакПериод 	= Истина;
	НастройкаПериода.ВариантНастройки 			= ВариантНастройкиПериода.Период;
	
	Если НастройкаПериода.Редактировать() Тогда
		НачалоБлокировки 	= НастройкаПериода.ПолучитьДатуНачала();
		ОкончаниеБлокировки = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
	
КонецПроцедуры // ВыбПериодНажатие


Процедура ПараметрыАдминистрированияСервераНажатие(Элемент)
	
	ЭтотОбъект.ПолучитьФорму("ПараметрыАдминистрированияСервера").ОткрытьМодально();
	
КонецПроцедуры

