Перем ИмяПоследовательности Экспорт;
Перем ПоследнийДокумент Экспорт;

Функция ПроверитьПоследовательность(Организация, Дата, ЗначениеГраницы) Экспорт
	
	Если НЕ (ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Дата)) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОтказУП = Ложь;
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, ОтказУП, Организация, Ложь);
	Если Не ОтказУП Тогда
		ИмяПоследовательности = ?(УчетнаяПолитика.СпособОценкиМПЗ = Перечисления.СпособыОценки.ПоСредней, "Взаиморасчеты", "ОбщаяПоследовательность");
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	ЗначениеГраницы = Последовательности[ИмяПоследовательности].ПолучитьГраницу(Новый Структура("Организация", Организация));
	
	Если ЗначениеГраницы.Дата >= КонецДня(Дата) Тогда
		Возврат Истина;
	Иначе
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОбщаяПоследовательность.Период
		|ИЗ
		|	Последовательность." + ИмяПоследовательности + " КАК ОбщаяПоследовательность
		|ГДЕ
		|	ОбщаяПоследовательность.Организация = &Организация И
		|	ОбщаяПоследовательность.Регистратор.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОбщаяПоследовательность.МоментВремени УБЫВ";
		
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			ПоследнийДокумент = Результат.Период;
			Возврат (Результат.Период <= ЗначениеГраницы.Дата);
		Иначе
			ПоследнийДокумент = ЗначениеГраницы.Дата;
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
		
КонецФункции

Процедура ВосстановитьПоследовательность(Организация, Дата, Момент = Неопределено) Экспорт
	
	Если НЕ (ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Дата)) Тогда
		Возврат;
	КонецЕсли;
	
	ОтказУП = Ложь;
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(Дата, ОтказУП, Организация, Ложь);
	Если Не ОтказУП Тогда
		ИмяПоследовательности = ?(УчетнаяПолитика.СпособОценкиМПЗ = Перечисления.СпособыОценки.ПоСредней, "Взаиморасчеты", "ОбщаяПоследовательность");
	Иначе
		Возврат;
	КонецЕсли;
	
	ТаблицаОтбора = Новый ТаблицаЗначений;
	ТаблицаОтбора.Колонки.Добавить("Организация");
	ТаблицаОтбора.Добавить().Организация = Организация;
	
	Попытка
		Если ЗначениеЗаполнено(Момент) Тогда
			Последовательности[ИмяПоследовательности].Восстановить(Момент, ТаблицаОтбора);
		Иначе
			Последовательности[ИмяПоследовательности].Восстановить(	,ТаблицаОтбора);
		КонецЕсли;
	Исключение
		
		ЗначениеГраницы = Последовательности[ИмяПоследовательности].ПолучитьГраницу(Новый Структура("Организация", Организация));
		Если НЕ ЗначениеГраницы.Дата >= КонецДня(Дата) Тогда
			
			Запрос = Новый Запрос();
			Запрос.УстановитьПараметр("Организация", Организация);
			Запрос.УстановитьПараметр("МоментВремени", ЗначениеГраницы);
			Запрос.Текст = "
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ОбщаяПоследовательность.Регистратор
			|ИЗ
			|	Последовательность." + ИмяПоследовательности + " КАК ОбщаяПоследовательность
			|ГДЕ
			|	ОбщаяПоследовательность.Организация = &Организация
			|	И ОбщаяПоследовательность.МоментВремени > &МоментВремени
			|
			|УПОРЯДОЧИТЬ ПО
			|	ОбщаяПоследовательность.МоментВремени";
			
			Результат = Запрос.Выполнить().Выбрать();
			Если Результат.Следующий() Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не проводится документ " + Результат.Регистратор,,"Автоматическое восстановление последовательности прервано.",СтатусСообщения.Обычное);
			КонецЕсли;
		КонецЕсли;
		
	КонецПопытки;
                 	
КонецПроцедуры