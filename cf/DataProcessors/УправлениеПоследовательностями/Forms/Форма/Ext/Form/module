
//Определяет имя используемой для организации последовательности
Процедура ВыбратьПоследовательность()
	
	Если НЕ (ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ПериодКон)) Тогда
		Возврат;
	КонецЕсли;
	
	ОтказУП = Ложь;
	УчетнаяПолитика = ОбщегоНазначения.ПолучитьПараметрыУчетнойПолитики(ПериодКон, ОтказУП, Организация, Ложь);
		
	//Документ не регистрируется в последовательности если не задана учетная политика или
	//по данным учетной политики документ регистрируется в другой последовательности
	Если Не ОтказУП Тогда
		ИмяПоследовательности = ?(УчетнаяПолитика.СпособОценкиМПЗ = Перечисления.СпособыОценки.ПоСредней, "Взаиморасчеты", "ОбщаяПоследовательность");
	Иначе
		ИмяПоследовательности = Неопределено;
	КонецЕсли;
КонецПроцедуры

//Выводит сведения о текущей 
Процедура ВывестиСведения()
	
	Если Не ЗначениеЗаполнено(ИмяПоследовательности) Тогда
		ЭлементыФормы.СостояниеПоследовательности.Заголовок = "Используемая последовательность не определена";
		ЭлементыФормы.ТекущаяГраница.Заголовок = "";
		ЭлементыФормы.ПанельВосстановления.Свертка = РежимСверткиЭлементаУправления.Верх;
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы.НадписьНачалоПериода.Заголовок = Формат(НачалоДня(ПериодНач),"ДЛФ=DD");
	ЭлементыФормы.НадписьКонецПериода.Заголовок = Формат(КонецДня(ПериодКон),"ДЛФ=DD");

	ЗначениеГраницы = Неопределено;
	РезультатПроверки = ПроверитьПоследовательность(Организация, КонецДня(ПериодКон), ЗначениеГраницы);
	
	Если НЕ ЗначениеЗаполнено(ЗначениеГраницы) Тогда
		ЭлементыФормы.СостояниеПоследовательности.Заголовок = "В этом периоде очередность проведения не контролировалась";
		ЭлементыФормы.РазделительПанелей.Лево = ЭлементыФормы.ТекущаяГраница.Лево + ЭлементыФормы.ТекущаяГраница.Ширина - 18;
		ЭлементыФормы.ТекущаяГраница.Заголовок = "";
		ЭлементыФормы.ПанельВосстановления.Свертка = РежимСверткиЭлементаУправления.Верх;
		Возврат;
	КонецЕсли;
	
	Если РезультатПроверки Тогда
		ЭлементыФормы.РазделительПанелей.Лево = ЭлементыФормы.ТекущаяГраница.Лево + ЭлементыФормы.ТекущаяГраница.Ширина - 18;
		ЭлементыФормы.СостояниеПоследовательности.Заголовок = "Результат проверки: Документы проведены в правильной последовательности";
		ЭлементыФормы.СостояниеПоследовательности.ЦветТекста = Новый Цвет(0,128,0);
		ЭлементыФормы.ТекущаяГраница.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
		ЭлементыФормы.ТекущаяГраница.Заголовок = "Перепроводить документы не требуется";
		ЭлементыФормы.ПанельВосстановления.Свертка = РежимСверткиЭлементаУправления.Верх;
		ЭлементыФормы.НадписьКнопкаПерепровести.Заголовок = "";
	Иначе
		ЭлементыФормы.ТекущаяГраница.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
		Если ЗначениеЗаполнено(ЗначениеГраницы.Ссылка) Тогда
			ПредставлениеДокумента = СокрЛП(ЗначениеГраницы.Ссылка);
			ЭлементыФормы.ТекущаяГраница.Заголовок = "Рекомендуется перепровести документы, начиная с: """ + ПредставлениеДокумента + """";
			ЭлементыФормы.НадписьКнопкаПерепровести.Заголовок = "Перепровести документы, начиная с: """ + ПредставлениеДокумента + """";
		Иначе
			ЭлементыФормы.ТекущаяГраница.Заголовок = "Рекомендуется перепровести документы, начиная с: " + Формат(ЗначениеГраницы.Дата,"ДЛФ=DD");
			ЭлементыФормы.НадписьКнопкаПерепровести.Заголовок = "Перепровести документы, начиная с: " + Формат(ЗначениеГраницы.Дата,"ДЛФ=DD");
		КонецЕсли;
		
		ЭлементыФормы.СостояниеПоследовательности.Заголовок = "Результат проверки: Нарушена последовательность проведения документов";
		ЭлементыФормы.СостояниеПоследовательности.ЦветТекста = Новый Цвет(255,0,0);
		ЭлементыФормы.ПанельВосстановления.Свертка = РежимСверткиЭлементаУправления.Нет;
		
		Если ЗначениеГраницы.Дата <= НачалоДня(ПериодНач) Тогда
			ЭлементыФормы.РазделительПанелей.Лево = ЭлементыФормы.ТекущаяГраница.Лево + 14;
			ЭлементыФормы.НадписьНачалоПериода.Заголовок = Формат(ЗначениеГраницы.Дата,"ДЛФ=DD");
		Иначе
			Коэфф = (ЗначениеГраницы.Дата - НачалоДня(ПериодНач)) / (КонецДня(ПериодКон) - НачалоДня(ПериодНач));
			ЭлементыФормы.РазделительПанелей.Лево = Окр(ЭлементыФормы.ТекущаяГраница.Лево + 14 + ((ЭлементыФормы.ТекущаяГраница.Ширина - 32)*Коэфф),0,1);
		КонецЕсли;			
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	Если НЕ ЗначениеЗаполнено(ПериодНач) Тогда
		ПериодНач = НачалоМесяца(ПериодКон);
	КонецЕсли;
	
	ВыбратьПоследовательность();
	ВывестиСведения();
КонецПроцедуры

Процедура КнопкаВосстановитьПоследовательностьНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(ИмяПоследовательности) Тогда
		Предупреждение("Не определена последовательность.");
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы.СостояниеПоследовательности.Заголовок = "Выполняется восстановление хронологической последовательности документов";
	ЭлементыФормы.ПанельНеАктуально.ЦветФона = Новый Цвет(150,150,150);
	
	Если ПоследнийДокумент > ДобавитьМесяц(ПериодКон, 1) Тогда
		ВосстановитьПоследовательность(Организация, ПериодКон, КонецДня(ПериодКон));		
	Иначе
		ВосстановитьПоследовательность(Организация, ПериодКон);		
	КонецЕсли;
	
	ВывестиСведения();
	ЭлементыФормы.ПанельНеАктуально.ЦветФона = Новый Цвет(255, 190, 190);
	
КонецПроцедуры

Процедура КнопкаУстановитьГраницуНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(ИмяПоследовательности) Тогда
		Предупреждение("Не определена последовательность.");
		Возврат;
	КонецЕсли;
	
	ДатаГраницы = КонецДня(ПериодКон);
	Если ВвестиДату(ДатаГраницы, "Введите новую контрольную дату", ЧастиДаты.Дата) Тогда
		СтруктураОтбора = Новый Структура("Организация", Организация);
		Последовательности[ИмяПоследовательности].УстановитьГраницу(КонецДня(ДатаГраницы) ,СтруктураОтбора);
	КонецЕсли;
	
	ВывестиСведения();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если (НЕ ЗначениеЗаполнено(ПериодКон)) ИЛИ (НЕ ЗначениеЗаполнено(Организация)) Тогда
		Предупреждение("Обработка предназначена для вызова из документов");
		Отказ = Истина;
	КонецЕсли;		
		
КонецПроцедуры

Процедура ПриЗакрытии()
	
		Оповестить("РегОперация", , "Последовательность");

КонецПроцедуры
