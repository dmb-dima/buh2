Процедура УстановитьОплатуСтрокТаблицы(ТаблицаДляОплаты, ЗначениеОтметки)

	Для каждого СтрокаТаблицы Из ТаблицаДляОплаты Цикл
		СтрокаТаблицы.Оплатить = ЗначениеОтметки;
	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьНадписи()

	ИтогоОплатить = 0;
	Для каждого СтрокаОстатка Из БухОстатки Цикл
		Если СтрокаОстатка.Оплатить Тогда
			ИтогоОплатить = ИтогоОплатить + СтрокаОстатка.Сумма; 
		КонецЕсли;
	КонецЦикла;
	
	ЭлементыФормы.НадписьИтогоОплатить.Заголовок = "Итого оплатить: " 
		+ Формат(ИтогоОплатить, "ЧЦ=15; ЧДЦ=2; ЧН=-") + " руб.";

	ЭлементыФормы.НадписьИтогоСуммаДокументов.Заголовок = "Итого на сумму: " 
		+ Формат(ПлатежныеПоручения.Итог("СуммаДокумента"), "ЧЦ=15; ЧДЦ=2; ЧН=-") + " руб.";
	
КонецПроцедуры

Процедура КнопкаСформироватьНажатие(Кнопка)
	
	Если НЕ ЗначениеЗаполнено(СчетОрганизации) Тогда
		Предупреждение("Не указан счет организации");
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.СчетОрганизации;
		Возврат;
	КонецЕсли;
	
	Если БухОстатки.Найти(Истина, "Оплатить") = Неопределено Тогда
	    Предупреждение("Нет остатков, отмеченных для оплаты");
		Возврат;
	КонецЕсли;
	СформироватьПлатежныеПорученияПоОстаткам();
	
	ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = ЭлементыФормы.ОсновнаяПанель.Страницы.ПлатежныеПоручения;
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОсновныеДействияФормыСформировать.Доступность  = Ложь;
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОсновныеДействияФормыЗакрыть.КнопкаПоУмолчанию = Истина;	
	ТекущийЭлемент = ЭлементыФормы.ПлатежныеПоручения;
	
	ОбновитьНадписи();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ДатаОстатков = '00010101' Тогда
		ДатаОстатков = РабочаяДата;
	КонецЕсли;
	
	Организация = глЗначениеПеременной("ОсновнаяОрганизация");
	УправлениеДенежнымиСредствами.УстановитьБанковскийСчет(СчетОрганизации, Организация, мВалютаРегламентированногоУчета);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТаблицаОстатков = ЗаполнитьОстаткиНаСчетах68и69(ДатаОстатков);
		БухОстатки.Загрузить(ТаблицаОстатков);
		Если БухОстатки.Количество() > 0 Тогда
			УстановитьОплатуСтрокТаблицы(БухОстатки, Истина);
			ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОсновныеДействияФормыСформировать.КнопкаПоУмолчанию = Истина;
		Иначе
			ЭлементыФормы.КоманднаяПанельБухОстатки.Кнопки.Заполнить.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		ТекущийЭлемент = ЭлементыФормы.БухОстатки;
	Иначе
		ЭлементыФормы.КоманднаяПанельБухОстатки.Кнопки.Заполнить.КнопкаПоУмолчанию = Истина;		
	КонецЕсли;
	
	ОбновитьНадписи();
	
КонецПроцедуры

Процедура БухИтогиПриИзмененииФлажка(Элемент, Колонка)
	
	ОбновитьНадписи();
	
КонецПроцедуры

Процедура БухОстаткиПередНачаломИзменения(Элемент, Отказ)
	
	ТекКолонка = ЭлементыФормы.БухОстатки.ТекущаяКолонка;
	Если ТекКолонка.Имя <> "Оплатить" Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельБухОстаткиЗаполнить(Кнопка)
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Предупреждение("Не выбрана организация");
		ЭтаФорма.ТекущийЭлемент = ЭлементыФормы.Организация;
		Возврат;
	КонецЕсли;
	Если ДатаОстатков = '00010101' Тогда
		ДатаОстатков = ТекущаяДата();
	КонецЕсли;
	
	ТаблицаОстатков = ЗаполнитьОстаткиНаСчетах68и69(ДатаОстатков);
	БухОстатки.Загрузить(ТаблицаОстатков);
	
	Если БухОстатки.Количество() > 0 Тогда
		УстановитьОплатуСтрокТаблицы(БухОстатки, Истина);
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОсновныеДействияФормыСформировать.КнопкаПоУмолчанию = Истина;
	Иначе
		ЭлементыФормы.КоманднаяПанельБухОстатки.Кнопки.Заполнить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	ТекущийЭлемент = ЭлементыФормы.БухОстатки;
	
	ОбновитьНадписи();
	
КонецПроцедуры

Процедура КоманднаяПанельБухОстаткиОтметитьВсе(Кнопка)
	
	УстановитьОплатуСтрокТаблицы(БухОстатки, Истина);
	
КонецПроцедуры

Процедура КоманднаяПанельБухОстаткиСнятьВсеОтметки(Кнопка)
	
	УстановитьОплатуСтрокТаблицы(БухОстатки, Ложь);
	
КонецПроцедуры

Процедура ПлатежныеПорученияПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекДанные = ЭлементыФормы.ПлатежныеПоручения.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Предупреждение("Не выбрано платежное поручение");
		Возврат;
	КонецЕсли;
	
	ТекДокумент = ТекДанные.Ссылка;
	Если НЕ ЗначениеЗаполнено(ТекДокумент) Тогда
		Предупреждение("Не выбрано платежное поручение");
		Возврат;
	КонецЕсли;
	
	ТекДокумент.ПолучитьФорму(, ЭтаФорма).Открыть();
	
КонецПроцедуры

Процедура ОрганизацияПриИзменении(Элемент)
	
	УправлениеДенежнымиСредствами.УстановитьБанковскийСчет(СчетОрганизации, Организация, мВалютаРегламентированногоУчета);
	
	БухОстатки.Очистить();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТаблицаОстатков = ЗаполнитьОстаткиНаСчетах68и69(ДатаОстатков);
		БухОстатки.Загрузить(ТаблицаОстатков);
		Если БухОстатки.Количество() > 0 Тогда
			УстановитьОплатуСтрокТаблицы(БухОстатки, Истина);
			ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОсновныеДействияФормыСформировать.КнопкаПоУмолчанию = Истина;
		Иначе
			ЭлементыФормы.КоманднаяПанельБухОстатки.Кнопки.Заполнить.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
		ТекущийЭлемент = ЭлементыФормы.БухОстатки;
	Иначе
		ЭлементыФормы.КоманднаяПанельБухОстатки.Кнопки.Заполнить.КнопкаПоУмолчанию = Истина;		
	КонецЕсли;
	
	ЭлементыФормы.ОсновнаяПанель.ТекущаяСтраница = ЭлементыФормы.ОсновнаяПанель.Страницы.ВыборВарианта;
	ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОсновныеДействияФормыСформировать.Доступность  = Истина;
	
	ОбновитьНадписи();
	
КонецПроцедуры

Процедура КоманднаяПанельПлатежныеПорученияНайтиВСписке(Кнопка)
	
	ТекДанные = ЭлементыФормы.ПлатежныеПоручения.ТекущиеДанные;
	
	ФормаСписка = Документы.ПлатежноеПоручение.ПолучитьФормуСписка();
	
	ФормаСписка.Организация = Организация;
	ФормаСписка.ДокументСписок.Отбор.Организация.Установить(Организация);
	ФормаСписка.БанковскийСчет = СчетОрганизации;
	ФормаСписка.ДокументСписок.Отбор.СчетОрганизации.Установить(СчетОрганизации);
	ФормаСписка.ПараметрТекущаяСтрока = ТекДанные.Ссылка;
	
	ФормаСписка.Открыть();
	
КонецПроцедуры

Процедура ПлатежныеПорученияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.НомерСтроки.УстановитьКартинку(
		ЭлементыФормы.ПлатежныеПоручения.Колонки.НомерСтроки.КартинкиСтрок);
	
КонецПроцедуры
