Перем ИмяФайла;

#Если Клиент Тогда

// Выделяет из переданной строки первое значение
 //  до символа "TAB"
 //
 // Параметры: 
 //  ИсходнаяСтрока - Строка - строка для разбора
 //
 // Возвращаемое значение:
 //  подстроку до символа "TAB"
 //
Функция ВыделитьПодСтроку(ИсходнаяСтрока)

	Перем ПодСтрока;
	
    Поз = Найти(ИсходнаяСтрока,Символы.Таб);
	Если Поз > 0 Тогда
		ПодСтрока = Лев(ИсходнаяСтрока,Поз-1);
		ИсходнаяСтрока = Сред(ИсходнаяСтрока,Поз+1);
	Иначе
		ПодСтрока = ИсходнаяСтрока;
		ИсходнаяСтрока = "";
	КонецЕсли;
	
	Возврат ПодСтрока;
 
 КонецФункции // ВыделитьПодСтроку()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура выполняет загрузку курсов валют с РБК.
//
Процедура ЗагрузитьКурсыВалют() Экспорт
	
	Если СписокВалют.Количество() = 0 Тогда
		Сообщить("Не заполнен список валют", СтатусСообщения.Важное);
	КонецЕсли;
	
	ФормаИндикации = ПолучитьОбщуюФорму("ХодВыполненияОбработкиДанных");
	ФормаИндикации.НаименованиеОбработкиДанных = "Загрузка курсов валют с сайта РосБизнесКонсалтинг";
	ФормаИндикации.КомментарийЗначения         = "Загружено:";
	ФормаИндикации.МаксимальноеЗначение         = 100;
	ФормаИндикации.Открыть();
	
	ЗагрузитьКурсыСРБК(ФормаИндикации.Значение,ФормаИндикации.КомментарийОбработкиДанных);
	
	ФормаИндикации.Закрыть();
	
КонецПроцедуры // ЗагрузитьКурсыВалют()

// Процедура заполняет табличную часть СписокВалют.
// Список валют и текущих курсов валют в ИБ.
//
Процедура ОбновитьСписокВалют() Экспорт
	
	СписокВалют.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Валюты.Ссылка,
	               |	КурсыВалют.Период КАК ДатаКурса,
	               |	КурсыВалют.Курс
	               |ИЗ
	               |	Справочник.Валюты КАК Валюты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних() КАК КурсыВалют
	               |		ПО КурсыВалют.Валюта = Валюты.Ссылка
	               |ГДЕ
	               |	(НЕ Валюты.ПометкаУдаления)
	               |	И (НЕ Валюты.Наименование ПОДОБНО ""%руб.%"")";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Валюта = СписокВалют.Добавить();
		
		Валюта.Валюта = Выборка.Ссылка;
		Валюта.ДатаКурса = Выборка.ДатаКурса;
		Валюта.Курс = Выборка.Курс;
		
		Если ЗначениеЗаполнено(Валюта.ДатаКурса) И НачалоДня(Валюта.ДатаКурса) < НачалоДня(ТекущаяДата()) Тогда
			Валюта.Загрузка = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ОбновитьСписокВалют()

// Производит загрузку курсов и кратностей валют с сайта РБК
//
// Параметры:
//  ИндикаторФормы     - ЭлементыФормы типа Индикатор - для отработки индикатора формы
//  НадписьВалютыФормы - ЭлементыФормы типа Надпись  - Для отработки надписи 
//													   загружаемой валюты
//
Процедура ЗагрузитьКурсыСРБК(ИндикаторФормы ,НадписьВалютыФормы ) Экспорт
	
	Перем HTTP;
	
	РегистрКурсыВалют = РегистрыСведений.КурсыВалют;
	ЗаписьКурсовВалют = РегистрКурсыВалют.СоздатьМенеджерЗаписи();

	Текст = Новый ТекстовыйДокумент();

	СерверИсточник = "cbrates.rbc.ru";
	ОбработкаПолученияФайлов = Обработки.ПолучениеФайловИзИнтернета.Создать();

	Адрес1 = "tsv/cb/";  // в интервале
	Адрес2 = "tsv/";     // по 1 дате
	Если НачДата = КонДата Тогда  // по 1 дате
		Адрес = Адрес2;
		ТМП   = "/"+Формат(Год(КонДата),"ЧРГ=; ЧГ=0")+"/"+Формат(Месяц(КонДата),"ЧЦ=2;ЧДЦ=0;ЧВН=")+"/"+Формат(День(КонДата),"ЧЦ=2;ЧДЦ=0;ЧВН=");
	Иначе    // в интервале
		Адрес = Адрес1;
		ТМП   = "";
	КонецЕсли;

	ВремКаталог = КаталогВременныхФайлов() + "tempKurs";
	СоздатьКаталог(ВремКаталог);
	УдалитьФайлы(ВремКаталог,"*.*");
	
	Для каждого СтрокаСпВалют из СписокВалют Цикл
		
		Если НЕ СтрокаСпВалют.Загрузка Тогда
			Продолжить;
		КонецЕсли;

		ТекВалюта = СтрокаСпВалют.Валюта;
		Стр = "";
		ИмяВходящегоФайла = "" + ВремКаталог + "\" + ИмяФайла;
		СтрокаПараметраПолучения = Адрес + Прав(ТекВалюта.Код,3) + ТМП + ".tsv";
		Если ОбработкаПолученияФайлов.ЗапроситьФайлыССервера(СерверИсточник, СтрокаПараметраПолучения, ИмяВходящегоФайла, HTTP) <> Истина Тогда
			Сообщить("Не удалось получить ресурс для валюты " + СокрЛП(ТекВалюта.Наименование) + " (код " + ТекВалюта.Код + "). Курс для валюты не загружен."); 
			Продолжить;
		КонецЕсли; 

		ВходящийФайл = Новый Файл(ИмяВходящегоФайла);
		Если НЕ ВходящийФайл.Существует() Тогда
			Сообщить("Не удалось получить ресурс для валюты " + СокрЛП(ТекВалюта.Наименование) + " (код " + ТекВалюта.Код + "). Курс для валюты не загружен."); 
			Продолжить;
		КонецЕсли;	

		Текст.Прочитать(ИмяВходящегоФайла,КодировкаТекста.ANSI);
		
		КолСтрок = Текст.КоличествоСтрок();
		Для Инд = 1 По КолСтрок Цикл
			НадписьВалютыФормы = СокрЛП(ТекВалюта.Наименование);

			ИндикаторФормы = Инд/КолСтрок * 100;
				
			Стр = Текст.ПолучитьСтроку(Инд);
			Если (Стр = "") ИЛИ (Найти(Стр,Символы.Таб) = 0) Тогда
			   Продолжить;
			КонецЕсли;
			Если НачДата = КонДата Тогда  
			   ДатаКурса = КонДата;
			Иначе 
			   ДатаКурсаСтр = ВыделитьПодСтроку(Стр);
			   ДатаКурса    = Дата(Лев(ДатаКурсаСтр,4),Сред(ДатаКурсаСтр,5,2),Сред(ДатаКурсаСтр,7,2));
			КонецЕсли;
			Кратность = Число(ВыделитьПодСтроку(Стр));
			Курс      = Число(ВыделитьПодСтроку(Стр));

			Если ДатаКурса > КонДата Тогда
			   Прервать;
			КонецЕсли;

			Если ДатаКурса < НачДата Тогда 
			   Продолжить;
			КонецЕсли;

            ЗаписьКурсовВалют.Валюта = ТекВалюта;
			ЗаписьКурсовВалют.Период = ДатаКурса;
			ЗаписьКурсовВалют.Прочитать();
			ЗаписьКурсовВалют.Валюта    = ТекВалюта;
			ЗаписьКурсовВалют.Период    = ДатаКурса;
			ЗаписьКурсовВалют.Курс      = Курс;
			ЗаписьКурсовВалют.Кратность = Кратность;
			ЗаписьКурсовВалют.Записать();
		КонецЦикла;	   
	КонецЦикла;	
	УдалитьФайлы(ВремКаталог,"*.*");

КонецПроцедуры // ЗагрузитьКурсыСРБК()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ИмяФайла = "Curses.txt";  

#КонецЕсли