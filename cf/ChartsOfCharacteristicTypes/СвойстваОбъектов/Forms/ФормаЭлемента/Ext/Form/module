////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если ЭтоНовый() Тогда
		ДлинаЧисла    = 15;
		ТочностьЧисла = 2;
		ДлинаСтроки   = 150;
		ВидСтроки     = 1;
		СоставДаты    = 2;
	КонецЕсли;
	
	Если ТипЗначения.СодержитТип(Тип("Число")) Тогда
		ТипРеквизита = 0;
		ДлинаЧисла    = ТипЗначения.КвалификаторыЧисла.Разрядность;
		ТочностьЧисла = ТипЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти;
		Неотрицательное = ?(ТипЗначения.КвалификаторыЧисла.ДопустимыйЗнак = ДопустимыйЗнак.Неотрицательный, Истина, Ложь);
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ТипЧисло;
	ИначеЕсли ТипЗначения.СодержитТип(Тип("Строка")) Тогда
		ТипРеквизита = 1;
		ДлинаСтроки = ТипЗначения.КвалификаторыСтроки.Длина;
		ВидСтроки   = ?(ТипЗначения.КвалификаторыСтроки.ДопустимаяДлина = ДопустимаяДлина.Переменная, 1, 0);
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ТипСтрока;
	ИначеЕсли ТипЗначения.СодержитТип(Тип("Дата")) Тогда
		ТипРеквизита = 2;
		Если ТипЗначения.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Дата Тогда
			СоставДаты = 0;
		ИначеЕсли ТипЗначения.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Время Тогда
			СоставДаты = 1;
		ИначеЕсли ТипЗначения.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.ДатаВремя Тогда
			СоставДаты = 2;
		КонецЕсли;
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ТипДата;	
	ИначеЕсли ТипЗначения.СодержитТип(Тип("Булево")) Тогда
		ТипРеквизита = 3;
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ПустаяСтраница;
	ИначеЕсли ТипЗначения.СодержитТип(Тип("СправочникСсылка.ЗначенияСвойствОбъектов")) Тогда
		ТипРеквизита = 5;
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ПустаяСтраница;
	Иначе       
		ТипРеквизита = 4;
		Справочник = ТипЗначения.ПривестиЗначение().Метаданные().Имя;
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ТипСправочник;
	КонецЕсли;
	
КонецПроцедуры
 
// Обработчик события ПриЗаписи формы.
//
Процедура ПриЗаписи(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		Предупреждение("Не заполнен обязательный реквизит: ""Наименование""");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(НазначениеСвойства) Тогда
		Предупреждение("Не заполнен обязательный реквизит: ""Назначение свойства""");
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ТипЗначения) Тогда
		Предупреждение("Не заполнен обязательный реквизит: ""Тип значения""");
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

Процедура ТипПриИзменении(Элемент)
	
	Если ТипРеквизита = 0 Тогда
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ТипЧисло;
	ИначеЕсли ТипРеквизита = 1 Тогда
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ТипСтрока; 
	ИначеЕсли ТипРеквизита = 2 Тогда
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ТипДата;
	ИначеЕсли ТипРеквизита = 4 Тогда
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ТипСправочник;
	Иначе
		ЭлементыФормы.ПанельТипов.ТекущаяСтраница = ЭлементыФормы.ПанельТипов.Страницы.ПустаяСтраница;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Категория = Ложь;
	Если ТипРеквизита = 0 Тогда
		РазрядностьЦелойЧастиДопустимая = Метаданные().Тип.КвалификаторыЧисла.Разрядность - Метаданные().Тип.КвалификаторыЧисла.РазрядностьДробнойЧасти;
		РазрядностьЦелойЧастиФактическая = ДлинаЧисла - ТочностьЧисла;
		Если РазрядностьЦелойЧастиФактическая > РазрядностьЦелойЧастиДопустимая Тогда
			ДлинаЧисла = ДлинаЧисла - (РазрядностьЦелойЧастиФактическая - РазрядностьЦелойЧастиДопустимая);
		КонецЕсли;
		ТипЗначения = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(ДлинаЧисла, ТочностьЧисла, ?(Неотрицательное, ДопустимыйЗнак.Неотрицательный, ДопустимыйЗнак.Любой)));
	ИначеЕсли ТипРеквизита = 1 Тогда
		ТипСтроки = ?(ВидСтроки = 0, ДопустимаяДлина.Фиксированная, ДопустимаяДлина.Переменная);
		ТипЗначения = Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(ДлинаСтроки, ТипСтроки));
	ИначеЕсли ТипРеквизита = 2 Тогда
		Если СоставДаты = 0 Тогда
			ЧастьДаты = ЧастиДаты.Дата;
		ИначеЕсли СоставДаты = 1 Тогда
			ЧастьДаты = ЧастиДаты.Время;
		ИначеЕсли СоставДаты = 2 Тогда
			ЧастьДаты = ЧастиДаты.ДатаВремя;
		КонецЕсли;
		ТипЗначения = Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастьДаты));	
	ИначеЕсли ТипРеквизита = 3 Тогда
		ТипЗначения = Новый ОписаниеТипов("Булево");
		Категория = Истина;
	ИначеЕсли ТипРеквизита = 5 Тогда
		ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ЗначенияСвойствОбъектов");
	Иначе       
		ТипЗначения = Новый ОписаниеТипов("СправочникСсылка." + Справочник);
	КонецЕсли;

КонецПроцедуры




