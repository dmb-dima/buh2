Перем мКэшРеквизитовДокумента;

Процедура ЗаполнитьТабличнуюЧасть()

	Список.Очистить();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Параметр", ДоговорКонтрагента);

	Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Ссылка                 КАК Ссылка,
		|	Ссылка.Дата            КАК Дата,
		|	Ссылка.Номер           КАК Номер
		|
		|ИЗ
		|	КритерийОтбора.ДокументыПоДоговоруКонтрагента(&Параметр) КАК Договоры
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка.Дата
		|";

	Список = Запрос.Выполнить().Выгрузить();

КонецПроцедуры

Процедура ПриОткрытии()

	Контрагент = ПараметрОтборПоЗначению.Владелец;
	ДоговорКонтрагента = ПараметрОтборПоЗначению;
	ЗаполнитьТабличнуюЧасть();
	
КонецПроцедуры

Процедура КоманднаяПанельФормыОбновить(Кнопка)

	ЗаполнитьТабличнуюЧасть();

КонецПроцедуры

Процедура ОтборЗначениеОтбораПриИзменении(Элемент)

	ЗаполнитьТабличнуюЧасть();

КонецПроцедуры

Процедура ОтборЗначениеОтбораОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ВыбраннаяСтрока.Ссылка.ПолучитьФорму().Открыть();

КонецПроцедуры

Процедура КонтрагентПриИзменении(Элемент)

	// Если изменили контрагента, то установим отбор по основному 
	// договору выбранного контрагента.
	Если ДоговорКонтрагента.Владелец <> Контрагент Тогда
		ДоговорКонтрагента = Контрагент.ОсновнойДоговорКонтрагента;
		ЗаполнитьТабличнуюЧасть();
	КонецЕсли;

КонецПроцедуры

Процедура КонтрагентОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

Процедура СписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	КэшОформленияСтрок = Новый Соответствие;
	КэшПоТипамДокументов = Новый Соответствие;
	
	Для Каждого ОформлениеСтроки ИЗ ОформленияСтрок Цикл
		КэшОформленияСтрок.Вставить(ОформлениеСтроки.ДанныеСтроки.Ссылка, ОформлениеСтроки);		
		МетаданныеДокумента = ОформлениеСтроки.ДанныеСтроки.Ссылка.Метаданные();
		ИмяДокумента = МетаданныеДокумента.Имя;
		СинонимДокумента = МетаданныеДокумента.Синоним;
		
		ДополнитьКэшМетаданных(МетаданныеДокумента, ИмяДокумента);
		
		СтруктураТипа = КэшПоТипамДокументов[ИмяДокумента];
		Если СтруктураТипа = Неопределено Тогда
			СтруктураТипа = Новый Структура("Синоним, МассивСсылок", СинонимДокумента, Новый Массив);
			КэшПоТипамДокументов.Вставить(ИмяДокумента, СтруктураТипа);
		КонецЕсли;
		СтруктураТипа.МассивСсылок.Добавить(ОформлениеСтроки.ДанныеСтроки);		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Для Каждого КлючИЗначение ИЗ КэшПоТипамДокументов Цикл
		Запрос.Текст = Запрос.Текст + ?(Запрос.Текст = "", "", "
					|ОБЪЕДИНИТЬ ВСЕ 
					|") + "ВЫБРАТЬ
					|Ссылка,Проведен,ПометкаУдаления,
					|" + ?(мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["ВидОперации"], "ВидОперации", "NULL") + " КАК ВидОперации,
					|" + ?(мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["СуммаДокумента"], "СуммаДокумента", "NULL") + " КАК СуммаДокумента,
					|" + ?(мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["ВалютаДокумента"], "ВалютаДокумента.Представление", "NULL") + " КАК Валюта,
					|" + ?(мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["Организация"], "Организация.Представление", "NULL") + " КАК Организация,
					|" + ?(мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["Ответственный"], "Ответственный.Представление", "NULL") + " КАК Ответственный,
					|""" + КлючИЗначение.Значение.Синоним + """ КАК Синоним 
					|ИЗ Документ." + КлючИЗначение.Ключ + "
					|ГДЕ Ссылка В (&" + КлючИЗначение.Ключ + ")";
					
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение.МассивСсылок);		
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОформлениеСтроки = КэшОформленияСтрок[Выборка.Ссылка];
		ОформлениеСтроки.Ячейки.Вид.УстановитьТекст(Выборка.Синоним);			
		ОформлениеСтроки.Ячейки.ВидОперации.УстановитьТекст(Выборка.ВидОперации);
		ОформлениеСтроки.Ячейки.Сумма.УстановитьТекст(Формат(Выборка.СуммаДокумента, "ЧДЦ=2"));
		ОформлениеСтроки.Ячейки.Валюта.УстановитьТекст(Выборка.Валюта);
		ОформлениеСтроки.Ячейки.Организация.УстановитьТекст(Выборка.Организация);
		ОформлениеСтроки.Ячейки.Ответственный.УстановитьТекст(Выборка.Ответственный);
		ОформлениеСтроки.Ячейки.Картинка.ОтображатьКартинку = Истина;
		Если Выборка.Проведен Тогда	
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 0;
		Иначе 
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 2;
		КонецЕсли; 
		
		Если Выборка.ПометкаУдаления Тогда
			ОформлениеСтроки.Ячейки.Картинка.ИндексКартинки = 1;
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьКэшМетаданных(МетаданныеДокумента, ИмяДокумента)
	РеквизитыДокумента = мКэшРеквизитовДокумента[ИмяДокумента];
	Если РеквизитыДокумента = Неопределено Тогда
		РеквизитыДокумента = Новый Соответствие;
		
		РеквизитыДокумента.Вставить("ВидОперации", МетаданныеДокумента.Реквизиты.Найти("ВидОперации") <> Неопределено);
		РеквизитыДокумента.Вставить("СуммаДокумента", МетаданныеДокумента.Реквизиты.Найти("СуммаДокумента") <> Неопределено);
		РеквизитыДокумента.Вставить("ВалютаДокумента", МетаданныеДокумента.Реквизиты.Найти("ВалютаДокумента") <> Неопределено);
		РеквизитыДокумента.Вставить("Организация", МетаданныеДокумента.Реквизиты.Найти("Организация") <> Неопределено);
		РеквизитыДокумента.Вставить("Ответственный", МетаданныеДокумента.Реквизиты.Найти("Ответственный") <> Неопределено);	
		
		мКэшРеквизитовДокумента.Вставить(ИмяДокумента, РеквизитыДокумента);
	КонецЕсли;
	
КонецПроцедуры


мКэшРеквизитовДокумента = Новый Соответствие;