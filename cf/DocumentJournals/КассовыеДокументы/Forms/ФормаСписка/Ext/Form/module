Перем КартинкаРучнаяКорректировка;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура установки быстрого отбора
//
Процедура УстановитьОтбор()

	ОтключитьОбработчикИзмененияДанных("ЖурналДокументовСписок.Отбор");
	
	Отбор.Организация.Установить(Организация, ЗначениеЗаполнено(Организация));
	
	ПодключитьОбработчикИзмененияДанных("ЖурналДокументовСписок.Отбор", "ЖурналДокументовСписокПриИзмененииОтбора", Истина);

КонецПроцедуры // УстановитьОтбор()	

// Процедура вызывается при изменении значения быстрого отбора по организации
//
Процедура ОрганизацияПриИзменении(Элемент)

	УстановитьОтбор();

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка подменю "Советы".							   
	РаботаСДиалогами.УстановитьПодменюСоветы(ЭлементыФормы.ДействияФормы, "Касса");
	
КонецПроцедуры

// Обработчик события "ПриОткрытии" формы.
//
Процедура ПриОткрытии()

	ОтборПоОрганизации = РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "ЖурналДокументовСписок");

	Организация = Неопределено;
	Если ОтборПоОрганизации <> Неопределено Тогда
		Если ОтборПоОрганизации.ВидСравнения = ВидСравнения.Равно
			И ОтборПоОрганизации.Использование Тогда
			Организация = ОтборПоОрганизации.Значение;
		КонецЕсли; 
	КонецЕсли;

	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока));
	
	ПодключитьОбработчикИзмененияДанных("ЖурналДокументовСписок.Отбор", "ЖурналДокументовСписокПриИзмененииОтбора", Истина);

КонецПроцедуры // ПриОткрытии()

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеЗначенийСвойств" И Источник = ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока Тогда
		РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, Параметр);
	ИначеЕсли ИмяСобытия = "ИзменениеТекущейОрганизации" Тогда
		РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "ЖурналДокументовСписок", Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки в подменю "Советы" командной панели
// формы.
//
Процедура ДействияФормыОткрытьСоветы(Кнопка)
	
	РаботаСДиалогами.ОткрытьСоветы(Кнопка);
	
КонецПроцедуры //ДействияФормыОткрытьСоветы()

// Процедура вызывается при нажатии на кнопку "Кассовая книга"
//
Процедура ДействияФормыКассоваяКнига(Кнопка)
	
	КассоваяКнига=Отчеты.КассоваяКнига.Создать();
	КассоваяКнига.Организация=Организация;
	КассоваяКнига.ДатаНач=НачалоДня(РабочаяДата);
	КассоваяКнига.ДатаКон=КонецДня(РабочаяДата);
	
	ФормаКассоваяКнига=КассоваяКнига.ПолучитьФорму();
	
	Если НЕ Организация.Пустая() Тогда
		ФормаКассоваяКнига.НеЗаполнятьНастройкиПриОткрытии = Истина;
	КонецЕсли;

	ФормаКассоваяКнига.Открыть();
		
КонецПроцедуры

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	Если ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные = Неопределено тогда
		Возврат
	КонецЕсли;

	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

// Процедура вызывается при нажатии на кнопку "Журнал регистрации"
//
Процедура ДействияФормыЖурналРегистрацииКассовыхДокументов(Кнопка)
	
	ЖурналРегистрацииКассовыхДокументов = Отчеты.ЖурналРегистрацииКассовыхДокументов.Создать();	
	ЖурналРегистрацииКассовыхДокументов.Организация = Организация;
	ЖурналРегистрацииКассовыхДокументов.ДатаНач=НачалоДня(РабочаяДата);
	ЖурналРегистрацииКассовыхДокументов.ДатаКон=КонецДня(РабочаяДата);
	
	ФормаЖурналРегистрацииКассовыхДокументов = ЖурналРегистрацииКассовыхДокументов.ПолучитьФорму();
	Если НЕ Организация.Пустая() Тогда
		ФормаЖурналРегистрацииКассовыхДокументов.НеЗаполнятьНастройкиПриОткрытии = Истина;
	КонецЕсли;
	
	ФормаЖурналРегистрацииКассовыхДокументов.Открыть();
	
КонецПроцедуры

// Процедура открывает журнал проводок с отбором по текущему регистратору
//
Процедура ОсновныеДействияФормыНастройка(Кнопка)
	
	Если ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.РучнаяКорректировкаОсновнаяФорма(Ложь,ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные.Ссылка,
	ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные.Ссылка.ПолучитьОбъект());
	
КонецПроцедуры

Процедура ДействияФормыДействиеСвойства(Кнопка)
	
	РаботаСДиалогами.ОткрытьСвойстваИзСписка(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока, ЭтаФорма);
	
КонецПроцедуры

// Процедура вызывается при нажатии кнопки "Печать" командной панели формы,
// вызывает печать по умолчанию для формы документа.
//
Процедура ДействияФормыДействиеПечать(Кнопка)

	Если ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока <> Неопределено Тогда

		Попытка
			УниверсальныеМеханизмы.НапечататьДокументИзФормыСписка(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока.Ссылка);
		Исключение
		КонецПопытки
			
	КонецЕсли;

КонецПроцедуры // ОсновныеДействияФормыДействиеПечать()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

Процедура ЖурналДокументовСписокПриАктивизацииСтроки(Элемент)
	
	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока));
	
КонецПроцедуры

// Обработчик события "ПриПолученииДанных" формы.
Процедура ЖурналДокументовСписокПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Если Элемент.Колонки.РучнаяКорректировка.Видимость Тогда
		Для Каждого Строка Из ОформленияСтрок Цикл
			Отображать = Строка.Ячейки.РучнаяКорректировка.Значение;
			Если ТипЗнч(Отображать) = Тип("Булево") и отображать Тогда
				Строка.Ячейки.РучнаяКорректировка.УстановитьКартинку(КартинкаРучнаяКорректировка);
			Иначе 
				Строка.Ячейки.РучнаяКорректировка.Текст = "";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЖурналДокументовСписокПриИзмененииОтбора(ПутьКДанным = "")
	
	Если ЖурналДокументовСписок.Отбор.Организация.Использование
		И ЖурналДокументовСписок.Отбор.Организация.ВидСравнения = ВидСравнения.Равно Тогда
		Организация = ЖурналДокументовСписок.Отбор.Организация.Значение;
	Иначе
		Организация = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

КартинкаРучнаяКорректировка = БиблиотекаКартинок.РучнаяКорректировка;
