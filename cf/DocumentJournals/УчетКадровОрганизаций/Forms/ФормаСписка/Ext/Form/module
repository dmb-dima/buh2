Перем	мИмяОбъектаМетаданных;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура выполняет запись новой строки в истории ввода
//
Процедура ДополнитьИсторию()

	РаботаСДиалогами.ДополнитьСписокИсторииВвода(мИмяОбъектаМетаданных + "." + "Сотрудник", Отбор.ДокументыПоСотрудникуОрганизации.Значение);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки в подменю "Советы" командной панели
// формы.
//
Процедура ДействияФормыОткрытьСоветы(Кнопка)
	
	РаботаСДиалогами.ОткрытьСоветы(Кнопка);
	
КонецПроцедуры //ДействияФормыОткрытьСоветы()

// Процедура вызывается при нажатии кнопки "Печать" командной панели формы,
// вызывает печать по умолчанию для формы документа.
//
Процедура ДействияФормыДействиеПечать(Кнопка)

	Если ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока <> Неопределено Тогда

		Попытка
			УниверсальныеМеханизмы.НапечататьДокументИзФормыСписка(ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные.Ссылка)
		Исключение
		КонецПопытки

	КонецЕсли;

КонецПроцедуры // ОсновныеДействияФормыДействиеПечать()

Процедура ДействияФормыДвиженияДокументаПоРегистрам(Кнопка)

	Если ЭтаФорма.ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока = Неопределено тогда
		Возврат
	КонецЕсли;

	РаботаСДиалогами.НапечататьДвиженияДокумента(ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные.Ссылка);

КонецПроцедуры

// Процедура вызова структуры подчиненности документа
//
Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	Если ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные = Неопределено тогда
		Возврат
	КонецЕсли;

	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

Процедура ДействияФормыДействиеСвойства(Кнопка)
	
	РаботаСДиалогами.ОткрытьСвойстваИзСписка(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка подменю "Советы".							   
	РаботаСДиалогами.УстановитьПодменюСоветы(ЭлементыФормы.ДействияФормы, "Кадры");
	
КонецПроцедуры

Процедура ПриОткрытии()

	// Проверка однофирменности
	РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "ЖурналДокументовСписок");

	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока));
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеЗначенийСвойств" И Источник = ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока Тогда
		РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, Параметр);
	ИначеЕсли ИмяСобытия = "ИзменениеТекущейОрганизации" Тогда
		РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "ЖурналДокументовСписок", Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

// Процедура - обработчик события "ПриИзменении" поля ввода Физ Лицо.
//
Процедура СотрудникПриИзменении(Элемент)

	ДополнитьИсторию();
	Отбор.ДокументыПоСотрудникуОрганизации.Использование = ЗначениеЗаполнено(Элемент.Значение);

КонецПроцедуры

// Процедура - обработчик события "НачалоВыбораИзСписка" поля ввода Физ Лицо.
//
Процедура СотрудникНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	НовоеЗначение = ВыбратьИзСписка(РаботаСДиалогами.ПолучитьСписокИсторииВвода(мИмяОбъектаМетаданных + "." + "Сотрудник"), Элемент);
	
	Если НовоеЗначение <> Неопределено Тогда
		Отбор.ДокументыПоСотрудникуОрганизации.ВидСравнения = ВидСравнения.Равно;
		Элемент.Значение = НовоеЗначение.Значение;
		ДополнитьИсторию();
		Отбор.ДокументыПоСотрудникуОрганизации.Использование = ЗначениеЗаполнено(Элемент.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура СотрудникНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	мГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
	Если Отбор.Организация.Использование И ЗначениеЗаполнено(Отбор.Организация.Значение) Тогда
		Если ТипЗнч(Отбор.Организация.Значение) = Тип("СправочникСсылка.Организации") Тогда
			мГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Отбор.Организация.Значение)
		КонецЕсли;
	КонецЕсли;
	ПроцедурыУправленияПерсоналом.ОткрытьФормуВыбораСотрудникаОрганизации(Элемент, ЭтаФорма, Истина, ОбщегоНазначения.ПолучитьРабочуюДату(), мГоловнаяОрганизация, 1, СтандартнаяОбработка, Элемент.Значение);
	
КонецПроцедуры

Процедура СотрудникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ДополнитьИсторию();
	
КонецПроцедуры

Процедура СотрудникАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	мГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
	Если Отбор.Организация.Использование И ЗначениеЗаполнено(Отбор.Организация.Значение) Тогда
		Если ТипЗнч(Отбор.Организация.Значение) = Тип("СправочникСсылка.Организации") Тогда
			мГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Отбор.Организация.Значение)
		КонецЕсли;
	КонецЕсли;

	ТекстАвтоПодбора = ПроцедурыУправленияПерсоналом.ПодобратьФИОРаботникаКандидата(СтандартнаяОбработка, "Сотрудники", Текст, мГоловнаяОрганизация);
	
КонецПроцедуры

Процедура СотрудникОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	мГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
	Если Отбор.Организация.Использование И ЗначениеЗаполнено(Отбор.Организация.Значение) Тогда
		Если ТипЗнч(Отбор.Организация.Значение) = Тип("СправочникСсылка.Организации") Тогда
			мГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Отбор.Организация.Значение)
		КонецЕсли;
	КонецЕсли;
	
	Значение = ПроцедурыУправленияПерсоналом.ПодобратьСписокРаботниковКандидатов(СтандартнаяОбработка, "Сотрудники", Текст, Элемент.Значение, мГоловнаяОрганизация);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

Процедура ЖурналДокументовСписокПриАктивизацииСтроки(Элемент)
	
	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока));
	
КонецПроцедуры

мИмяОбъектаМетаданных = "ЖурналДокументов.УчетКадровОрганизаций";
