Перем	мИмяОбъектаМетаданных;
Перем КартинкаРучнаяКорректировка;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Процедура выполняет запись новой строки в истории ввода
//
Процедура ДополнитьИсторию()
	
	РаботаСДиалогами.ДополнитьСписокИсторииВвода(мИмяОбъектаМетаданных + "." + "ФизЛицо", Отбор.ДокументыПоРаботникуОрганизации.Значение);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// Установка подменю "Советы".							   
	РаботаСДиалогами.УстановитьПодменюСоветы(ЭлементыФормы.ДействияФормы, "Зарплата");
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "ЖурналДокументовСписок");

	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока));
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеЗначенийСвойств" И Источник = ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока Тогда
		РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, Параметр);
	ИначеЕсли ИмяСобытия = "ИзменениеТекущейОрганизации" Тогда
		РаботаСДиалогами.УстановитьОтборПоОрганизации(ЭтаФорма,, глЗначениеПеременной("ОсновнаяОрганизация"), "ЖурналДокументовСписок", Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура вызывается при нажатии кнопки в подменю "Советы" командной панели
// формы.
//
Процедура ДействияФормыОткрытьСоветы(Кнопка)
	
	РаботаСДиалогами.ОткрытьСоветы(Кнопка);
	
КонецПроцедуры //ДействияФормыОткрытьСоветы()

// Процедура вызывается при нажатии кнопки "Печать" командной панели формы,
// вызывает печать по умолчанию для формы документа.
//
Процедура ДействияФормыДействиеПечать(Кнопка)

	Если ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока <> Неопределено Тогда

		Попытка
			УниверсальныеМеханизмы.НапечататьДокументИзФормыСписка(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока)
		Исключение
		КонецПопытки
			
	КонецЕсли;

КонецПроцедуры // ОсновныеДействияФормыДействиеПечать()

Процедура ДействияФормыСтруктураПодчиненностиДокумента(Кнопка)
	
	Если ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные = Неопределено тогда
		Возврат
	КонецЕсли;

	РаботаСДиалогами.ПоказатьСтруктуруПодчиненностиДокумента(ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

Процедура ДействияФормыДействиеСвойства(Кнопка)
	
	РаботаСДиалогами.ОткрытьСвойстваИзСписка(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока, ЭтаФорма);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыНастройка(Кнопка)

	Если ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные = Неопределено тогда
		Возврат
	КонецЕсли;

	ОбщегоНазначения.РучнаяКорректировкаОсновнаяФорма(Ложь,ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные.Ссылка,
													  ЭлементыФормы.ЖурналДокументовСписок.ТекущиеДанные.Ссылка.ПолучитьОбъект());

КонецПроцедуры // ДействияФормыДвиженияДокументаПоРегистрам()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ

// Процедура - обработчик события "ПриИзменении" поля ввода Физ Лицо.
//
Процедура ФизЛицоПриИзменении(Элемент)

	ДополнитьИсторию();
	Отбор.ДокументыПоРаботникуОрганизации.Использование = ЗначениеЗаполнено(Элемент.Значение);
		
КонецПроцедуры

// Процедура - обработчик события "НачалоВыбораИзСписка" поля ввода Физ Лицо.
//
Процедура ФизЛицоНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	НовоеЗначение = ВыбратьИзСписка(РаботаСДиалогами.ПолучитьСписокИсторииВвода(мИмяОбъектаМетаданных + "." + "ФизЛицо"), Элемент);
	
	Если НовоеЗначение <> Неопределено Тогда
		Отбор.ДокументыПоРаботникуОрганизации.ВидСравнения = ВидСравнения.Равно;
		Элемент.Значение = НовоеЗначение.Значение;
		ДополнитьИсторию();
		Отбор.ДокументыПоРаботникуОрганизации.Использование = ЗначениеЗаполнено(Элемент.Значение);
	КонецЕсли;


КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

Процедура ЖурналДокументовСписокПриАктивизацииСтроки(Элемент)
	
	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.ЖурналДокументовСписок.ТекущаяСтрока));
	
КонецПроцедуры

Процедура ЖурналДокументовСписокПриПолученииДанных(Элемент, ОформленияСтрок)
			
	Если Элемент.Колонки.РучнаяКорректировка.Видимость Тогда
		Для Каждого Строка Из ОформленияСтрок Цикл
			Отображать = Строка.Ячейки.РучнаяКорректировка.Значение;
			Если ТипЗнч(Отображать) = Тип("Булево") и отображать Тогда
				Строка.Ячейки.РучнаяКорректировка.УстановитьКартинку(КартинкаРучнаяКорректировка);
			Иначе 
				Строка.Ячейки.РучнаяКорректировка.Текст = "";
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры


КартинкаРучнаяКорректировка = БиблиотекаКартинок.РучнаяКорректировка;

мИмяОбъектаМетаданных =  "ЖурналДокументов.ПервичныеДокументыОрганизаций";


