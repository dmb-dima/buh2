////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ОбновитьДанные()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СписаниеСРасчетногоСчета.Дата,
	|	СписаниеСРасчетногоСчета.Номер,
	|	0 КАК Списано,
	|	СписаниеСРасчетногоСчета.СуммаДокумента КАК Поступило,
	|	СписаниеСРасчетногоСчета.Ссылка,
	|	НЕОПРЕДЕЛЕНО КАК ВидДокумента
	|ИЗ
	|	Документ.СписаниеСРасчетногоСчета КАК СписаниеСРасчетногоСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ПО СписаниеСРасчетногоСчета.СчетКонтрагента = БанковскиеСчета.Ссылка
	|ГДЕ
	|	БанковскиеСчета.Ссылка = &БанковскийСчетИтогов
	|	И СписаниеСРасчетногоСчета.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПереводНаДругойСчет)
	|	И СписаниеСРасчетногоСчета.Организация = &Организация
	|	И СписаниеСРасчетногоСчета.Дата МЕЖДУ &НачалоДня И &КонецДня
	|	И СписаниеСРасчетногоСчета.Проведен = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдер.Дата,
	|	ПриходныйКассовыйОрдер.Номер,
	|	ПриходныйКассовыйОрдер.СуммаДокумента,
	|	0,
	|	ПриходныйКассовыйОрдер.Ссылка,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ПО ПриходныйКассовыйОрдер.Контрагент = БанковскиеСчета.Ссылка
	|ГДЕ
	|	БанковскиеСчета.Ссылка = &БанковскийСчетИтогов
	|	И ПриходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.ПолучениеНаличныхВБанке)
	|	И ПриходныйКассовыйОрдер.Организация = &Организация
	|	И ПриходныйКассовыйОрдер.Дата МЕЖДУ &НачалоДня И &КонецДня
	|	И ПриходныйКассовыйОрдер.Проведен = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Дата,
	|	РасходныйКассовыйОрдер.Номер,
	|	0,
	|	РасходныйКассовыйОрдер.СуммаДокумента,
	|	РасходныйКассовыйОрдер.Ссылка,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ПО РасходныйКассовыйОрдер.СчетОрганизации = БанковскиеСчета.Ссылка
	|ГДЕ
	|	БанковскиеСчета.Ссылка = &БанковскийСчетИтогов
	|	И РасходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРКО.ВзносНаличнымиВБанк)
	|	И РасходныйКассовыйОрдер.Организация = &Организация
	|	И РасходныйКассовыйОрдер.Дата МЕЖДУ &НачалоДня И &КонецДня
	|	И РасходныйКассовыйОрдер.Проведен = ИСТИНА";
	
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ДатаВыписки));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(ДатаВыписки));
	Запрос.УстановитьПараметр("БанковскийСчетИтогов", БанковскийСчет);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ТаблицаИтогов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаИтогов Цикл
		СтрокаТаблицы.ВидДокумента = СтрокаТаблицы.Ссылка.Метаданные().Синоним;
	КонецЦикла;
		
	СписокДокументов =  ТаблицаИтогов;
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьФорму" 
		И ( (ТипЗнч(Источник) = Тип("ДокументОбъект.СписаниеСРасчетногоСчета"))
		ИЛИ (ТипЗнч(Источник) = Тип("ДокументОбъект.РасходныйКассовыйОрдер")) 
		ИЛИ (ТипЗнч(Источник) = Тип("ДокументОбъект.ПриходныйКассовыйОрдер")) ) Тогда
		ОбновитьДанные();
		Если Параметр = ЭтаФорма Тогда
			Оповестить("ОбновитьФорму", ВладелецФормы, Источник);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ТекстНадписи = "Перемещения денежных средств";
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		ТекстНадписи = ТекстНадписи + ": р/с """ + БанковскийСчет.Наименование + """";
	КонецЕсли;
	
	Если ДатаВыписки <> '00010101' Тогда
		ТекстНадписи = ТекстНадписи + " на дату " + Формат(ДатаВыписки, "ДЛФ=DD");
	КонецЕсли;
	
	ЭлементыФормы.НадписьПеремещения.Заголовок = ТекстНадписи;
	
	ОбновитьДанные();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

Процедура СписокДокументовПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Отказ = Истина;
	
КонецПроцедуры

Процедура СписокДокументовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущийДокумент = Элемент.ТекущиеДанные.Ссылка;
	ТекущийДокумент.ПолучитьФорму( , ЭтаФорма).Открыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура РезультатПроведения(Кнопка)
	
	ТекущиеДанные = ЭлементыФормы.СписокДокументов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		//Открывает форму движений документа для ручного редактирования
		ОбщегоНазначения.РучнаяКорректировкаОсновнаяФорма(Ложь,ТекущиеДанные.Ссылка,ТекущиеДанные.Ссылка.ПолучитьОбъект());
	КонецЕсли;

КонецПроцедуры // РезультатПроведения()

Процедура КоманднаяПанельПометитьНаУдаление(Кнопка)
	
	ТекущиеДанные = ЭлементыФормы.СписокДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущийДокумент = ТекущиеДанные.Ссылка;
		ТекущийДокументОбъект = ТекущийДокумент.ПолучитьОбъект();
		ТекущийДокументОбъект.ПометкаУдаления = Истина;
		ТекущийДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Оповестить( "ОбновитьФорму", ВладелецФормы, ТекущийДокументОбъект);
		ОбновитьДанные();
	КонецЕсли;

КонецПроцедуры

Процедура КоманднаяПанельОбновить(Кнопка)
	
	ОбновитьДанные();
	
КонецПроцедуры


