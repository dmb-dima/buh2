
Перем мВозможностьПросмотраПользователейБД;

Перем мСоответсвиеПользователейБД;

//Процедура позиционирует текущую строку списка пользователей БД по имени пользователя
Функция СпозиционироватьсяНаПользователеБД(Знач ИмяПользователяБД)
	
	ТекСтрока = ОбработкаОбъектСписокПользователейБД.ТабличнаяЧастьПользователей.Найти(ИмяПользователяБД, "Имя");
		
	Если ТекСтрока <> Неопределено Тогда
			
		ЭлементыФормы.ТаблицаПользователейБД.ТекущаяСтрока = ТекСтрока;
		Возврат Истина
		
	Иначе
		
		Возврат Ложь;
			
	КонецЕсли;
	
КонецФункции

//Процедура обновляет список пользователей БД
Процедура ОбновитьСписокПользователейБД(Знач ИмяПользователяБД = "")
	
	Если мВозможностьПросмотраПользователейБД Тогда 	
		
		ОбработкаОбъектСписокПользователейБД.ЗаполнитьСписокПользователейБД(ЭлементыФормы.ТаблицаПользователейБД.ОтборСтрок);
				
		СпозиционироватьсяНаПользователеБД(ИмяПользователяБД);
		
		мСоответсвиеПользователейБД = ОбработкаОбъектСписокПользователейБД.ПолучитьСоответсвиеИменИПользователейБД();
						
	КонецЕсли;
	
КонецПроцедуры

// перед открытием
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	мВозможностьПросмотраПользователейБД = ПравоДоступа("Администрирование", Метаданные);
	
	Если НЕ мВозможностьПросмотраПользователейБД Тогда 
		
		// скрываем закладку ПользователиБД
		ЭлементыФормы.ПанельОсновная.Страницы.ПользователиБД.Видимость   = Ложь;
		
		// удаляем лишние кнопки
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.РазделительПользовательБД);
		ЭлементыФормы.КоманднаяПанельФормы.Кнопки.Удалить(ЭлементыФормы.КоманднаяПанельФормы.Кнопки.ПользовательБД);
		
		// скрываем колонку "КартинкаПользователяБД"
		ЭлементыФормы.СправочникСписок.Колонки.КартинкаПользователяБД.Видимость         = Ложь;
		ЭлементыФормы.СправочникСписок.Колонки.КартинкаПользователяБД.ИзменятьВидимость = Ложь;
		
	КонецЕсли;
	
	мСоответсвиеПользователейБД = ОбработкаОбъектСписокПользователейБД.ПолучитьСоответсвиеИменИПользователейБД();
		
	ОбновитьСписокПользователейБД();
	
КонецПроцедуры

// обновление пользователей БД
Процедура КоманднаяПанельПользователейБДОбновить(Кнопка)
	
	ИмяТекПользователяБД = "";
	
	Если ЭлементыФормы.ТаблицаПользователейБД.ТекущиеДанные <> Неопределено Тогда
		
		ИмяТекПользователяБД = ЭлементыФормы.ТаблицаПользователейБД.ТекущиеДанные.Имя;	
		
	КонецЕсли;
	
	ОбновитьСписокПользователейБД(ИмяТекПользователяБД);
		
КонецПроцедуры

//Процедура редактирует пользователя БД
Функция РедактироватьПользователяБД(ОбъектПользователя, ТекущийПользовательБД, Знач Модифицированность = Ложь,
	Знач ПользовательДляКопированияНастроек = Неопределено)
	
	РезультатОткрытия = УправлениеПользователями.РедактироватьИлиСоздатьПользователяБД(ОбъектПользователя, ТекущийПользовательБД, Модифицированность,
		ПользовательДляКопированияНастроек);
		
	Если РезультатОткрытия = Истина Тогда
		
		ОбновитьСписокПользователейБД(ТекущийПользовательБД.Имя);
						
	КонецЕсли;
	
	Возврат РезультатОткрытия;
	
КонецФункции

//процедура редактирует текущего пользователя БД
Процедура РедактироватьТекущегоПользователяБД()
	
	// при выборе пользователя БД
	ОбъектПользователя = ПолучитьТекущегоПользователяСправочника();
		
	СтарыйПользовательБД = ПолучитьТекущегоПользователяБДВСписке(); 
	
	РедактироватьПользователяБД(ОбъектПользователя, СтарыйПользовательБД);
			
КонецПроцедуры

//Функция возвращает текущего пользователя БД в списке
Функция ПолучитьТекущегоПользователяБДВСписке()
	
	Если ЭлементыФормы.ТаблицаПользователейБД.ТекущиеДанные = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СтарыйПользовательБД = УправлениеПользователями.НайтиПользователяБД(ЭлементыФормы.ТаблицаПользователейБД.ТекущиеДанные.Имя);
	
	Возврат СтарыйПользовательБД;
		
КонецФункции

//Функция возвращает текущего пользователя справочника
Функция ПолучитьТекущегоПользователяСправочника()
	
	Если ЭлементыФормы.ТаблицаПользователейБД.ТекущиеДанные = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// при выборе пользователя БД
	ОбъектПользователя = Неопределено;
	СсылкаНаСправочникПользователей = Справочники.Пользователи.НайтиПоКоду(ЭлементыФормы.ТаблицаПользователейБД.ТекущиеДанные.Имя);
	
	Если ЗначениеЗаполнено(СсылкаНаСправочникПользователей) Тогда
		
		ОбъектПользователя = СсылкаНаСправочникПользователей.ПолучитьОбъект();
		
	КонецЕсли;
	
	Возврат ОбъектПользователя;
		
КонецФункции

// Нахождение пользователя БД
Процедура КоманднаяПанельФормыЭлементСпискаПользователейБД(Кнопка)
	
	Если ЭлементыФормы.СправочникСписок.ТекущиеДанные = Неопределено
		ИЛИ ЭлементыФормы.СправочникСписок.ТекущиеДанные.ЭтоГруппа Тогда
		
		Возврат;
		
	КонецЕсли;
	
	// ищем нужного пользователя БД в списке и переходим на него
	ИмяПользователяБД = СокрЛП(ЭлементыФормы.СправочникСписок.ТекущиеДанные.Код);
	
	Если ИмяПользователяБД = "НеАвторизован" Тогда
		
		Предупреждение("Для пользователя ""НеАвторизован"" недопустимо создавать пользователя БД.");
		Возврат;
		
	КонецЕсли;
	
	ПользовательБД = УправлениеПользователями.НайтиПользователяБД(ИмяПользователяБД);
	
	// надо открыть форму элемента пользователя БД
	ОбъектПользователя = ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка.ПолучитьОбъект();
		
	РедактироватьПользователяБД(ОбъектПользователя, ПользовательБД);
		
КонецПроцедуры

// найти элемент справочника
Процедура КоманднаяПанельПользователейБДНайтиЭлементСправочника(Кнопка)
	
	Если ЭлементыФормы.ТаблицаПользователейБД.ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ОбъектПользователя = ПолучитьТекущегоПользователяСправочника();
	
	Если ОбъектПользователя = Неопределено Тогда
		
		Предупреждение("Не найден элемент справочника с именем: " + ЭлементыФормы.ТаблицаПользователейБД.ТекущиеДанные.Имя);
		Возврат;
		
	КонецЕсли;
	
	ЭлементыФормы.СправочникСписок.ТекущаяСтрока = ОбъектПользователя.Ссылка;
	
	ЭлементыФормы.ПанельОсновная.ТекущаяСтраница = ЭлементыФормы.ПанельОсновная.Страницы.СправочникПользователи;
	
КонецПроцедуры

// сложный отбор
Процедура КоманднаяПанельПользователейБДОтбор(Кнопка)
	
	ОбработкаОбъектСписокПользователейБД.УстановитьОтбораДанныхПользовательБД(ЭлементыФормы.ТаблицаПользователейБД.ОтборСтрок, ЭтаФорма, 
		ЭлементыФормы.КоманднаяПанельПользователейБД.Кнопки.ИсторияОтборов, ЭлементыФормы.КоманднаяПанельПользователейБД.Кнопки.ОтключитьОтбор);
			
КонецПроцедуры

// отменить отбор
Процедура КоманднаяПанельПользователейБДОтключитьОтбор(Кнопка)
	
	ОбработкаОбъектСписокПользователейБД.ОтменитьОтборДанныхПоПользователямБД(ЭлементыФормы.ТаблицаПользователейБД.ОтборСтрок, 
		ЭлементыФормы.КоманднаяПанельПользователейБД.Кнопки.ОтключитьОтбор);
	
КонецПроцедуры

Процедура ТаблицаПользователейБДПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОбработкаОбъектСписокПользователейБД.ПриВыводеСтрокиПользователяБД(Элемент, ОформлениеСтроки, ДанныеСтроки);
	
КонецПроцедуры

Процедура ОбработкаИсторииОтбора(Кнопка, ОтборСтрок, КнопкаОтменыОтбора) Экспорт
		
	ОбработкаОбъектСписокПользователейБД.ОбработкаИсторииОтбора(Кнопка, ЭлементыФормы.ТаблицаПользователейБД.ОтборСтрок, 
		ЭлементыФормы.КоманднаяПанельПользователейБД.Кнопки.ОтключитьОтбор);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ОбработкаОбъектСписокПользователейБД.ИнициализироватьРаботуСПользователямиБД();
	
КонецПроцедуры

// сохранение параметров для работы с пользователями БД
Процедура ПриЗакрытии()
	
	ОбработкаОбъектСписокПользователейБД.СохранитьПараметрыРаботыСПользователямиБД();
	
КонецПроцедуры

Процедура ТаблицаПользователейБДПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	РезультатОткрытияФормы = Ложь;
	
	Если Копирование Тогда
		СтарыйПользовательБД = ПолучитьТекущегоПользователяБДВСписке();
	Иначе	
		СтарыйПользовательБД = Неопределено;
	КонецЕсли;
		
	НовыйПользовательБД = ПользователиИнформационнойБазы.СоздатьПользователя();
	РезультатОткрытияФормы = РедактироватьПользователяБД(Неопределено, НовыйПользовательБД, Истина, СтарыйПользовательБД);
		
	Отказ = Истина;
	
КонецПроцедуры

Процедура ТаблицаПользователейБДПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	СтарыйПользовательБД = ПолучитьТекущегоПользователяБДВСписке();
	
	Если СтарыйПользовательБД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтветПользователя = Вопрос("Удалить пользователя?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет, "Пользователь БД");
	Если ОтветПользователя <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	// запись пользователя БД
	Попытка
		
		СтарыйПользовательБД.Удалить();
		
	Исключение
		
		ОбщегоНазначения.СообщитьОбОшибке("Ошибка при удалении пользователя БД. " + ОписаниеОшибки());
		Возврат;
				
	КонецПопытки;
	
	ОбновитьСписокПользователейБД("");
		
КонецПроцедуры

Процедура ТаблицаПользователейБДПередНачаломИзменения(Элемент, Отказ)
	
	РедактироватьТекущегоПользователяБД();
	Отказ = Истина;
		
КонецПроцедуры

Процедура СправочникСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если НЕ мВозможностьПросмотраПользователейБД
		ИЛИ ДанныеСтроки.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаОбъектСписокПользователейБД.ПриВыводеСтрокиПользователя(Элемент, ОформлениеСтроки, ДанныеСтроки, мСоответсвиеПользователейБД);
	
КонецПроцедуры

// обработка оповещения
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененПользовательБД" Тогда
		
		КоманднаяПанельПользователейБДОбновить(Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельФормыГруппыПользователя(Кнопка)
	
	ТекущиеДанные = ЭлементыФормы.СправочникСписок.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено или ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ФормаНастройкиГруппПользователя = Справочники.Пользователи.ПолучитьФорму("ФормаНастройкиГруппПользователя");
	ФормаНастройкиГруппПользователя.Пользователь = ТекущиеДанные.Ссылка;
	ФормаНастройкиГруппПользователя.ОткрытьМодально();
	
КонецПроцедуры

// устанавливаем начальную страницу
ЭлементыФормы.ПанельОсновная.ТекущаяСтраница = ЭлементыФормы.ПанельОсновная.Страницы.СправочникПользователи;

мСоответсвиеПользователейБД = Неопределено;

