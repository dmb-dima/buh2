Процедура ПриОткрытии()
	
	ЭлементыФормы.НадписьПользователь.Заголовок = "Пользователь: " + СокрЛП(Пользователь);
	
	ЗаполнитьГруппыПользователя();
	
КонецПроцедуры

Процедура ЗаполнитьГруппыПользователя()

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТекПользователь", Пользователь);
	Запрос.УстановитьПараметр("ВсеПользователи", Справочники.ГруппыПользователей.ВсеПользователи);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГруппыПользователей.Ссылка КАК Группа,
	|	ВЫБОР
	|		КОГДА ТЧПользователиГруппы.Пользователь ЕСТЬ NULL 
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Флаг,
	|	Истина КАК ВсеСтроки
	|ИЗ
	|	Справочник.ГруппыПользователей КАК ГруппыПользователей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.ПользователиГруппы КАК ТЧПользователиГруппы
	|		ПО ТЧПользователиГруппы.Ссылка = ГруппыПользователей.Ссылка
	|			И (ТЧПользователиГруппы.Пользователь = &ТекПользователь)
	|ГДЕ
	|	ГруппыПользователей.Ссылка <> &ВсеПользователи
	|
	|УПОРЯДОЧИТЬ ПО
	|	Группа ИЕРАРХИЯ";
	
	ДеревоГрупп = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ОтметитьСвязныеГруппы(ДеревоГрупп.Строки);
	
КонецПроцедуры

Функция ОтметитьСвязныеГруппы(Строки, РодительскаяОтметка = Ложь)
	
	БылиОтметки = Ложь;
	Для каждого Строка Из Строки Цикл
		
		Если Строка.Флаг = 2 Тогда
			Строка.Флаг = 0;
		КонецЕсли; 
		
		
		Если РодительскаяОтметка и Строка.Флаг = 0 Тогда
			Строка.Флаг = 2;
		КонецЕсли;
		
		БылиПодчиненныеОтметки = ОтметитьСвязныеГруппы(Строка.Строки, РодительскаяОтметка или Строка.Флаг = 1);
		
		Если БылиПодчиненныеОтметки и Строка.Флаг = 0 Тогда
			Строка.Флаг = 2;
		КонецЕсли;
		
		БылиОтметки = БылиОтметки или БылиПодчиненныеОтметки или Строка.Флаг = 1;
		
	КонецЦикла;
	
	Возврат БылиОтметки;
	
КонецФункции

Процедура ДеревоГруппПриИзмененииФлажка(Элемент, Колонка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные.Флаг = 0 Тогда
		ТекущиеДанные.Флаг = 1;
	ИначеЕсли ТекущиеДанные.Флаг = 2 Тогда
		ТекущиеДанные.Флаг = 0;
	КонецЕсли;
	
	ОтметитьСвязныеГруппы(ДеревоГрупп.Строки);
	
КонецПроцедуры

Функция СохранитьЗначения()

	НачатьТранзакцию();
	
	Для каждого СтрокаТЗ Из ДеревоГрупп.Строки.НайтиСтроки(Новый Структура("ВсеСтроки", Истина), Истина) Цикл
		
		ВходитВГруппу = СтрокаТЗ.Группа.ПользователиГруппы.Найти(Пользователь, "Пользователь") <> Неопределено;
		
		Если СтрокаТЗ.Флаг = 1 Тогда
			Если ВходитВГруппу Тогда
				Продолжить;
			КонецЕсли;
		Иначе
			Если Не ВходитВГруппу Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли; 
		
		Объект = СтрокаТЗ.Группа.ПолучитьОбъект();
		
		Если СтрокаТЗ.Флаг = 1 Тогда
			НоваяСтрока = Объект.ПользователиГруппы.Добавить();
			НоваяСтрока.Пользователь = Пользователь;
		Иначе
			Объект.ПользователиГруппы.Удалить(Объект.ПользователиГруппы.Найти(Пользователь, "Пользователь"));
		КонецЕсли;
		
		Попытка
			Объект.Записать();
		Исключение
			ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки(),, "Изменения не сохранены!");
			ОтменитьТранзакцию();
			Возврат Ложь;
		КонецПопытки;
	
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	
	Модифицированность = Ложь;
	Возврат Истина;
	
КонецФункции // СохранитьЗначения()

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если СохранитьЗначения() Тогда
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		ОтветНаВопрос = Вопрос("Данные были изменены. Сохранить?", РежимДиалогаВопрос.ДаНетОтмена);
		Если ОтветНаВопрос = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			Отказ = НЕ СохранитьЗначения();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


