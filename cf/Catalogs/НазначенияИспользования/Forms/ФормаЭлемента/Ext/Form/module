////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура устанавливает видимость для элементов формы
//
// Параметры:
//  Нет.
//
Процедура УстановитьВидимость()

	ПропорциональноОбъемуПродукции = (СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.ПропорциональноОбъемуПродукцииРабот);
	Если ПропорциональноОбъемуПродукции Тогда
		ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.ОбщийОбъемПродукции;
	Иначе
		ЭлементыФормы.Панель.ТекущаяСтраница = ЭлементыФормы.Панель.Страницы.СрокПолезногоИспользования;
	КонецЕсли;
	
	ЭлементыФормы.НадписьЕдиницаИзмерения.Заголовок = Владелец.БазоваяЕдиницаИзмерения;
	
	ЭлементыФормы.НадписьРасшифровкаСрокаПолезногоИспользования.Заголовок = УправлениеВнеоборотнымиАктивами.РасшифровкаСрокаПолезногоИспользования(СрокПолезногоИспользования);
	
КонецПроцедуры // УстановитьВидимость()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события "ПриОткрытии" для Формы.
//
Процедура ПриОткрытии()
	
	Если ЭтоНовый() Тогда
		
		Количество = 1;
		СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.ПогашатьСтоимостьПриПередачеВЭксплуатацию;
		
	КонецЕсли;
	
	УстановитьВидимость();
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Код);
	
КонецПроцедуры // ПриОткрытии()

// Обработчик события "ПриЗаписи" формы.
//
Процедура ПриЗаписи(Отказ)
	
	Если СпособПогашенияСтоимости.Пустая() Тогда
		Предупреждение("Перед записью необходимо указать способ погашения стоимости!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если СпособОтраженияРасходов.Пустая() Тогда
		Предупреждение("Перед записью необходимо указать способ отражения расходов!");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
КонецПроцедуры // ПриЗаписи()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

// Обработчик события "ПриИзменении" поля ввода "СпособПогашенияСтоимости"
//
Процедура СпособПогашенияСтоимостиПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры // СпособПогашенияСтоимостиПриИзменении()

// Обработчик события "ПриИзменении" поля ввода "Владелец"
//
Процедура ВладелецПриИзменении(Элемент)
	
	УстановитьВидимость();
		
КонецПроцедуры

// Обработчик события "ПриИзменении" поля ввода "Срок полезного использования"
//
Процедура СрокПолезногоИспользованияПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры // СрокПолезногоИспользованияПриИзменении()

// Процедура разрешения/запрещения редактирования номера документа
Процедура ДействияФормыРедактироватьКод(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
			
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если СпособПогашенияСтоимости = Перечисления.СпособыПогашенияСтоимости.ПропорциональноОбъемуПродукцииРабот Тогда
		СрокПолезногоИспользования = 0;
	КонецЕсли;
КонецПроцедуры
                           



