///////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "При открытии" формы. Данное событие
// возникает при открытии формы, до показа окна пользователю.
//
// Параметры:
//  Нет.
//
Процедура ПриОткрытии()

	Макет        = Справочники.ТорговоеОборудование.ПолучитьМакет("СписокОборудования");
	ТабДокумент  = ЭлементыФормы.ПоддерживаемоеОборудование;
	ОблШапка     = Макет.ПолучитьОбласть("Шапка");
	ОблВид       = Макет.ПолучитьОбласть("ВидТорговогоОборудования");
	ОблВидЧетный = Макет.ПолучитьОбласть("ВидТорговогоОборудованияЧетный");
	ОблТО        = Макет.ПолучитьОбласть("ТорговоеОборудование");
	ОблТОЧетный  = Макет.ПолучитьОбласть("ТорговоеОборудованиеЧетный");

	ТабДокумент.ФиксацияСверху = ОблШапка.ВысотаТаблицы;

	ТабДокумент.Вывести(ОблШапка);

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(ТчМодели.Ссылка.Вид) КАК ВидТО,
	|	ПРЕДСТАВЛЕНИЕ(ТчМодели.Ссылка) КАК Обработка,
	|	ТчМодели.Модель КАК Модель,
	|	ТчМодели.Ссылка.Код КАК Код
	|ИЗ
	|	Справочник.ОбработкиОбслуживанияТО.Модели КАК ТчМодели
	|ГДЕ
	|	НЕ ТчМодели.Ссылка.ПометкаУдаления
	|УПОРЯДОЧИТЬ ПО
	|	ТчМодели.Ссылка.Вид.Порядок,
	|	ТчМодели.Ссылка,
	|	Модель
	|ИТОГИ ПО
	|	ВидТО
	|АВТОУПОРЯДОЧИВАНИЕ
	|");

	ВыборкаВидыТО = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ЭтоЧетныйВидТО = Ложь;
	Пока ВыборкаВидыТО.Следующий() Цикл
		ТекОблВидТО = ?(ЭтоЧетныйВидТО, ОблВидЧетный, ОблВид);
		ТекОблВидТО.Параметры.ВидТорговогоОборудования = ВыборкаВидыТО.ВидТО;
		ТабДокумент.Вывести(ТекОблВидТО);

		ТабДокумент.НачатьГруппуСтрок(ВыборкаВидыТО.ВидТО, Ложь);

		ЭтоЧетныйОблТО = Ложь;

		Выборка = ВыборкаВидыТО.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекОблТО = ?(ЭтоЧетныйОблТО, ОблТОЧетный, ОблТО);

			ТекОблТО.Параметры.Заполнить(Выборка);
			ТекОблТО.Параметры.Расшифровка = Выборка.Код + Символы.ПС + Выборка.Модель;
			ТабДокумент.Вывести(ТекОблТО);

			ЭтоЧетныйОблТО = Не ЭтоЧетныйОблТО;
		КонецЦикла;
		ЭтоЧетныйВидТО = Не ЭтоЧетныйВидТО;

		ТабДокумент.ЗакончитьГруппуСтрок();
	КонецЦикла;

	ТабДокумент.ОтображатьЗаголовки = Ложь;
	ТабДокумент.ОтображатьСетку     = Ложь;
	ТабДокумент.ТолькоПросмотр      = Истина;

КонецПроцедуры

// Процедура - обработчик события "Обработка расшифровки" поля табличного
// документа "ПоддерживаемоеОборудование".
//
// Параметры:
//  Элемент              - <ПолеТабличногоДокумента>
//                       - Элемент управления, с которым связано данное событие
//                         ("ПоддерживаемоеОборудование").
//
//  Расшифровка          - <*>
//                       - Значение расшифровки ячейки или рисунка.
//
//  СтандартнаяОбработка - <Булево>
//                       - В данный параметр передается признак выполнения
//                         стандартной (системной) обработки события. Если в
//                         теле процедуры-обработчика установить данному
//                         параметру значение Ложь стандартная обработка
//                         события производиться не будет.
//                         Значение по умолчанию: Истина.
//
Процедура ПоддерживаемоеОборудованиеОбработкаРасшифровки(Элемент, Расшифровка,
                                                         СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Код                  = СтрПолучитьСтроку(Расшифровка, 1);
	Модель               = СтрПолучитьСтроку(Расшифровка, 2);

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|    СпрТО.Ссылка КАК Оборудование
	|ИЗ
	|    Справочник.ТорговоеОборудование КАК СпрТО
	|ГДЕ
	|    СпрТО.ОбработкаОбслуживания.Код = &Код
	|    И СпрТО.Модель = &Модель");
	Запрос.УстановитьПараметр("Код",    Код);
	Запрос.УстановитьПараметр("Модель", Модель);

	Выборка = Запрос.Выполнить();
	Если Не Выборка.Пустой() Тогда
		Ответ = Вопрос("Элемент справочника ""Торговое оборудование"" с данными параметрами
		               |уже существует. Открыть существующий элемент?
		               |(в случае отрицательного ответа будет создан новый элемент)",
		               РежимДиалогаВопрос.ДаНетОтмена);
		Если Ответ = КодВозвратаДиалога.Отмена Тогда
			Возврат;
		КонецЕсли;

		Если Ответ = КодВозвратаДиалога.Да Тогда
			Выборка = Выборка.Выгрузить();
			Если Выборка.Количество() = 1 Тогда
				Выборка = Выборка[0];
			Иначе
				Выборка = Выборка.ВыбратьСтроку("Выберите подходящий элемент");
				Если Выборка = Неопределено Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			Форма = Выборка.Оборудование.ПолучитьФорму();
			Если Форма.Открыта() Тогда
				Форма.Активизировать();
			Иначе
				Форма.Открыть();
			КонецЕсли;
			Возврат;
		КонецЕсли;
	КонецЕсли;

	ОбработкаОбслуживания              = Справочники.ОбработкиОбслуживанияТО.НайтиПоКоду(Код);
	НовыйЭлемент                       = Справочники.ТорговоеОборудование.СоздатьЭлемент();
	НовыйЭлемент.Модель                = Модель;
	НовыйЭлемент.Наименование          = Модель;
	НовыйЭлемент.ОбработкаОбслуживания = ОбработкаОбслуживания;
	Форма                              = НовыйЭлемент.ПолучитьФорму();
	Форма.Открыть();

КонецПроцедуры // ПоддерживаемоеОборудованиеОбработкаРасшифровки()