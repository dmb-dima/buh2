
Процедура ПриОткрытии()
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные.Справочники.Субконто, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);	
	
	Если ТипЗнч(ВладелецФормы) = Тип("ПолеВвода")
		И ТипЗнч(ВладелецФормы.ВыборПоВладельцу) = Тип("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные") Тогда
		ВидСубконто = ВладелецФормы.ВыборПоВладельцу;
		ЭлементыФормы.ВидСубконто.Доступность = НЕ ЗначениеЗаполнено(ВладелецФормы.ВыборПоВладельцу);
	КонецЕсли;
	
	УстановитьОтборПоВладельцу();
КонецПроцедуры

Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(Метаданные.Справочники.Субконто, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);
КонецПроцедуры
                      



Процедура УстановитьОтборПоВладельцу()
	
	Если Не ЗначениеЗаполнено(ВидСубконто) Тогда
		ВидСубконто = ОбщегоНазначенияПереопределяемый.ПолучитьВидСубконтоПоУмолчаниюДляСправочникаСубконто();
	КонецЕсли;
	СправочникСписок.Отбор.Владелец.Установить(ВидСубконто);
	
	ЭлементыФормы.СправочникСписок.НастройкаОтбора.Владелец.Доступность = Ложь;
	
КонецПроцедуры

Процедура ВидСубконтоНачалоВыбора(Элемент, СтандартнаяОбработка)

		СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос;
	ТипСубконто = Тип("СправочникСсылка.Субконто");
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыСубконтоХозрасчетные.Ссылка,
	|	ВидыСубконтоХозрасчетные.Наименование,
	|	ВидыСубконтоХозрасчетные.ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.ВидыСубконтоХозрасчетные КАК ВидыСубконтоХозрасчетные";
	Выборка = Запрос.Выполнить().Выбрать();
	Список = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипЗначения.СодержитТип(ТипСубконто) Тогда
			Список.Добавить(Выборка.Ссылка,Выборка.Наименование);
		КонецЕсли;
	КонецЦикла;
	
	ФормаВыбора = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПолучитьФормуВыбора(,Элемент,);
	ФормаВыбора.Отбор.Ссылка.ВидСравнения  = ВидСравнения.ВСписке;
	ФормаВыбора.Отбор.Ссылка.Значение      = Список;
	ФормаВыбора.Отбор.Ссылка.Использование = Истина;
	
	ФормаВыбора.Открыть();


КонецПроцедуры