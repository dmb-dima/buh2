
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если КонтекстЭДО <> Неопределено Тогда
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(ЭлементыФормы.Сертификат, Сертификаты);
	Иначе
		
		Если Сертификаты.Количество() <> 0 Тогда
			СертификатПредставление = "Сертификатов: " + Сертификаты.Количество();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НаименованиеПриИзменении(Элемент)
	
	Если ПустаяСтрока(ПолноеНаименование) Тогда
		ПолноеНаименование = Наименование;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолноеНаименованиеПриИзменении(Элемент)
	
	Если ПустаяСтрока(Наименование) Тогда
		Наименование = ПолноеНаименование;
	КонецЕсли;
	
КонецПроцедуры

Процедура АдресСайтаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НЕ ПустаяСтрока(АдресСайта) Тогда
		ЗапуститьПриложение(АдресСайта);
	КонецЕсли;
	
КонецПроцедуры

Процедура СертификатНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если КонтекстЭДО = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = КонтекстЭДО.ВыбратьСертификат(Сертификаты, "AddressBook", , Истина);
	Если РезультатВыбора <> Неопределено Тогда
		
		Сертификаты.Очистить();
		Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
			НовСтр = Сертификаты.Добавить();
			НовСтр.Сертификат = РезультатВыбора.Отпечаток;
		Иначе
			Для Каждого Стр Из РезультатВыбора Цикл
				НовСтр = Сертификаты.Добавить();
				НовСтр.Сертификат = Стр.Отпечаток;
			КонецЦикла;
		КонецЕсли;
		
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(Элемент, Сертификаты);
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СертификатОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Сертификаты.Количество() > 0 Тогда
		Если КонтекстЭДО <> Неопределено Тогда
			КонтекстЭДО.ПоказатьСертификат(Сертификаты, "AddressBook");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СертификатОчистка(Элемент, СтандартнаяОбработка)
	
	Сертификаты.Очистить();
	Если КонтекстЭДО <> Неопределено Тогда
		КонтекстЭДО.ОтобразитьСертификатВПолеВвода(Элемент, Сертификаты);
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры
