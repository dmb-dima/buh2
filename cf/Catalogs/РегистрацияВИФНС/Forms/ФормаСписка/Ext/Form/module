// Устанавливает отбор по организации.
//
Процедура УстановитьОтбор()

	Если НЕ Организация.Пустая() Тогда
		Отбор.Владелец.Установить(Организация);
	КонецЕсли;

КонецПроцедуры // УстановитьОтбор()

// Управление видимостью и доступностью элементов формы. 
//
Процедура УстановитьВидимость()

	ЭлементыФормы.СправочникСписок.Колонки.Владелец.Видимость = 
		(НЕ Отбор.Владелец.Использование)
		ИЛИ (Отбор.Владелец.ВидСравнения <> ВидСравнения.Равно);
	
КонецПроцедуры // УстановитьВидимость()


// Обработчик события "ПриИзменении" флага "ИспользованиеОтбораОрганизация".
//
Процедура ИспользованиеОтбораОрганизацияПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

// Обработчик события "ПриОткрытии".
//
Процедура ПриОткрытии()
	
	ЭлементыФормы.Организация.СписокВыбора = ПроцедурыУправленияПерсоналом.ПолучитьСписокГоловныхОрганизаций();
	
	ВладелецУстановлен = (Отбор.Владелец.ВидСравнения = ВидСравнения.Равно);
	
	Если ВладелецУстановлен Тогда
		Если ОбщегоНазначения.ЭтоОбособленноеПодразделение(Отбор.Владелец.Значение) Тогда
			Отбор.Владелец.Значение = ОбщегоНазначения.ГоловнаяОрганизация(Отбор.Владелец.Значение);
		КонецЕсли;
		Организация = Отбор.Владелец.Значение;
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

Процедура ОрганизацияНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Отказ = Ложь;
	РаботаСДиалогами.ВыбратьЗначениеПоляВводаИзСписка(
		ЭтаФорма, Элемент, СтандартнаяОбработка, ЭлементыФормы.Организация.СписокВыбора, Организация, Отказ);
		
	Если НЕ Отказ Тогда
		Отбор.Владелец.Использование = Ложь;
		УстановитьОтбор();
		УстановитьВидимость();
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОрганизацияАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	РаботаСДиалогами.ОбработатьАвтоПодборТекста(
		Текст, ТекстАвтоПодбора, СтандартнаяОбработка, ЭлементыФормы.Организация.СписокВыбора);
	
КонецПроцедуры

Процедура ОрганизацияОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	РаботаСДиалогами.ОбработатьОкончаниеВводаТекста(
		Текст, Значение, СтандартнаяОбработка, ЭлементыФормы.Организация.СписокВыбора);
	
КонецПроцедуры

