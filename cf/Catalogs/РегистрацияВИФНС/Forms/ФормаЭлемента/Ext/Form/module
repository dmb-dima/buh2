
Процедура УправлениеЭУ()
	
	Если ТипЗнч(Представитель) = Тип("СправочникСсылка.Контрагенты") Тогда
		ЭлементыФормы.ПанельПредставитель.ТекущаяСтраница = ЭлементыФормы.ПанельПредставитель.Страницы.ПредставительЮрЛицо;
	Иначе
		ЭлементыФормы.ПанельПредставитель.ТекущаяСтраница = ЭлементыФормы.ПанельПредставитель.Страницы.ПредставительФизЛицо;
	КонецЕсли;
	
	Если РегламентированнаяОтчетность.ЭтоПБОЮЛ(Владелец) Тогда
		ЭлементыФормы.ПанельКПП.Свертка = РежимСверткиЭлементаУправления.Верх;
	Иначе
		ЭлементыФормы.ПанельКПП.Свертка = РежимСверткиЭлементаУправления.Нет;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыЗаполнить(Кнопка)

	СведОбОрганизации = РегламентированнаяОтчетность.ПолучитьСведенияОбОрганизации(Владелец, ОбщегоНазначения.ПолучитьРабочуюДату(), "КодНО, НаимНО, КППЮЛ");
	Если ЗначениеЗаполнено(СведОбОрганизации) Тогда
		Если НЕ ПустаяСтрока(СведОбОрганизации.КодНО) Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			                      |	РегистрацияВИФНС.Ссылка КАК Ссылка
			                      |ИЗ
			                      |	Справочник.РегистрацияВИФНС КАК РегистрацияВИФНС
			                      |ГДЕ
			                      |	РегистрацияВИФНС.КПП = &КПП
			                      |	И РегистрацияВИФНС.Код = &Код");
			Запрос.УстановитьПараметр("Код", СведОбОрганизации.КодНО);
			Запрос.УстановитьПараметр("КПП", СведОбОрганизации.КППЮЛ);
			Если Запрос.Выполнить().Пустой() Тогда
				Код = СведОбОрганизации.КодНО;
				НаименованиеИФНС = СведОбОрганизации.НаимНО;
				КПП = СведОбОрганизации.КППЮЛ;
			Иначе
				Сообщить("В справочнике уже есть необходимая запись об инспекции.", СтатусСообщения.Информация);
			КонецЕсли;
		Иначе
			Сообщить("Необходимо заполнить реквизит ""Код ИФНС"" в справочнике ""Организации"".", СтатусСообщения.Информация);
		КонецЕсли;
	Иначе
		Сообщить("Не удалось получить необходимые данные об организации.", СтатусСообщения.Важное);
	КонецЕсли;

КонецПроцедуры

Процедура ПредставительПриИзменении(Элемент)
	
	УправлениеЭУ();
	
КонецПроцедуры

Процедура ПослеЗаписи()
	
	Если ВладелецФормы <> Неопределено Тогда
		
		ОповеститьОЗаписиНовогоОбъекта(Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ЭлементыФормы.Владелец.СписокВыбора = ПроцедурыУправленияПерсоналом.ПолучитьСписокГоловныхОрганизаций();
	
	Если ЭтоНовый() Тогда
	
		Если НЕ ЗначениеЗаполнено(Владелец) Тогда
			Владелец = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Владелец) Тогда
			Владелец = ОбщегоНазначения.ГоловнаяОрганизация(Владелец);
		КонецЕсли;
	
	КонецЕсли;
	
	УправлениеЭУ();
	
КонецПроцедуры

Процедура ВладелецНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Отказ = Ложь;
	
	РаботаСДиалогами.ВыбратьЗначениеПоляВводаИзСписка(
		ЭтаФорма, Элемент, СтандартнаяОбработка, ЭлементыФормы.Владелец.СписокВыбора, Владелец, Отказ);
		
	Если НЕ Отказ Тогда
		УправлениеЭУ();
	КонецЕсли;
	
КонецПроцедуры

Процедура ВладелецАвтоПодборТекста(Элемент, Текст, ТекстАвтоПодбора, СтандартнаяОбработка)
	
	РаботаСДиалогами.ОбработатьАвтоПодборТекста(
		Текст, ТекстАвтоПодбора, СтандартнаяОбработка, ЭлементыФормы.Владелец.СписокВыбора);
	
КонецПроцедуры

Процедура ВладелецОкончаниеВводаТекста(Элемент, Текст, Значение, СтандартнаяОбработка)
	
	РаботаСДиалогами.ОбработатьОкончаниеВводаТекста(
		Текст, Значение, СтандартнаяОбработка, ЭлементыФормы.Владелец.СписокВыбора);
	
КонецПроцедуры

Процедура КПППриИзменении(Элемент)
	
	Код = Лев(КПП, 4);
	
КонецПроцедуры

// {ОбособленныеПодразделения

Процедура КодПоОКАТОНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ФормаВыбора = Справочники.КодыОКАТО.ПолучитьФормуВыбора(, Элемент);
	ТекЗначение = Справочники.КодыОКАТО.НайтиПоКоду(КодПоОКАТО);
	Если ЗначениеЗаполнено(ТекЗначение) Тогда
		ФормаВыбора.НачальноеЗначениеВыбора = ТекЗначение;
	КонецЕсли;
	ФормаВыбора.Открыть();
	
КонецПроцедуры

Процедура КодПоОКАТООбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КодыОКАТО") Тогда
		СтандартнаяОбработка = Ложь;
		КодПоОКАТО = ВыбранноеЗначение.Код;
	КонецЕсли;
	
КонецПроцедуры

Процедура КодПоОКАТОПриИзменении(Элемент)
	
	ТекЗначение = Справочники.КодыОКАТО.НайтиПоКоду(КодПоОКАТО);
	Если НЕ ЗначениеЗаполнено(ТекЗначение) Тогда
		
		ТекстВопроса = "Такого кода нет в справочнике кодов ОКАТО. 
			|Добавить код в справочник?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Да Тогда
		
			НовыйЭлемент = Справочники.КодыОКАТО.СоздатьЭлемент();
			НовыйЭлемент.Код = КодПоОКАТО;
			ФормаЭлемента = НовыйЭлемент.ПолучитьФорму();
			ФормаЭлемента.ТекущийЭлемент = ФормаЭлемента.ЭлементыФормы.Наименование;
			ФормаЭлемента.Открыть();
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// }ОбособленныеПодразделения 
