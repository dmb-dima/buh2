Перем СтараяОбласть;

Процедура ОсновныеДействияФормыВыбрать(Кнопка)
	
	Область = ЭлементыФормы.ТабДокумент.ТекущаяОбласть;
	ВыбранныйКод = ЭлементыФормы.ТабДокумент.Область(Область.Верх, 1, Область.Верх, 1).Текст;
	Если ПустаяСтрока(ВыбранныйКод) 
		ИЛИ (НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(ВыбранныйКод)) Тогда
		Предупреждение("Выберите строку с кодом");
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ВыбранныйКод);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Макет = Справочники.ВидыНалоговИПлатежейВБюджет.ПолучитьМакет(ИмяМакета);
	ЭлементыФормы.ТабДокумент.Вывести(Макет);
	ЭлементыФормы.ТабДокумент.ФиксацияСверху = 1;
	
	Если НЕ ПустаяСтрока(СтрокаПоиска) Тогда
		НайденнаяОбласть = ЭлементыФормы.ТабДокумент.НайтиТекст(СтрокаПоиска, , , , , , Истина);
		Если НайденнаяОбласть = Неопределено Тогда
			ТекущийЭлемент = ЭлементыФормы.СтрокаПоиска;
		Иначе
			ТекущийЭлемент = ЭлементыФормы.ТабДокумент;
			ТекущийЭлемент.ТекущаяОбласть = НайденнаяОбласть;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТабДокументВыбор(Элемент, Область, СтандартнаяОбработка)
	
	ВыбранныйКод = ЭлементыФормы.ТабДокумент.Область(Область.Верх, 1, Область.Верх, 1).Текст;
	Если ПустаяСтрока(ВыбранныйКод) 
		ИЛИ (НЕ ОбщегоНазначения.ТолькоЦифрыВСтроке(ВыбранныйКод)) Тогда
		Предупреждение("Выберите строку с кодом");
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ВыбранныйКод);
	
КонецПроцедуры

Процедура ТабДокументПриАктивизацииОбласти(Элемент)
	
	Если СтараяОбласть <> Неопределено Тогда
		СтараяОбласть.ЦветФона = ЦветаСтиля.ЦветФонаПоля;
		СтараяОбласть.ЦветТекста = ЦветаСтиля.ЦветТекстаПоля;
	КонецЕсли;
	
	НомерСтроки = ЭлементыФормы.ТабДокумент.ТекущаяОбласть.Верх;
	ТекОбласть = ЭлементыФормы.ТабДокумент.Область(НомерСтроки, , НомерСтроки);
	ТекОбласть.ЦветФона = ЦветаСтиля.ЦветФонаВыделенияПоля;
	ТекОбласть.ЦветТекста = ЦветаСтиля.ЦветТекстаВыделенияПоля;
	
	СтараяОбласть = ТекОбласть;
	
КонецПроцедуры

Процедура ИскатьНажатие(Элемент)
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		Предупреждение("Не задана строка поиска");
		ТекущийЭлемент = ЭлементыФормы.СтрокаПоиска;
		Возврат;
	КонецЕсли;
	
	НайденнаяОбласть = ЭлементыФормы.ТабДокумент.НайтиТекст(СтрокаПоиска, ЭлементыФормы.ТабДокумент.ТекущаяОбласть, , , , , Истина);
	Если НайденнаяОбласть = Неопределено Тогда
		НайденнаяОбласть = ЭлементыФормы.ТабДокумент.НайтиТекст(СтрокаПоиска, , , , , , Истина);
		Если НайденнаяОбласть = Неопределено Тогда
			Предупреждение("Строка не найдена");
			ТекущийЭлемент = ЭлементыФормы.СтрокаПоиска;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ТекущийЭлемент = ЭлементыФормы.ТабДокумент;
	ТекущийЭлемент.ТекущаяОбласть = НайденнаяОбласть;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(ИмяМакета) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры
