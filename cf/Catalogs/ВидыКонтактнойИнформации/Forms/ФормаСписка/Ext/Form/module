////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ



Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(Метаданные.Справочники.ВидыКонтактнойИнформации, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);
КонецПроцедуры
                

Процедура ПриОткрытии()
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные.Справочники.ВидыКонтактнойИнформации, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);	
	ПроверитьДоступностьПолей();
КонецПроцедуры

Процедура СправочникСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если ДанныеСтроки.Ссылка.МожноРедактироватьТолькоВДиалоге Тогда
		ОформлениеСтроки.Ячейки.РедактированиеВДиалоге.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Картинка = УправлениеКонтактнойИнформацией.ПолучитьКартинкуПоТипу(ДанныеСтроки.Тип, ДанныеСтроки.НомерКартинки);
	Если Картинка <> Неопределено Тогда
		ОформлениеСтроки.Ячейки.НомерКартинки.Картинка = Картинка;
		ОформлениеСтроки.Ячейки.НомерКартинки.ОтображатьКартинку = Истина;
	КонецЕсли;
КонецПроцедуры


Процедура СправочникСписокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.МожноРедактироватьТолькоВДиалоге = Ложь;
	КонецЕсли;
	
	ПроверитьДоступностьПолей();
	
	Если НоваяСтрока И СправочникСписок.Отбор.ВидОбъектаКонтактнойИнформации.Использование И Элемент.ТекущиеДанные.ВидОбъектаКонтактнойИнформации.Пустая() Тогда
		Элемент.ТекущиеДанные.ВидОбъектаКонтактнойИнформации = СправочникСписок.Отбор.ВидОбъектаКонтактнойИнформации.Значение;
	КонецЕсли;
	
КонецПроцедуры

Процедура СправочникСписокПриАктивизацииСтроки(Элемент)
	ПроверитьДоступностьПолей();
КонецПроцедуры

Процедура ПроверитьДоступностьПолей()

	текСтр = ЭлементыФормы.СправочникСписок.ТекущаяСтрока;
	Доступны = (текСтр = Неопределено ИЛИ текСтр.Ссылка.Пустая());
	ЭлементыФормы.СправочникСписок.Колонки.ВидОбъектаКонтактнойИнформации.ТолькоПросмотр = Не Доступны;
	ЭлементыФормы.СправочникСписок.Колонки.Тип.ТолькоПросмотр = Не Доступны;

КонецПроцедуры

Процедура ДействияФормыПереместитьВверх(Кнопка)
	ИзменитьПорядокУТекущегоЭлемента(Истина);
КонецПроцедуры

Процедура ДействияФормыПереместитьВниз(Кнопка)
	ИзменитьПорядокУТекущегоЭлемента(Ложь);
КонецПроцедуры

Процедура ИзменитьПорядокУТекущегоЭлемента(ПоднятьВверх)

	текСтр = ЭлементыФормы.СправочникСписок.ТекущаяСтрока;
	Если текСтр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	текЭлемент = текСтр.Ссылка;
	
	ТЗ =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВидыКонтактнойИнформации.Ссылка
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.ВидОбъектаКонтактнойИнформации = &ВидОбъектаКонтактнойИнформации
	|	И ВидыКонтактнойИнформации.Порядок " + ?(ПоднятьВверх, "<", ">") + " &Порядок
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидыКонтактнойИнформации.Порядок " + ?(ПоднятьВверх, "Убыв", "Возр");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТЗ;
	Запрос.УстановитьПараметр("ВидОбъектаКонтактнойИнформации", текСтр.ВидОбъектаКонтактнойИнформации);
	Запрос.УстановитьПараметр("Порядок", текСтр.Порядок);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Объект1 = текЭлемент.ПолучитьОбъект();
	Объект2 = Выборка.Ссылка.ПолучитьОбъект();
	
	НачатьТранзакцию();
	
	Порядок1 = Объект1.Порядок;
	Объект1.Порядок = Объект2.Порядок;
	Объект2.Порядок = Порядок1;
	Объект1.Записать();
	Объект2.Записать();
	
	ЗафиксироватьТранзакцию();

КонецПроцедуры

СправочникСписок.Колонки.Добавить("Порядок");
СправочникСписок.Колонки.Добавить("НомерКартинки");
СправочникСписок.Порядок.Установить("Порядок");


