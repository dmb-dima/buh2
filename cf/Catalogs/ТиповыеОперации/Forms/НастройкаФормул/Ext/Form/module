
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ


// Вставляет в формулу указанной строки выражение
//
// Параметры:
//  Стр            - Строка - формульное выражение
//  СтрокаТЗ       - ссылка на строку таблицы значений
// 
Процедура ВставитьВТекст(Стр, СтрокаТЗ=Неопределено) Экспорт

	Если СтрокаТЗ <> Неопределено Тогда

		Тек = СокрЛП(СтрокаТЗ.Формула);
		СтрокаТЗ.Формула = Тек + ?(Тек="", "", " ") + Стр;

	КонецЕсли;

КонецПроцедуры

// Устанавливает заголовок формы
//
Процедура УстановитьЗаголовокФормы()

	НомерЗаписи        = 1 + ВладелецФормы.ЭлементыФормы.Хозрасчетный.Значение.Индекс(СтрокаТаблицы);
	ЭтаФорма.Заголовок = "Проводка № " + НомерЗаписи + ". Формулы вычисления значений реквизитов";

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОГО ПОЛЯ ТАБЛИЦАФОРМУЛ

Процедура ТаблицаФормулПередНачаломДобавления(Элемент, Отказ, Копирование)

	Отказ = Истина;

КонецПроцедуры

Процедура ТаблицаФормулПередУдалением(Элемент, Отказ)

	Отказ = Истина;

КонецПроцедуры

Процедура ТаблицаФормулФормулаНачалоВыбора(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Таб = ВладелецФормы.ЭлементыФормы.Хозрасчетный;

	СтрокаТЗ    = ЭлементыФормы.ТаблицаФормул.ТекущиеДанные;
	ИмяКолонки  = СтрокаТЗ.Имя;
	//ТипыКолонки = ВладелецФормы.ПолучитьТипыКолонки(ВладелецФормы.ЭлементыФормы.Хозрасчетный, СтрокаТаблицы, ИмяКолонки);
	ТипыКолонки = ВладелецФормы.ПолучитьТипыКолонки(Таб, СтрокаТаблицы, ИмяКолонки);

	ФормаВыбора = Справочники["ТиповыеОперации"].ПолучитьФорму("ВыборПараметра", ВладелецФормы);

	ФормаВыбора.ОтборПоТипу     = Истина;
	ФормаВыбора.ТипыКолонки     = ТипыКолонки;
	ФормаВыбора.ИмяКолонки      = ИмяКолонки;
	ФормаВыбора.ТекущаяПроводка = СтрокаТаблицы;

	ФормаВыбора.ЗаполнитьДеревоПараметров();
	ФормаВыбора.Заголовок = "Выбор параметра";

	Парам = ФормаВыбора.ОткрытьМодально();

	Если Парам = Неопределено Тогда
		Возврат
	КонецЕсли;

	ВставитьВТекст(Парам, СтрокаТЗ);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()

	УстановитьЗаголовокФормы();

КонецПроцедуры

Процедура ОбработкаАктивизацииОбъекта(АктивныйОбъект, Источник)

	УстановитьЗаголовокФормы();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ

Процедура КнопкаОКНажатие(Элемент)

	Для каждого Стр Из ЭлементыФормы.ТаблицаФормул.Значение Цикл
		ВладелецФормы.УстановитьФормулу(СтрокаТаблицы, Стр.Имя, Стр.Формула);
	КонецЦикла;

	Если Элемент.Имя = "КнопкаОК" Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры
