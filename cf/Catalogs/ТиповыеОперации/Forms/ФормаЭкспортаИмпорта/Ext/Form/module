
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Процедура - обработчик события "Начало выбора" поля "Имя файла"
//
Процедура ИмяФайлаНачалоВыбора(Элемент, СтандартнаяОбработка)

	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	ДиалогВыбораФайла.Фильтр                  = "Файл данных (*.xml)|*.xml";
	ДиалогВыбораФайла.Заголовок               = "Выберите файл";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение              = "xml";
	ДиалогВыбораФайла.ИндексФильтра           = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла          = Элемент.Значение;

	Если ДиалогВыбораФайла.Выбрать() Тогда
		Элемент.Значение = ДиалогВыбораФайла.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры // ИмяФайлаНачалоВыбора()

// Процедура - обработчик события "Нажатие" кнопки "Экспорт"
//
Процедура ОсновныеДействияФормыЭкспортНажатие(Кнопка)

	ФормаОбмена    = Обработки.УниверсальныйОбменДаннымиXML.ПолучитьФорму("Форма", ЭтаФорма);
	ПравилаОбмена  = Справочники.ТиповыеОперации.ПолучитьМакет("ПравилаОбмена");

	ИмяФайлаПравил = КаталогВременныхФайлов() + "ExcRules.xml";
	ЗаписьТекста   = Новый ЗаписьТекста(ИмяФайлаПравил);
	ЗаписьТекста.Записать(ПравилаОбмена.ПолучитьТекст());
	ЗаписьТекста.Закрыть();

	ФормаОбмена.ИмяФайлаОбмена       = ИмяФайла;
	ФормаОбмена.ИмяФайлаПравилОбмена = ИмяФайлаПравил;
	ФормаОбмена.ФлагКомментироватьОбработкуОбъектов = КомментироватьПроцесс;
	ФормаОбмена.ВыполнитьОбменДаннымиВОптимизированномФормате = Истина;
	ФормаОбмена.ЗагрузитьПравилаОбмена();
	ФормаОбмена.ВыполнитьВыгрузку();

	УдалитьФайлы(ИмяФайлаПравил); // удалим за собой мусор.

КонецПроцедуры // ОсновныеДействияФормыЭкспортНажатие()

// Процедура - обработчик события "Нажатие" кнопки "Импорт"
//
Процедура ОсновныеДействияФормыИмпортНажатие(Кнопка)

	ФормаОбмена = Обработки.УниверсальныйОбменДаннымиXML.ПолучитьФорму("Форма", ЭтаФорма);
	ФормаОбмена.ИмяФайлаОбмена = ИмяФайла;
	ФормаОбмена.РежимОбмена    = "Загрузка";
	ФормаОбмена.ФлагКомментироватьОбработкуОбъектов = КомментироватьПроцесс;
	ФормаОбмена.ВыполнитьЗагрузку();

КонецПроцедуры // ОсновныеДействияФормыИмпортНажатие()


