Перем ИсключатьДокументы;


Функция ПолучитьСписокВыбранныхОбъектов()
	
	СписокОбъектов = Новый СписокЗначений;
	
	Для Каждого Документ Из СписокДокументов Цикл
		
		Если Документ.Пометка Тогда
			СписокОбъектов.Добавить(Документ.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Справочник Из СписокСправочников Цикл
		
		Если Справочник.Пометка Тогда
			СписокОбъектов.Добавить(Справочник.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СписокОбъектов;
	
КонецФункции

Процедура ЗаполнитьСписокОбъектов()
	
	Если УжеВыбранные = Неопределено  Тогда
		УжеВыбранные = Новый Соответствие;
	КонецЕсли;
	
	Для Каждого Документ Из Метаданные.Документы Цикл
		
		Если (УжеВыбранные[Документ] = Неопределено) И (ИсключатьДокументы.Получить(Документ.Имя) <> Истина) Тогда
			СписокДокументов.Добавить(Документ, ?(Документ.Синоним="", Документ.Имя, Документ.Синоним));
		КонецЕсли;
		
	КонецЦикла;
	
	ДоступныеСправочники = Новый Массив;
	ДоступныеСправочники.Добавить(Метаданные.Справочники.ДоговорыКонтрагентов);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.Контрагенты);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.НематериальныеАктивы);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.Номенклатура);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.ОсновныеСредства);
	ДоступныеСправочники.Добавить(Метаданные.Справочники.ФизическиеЛица);
	
	Для Каждого Справочник Из ДоступныеСправочники Цикл
		
		Если УжеВыбранные[Справочник] = Неопределено Тогда
			СписокСправочников.Добавить(Справочник, ?(Справочник.Синоним="", Справочник.Имя, Справочник.Синоним));
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ИсключатьДокументы = Новый Соответствие;
	
	Если НЕ РежимПодбора Тогда
		ЭлементыФормы.СписокДокументов.Колонки.Значение.ДанныеФлажка = "";
		ЭлементыФормы.СписокСправочников.Колонки.Значение.ДанныеФлажка = "";
	КонецЕсли;
	
	Если ВидОбработки = Перечисления.ВидыДополнительныхВнешнихОбработок.ЗаполнениеТабличныхЧастей Тогда
		Заголовок = "Принадлежность внешней обработки";
		ИсключатьДокументы.Вставить("ОперацияБух", Истина);
	КонецЕсли;
	
	ЗаполнитьСписокОбъектов();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыКнопкаОК(Кнопка)
	
	Если НЕ РежимПодбора Тогда
		ОповеститьОВыборе(ЭлементыФормы.СписокДокументов.ТекущаяСтрока.Значение);
	Иначе
		ОповеститьОВыборе(ПолучитьСписокВыбранныхОбъектов());
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если НЕ РежимПодбора Тогда
		ОповеститьОВыборе(ЭлементыФормы.СписокДокументов.ТекущаяСтрока.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура СписокСправочниковВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если НЕ РежимПодбора Тогда
		ОповеститьОВыборе(ЭлементыФормы.СписокСправочников.ТекущаяСтрока.Значение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриПовторномОткрытии(СтандартнаяОбработка)
	
	ЗаполнитьСписокОбъектов();
	
КонецПроцедуры






