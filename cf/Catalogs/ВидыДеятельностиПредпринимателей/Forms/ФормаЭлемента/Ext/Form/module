Процедура ПриОткрытии()
	
	ДоступныеХарактерыДеятельности = Новый СписокЗначений;
	ДоступныеХарактерыДеятельности.Добавить(Перечисления.ХарактерДеятельности.ОптоваяТорговля);
	ДоступныеХарактерыДеятельности.Добавить(Перечисления.ХарактерДеятельности.РозничнаяТорговляНеЕНВД);
	ДоступныеХарактерыДеятельности.Добавить(Перечисления.ХарактерДеятельности.РозничнаяТорговляЕНВД);
	ДоступныеХарактерыДеятельности.Добавить(Перечисления.ХарактерДеятельности.ПроизводствоРаботыУслуги);
	ДоступныеХарактерыДеятельности.Добавить(Перечисления.ХарактерДеятельности.УслугиЕНВД);
	
	ЭлементыФормы.ХарактерДеятельности.ДоступныеЗначения = ДоступныеХарактерыДеятельности;
	
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю,ЭлементыФормы.Код);
	
	УстановитьОтбор();
	
КонецПроцедуры

Процедура УстановитьОтбор()
	
	НоменклатурныеГруппы.Отбор.ХарактерДеятельности.Установить(ХарактерДеятельности);
	
	НоменклатурныеГруппы.Отбор.ВидДеятельности.Установить(Ссылка);
	
КонецПроцедуры

// Процедура разрешения/запрещения редактирования номера документа
Процедура ДействияФормыРедактироватьКод(Кнопка)
	
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(ЭтотОбъект.Метаданные(), ЭтаФорма, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.Код);
			
КонецПроцедуры
                 

Процедура НоменклатурныеГруппыПередНачаломДобавления(Элемент, Отказ, Копирование)
	
	Если ПометкаУдаления Тогда
		Предупреждение("Для элемента справочника, помеченного на удаление
						|номенклатурные группы не указываются!");
		Отказ = Истина;
		
	ИначеЕсли ЭтоНовый() Тогда
		
		ТекстВопроса = "Для выполнения данной операции необходимо записать элемент. Записать?";
		Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Отказ = Истина;
		Иначе
			Попытка
				Отказ = НЕ ЗаписатьВФорме();
			Исключение
				Отказ = Истина;
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


Процедура НаименованиеПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(НаименованиеПолное) Тогда
		НаименованиеПолное = Наименование;
	КонецЕсли;
	
КонецПроцедуры

Процедура ХарактерДеятельностиПриИзменении(Элемент)
	
	НоменклатурныеГруппы.Отбор.ХарактерДеятельности.Установить(ХарактерДеятельности);
	
КонецПроцедуры

Процедура НоменклатурныеГруппыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ЭлементыФормы.НоменклатурныеГруппы.ТекущиеДанные.ВидДеятельности = Ссылка;
	ЭлементыФормы.НоменклатурныеГруппы.ТекущиеДанные.ХарактерДеятельности = ХарактерДеятельности;
	
КонецПроцедуры

Процедура ХарактерДеятельностиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура НоменклатурныеГруппыНоменклатурнаяГруппаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	УстановитьОтбор();
	
КонецПроцедуры

Процедура ХарактерДеятельностиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВидыДеятельностиПредпринимателей.ВидДеятельности,
	               |	ВидыДеятельностиПредпринимателей.НоменклатурнаяГруппа,
	               |	ВидыДеятельностиПредпринимателей.ХарактерДеятельности
	               |ИЗ
	               |	РегистрСведений.ВидыДеятельностиПредпринимателей КАК ВидыДеятельностиПредпринимателей
	               |ГДЕ
	               |	ВидыДеятельностиПредпринимателей.ВидДеятельности = &Ссылка";
				   
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ЗапросПоВидамДеятельности = Запрос.Выполнить().Выгрузить();
	
	Если ЗапросПоВидамДеятельности.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "Изменился характер деятельности! Список номенклатурных групп будет очищен. Продолжить?";
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;

	МенеджерЗаписиРегистра = РегистрыСведений.ВидыДеятельностиПредпринимателей.СоздатьМенеджерЗаписи();
	
	Для Каждого ЗаписьРегистра ИЗ ЗапросПоВидамДеятельности Цикл
		МенеджерЗаписиРегистра.НоменклатурнаяГруппа	= ЗаписьРегистра.НоменклатурнаяГруппа;
		МенеджерЗаписиРегистра.ВидДеятельности		= ЗаписьРегистра.ВидДеятельности;
		МенеджерЗаписиРегистра.ХарактерДеятельности	= ЗаписьРегистра.ХарактерДеятельности;
		МенеджерЗаписиРегистра.Прочитать();
		Если МенеджерЗаписиРегистра.Выбран() Тогда
			МенеджерЗаписиРегистра.Удалить();
		КонецЕсли;
	КонецЦикла;
	
	УстановитьОтбор();
	
КонецПроцедуры




