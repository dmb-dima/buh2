////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// в переменной запоминаем страну которая задана до установки
// переключателя - Лицо без гражданства
Перем мСтрана;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Процедура ОбновитьПредставлениеЭлемента(ИмяОбновляемогоЭлемента = "Все")
	
	Если ИмяОбновляемогоЭлемента = "НадписьПФР" или ИмяОбновляемогоЭлемента = "Все" Тогда
		
		Если НеИмеетПравоНаПенсию Тогда
			РасшифровкаПФР = "На физическое лицо не распространяется обязательное пенсионное, медицинское и социальное страхование в РФ. Таковыми являются иностранные граждане и лица без гражданства, временно пребывающие на территории РФ.";
		Иначе
			РасшифровкаПФР = "На физическое лицо распространяется обязательное пенсионное, медицинское и социальное страхование в РФ. Таковыми являются граждане РФ, а также постоянно или временно проживающие на территории РФ иностранные граждане и лица без гражданства.";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если НаборЗаписей.Количество() = 0 тогда
		НаборЗаписей.Добавить();
		ЗаписьИзНабора = НаборЗаписей[0];
	КонецЕсли;
	ЗаписьИзНабора 						= НаборЗаписей[0];
	Период 								= ?(ЗначениеЗаполнено(ЗаписьИзНабора.Период), ЗаписьИзНабора.Период, '19000101');
	НеИмеетПравоНаПенсию 				= ЗаписьИзНабора.НеИмеетПравоНаПенсию;
	Страна 								= ЗаписьИзНабора.Страна;
	ФизЛицо								= ЗаписьИзНабора.Физлицо;
	
	Если ФизЛицо.Пустая() тогда
		ИндКнопки = ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Индекс(ЭлементыФормы.ОсновныеДействияФормы.Кнопки.КнопкаИстория);
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(ИндКнопки);
		
		ИндКнопки = ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Индекс(ЭлементыФормы.ОсновныеДействияФормы.Кнопки.РазделительИстория);
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.Удалить(ИндКнопки);
	КонецЕсли;
	
		
	// установим переключатель в форме в соответствии с выбранным гражданством
	ЛицоБезГражданства = Страна = Справочники.КлассификаторСтранМира.ЛицоБезГражданства;
	ЭлементыФормы.ГражданинСтраны.ТолькоПросмотр = ЛицоБезГражданства;
	Если ЛицоБезГражданства Тогда
		ЭлементыФормы.ГражданинСтраны.Значение = Справочники.КлассификаторСтранМира.ПустаяСсылка();
	Иначе
		ЭлементыФормы.ГражданинСтраны.Значение = Страна;
	КонецЕсли;	
	
	ОбновитьПредставлениеЭлемента();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура КнопкаИсторияНажатие(Элемент)
	
	ФормаРегистра = РегистрыСведений.ГражданствоФизЛиц.ПолучитьФормуСписка();
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Значение 					 = ФизЛицо;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.ВидСравнения 				 = ВидСравнения.Равно;
	ФормаРегистра.РегистрСведенийСписок.Отбор.ФизЛицо.Использование 			 = Истина;
	ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры

Процедура ПодтвердитьИзменения(Кнопка)
	
	ЗаписьИзНабора = НаборЗаписей[0];
	Если НеИмеетПравоНаПенсиюПриОткрытии <> НеИмеетПравоНаПенсию ИЛИ
		СтранаПриОткрытии <> Страна ИЛИ
		ПериодПриОткрытии <> Период Тогда
	
		ЗаписьИзНабора.Период   						= Период;
		ЗаписьИзНабора.НеИмеетПравоНаПенсию  		   	= НеИмеетПравоНаПенсию;
		ЗаписьИзНабора.Страна						   	= Страна;
	КонецЕсли;
	
	// оповестим все формы, показывающие данные физлица, для которого 
	// редактируются связанные данные
	
	Оповестить("ОбновитьФорму", Новый Структура("ИмяЭлемента","Гражданство"), ОбщегоНазначенияЗК.ОпределитьОповещаемоеФизЛицо(ФизЛицо, ВладелецФормы));
	
	Закрыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ПереключательГражданствоПриИзменении(Элемент)
	
	Если ЛицоБезГражданства Тогда
		мСтрана = ЭлементыФормы.ГражданинСтраны.Значение;
		Страна = Справочники.КлассификаторСтранМира.ЛицоБезГражданства;
		ЭлементыФормы.ГражданинСтраны.Значение = Справочники.КлассификаторСтранМира.ПустаяСсылка();
	Иначе
		Страна = мСтрана;
		ЭлементыФормы.ГражданинСтраны.Значение = Страна;
	КонецЕсли;
	
	ЭлементыФормы.ГражданинСтраны.ТолькоПросмотр = ЛицоБезГражданства;
	
КонецПроцедуры

Процедура ГражданинСтраныПриИзменении(Элемент)
	
	Страна = Элемент.Значение;
	
КонецПроцедуры

Процедура НеИмеетПравоНаПенсиюПриИзменении(Элемент)
	ОбновитьПредставлениеЭлемента("НадписьПФР")
КонецПроцедуры




