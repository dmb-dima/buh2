////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЕ МОДУЛЯ

Перем мЖирныйШрифт;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	СправочникСписокПриАктивизацииСтроки(ЭлементыФормы.СправочникСписок);
	
	// {ОбособленныеПодразделения
	СправочникСписок.Колонки.Добавить("ГоловнаяОрганизация", Ложь);
	// }ОбособленныеПодразделения 
	
	Если НЕ ПараметрыСеанса.ИспользоватьОграниченияПравДоступаНаУровнеЗаписей Тогда
		ЭлементыФормы.ДействияФормы.Кнопки.Удалить(ЭлементыФормы.ДействияФормы.Кнопки.Права);
		ЭлементыФормы.ДействияФормы.Кнопки.Удалить(ЭлементыФормы.ДействияФормы.Кнопки.РазделительПрава);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	глЗначениеПеременнойУстановить("ОсновнаяОрганизация", УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация"), Истина);

	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.СправочникСписок.ТекущаяСтрока));
	
КонецПроцедуры // ПриОткрытии()

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеЗначенийСвойств" И Источник = ЭлементыФормы.СправочникСписок.ТекущаяСтрока Тогда
		РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, Параметр);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ДействияФормыПраваДоступаПользователейКТекущемуЭлементу(Кнопка)
	
	Если Не ЭлементыФормы.СправочникСписок.ТекущиеДанные = Неопределено Тогда
		НастройкаПравДоступа.РедактироватьПраваДоступа(ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыПраваДоступаПользователейКоВсемуСправочнику(Кнопка)
	
	НастройкаПравДоступа.РедактироватьПраваДоступа(Справочники.Организации.ПустаяСсылка());
	
КонецПроцедуры

Процедура ДействияФормыДействиеСвойства(Кнопка)
	
	РаботаСДиалогами.ОткрытьСвойстваИзСписка(ЭлементыФормы.СправочникСписок.ТекущаяСтрока, ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

Процедура СправочникСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)

	Если ЗначениеЗаполнено(ДанныеСтроки.Ссылка)
		  И глЗначениеПеременной("ОсновнаяОрганизация") = ДанныеСтроки.Ссылка Тогда

		ОформлениеСтроки.Шрифт = мЖирныйШрифт;

	КонецЕсли;
	
	// {ОбособленныеПодразделения
	Если Элемент.Колонки.ОбособленноеПодразделение.Видимость Тогда
		 ОформлениеСтроки.Ячейки.ОбособленноеПодразделение.ОтображатьФлажок = Истина;
		 ОформлениеСтроки.Ячейки.ОбособленноеПодразделение.Флажок = Не ДанныеСтроки.ГоловнаяОрганизация.Пустая();
	КонецЕсли;
	// }ОбособленныеПодразделения 
	 
КонецПроцедуры // СправочникСписокПриВыводеСтроки()

Процедура СправочникСписокПриАктивизацииСтроки(Элемент)

	Если ЭлементыФормы.СправочникСписок.ТекущиеДанные = Неопределено Тогда
		ЭлементыФормы.ДействияФормы.Кнопки.УстановитьОсновной.Доступность = Ложь;

	Иначе
		ЭлементыФормы.ДействияФормы.Кнопки.УстановитьОсновной.Доступность = Истина;

	КонецЕсли;
	
	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.СправочникСписок.ТекущаяСтрока));

КонецПроцедуры // СправочникСписокПриАктивизацииСтроки()

Процедура ДействияФормыУстановитьОсновной(Кнопка)
	
	РаботаСДиалогами.УстановитьОсновнуюОрганизацию(ЭлементыФормы.СправочникСписок.ТекущиеДанные.Ссылка);
	Обновить();

КонецПроцедуры // ДействияФормыУстановитьОсновной()
                            

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мЖирныйШрифт  = Новый Шрифт(,, Истина);
