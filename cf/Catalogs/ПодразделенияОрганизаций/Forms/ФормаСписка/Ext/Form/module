
// Процедура обновляет заголовок формы по выбранному 
//  владельцу справочника
//
// Параметры: 
//  Нет.
//
// Возвращаемое значение:
//  Нет.
//
Процедура ОбновитьЗаголовок()

	Заголовок = "Список подразделений организации " + Организация.Наименование;
	
	ЭтоЮрЛицо = (Организация.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо);
    ЭлементыФормы.СправочникСписок.Колонки.КодПоОКАТО.Видимость = ЭтоЮрЛицо;
    ЭлементыФормы.СправочникСписок.Колонки.КодПоОКАТО.ИзменятьВидимость = ЭтоЮрЛицо;
    ЭлементыФормы.СправочникСписок.Колонки.КПП.Видимость = ЭтоЮрЛицо;
    ЭлементыФормы.СправочникСписок.Колонки.КПП.ИзменятьВидимость = ЭтоЮрЛицо;
    ЭлементыФормы.СправочникСписок.Колонки.ОбособленноеПодразделение.Видимость = ЭтоЮрЛицо;
    ЭлементыФормы.СправочникСписок.Колонки.ОбособленноеПодразделение.ИзменятьВидимость = ЭтоЮрЛицо;
	
КонецПроцедуры // ОбновитьЗаголовок()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ ИЗМЕНЕНИЯ ЗНАЧЕНИЙ РЕКВИЗИТОВ ШАПКИ

// обновляем заголовок формы
Процедура ОрганизацияПриИзменении(Элемент)

	ОбновитьЗаголовок()

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ, ОБЩИХ ДЛЯ ВСЕЙ ФОРМЫ

Процедура ОтборИВыборПоВладельцу()
	
	Если ПараметрВыборПоВладельцу <> Неопределено Тогда
        Организация = ПараметрВыборПоВладельцу
	КонецЕсли;
    Если ПараметрОтборПоВладельцу <> Неопределено Тогда
        Организация = ПараметрОтборПоВладельцу
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ОтборИВыборПоВладельцу();
	
	// проставляем организацию пользователя по умолчанию
    Если Организация.Пустая() Тогда
		Организация = глЗначениеПеременной("ОсновнаяОрганизация");
	КонецЕсли;
	          
    ОбновитьЗаголовок();
		
	МеханизмНумерацииОбъектов.ДобавитьВМенюДействияКнопкуРедактированияКода(ЭлементыФормы.ДействияФормы.Кнопки.Подменю);
	МеханизмНумерацииОбъектов.УстановитьДоступностьПоляВводаНомера(Метаданные.Справочники.ПодразделенияОрганизаций, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);	

	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.СправочникСписок.ТекущаяСтрока));
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеЗначенийСвойств" И Источник = ЭлементыФормы.СправочникСписок.ТекущаяСтрока Тогда
		РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, Параметр);
	ИначеЕсли ИмяСобытия = "ИзменениеТекущейОрганизации" Тогда
		Организация = глЗначениеПеременной("ОсновнаяОрганизация");		
		ОбновитьЗаголовок();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриПовторномОткрытии()

    ОтборИВыборПоВладельцу();
	ОбновитьЗаголовок()

КонецПроцедуры



Процедура ДействияФормыРедактироватьКод(Кнопка)
	МеханизмНумерацииОбъектов.ИзменениеВозможностиРедактированияНомера(Метаданные.Справочники.ПодразделенияОрганизаций, ЭлементыФормы.СправочникСписок, ЭлементыФормы.ДействияФормы.Кнопки.Подменю, ЭлементыФормы.СправочникСписок.Колонки.Код);
КонецПроцедуры

Процедура ДействияФормыДействиеСвойства(Кнопка)
	
	РаботаСДиалогами.ОткрытьСвойстваИзСписка(ЭлементыФормы.СправочникСписок.ТекущаяСтрока, ЭтаФорма);
	
КонецПроцедуры


Процедура СправочникСписокПриАктивизацииСтроки(Элемент)
	
	РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, РаботаСДиалогами.ПолучитьОписаниеЗначенийСвойствОбъекта(ЭлементыФормы.СправочникСписок.ТекущаяСтрока));
	
КонецПроцедуры



